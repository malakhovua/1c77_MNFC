// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Срок;
Перем СписокДействий;
Перем СтараяДата;
Перем КэшРКО;
Перем НачальнаяДатаДокумента;


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================
Процедура ОбновитьКэшРКО()
	КэшРКО = СоздатьОбъект("ТаблицаЗначений");
	КэшРКО.НоваяКолонка("РКО", "Документ.РасходныйКассовый");
	КэшРКО.НоваяКолонка("Контрагент", "Справочник.Контрагенты");
	
	Если ТекущийДокумент().Выбран() = 1 Тогда
		Док = СоздатьОбъект("Документ");
		Док.ВыбратьПодчиненныеДокументы(,,ТекущийДокумент());
		Пока Док.ПолучитьДокумент() = 1 Цикл
			Если (Док.Вид() = "РасходныйКассовый") Тогда
				КэшРКО.НоваяСтрока();
				КэшРКО.РКО = Док.ТекущийДокумент();
				КэшРКО.Контрагент = Док.Субконто1;
			КонецЕсли;
		КонецЦикла;	    
	КонецЕсли;
	Если КэшРКО.КоличествоСтрок() > 0 Тогда
		Если Режим = 0 Тогда
			Предупреждение("Менять режим нельзя, если уже сформированы РКО");
			Режим = 1;						
		КонецЕсли;
		Форма.Режим.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Процедура ИзмРежим()
	Форма.РКО.Видимость(Режим);
	Форма.кФРКО.Видимость(Режим);
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Повернення"));
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмДата()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

// ===============================
Процедура ИзмФирма()
	СтараяФирма = Фирма;
	глУстФирма(Контекст);	
	Если СтараяФирма = Фирма Тогда
		Возврат;
	КонецЕсли;
	
			
	Если ПустоеЗначение(Фирма) = 1 Тогда
		Фирма = СтараяФирма;
		глУстановитьНомер(Контекст);
	КонецЕсли;
КонецПроцедуры //ИзмФирма

// ===============================
Процедура ВводНового(Скопирован)
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	глЗаполнитьШапку(Контекст);
	ДатаДок=РабочаяДата();
//	Заполнить(1);
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
	ИзмРежим();
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;

	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.			Доступность(0);
		Форма.кПровести.	Доступность(0);
		Форма.кДействия.	Доступность(0);
		Форма.кЗаполнить.	Доступность(0);
		Форма.кФирма.		Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	ОбновитьКэшРКО();

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если (ПустоеЗначение(Контрагент.Родитель) = 0) И (ПустоеЗначение(Родитель) = 1) Тогда
			Родитель = Контрагент.Родитель;
		КонецЕсли;
	КонецЦикла;
	СтараяДата = ДатаДок;
	
	СписФ = СоздатьОбъект("СписокЗначений");
    СписФ.ИзСтрокиСРазделителями(СокрЛП(СтрФильтр));
	СпрТМЦ = СоздатьОбъект("Справочник.Контрагенты");
	
	Для Инд = 1 По СписФ.РазмерСписка() Цикл
		Если СпрТМЦ.НайтиПоКоду(СписФ.ПолучитьЗначение(Инд), 0) = 1 Тогда
		    списКонтрагент.ДобавитьЗначение(СпрТМЦ.ТекущийЭлемент());
		КонецЕсли;		
	КонецЦикла;		
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	АвтоВремяОтключить();
	
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	СписФ = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По списКонтрагент.РазмерСписка() Цикл
		СписФ.ДобавитьЗначение(списКонтрагент.ПолучитьЗначение(Инд).Код);
	КонецЦикла;	
	СтрФильтр = СписФ.ВСтрокуСРазделителями();	
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Элемент,Флаг)	// Предопределенная процедура
	Если Элемент="Договор" Тогда	// выбираем договор
		Фрм = СоздатьОбъект("СписокЗначений");
		Фрм.ДобавитьЗначение("ДоговораКонтрагентов","ГрафаОтбора");
		Фрм.ДобавитьЗначение(Контрагент,"Контрагент");
		ОткрытьПодбор("Журнал.ПолныйЖурнал","ДляВыбораДоговоровИСчетов",Фрм,0,Договор);
		Флаг=0;
	КонецЕсли;	           
КонецПроцедуры	// ПриНачалеВыбораЗначения

Процедура Заполнить()
	Если (ТекущийДокумент().Выбран() = 0) ИЛИ (Модифицированность() = 1) Тогда
		Если Вопрос("Перед заполнением документ необходимо записать. Сделать это сейчас?", "Да+Нет") = "Да" Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	СпМеню = СоздатьОбъект("СписокЗначений");
	СпМеню.ДобавитьЗначение(1, "Заполнить заново");
	СпМеню.ДобавитьЗначение(2, "Обновить данные");
	Зн = 1;
	Если СпМеню.ВыбратьЗначение(Зн,,,,1) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Зн = 1 Тогда
		Если КоличествоСтрок() > 0 Тогда		
			Если Вопрос("Удалить строки?", "Да+Нет") = "Да" Тогда
				УдалитьСтроки();
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;	    
	КонецЕсли;
	
	ПозДок = СформироватьПозициюДокумента(ТекущийДокумент(), -1);
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если (Зн = 1) И (списКонтрагент.РазмерСписка() > 0) И (ВсеКроме <> 1) Тогда
	    Ит.ИспользоватьСубконто(ВидыСубконто.Контрагенты,списКонтрагент,1,0);
	ИначеЕсли Зн = 2 Тогда
		СписК = СоздатьОбъект("СписокЗначений");
		ВыгрузитьТабличнуюЧасть(СписК, "Контрагент");
		Ит.ИспользоватьСубконто(ВидыСубконто.Контрагенты,СписК,1,0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.Контрагенты,,1,0);
	КонецЕсли;
	
	Ит.ВключатьСубсчета(-1);
	Ит.ВыполнитьЗапрос(,ПозДок, "373");
	Если Зн = 1 Тогда
		Ит.ВыбратьСубконто(1);
		Пока Ит.ПолучитьСубконто(1) = 1 Цикл
			Если (списКонтрагент.РазмерСписка() > 0) И (ВсеКроме = 1) Тогда
				Если списКонтрагент.Принадлежит(Ит.Субконто(1)) = 1 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Ит.ВыбратьСчета();
			Пока Ит.ПолучитьСчет() = 1 Цикл
				НоваяСтрока();
				Контрагент = Ит.Субконто(1);
				Родитель = Контрагент.Родитель;
				Договор = Контрагент.БазДоговор;
				Счет = Ит.Счет;
				Сумма = Ит.СКК();
			КонецЦикла;
		КонецЦикла;
	Иначе
		Отв = Вопрос("Добавлять новые?", "Да+Нет");
		
		ТЗ = СоздатьОбъект("ТаблицаЗначений");		
		ВыгрузитьТабличнуюЧасть(ТЗ);
		ТЗ.НоваяКолонка("Ключ", "Строка");		
		ТЗ.НоваяКолонка("Обновлено", "Число", 1);
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			ТЗ.Ключ = ТЗ.Счет.Код + "\\" + ЗначениеВСтрокуВнутр(ТЗ.Контрагент) + "\\" + ЗначениеВСтрокуВнутр(ТЗ.Договор);
		КонецЦикла;		
		
		Ит.ВыбратьСубконто(1);
		Пока Ит.ПолучитьСубконто(1) = 1 Цикл
			Ит.ВыбратьСчета();
			Пока Ит.ПолучитьСчет() = 1 Цикл
				Ключ = Ит.Счет.Код + "\\" + ЗначениеВСтрокуВнутр(Ит.Субконто(1)) + "\\" + ЗначениеВСтрокуВнутр(Ит.Субконто(1).БазДоговор);
				Стр = 0;
				Если ТЗ.НайтиЗначение(Ключ, Стр, "Ключ") = 0 Тогда
					Если Отв = "Да" Тогда
						ТЗ.НоваяСтрока();
						ТЗ.Контрагент = Ит.Субконто(1);
						ТЗ.Родитель = ТЗ.Контрагент.Родитель;
						ТЗ.Договор = Контрагент.БазДоговор;
						ТЗ.Счет = Ит.Счет;
						ТЗ.Сумма = Ит.СКК();
						ТЗ.Обновлено = 1;
					КонецЕсли;
				Иначе
					ТЗ.ПолучитьСтрокуПоНомеру(Стр);
					ТЗ.Сумма = Ит.СКК();
					ТЗ.Обновлено = 1;
				КонецЕсли;				
			КонецЦикла;
		КонецЦикла;
		
		Инд = 1;
		Пока Инд <= ТЗ.КоличествоСтрок() Цикл
			Если ТЗ.ПолучитьЗначение(Инд, "Обновлено") = 0 Тогда
				ТЗ.УдалитьСтроку(Инд);
			Иначе
				Инд = Инд + 1;
			КонецЕсли;
		КонецЦикла;
		ЗагрузитьТабличнуюЧасть(ТЗ);
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьРКО()
	ДокР = СоздатьОбъект("Документ.РасходныйКассовый");
	ДокР.Новый();
	ДокР.ДатаДок = ТекущаяДата();
	глЗаполнитьШапку(ДокР);
	ДокР.РСчет = Константа.КассаДляРКОБонусов;//глПолучитьКассу(ДокР.Фирма,Гривня);
	Если ДокР.РСчет.Выбран() = 1 Тогда
		ДокР.НомерРО = ДокР.РСчет.ПоследнийРасхДок + 1;
	КонецЕсли;
	ДокР.ВидНДС = глВосстановитьЗначение(,"БазНДС");
	ДокР.СубконтоВалДохРасх = Константа.НиДоходНиРасход;
	ДокР.Кассир = ДокР.Фирма.Кассир.Получить(ДатаДок);
	ДокР.Счет = Счет;
	ДокР.ОперационнаяКР = 1;
	ДокР.ВидОплаты = Перечисление.ВидыОплаты.Оплата;
	ДокР.РежимОплаты = глВосстановитьЗначение(,"БазРежимОплаты");
	Если ПустоеЗначение(ДокР.РежимОплаты)=1 Тогда
		ДокР.РежимОплаты = Перечисление.РежимыОплаты.ПоСчету;
	КонецЕсли;
	ДокР.НазначитьТип("Субконто1", ВидыСубконто.Контрагенты);
	ДокР.НазначитьТип("Субконто2", ВидыСубконто.РасходныеНакладные);
	ДокР.НазначитьТип("Субконто3", ВидыСубконто.ТМЦ);
	ДокР.Субконто1 = Контрагент;
	ДокР.СуммаВал = Сумма;
	ДокР.НДС = 0;
	ДокР.ВидРасходаДенег = Перечисление.ВидыРасходаДенег.НаВедениеХозДеятельности;
	ДокР.ДокументОснование = ТекущийДокумент();
	ДокР.Автор = глПользователь;
	ДокР.Записать();
	Если ДокР.РСчет.ПоследнийРасхДок < ДокР.НомерРО Тогда
		Сч = СоздатьОбъект("Справочник.НашиДенежныеСчета");
		Сч.НайтиЭлемент(ДокР.РСчет);
		Сч.ПоследнийРасхДок = ДокР.НомерРО;
		Сч.Записать();
	КонецЕсли;
	
	Сообщить("Добавлен: " + Строка(ДокР.ТекущийДокумент()));
КонецПроцедуры

Процедура СформироватьРКО()
	СпМеню = СоздатьОбъект("СписокЗначений");
	СпМеню.ДобавитьЗначение(1, "Сформировать");
	СпМеню.ДобавитьЗначение(2, "Переформировать");
	СпМеню.ДобавитьЗначение(3, "Удалить сформированные");
	СпМеню.ДобавитьЗначение(4, "Снять пометку удаления сформированных");
	НачатьТранзакцию();
	
	Зн = 1;
	Если спМеню.ВыбратьЗначение(Зн,,,,1) = 0 Тогда
		Возврат;
	КонецЕсли;
	Если (Модифицированность() = 1) ИЛИ (ТекущийДокумент().Выбран() = 0) Тогда
		Если Вопрос("Документ нужно записать. Записать сейчас?", "Да+Нет") = "Да" Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Зн = 1 Тогда
		Док = СоздатьОбъект("Документ");
		ОбновитьКэшРКО();
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			Если КэшРКО.НайтиЗначение(Контрагент, Стр, "Контрагент") = 0 Тогда
				СоздатьРКО();
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Зн = 2 Тогда
		Док = СоздатьОбъект("Документ");
		ОбновитьКэшРКО();
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			Если КэшРКО.НайтиЗначение(Контрагент, Стр, "Контрагент") = 0 Тогда
				СоздатьРКО();
			Иначе
				ДокРКО = СоздатьОбъект("Документ.РасходныйКассовый");
				ДокРКО.НайтиДокумент(КэшРКО.ПолучитьЗначение(Стр,"РКО"));
				ДокРКО.СуммаВал = Сумма;
				ДокРКО.Записать();
				Если ДокРКО.Проведен() = 1 Тогда
					ДокРКО.Провести();
				КонецЕсли;
				Сообщить("Изменен: " + Строка(ДокРКО));
			КонецЕсли;
		КонецЦикла;		
	ИначеЕсли Зн = 3 Тогда
		Док = СоздатьОбъект("Документ.РасходныйКассовый");
		ОбновитьКэшРКО();
		КэшРКО.ВыбратьСтроки();
		Пока КэшРКО.ПолучитьСтроку() = 1 Цикл
			Док.НайтиДокумент(КэшРКО.РКО);
			Док.Удалить(0);
			Сообщить("Помечен: " + Строка(Док.ТекущийДокумент()));
		КонецЦикла;		
	ИначеЕсли Зн = 4 Тогда
		Док = СоздатьОбъект("Документ.РасходныйКассовый");
		ОбновитьКэшРКО();
		КэшРКО.ВыбратьСтроки();
		Пока КэшРКО.ПолучитьСтроку() = 1 Цикл
			Док.НайтиДокумент(КэшРКО.РКО);
			Если Док.ПометкаУдаления() = 1 Тогда
				Док.СнятьПометкуУдаления();
				Сообщить("Снята пометка: " + Строка(Док.ТекущийДокумент()));
			КонецЕсли;			
		КонецЦикла;		
	КонецЕсли;
	Если Вопрос("Подтвердить изменения?", "Да+Нет") = "Да" Тогда
		ЗафиксироватьТранзакцию();
		ОбновитьКэшРКО();
	Иначе
		ОтменитьТранзакцию();		
	КонецЕсли;	
КонецПроцедуры

Функция РКО()
	Если Режим = 1 Тогда
		Стр = 0;
		Если КэшРКО.НайтиЗначение(Контрагент, Стр, "Контрагент") = 1 Тогда
			Возврат КэшРКО.ПолучитьЗначение(Стр, "РКО");
		КонецЕсли;
	КонецЕсли;
	Возврат "";
КонецФункции

Процедура ПриУдаленииСтроки()
	Если (Режим = 1) И (КэшРКО.КоличествоСтрок() > 0) Тогда
		Стр = 0;
		Если КэшРКО.НайтиЗначение(Контрагент, Стр, "Контрагент") = 1 Тогда
			Если КэшРКО.ПолучитьЗначение(Стр, "РКО").ПометкаУдаления() = 0 Тогда
				Предупреждение("Строки по которым уже сформированы РКО удалять запрещено. Нужно вначале пометить РКО на удаление");
				СтатусВозврата(0);			    
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ИзмКонтрагент()
	Родитель = Контрагент.Родитель;
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "Контрагенты" Тогда
		ДобавитьвСписок(Элт, списКонтрагент);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура Оборотка()
	Если ПустоеЗначение(Контрагент) = 1 Тогда
		Возврат;
	КонецЕсли;
	
	глРасшифровка = СоздатьОбъект("СписокЗначений");
	глРасшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
    глРасшифровка.Установить("РазделительУчета", Фирма);
   	глРасшифровка.Установить("Дата1", НачМесяца(ДатаДок));
   	глРасшифровка.Установить("Дата2", ДатаДок);
   	глРасшифровка.Установить("Счет", Счет);
   	глРасшифровка.Установить("ДанныеПоСубсчетам", 1);
    Для А=1 По 3 Цикл
		Если Счет.КоличествоСубконто() >= А Тогда
	   		глРасшифровка.Установить("ВидСубконто"+А, Счет.ВидСубконто(А));
	   		глРасшифровка.Установить("Субконто"+А, ?(А = 1, Контрагент, ""));
	   		глРасшифровка.Установить("ОтборСубконто"+А, ?(А = 1, 1, 3));
	   		глРасшифровка.Установить("ПоГруппам"+А, 0);
	   	КонецЕсли;
	КонецЦикла;
	   
	глФлагРасшифровки = 1;
	глОбновить = 1;
	
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
	глФлагРасшифровки = 0;
	глОбновить = 0;
КонецПроцедуры
// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");