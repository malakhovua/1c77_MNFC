Перем НачальнаяДатаДокумента;

Процедура ИзмЦенаБезНДС()
	СуммаБезНДС = ВесФакт * ЦенаБезНДС;	
КонецПроцедуры

Процедура ИзмВесФакт()
	глИзмПроцСписания(Контекст);
	ИзмЦенаБезНДС();
КонецПроцедуры

Процедура ИзмТМЦ()
	ПроцСписания = глПолучитьПроцентСписания(ТМЦ, МестоХранения, ДатаДок);
	ИзмВесФакт();
КонецПроцедуры

// ======================================
Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр) = "ГрупповойКонтекст" Тогда
		Провести(0, Форма.Параметр);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
		Форма.кПоНормам.Доступность(0);
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
	    ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если ТМЦ = Форма.Параметр Тогда
			    АктивизироватьСтроку(НомерСтроки);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

// ======================================
Процедура ВводНового(Копируем)
	Если Копируем=1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
КонецПроцедуры 

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;
	
	Попытка
		АвтоВремяОтключить();
		УстановитьВремя(9,0,0);		    
	Исключение КонецПопытки;	
КонецПроцедуры       

Процедура Рассчитать(Ц)
	ИтКво = 0;
	ДокОснование.ВыбратьСтроки();
	Пока ДокОснование.ПолучитьСтроку() = 1 Цикл
		ИтКво = ИтКво + ДокОснование.Кво * ДокОснование.Коэффициент;
	КонецЦикла;

	Попытка
		Ц = ДокОснование.Итог("СуммаБезНДС") / ДокОснование.Итог("Кво");
	Исключение
		Ц = 0;
	КонецПопытки;
	
	Если ВвестиЧисло(Ц, "Введите новую цену", 12, 2) = 0  Тогда
		Возврат;
	КонецЕсли;
	СС = Ц * ИтКво;
	
	// теперь себестоимость распределим по табличной части
	// находим ТМЦ с коэффициентом 1 и считаем для него цену по формуле
	// (Общая списанная сумма - Сумма отходов и ТМЦ без коэффициента) / Общая масса ТМЦ с коэффициентом
	ИтСуммаБезКоэф = 0;
	ИтМассаСКоэф = 0;
	НСтрКоэф1 = 0;
	ПоследСтрокаСКоэф = 0;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Коэф = 0 Тогда
			ИтСуммаБезКоэф = ИтСуммаБезКоэф + СуммаБезНДС;
		Иначе
			Если Коэф = 1 Тогда
				НСтрКоэф1 = НомерСтроки;
			КонецЕсли;
			ИтМассаСКоэф = ИтМассаСКоэф + ВесФакт * Коэф;
			Если ВесФакт <> 0 Тогда
				ПоследСтрокаСКоэф = НомерСтроки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НСтрКоэф1 = 0 Тогда
		Сообщить("Нет ТМЦ с коэффициентом 1");
		Возврат;
	КонецЕсли;
	
	ЦенаБазовая = (СС - ИтСуммаБезКоэф) / ИтМассаСКоэф;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Коэф > 0 Тогда
			ЦенаБезНДС = ЦенаБазовая * Коэф;
			СуммаБезНДС = ЦенаБезНДС * ВесФакт;
		КонецЕсли;
	КонецЦикла;
	Разница = СС - Итог("СуммаБезНДС");
		// конечно делаем проводку
	Попытка
		ПолучитьСтрокуПоНомеру(ПоследСтрокаСКоэф);
		СуммаБезНДС = СуммаБезНДС + Разница;			
	Исключение КонецПопытки;
КонецПроцедуры

// ======================================
Процедура Печать(НЦ = 0, НовыйВес = 0)
	КоэфПересчетаВеса = 1;
	
	Если НовыйВес = 1 Тогда
		СтарыйВес = ДокОснование.Итог("Кво");
		НВес = СтарыйВес;
		Если ВвестиЧисло(НВес, "Введите новый вес", 15, 3) = 0 Тогда
			Возврат;
		КонецЕсли;
		Если (НВес = 0) ИЛИ (СтарыйВес = 0) Тогда
			Возврат;
		КонецЕсли;
		
		КоэфПересчетаВеса = НВес / СтарыйВес;
	КонецЕсли;
	
	Если НЦ = 1 Тогда
		Ц = 0;
	    Рассчитать(Ц);
	КонецЕсли;
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	глУстПропись(Гривня);
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТЗ);
	ТЗО = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.Выгрузить(ТЗО);
	ТЗО.УдалитьСтроки();
	ТЗ.НоваяКолонка("ТМЦС", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("ВесС", "Число", 15, 3);
	ТЗ.НоваяКолонка("ЦенаС", "Число", 15, 3);
	ТЗ.НоваяКолонка("СуммаС", "Число", 12, 2);
	ТЗ.НоваяКолонка("ПроцОбщ", "Число", 12, 2);
	СписТ = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписТ);
	РегПарт = СоздатьОбъект("Регистр.Партии");
	РегПарт.УстановитьЗначениеФильтра("Фирма", Фирма, 1);
	РегПарт.УстановитьЗначениеФильтра("ТМЦ", СписТ, 2);
	Если НовыйВес = 1 Тогда
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			ТЗ.ВесФакт = ТЗ.ВесФакт * КоэфПересчетаВеса;
			ТЗ.ВесНорма = ТЗ.ВесНорма * КоэфПересчетаВеса;
			ТЗ.СуммаС = ТЗ.СуммаС * КоэфПересчетаВеса;
			ТЗ.СуммаБезНДС = ТЗ.СуммаБезНДС * КоэфПересчетаВеса;
		КонецЦикла;
	КонецЕсли;
	
	ИтВесФактДляПечати = ТЗ.Итог("ВесФакт");
	ИтВесНормаДляПечати = ТЗ.Итог("ВесНорма");
	ИтСуммаБезНДСДляПечати = ТЗ.Итог("СуммаБезНДС");

	
	Если ДатаДок < ПолучитьДатуТА() Тогда
	    РегПарт.ВременныйРасчет(1);
		РассчитатьРегистрыПо(ДатаДок);
	КонецЕсли;
	
	ТЗОст = СоздатьОбъект("ТаблицаЗначений");
	РегПарт.ВыгрузитьИтоги(ТЗОст,1);
	
	Инд = 1;
	Пока Инд <= ТЗ.КоличествоСтрок() Цикл
		ТЗ.ПолучитьСтрокуПоНомеру(Инд);
		Если ТЗ.Отход = Да Тогда
			ТЗО.НоваяСтрока();
			ТЗО.ТМЦ = ТЗ.ТМЦ;
			ТЗО.Коэф = ТЗ.Коэф;
			ТЗО.ВесФакт = ТЗ.ВесФакт;
			ТЗО.ВесНорма = ТЗ.ВесНорма;
			ТЗО.ЦенаБезНДС = ТЗ.ЦенаБезНДС;
			ТЗО.СуммаБезНДС = ТЗ.СуммаБезНДС;
			ТЗО.Отход = ТЗ.Отход;
			ТЗО.Приходовать = ТЗ.Приходовать;
			ТЗ.УдалитьСтроку(Инд);
		Иначе
			Инд = Инд + 1;
		КонецЕсли;
	КонецЦикла;
	ТЗ.Сортировать("Коэф-");
	
	НСтр = 0;
	Если НЦ = 1 Тогда
		ДокОснование.ВыбратьСтроки();
		Пока ДокОснование.ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			Если ТЗ.НайтиЗначение(ДокОснование.ТМЦ, Стр, "ТМЦС") = 0 Тогда
				НСтр = НСтр + 1;
				Если НСтр > ТЗ.КоличествоСтрок() Тогда
					ТЗ.НоваяСтрока();				
				Иначе
					ТЗ.ПолучитьСтрокуПоНомеру(НСтр);
				КонецЕсли;
				ТЗ.ТМЦС = ДокОснование.ТМЦ;
			Иначе
				ТЗ.ПолучитьСтрокуПоНомеру(Стр);
			КонецЕсли;
			ТЗ.ТМЦС = ДокОснование.ТМЦ;
			ТЗ.ВесС = ДокОснование.Кво * ДокОснование.Коэффициент * КоэфПересчетаВеса;
			ТЗ.ЦенаС = Ц;
			ТЗ.СуммаС = Ц * ТЗ.ВесС;
		КонецЦикла;
	Иначе		
		РегПарт = СоздатьОбъект("Регистр.Партии");
		РегПарт.ВыбратьДвиженияДокумента(ДокОснование);
		Пока РегПарт.ПолучитьДвижение() = 1 Цикл
			Стр = 0;
			Если ТЗ.НайтиЗначение(РегПарт.ТМЦ, Стр, "ТМЦС") = 0 Тогда
				НСтр = НСтр + 1;
				Если НСтр > ТЗ.КоличествоСтрок() Тогда
					ТЗ.НоваяСтрока();				
				Иначе
					ТЗ.ПолучитьСтрокуПоНомеру(НСтр);
				КонецЕсли;
				ТЗ.ТМЦС = РегПарт.ТМЦ;
			Иначе
				ТЗ.ПолучитьСтрокуПоНомеру(Стр);
			КонецЕсли;
			
			ТЗ.ВесС = ТЗ.ВесС + РегПарт.ОстатокТовара * КоэфПересчетаВеса;
			ТЗ.СуммаС = ТЗ.СуммаС + РегПарт.Стоимость * КоэфПересчетаВеса;
			ТЗ.ЦенаС = ТЗ.СуммаС / ?(ТЗ.ВесС = 0,1,ТЗ.ВесС);
		КонецЦикла;
	КонецЕсли;
	
	// теперь выберем документы с начала месяца по заданной категории, чтобы посчитать отклонение
	ИтНорма = 0; ИтФакт = 0;
	Док = СоздатьОбъект("Документ.ПереработкаМяса");
	Док.УстановитьФильтр(1,0);
	Док.ВыбратьДокументы(НачМесяца(ДатаДок), ДатаДок);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если (Док.Категория = Категория) и (Док.ТекущийДокумент() <> ТекущийДокумент()) Тогда
			Док.ВыбратьСтроки();
			Пока Док.ПолучитьСтроку() = 1 Цикл
				Если Док.Отход <> Да Тогда
					ИтНорма = ИтНорма + Док.ВесНорма;
					ИтФакт = ИтФакт + Док.ВесФакт;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	ИтОтклонение = (ТЗ.Итог("ВесФакт")+ИтФакт) - (ТЗ.Итог("ВесНорма") + ИтНорма);
	
	ИтВес = ТЗ.Итог("ВесС");
	Таб.ВывестиСекцию("Шапка");
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		ТЗ.ПроцОбщ = ?(ИтВес = 0, 0, (ТЗ.ВесС / ИтВес)) * 100;
		ТекЦенаСклада = 0;
		Стр = 0;
		Если ТЗОст.НайтиЗначение(ТЗ.ТМЦ, Стр, "ТМЦ") = 1 Тогда
			ТЗОст.ПолучитьСтрокуПоНомеру(Стр);
			Если ТЗОст.ОстатокТовара <> 0 Тогда
			    ТекЦенаСклада = ТЗОст.Стоимость / ТЗОст.ОстатокТовара;
			КонецЕсли;		    
		КонецЕсли;
		Если ТекЦенаСклада = 0 Тогда
			ТекЦенаСклада = глВернутьЦену(ТЗ.ТМЦ, Константа.ЗакупЦена, ДатаДок, Гривня);
		КонецЕсли;
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	Таб.ВывестиСекцию("ИтогоНО");
	
	ТЗО.ВыбратьСтроки();
	Пока ТЗО.ПолучитьСтроку() = 1 Цикл
		Стр = 0;
		Если ТЗОст.НайтиЗначение(ТЗО.ТМЦ, Стр, "ТМЦ") = 1 Тогда
			ТЗОст.ПолучитьСтрокуПоНомеру(Стр);
			Если ТЗОст.ОстатокТовара <> 0 Тогда
			    ТекЦенаСклада = ТЗОст.Стоимость / ТЗОст.ОстатокТовара;
			КонецЕсли;		    
		КонецЕсли;
		Таб.ВывестиСекцию("Отход");
	КонецЦикла;
	Таб.ВывестиСекцию("Всего");
	
	Таб.Опции(0,0,,);
	Таб.ПараметрыСтраницы(2);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("ПЕЧАТЬ: Рапорт","");
	глУстПропись(Гривня); 
КонецПроцедуры 

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Переработка мяса"));
	Форма.ЦенаБезНДС.Доступность(?(Коэф = 0, 1, 0));
	Возврат "";
КонецФункции
            
Процедура ВводНаОсновании(ДокОсн)
	глЗаполнитьШапкуНаОсн(Контекст,ДокОсн);
	ДокОснование = ДокОсн;
	//Категория = ДокОсн.Категория;
КонецПроцедуры

Процедура ЗаполнитьПоНорме()
	//Док = СоздатьОбъект("Документ.НормаПереработки");
	//Если Док.Выбрать("Выберите нормы для переботки", "Журнал.НормыЗатрат.ФормаСписка") = 0 Тогда
	//	Возврат;
	//КонецЕсли;
	Если НормаП.Выбран() = 0 Тогда
		КФормы = СоздатьОбъект("СписокЗначений");
		КФормы.Установить("ТМЦ", Категория);
		КФормы.Установить("ВидВыбора", 2);
		ОткрытьФормуМодально("Журнал.ПолныйЖурнал.ДляПросмотраНорм", КФормы);
		Если ТипЗначенияСтр(КФормы) = "Документ" Тогда
		    НормаП = КФормы;
			Если НормаП.Действует = 0 Тогда
			    Предупреждение("Данные нормы затрат не действуют");
			КонецЕсли;		
		Иначе
			Возврат;
		КонецЕсли;	    	
	КонецЕсли;
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТЗ);
	Если КоличествоСтрок() > 0 Тогда
		Если Вопрос("Удалить строки?", "Да+Нет") = "Да" Тогда
			УдалитьСтроки();
		КонецЕсли;
	КонецЕсли;
	
	ИтКво = 0;
	ДокОснование.ВыбратьСтроки();
	Пока ДокОснование.ПолучитьСтроку() = 1 Цикл
		ИтКво = ИтКво + ДокОснование.Кво * ДокОснование.Коэффициент;
	КонецЦикла;
	КвоН = НормаП.НаМассу;
    СписПоСрЦенам = СоздатьОбъект("СписокЗначений");
	НормаП.ВыбратьСтроки();
	Пока НормаП.ПолучитьСтроку() = 1 Цикл
		Если НормаП.БратьСЦены = Да Тогда
		    СписПоСрЦенам.ДобавитьЗначение(НормаП.ТМЦ);
		КонецЕсли;
	КонецЦикла;
	РегПарт = СоздатьОбъект("Регистр.Партии");
	Если СписПоСрЦенам.РазмерСписка() > 0 Тогда		
		РегПарт.УстановитьЗначениеФильтра("Фирма", Фирма, 1);
		РегПарт.УстановитьЗначениеФильтра("ТМЦ", СписПоСрЦенам, 2);
		
		РегПарт.ВременныйРасчет(1);
		РассчитатьРегистрыПо(ДатаДок-1);		    
	КонецЕсли;
	
	НормаП.ВыбратьСтроки();
	Пока НормаП.ПолучитьСтроку() = 1 Цикл
		НоваяСтрока();
		ТМЦ = НормаП.ТМЦ;
		ИзмТМЦ();
		Коэф = НормаП.Коэф;
		Отход = НормаП.Отход;
		Приходовать = НормаП.Приходовать;
		СкладП = НормаП.СкладП;
		Если НормаП.БратьСЦены = Да Тогда
			ОстКво = РегПарт.СводныйОстаток(Фирма, ТМЦ.Счет, , ТМЦ,,,,"ОстатокТовара");
			ОстСумма = РегПарт.СводныйОстаток(Фирма, ТМЦ.Счет, , ТМЦ,,,,"Стоимость");
			ЦенаБезНДС = ОстСумма / ?(ОстКво = 0, 1, ОстКво);
		Иначе
			ЦенаБезНДС = НормаП.ЦенаБезНДС;
		КонецЕсли;		
		ВесНорма = (ИтКво / ?(КвоН = 0, ИтКво, КвоН)) * НормаП.ВесНорма;
		Стр = 0;
		Если ТЗ.НайтиЗначение(ТМЦ, Стр, "ТМЦ") = 1 Тогда
		    ВесФакт = ТЗ.ПолучитьЗначение(Стр, "ВесФакт");
			ИзмВесФакт();
		КонецЕсли;
		ИзмЦенаБезНДС();
	КонецЦикла;
КонецПроцедуры

Процедура ПриНачалеВыбораЗначения(Идент, Флаг)
	Если Идент = "Категория" Тогда
		СпрК1 = СоздатьОбъект("Справочник.ВидыКатегорий");
		СпрК = СоздатьОбъект("Справочник.ВидыКатегорий");
		Спис = СоздатьОбъект("СписокЗначений");
		Если СпрК.НайтиПоНаименованию("Мясо", 0, 1) = 1 Тогда
			Если СпрК.ЭтоГруппа() = 1 Тогда
				СпрК1.ИспользоватьРодителя(СпрК.ТекущийЭлемент());
				СпрК1.ВыбратьЭлементы();
				Пока СпрК1.ПолучитьЭлемент() = 1 Цикл
					Спис.ДобавитьЗначение(СпрК1.ТекущийЭлемент());					
				КонецЦикла;
				
				Если Спис.ВыбратьЗначение(Категория,,,,2) = 1 Тогда
					Флаг = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Идент = "НормаП" Тогда
		Флаг = 0;
		КФормы = СоздатьОбъект("СписокЗначений");
		КФормы.Установить("ТМЦ", Категория);
		КФормы.Установить("ВидВыбора", 2);
		ОткрытьФормуМодально("Журнал.ПолныйЖурнал.ДляПросмотраНорм", КФормы);
		Если ТипЗначенияСтр(КФормы) = "Документ" Тогда
		    НормаП = КФормы;
			Если НормаП.Действует = 0 Тогда
			    Предупреждение("Данные нормы затрат не действуют");
			КонецЕсли;		
		Иначе
			Возврат;
		КонецЕсли;		
	КонецЕсли;
КонецПроцедуры

Процедура ОткрытьНормы()
	КФормы = СоздатьОбъект("СписокЗначений");
	КФормы.Установить("ТМЦ", Категория);
	КФормы.Установить("ВидВыбора", 2);
	КФормы.Установить("БезВыбора", 1);
	ОткрытьФорму("Журнал.ПолныйЖурнал.ДляПросмотраНорм", КФормы);	
КонецПроцедуры

Процедура ИзмМестоХранения()
	Если КоличествоСтрок() > 0 Тогда
		Если Вопрос("Пересчитать процент списания в строках?", "Да+Нет") = "Да" Тогда
		    ВыбратьСтроки();
			Пока ПолучитьСтроку() = 1 Цикл
				ПроцСписания = глПолучитьПроцентСписания(ТМЦ, МестоХранения, ДатаДок);
				глИзмПроцСписания(Контекст, 1);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция Раскраска()
	Возврат  "(BRUSH_S[" + Строка(100*255*100) + "])";	
КонецФункции


//====================================================================== //--- УМК Сандомирский В.Ю, (13.05.14)
Функция ВернутьЦенуСписания()
	Если ПустоеЗначение(ДокОснование) = 0 Тогда		
		ДокОснование.ВыбратьСтроки();
		Если ДокОснование.ПолучитьСтроку() = 1 Тогда  //--- Получаем первую строку
			Возврат "Цена из списания :  " + глФРМ(ДокОснование.ЦенаБезНДС,Гривня,1);
		КонецЕсли;	    
	КонецЕсли;	
КонецФункции //... УМК Сандомирский В.Ю, (13.05.14)


Сервис = СоздатьОбъект("Сервис");
Сервис.ВключитьРаскраскуТаблиц();
Сервис.ИспользоватьПланРаскраски(1);

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале,
	|Подчиненные");
