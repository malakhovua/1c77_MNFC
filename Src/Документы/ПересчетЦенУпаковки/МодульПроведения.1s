//======================================================================
Процедура ДобавитьСтрокуТаблицы(СпрТМЦ, СпрРВУ, СпрНормы, ТЗУст, ТМат, СписМатериалов)
	ТЗУст.НоваяСтрока();
	ТЗУст.Продукция = СпрТМЦ.ТекущийЭлемент();
	ТЗУст.ВидУпаковки = СпрРВУ.ТекущийЭлемент();
	ТЗУст.ТМЦ = СпрНормы.Материал.Получить(ДатаДок);
	ТЗУст.Норма = СпрНормы.ТекущийЭлемент();
	ТЗУст.ЦенаС = СпрНормы.Цена.Получить(ДатаДок);
	Если СпрНормы.Вид() = "УМК_НормыСписанияМатериаловУпаковокФорм" Тогда
		ТЗУст.Количество = СпрНормы.НормаСписания.Получить(ДатаДок) / СпрНормы.Владелец.Кратность / СпрРВУ.ВесУпаковки.Получить(ДатаДок);
		ТЗУст.СуммаС = ТЗУст.Количество * ТЗУст.ЦенаС;
	Иначе
		ТЗУст.СуммаС = СпрНормы.Сумма.Получить(ДатаДок);
		ТЗУст.Количество = СпрНормы.НормаСписания.Получить(ДатаДок);		
	КонецЕсли;
	Стр = 0;
	Если СписМатериалов.НайтиЗначение(ТЗУст.ТМЦ) = 0 Тогда
		СписМатериалов.ДобавитьЗначение(ТЗУст.ТМЦ);
		ТМат.НоваяСтрока();
		ТМат.ТМЦ = ТЗУст.ТМЦ;
	КонецЕсли;	
КонецПроцедуры // ДобавитьСтрокуТаблицы

Процедура ОбработкаПроведения()
	РегП = СоздатьОбъект("Регистр.Партии");
	ТЗУст = СоздатьОбъект("ТаблицаЗначений");
	ТЗУст.НоваяКолонка("Продукция", "Справочник.ТМЦ");
	ТЗУст.НоваяКолонка("ВидУпаковки", "Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ");
	ТЗУст.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗУст.НоваяКолонка("Норма", "Справочник");
	ТЗУст.НоваяКолонка("ЦенаС", "Число", 12, 2);
	ТЗУст.НоваяКолонка("Цена", "Число", 12, 2);
	ТЗУст.НоваяКолонка("Количество", "Число", 15, 5);
	ТЗУст.НоваяКолонка("Сумма", "Число", 12, 2);
	ТЗУст.НоваяКолонка("СуммаС", "Число", 12, 2);

	СписФ = СоздатьОбъект("СписокЗначений");
    СписФ.ИзСтрокиСРазделителями(СокрЛП(СтрФильтр));
	СпрТМЦ = СоздатьОбъект("Справочник.ТМЦ");
	СписТовар = СоздатьОбъект("СписокЗначений");
	
	Для Инд = 1 По СписФ.РазмерСписка() Цикл
		Если СпрТМЦ.НайтиПоКоду(СписФ.ПолучитьЗначение(Инд), 0) = 1 Тогда
		    списТовар.ДобавитьЗначение(СпрТМЦ.ТекущийЭлемент());
		КонецЕсли;		
	КонецЦикла;	

	СпрРВУ = СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ");	
	СпрНормы = СоздатьОбъект("Справочник.УМК_НормыСписанияМатериаловУпаковок");
	СпрНормыФорм = СоздатьОбъект("Справочник.УМК_НормыСписанияМатериаловУпаковокФорм");
	СписМатериалов = СоздатьОбъект("СписокЗначений");
	ТМат = СоздатьОбъект("ТаблицаЗначений");
	ТМат.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТМат.НоваяКолонка("Цена", "Число", 12, 2);
	
	СпрТМЦ.ВыбратьЭлементыПоРеквизиту("ВидТМЦ", Перечисление.ВидыТМЦ.Продукция, 0, 0);
	Пока СпрТМЦ.ПолучитьЭлемент() = 1 Цикл
		Если СписТовар.Принадлежит(СпрТМЦ.ТекущийЭлемент()) = 0 Тогда
			Продолжить;
		КонецЕсли;

		СпрРВУ.ИспользоватьВладельца(СпрТМЦ.ТекущийЭлемент());
		СпрРВУ.ВыбратьЭлементы();
		Пока СпрРВУ.ПолучитьЭлемент() = 1 Цикл
			Если СпрРВУ.ПометкаУдаления() = 0 Тогда
				СпрНормы.ИспользоватьВладельца(СпрРВУ.ТекущийЭлемент());
				СпрНормы.ВыбратьЭлементы();
				Пока СпрНормы.ПолучитьЭлемент() = 1 Цикл
					Если СпрНормы.ПометкаУдаления() = 0 Тогда
						ДобавитьСтрокуТаблицы(СпрТМЦ, СпрРВУ, СпрНормы, ТЗУст, ТМат, СписМатериалов);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если ПустоеЗначение(СпрРВУ.ФормаУпаковки.Получить(ДатаДок)) = 0 Тогда
				СпрНормыФорм.ИспользоватьВладельца(СпрРВУ.ФормаУпаковки.Получить(ДатаДок));
				СпрНормыФорм.ВыбратьЭлементы();
				Пока СпрНормыФорм.ПолучитьЭлемент() = 1 Цикл
					Если СпрНормыФорм.ПометкаУдаления() = 0 Тогда
						ДобавитьСтрокуТаблицы(СпрТМЦ, СпрРВУ, СпрНормыФорм, ТЗУст, ТМат, СписМатериалов);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если МетодРасчета = 1 Тогда
		РегП.УстановитьЗначениеФильтра("ТМЦ", СписМатериалов, 2);
		
		Если СравнитьТА() = -1 Тогда			
			РегП.ВременныйРасчет(1);
			РассчитатьРегистрыНа(ТекущийДокумент());
		КонецЕсли;

		ТИтоги = СоздатьОбъект("ТаблицаЗначений");		
		РегП.ВыгрузитьИтоги(ТИтоги, 1);
	КонецЕсли;
	
	КЗакуп = Константа.ЗакупЦена;
	ТМат.ВыбратьСтроки();
	Пока ТМат.ПолучитьСтроку() = 1 Цикл
		Если МетодРасчета = 1 Тогда
			Стр = 0;
			Если ТИтоги.НайтиЗначение(ТМат.ТМЦ, Стр, "ТМЦ") = 1 Тогда
				ТИтоги.ПолучитьСтрокуПоНомеру(Стр);
				Если ТИтоги.ОстатокТовара <> 0 Тогда
					ТМат.Цена = ТИтоги.Стоимость / ТИтоги.ОстатокТовара;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ТМат.Цена = 0 Тогда
			ТМат.Цена = глВернутьЦену(ТМат.ТМЦ, КЗакуп, ДатаДок, Гривня);			
		КонецЕсли;
	КонецЦикла;

	ТЗУст.Сортировать("ТМЦ");
	СтарТМЦ = ""; ТекЦена = 0;
	ТЗУст.ВыбратьСтроки();
	Пока ТЗУст.ПолучитьСтроку() = 1 Цикл		
		Если ТЗУст.ТМЦ <> СтарТМЦ Тогда
			Стр = 0;
			ТМат.НайтиЗначение(ТЗУст.ТМЦ, Стр, "ТМЦ");
			ТекЦена = ТМат.ПолучитьЗначение(Стр, "Цена");
			СтарТМЦ = ТЗУст.ТМЦ;
		КонецЕсли;

		Если ТЗУст.ЦенаС <> ТекЦена Тогда
			УстановитьРеквизитСправочника(ТЗУст.Норма, "Цена", ТекЦена, ДатаДок);
		КонецЕсли;
		ТЗУст.Сумма = Окр(ТекЦена * ТЗУст.Количество, 2);
		
		Если ТЗУст.Норма.Вид() = "УМК_НормыСписанияМатериаловУпаковок" Тогда
			Если ТЗУст.СуммаС <> ТЗУст.Сумма Тогда
				УстановитьРеквизитСправочника(ТЗУст.Норма, "Сумма", ТЗУст.Сумма, ДатаДок);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТЗУст.Свернуть("ВидУпаковки,Продукция", "Сумма");
	ТЗУст.ВыбратьСтроки();
	Пока ТЗУст.ПолучитьСтроку() = 1 Цикл
		Если ТЗУст.ВидУпаковки.Себестоимость.Получить(ДатаДок) <> ТЗУст.Сумма Тогда
			УстановитьРеквизитСправочника(ТЗУст.ВидУпаковки, "Себестоимость", ТЗУст.Сумма, ДатаДок);
		КонецЕсли;		
	КонецЦикла;	
КонецПроцедуры
