Перем СуммаУч, СуммаУчВ, Валюта;    
	
// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(НеоборотныйАктив,"Необоротный актив");
	глВыбранЛи(Состояние,"Состояние");
	глВыбранЛи(Счет,"Счет учета");
	глВыбранЛи(Кво,"Количество");
	глВыбранЛи(Подразделение,"Подразделение");
	глВыбранЛи(МестоХранения,"Место хранения");
	глВыбранЛи(НовыйСчет,"Новый счет учета");
	Если Состояние <> Перечисление.СостоянияНеоборАктивов.ВОперативнойАренде Тогда
	    глВыбранЛи(НовоеПодразделение,"Новое подразделение");
	КонецЕсли;
	глВыбранЛи(НовоеМестоХранения,"Новое место хранения");
	глВыбранЛи(НовыйМетодРасчетаИзноса,"Новый метод расчета износа");
	
	фОшибкаСчет = 0;
	фОшибкаНовыйСчет = 0;
	фПредупреждениеНовыйСчет = 0;
	Если НеоборотныйАктив.ВидНеоборотногоАктива = НМА Тогда
		Если (Сред(Счет.Код,1,2) <> "12") Тогда
			фОшибкаСчет = 1;
		КонецЕсли;
		Если (Сред(НовыйСчет.Код,1,2) <> "12") Тогда
		    фОшибкаНовыйСчет = 1;
		КонецЕсли;
	ИначеЕсли НеоборотныйАктив.ВидНеоборотногоАктива = ОсновныеСредства Тогда
		Если Сред(Счет.Код,1,2) <> "10" Тогда
			фОшибкаСчет = 1;
		КонецЕсли;
		Если (Сред(НовыйСчет.Код,1,2) <> "10") и (Сред(НовыйСчет.Код,1,2) <> "11") Тогда
			фОшибкаНовыйСчет = 1;
		ИначеЕсли (Сред(НовыйСчет.Код,1,2) = "11") Тогда
			фПредупреждениеНовыйСчет = 1;
		КонецЕсли;
	ИначеЕсли НеоборотныйАктив.ВидНеоборотногоАктива = Перечисление.ВидыНеоборотныхАктивов.ДругиеНеоборотныеМатериальныеАктивы Тогда
		Если Сред(Счет.Код,1,2) <> "11" Тогда
			фОшибкаСчет = 1;
		КонецЕсли;
		Если (Сред(НовыйСчет.Код,1,2) <> "10") и (Сред(НовыйСчет.Код,1,2) <> "11") Тогда
			фОшибкаНовыйСчет = 1;
		ИначеЕсли (Сред(НовыйСчет.Код,1,2) = "10") Тогда
			фПредупреждениеНовыйСчет = 1;
		КонецЕсли;
	КонецЕсли;
	Если фОшибкаСчет = 1 Тогда
		глВсеВыбрано = 0;
	    глКомментарий("Недопустимый Счет учета для необоротного актива с видом "+НеоборотныйАктив.ВидНеоборотногоАктива+" .",0);
	КонецЕсли;
	Если фОшибкаНовыйСчет = 1 Тогда
		глВсеВыбрано = 0;
	    глКомментарий("Недопустимый Новый счет учета для необоротного актива с видом "+НеоборотныйАктив.ВидНеоборотногоАктива+" .",0);
	КонецЕсли;
	Если фПредупреждениеНовыйСчет = 1 Тогда
	    глКомментарий("Внимание! Новый счет учета не соответствует виду необоротного актива ("+НеоборотныйАктив.ВидНеоборотногоАктива+").",1);
		глКомментарий("Вид необоротного актива необходимо изменить вручную !",1);
	КонецЕсли;
	
	//проверка счета затрат
	Если глПроверитьСчетЗатрат(НовыйСчетЗатрат,2)=0 Тогда
		глКомментарий("Недопустимый Новый счет затрат! ",0);
	    глВсеВыбрано = 0;
	КонецЕсли;
	
	Док = СоздатьОбъект("Документ.ИзменениеПараметровНеоборАктивов");
	Док.ВыбратьДокументы(ДатаДок,ДатаДок);
	Пока Док.ПолучитьДокумент()=1 Цикл
		Если (Док.НеоборотныйАктив = НеоборотныйАктив) и (Док.Проведен()=1)
		 и (Док.ТекущийДокумент() <> ТекущийДокумент()) Тогда
		 	глВсеВыбрано = 0;
		    глКомментарий("Уже существует проведенный документ "+Док.ТекущийДокумент()+" для необоротного актива "+НеоборотныйАктив+" .",0);
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат глВсеВыбрано;
КонецФункции
        
// ===============================
Функция РассчитатьШапку()
	
	БИ = СоздатьОбъект("БухгалтерскиеИтоги");
	БИ.ИспользоватьРазделительУчета(Фирма);
	БИ.ИспользоватьСубконто(ВидыСубконто.Подразделения,Подразделение,2);
	БИ.ИспользоватьСубконто(ВидыСубконто.МестаХранения,МестоХранения,2);
	БИ.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,НеоборотныйАктив,2);
	        
	ОстСум = 0;
	ОстКво = 0;
	
	Валюта = "";
	ОстСумВ = 0;
	
	БИ.ВыполнитьЗапрос(,ТекущийДокумент(),Счет,,,,,"СК");
	БИ.ВыбратьСубконто(3);
	Если БИ.ПолучитьСубконто(3)=1 Тогда
		ОстСум = БИ.СКД("С");
		ОстКво = БИ.СКД("К");
		БИ.ВыбратьВалюты();
		Пока БИ.ПолучитьВалюту() = 1 Цикл
			Валюта = БИ.Валюта;
			ОстСумВ = БИ.СКД("В");
		КонецЦикла;
	КонецЕсли;
	
	Если Кво > ОстКво Тогда
		глКомментарий("В подразделении "+ Подразделение +" нет нужного количества НА " + НеоборотныйАктив +" ! Необходимо "+Кво+", в наличии "+ОстКво,0,,"!");
		Возврат 0;
	КонецЕсли;
	          
	Коэф = ОстСум/ОстКво;
	КоэфВ = ОстСумВ/ОстКво;
	СуммаУч = Коэф*Кво;
	СуммаУчВ = КоэфВ*Кво;
	Возврат 1;
КонецФункции //

// ===============================
Процедура ПроводкиШапка()
	глПроводка(Контекст,НовыйСчет,Счет,СуммаУч,"Перемещение необоротного актива",Кво,НовоеПодразделение,НовоеМестоХранения,НеоборотныйАктив,
	Подразделение,МестоХранения,НеоборотныйАктив,Валюта,СуммаУчВ,"НА");
КонецПроцедуры //

// ===============================
Процедура ЗаписатьИзмененияПараметров();
	Если НовыйСчет <> НеоборотныйАктив.Счет.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"Счет",НовыйСчет);
	КонецЕсли;
	Если Состояние <> НеоборотныйАктив.Состояние.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"Состояние",Состояние);
	КонецЕсли;
	Если НовыйМетодРасчетаИзноса <> НеоборотныйАктив.МетодРасчетаИзноса.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"МетодРасчетаИзноса",НовыйМетодРасчетаИзноса);
	КонецЕсли;
	Если НоваяЛиквидационнаяСтоимость <> НеоборотныйАктив.ЛиквидационнаяСтоимость.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"ЛиквидационнаяСтоимость",НоваяЛиквидационнаяСтоимость);
	КонецЕсли;
	Если НовыйСрокИспользования <> НеоборотныйАктив.СрокИспользования.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"СрокИспользования",НовыйСрокИспользования);
	КонецЕсли;
	Если НовыйРасчетныйОбъемПроизводства <> НеоборотныйАктив.РасчетныйОбъемПроизводства.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"РасчетныйОбъемПроизводства",НовыйРасчетныйОбъемПроизводства);
	КонецЕсли;
	Если НовыйСчетЗатрат <> НеоборотныйАктив.СчетЗатрат.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"СчетЗатрат",НовыйСчетЗатрат);
	КонецЕсли;
	Если НовыйВидДеятельности <> НеоборотныйАктив.ВидДеятельности.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"ВидДеятельности",НовыйВидДеятельности);
	КонецЕсли;
	Если НовыйВидЗатрат <> НеоборотныйАктив.ВидЗатрат.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"ВидЗатрат",НовыйВидЗатрат);
	КонецЕсли;
	Если НоваяПродукция <> НеоборотныйАктив.Продукция.Получить(ДатаДок-1) Тогда
	    УстановитьРеквизитСправочника(НеоборотныйАктив,"Продукция",НоваяПродукция);
	КонецЕсли;
КонецПроцедуры //

// ===============================
Процедура ОбработкаПроведения()
	
	Если ПроверкаШапки()=0 Тогда
		глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	Если (НовыйСчет <> Счет) или (НовоеПодразделение <> Подразделение) или (НовоеМестоХранения <> МестоХранения) Тогда
		
		Если РассчитатьШапку() = 0 Тогда
			глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
		ПроводкиШапка();
		
		Операция.СуммаОперации = 0;
		Операция.Содержание = Примечание;
		Операция.Записать();
		
	КонецЕсли;
	
	ЗаписатьИзмененияПараметров();
	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры

