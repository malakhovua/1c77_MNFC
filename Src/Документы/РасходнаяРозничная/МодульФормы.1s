// ===============================
// Описание переменных 
Перем СписокДействий;  // Список действий по документу
Перем СтарыйКонтрагент;
Перем СтараяДата;

Перем ВариантыЗаполнения;
Перем НачальнаяДатаДокумента; // Для контроля даты документа
Перем Остатки, Резервы, ОбщРег;
// ===============================
// "служебные" функции и процедуры 

// ===============================
Процедура Взаиморасчеты()
	глПоказатьДолг(Контекст, Контрагент, "Продажа");
КонецПроцедуры

// ===============================
Функция БазыИСуммыТекстом(Ставка,База,НДС)
	Текст = ""+Ставка+". База: "+Формат(База,"Ч12.2")+". Сумма НДС: "+Формат(НДС,"Ч12.2")+".";
	Возврат Текст;
КонецФункции

// ===============================
Процедура ЗаголовокФормы() 
	Перем Заголовок;
	
	Заголовок = "Расходная розничная";
	Форма.Заголовок(глЗаголовок(Контекст,Заголовок));     
КонецПроцедуры //ЗаголовокФормы

Процедура Заполнить() Далее

// ===============================
Процедура Подбор()
	Варианы= СоздатьОбъект("СписокЗначений");
	Варианы.ДобавитьЗначение("&остатки по документу-основанию");
	Варианы.ДобавитьЗначение("&каталог");
	Варианы.ДобавитьЗначение("&прайс");
	Варианы.ДобавитьЗначение("&штрих-код");
	Результат = 0;
	Если Варианы.ВыбратьЗначение(,,Результат,,1)=1 Тогда
		Если Результат = 1 Тогда
			Заполнить();
	    ИначеЕсли Результат = 2 Тогда
			глПодбор(Контекст,"ТМЦ","ДляПодбора")
		ИначеЕсли Результат = 3 Тогда
			глПодбор(Контекст,"прайс_лист","ДляПодбора")
		ИначеЕсли Результат = 4 Тогда
			ОткрытьПодбор("Обработка.ПодборПоШтрихКоду");
	    КонецЕсли;
	КонецЕсли;	
КонецПроцедуры
 
// ===============================
Процедура УстДоступностьКнопок()
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Если РежимПроведения = 0 Тогда
			Форма.кПровести.Доступность(0);
		КонецЕсли;
		Форма.кДействия.Доступность(0);
		
		Форма.кПредварительно.Доступность(0);
		
		Форма.кПодбор.Доступность(0);
		
		Форма.кФирма.Доступность(0);               		
		Форма.кОсн.Доступность(0);
		Форма.кВалюта.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
КонецПроцедуры

// ===============================
Процедура ПровестиДокумент()
	глПроверкаДатыДок(Контекст,"Проведение");
	Если СтатусВозврата()=0 Тогда
	    Возврат;
	КонецЕсли;
	Провести();
КонецПроцедуры                      

// ===============================
Процедура ЧастичноПровести()
	фМожноПроводить = 1;
	Если Проведен()=1 Тогда
		Если РежимПроведения=0 Тогда
			фМожноПроводить = 0;
			Предупреждение("Документ "+ПредставлениеВида()+" № "+СокрЛП(НомерДок)+" от "+Формат(ДатаДок,"ДДММГГГГ")+
			               " полностью проведен. Предварительное проведение невозможно.");
		КонецЕсли;
	ИначеЕсли ДатаДок<ПолучитьДатуТА() Тогда
		фМожноПроводить = 0;
		Предупреждение("Предварительное проведение задним числом невозможно.");
	ИначеЕсли ДатаДок<>РабочаяДата() Тогда
		фМожноПроводить = 0;
		Предупреждение("Дата документа "+ПредставлениеВида()+" № "+СокрЛП(НомерДок)+" от "+Формат(ДатаДок,"ДДММГГГГ")+
		               " отличается от рабочей даты ("+Формат(РабочаяДата(),"ДДММГГГГ")+"). Предварительное проведение невозможно.");
	КонецЕсли;
				
	Если фМожноПроводить = 1 Тогда
		Провести(3,1);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПересчетРегистров()
	ОбщРег=СоздатьОбъект("Регистры");
	Остатки=ОбщРег.Остатки;
	Резервы=ОбщРег.Резервы;
КонецПроцедуры        
    
// ===============================
Функция УстДоступность()
	Закл = Форма.Закладки.ПолучитьЗначение(Форма.Закладки.ТекущаяСтрока());
	
	ЗаголовокФормы();
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		ДостНал = 1;
	Иначе
		ДостНал = 0;
	КонецЕсли;
	            
	Если ПустоеЗначение(МестоХранения) = 0 Тогда
		Если (МестоХранения.ВидСклада = Розничный) И (МестоХранения.СуммовойУчет=1) Тогда
			Форма.Кво.Видимость(0);
			Форма.ЦенаСНДС.Видимость(0);
		Иначе
			Форма.Кво.Видимость(1);
			Форма.ЦенаСНДС.Видимость(1);
		КонецЕсли;
	Иначе
		Форма.Кво.Видимость(1);
		Форма.ЦенаСНДС.Видимость(1);
	КонецЕсли;	
	
	Форма.ЭККА.Видимость(ДостНал);
	Форма.тЭККА.Видимость(ДостНал);
	Форма.Касса.Видимость(ДостНал);
	Форма.тКассы.Видимость(ДостНал);
	Форма.Получено.Видимость(ДостНал);
	Форма.тПолучено.Видимость(ДостНал);
	Форма.Сдача.Видимость(ДостНал);
	Форма.тСдача.Видимость(ДостНал);
	Форма.тЧекПробит.Видимость(ДостНал);
	Возврат "";
КонецФункции

// ===============================
Процедура УстКассу()
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		Касса = глПолучитьКассу(Фирма,Валюта);
	КонецЕсли;                                	
КонецПроцедуры	  
                       
// ===============================
Процедура ИзмВидТорговли()
	ВидТорговли = спВидТорговли.ПолучитьЗначение(спВидТорговли.ТекущаяСтрока());
	УстКассу();
КонецПроцедуры

// ===============================
Процедура ЗаполнитьСпВидТорговли()
	спВидТорговли.ДобавитьЗначение(Перечисление.ВидыТорговли.Нал,Строка(Перечисление.ВидыТорговли.Нал));
	спВидТорговли.ДобавитьЗначение(Перечисление.ВидыТорговли.Предоплата,Строка(Перечисление.ВидыТорговли.Предоплата));
КонецПроцедуры

// ===============================
Процедура ВыбранВидТорговли()   
	спВидТорговли.ТекущаяСтрока(спВидТорговли.НайтиЗначение(ВидТорговли));
КонецПроцедуры

// ===============================
Процедура ВыборДаты()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
	ПересчетРегистров();
КонецПроцедуры           

// ===============================
Процедура Сдача()
	ВыданаСдача=Получено-(Итог("СуммаСНДС")-Скидка);
	ВыданаСдача=?(ВыданаСдача<0,0,ВыданаСдача);
КонецПроцедуры

// ======================================
Процедура ИзмВидНДС()
	// пересчитаем суммы НДС в строках табличной части
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаБезНДС Тогда
			глВыч_Суммы_Накл(Контекст,-1);
		Иначе
			глВыч_Суммы_Накл(Контекст,1);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ===============================
Процедура ИзмТМЦ()
	Если (ТМЦ.Выбран() = 1) И (МестоХранения.ВидСклада = Розничный) И (МестоХранения.СуммовойУчет=1) Тогда
		Если ТМЦ <> Константа.ТоварыВАссортименте Тогда
			глКомментарий("ТМЦ "+ТМЦ+" нельзя продавать с розничного склада с признаком ведения суммового учета.",0);
			ТМЦ = 0;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	глПриИзмененииТовара(Контекст);
	Сдача();
КонецПроцедуры	

// ===============================
// Назначение: вызывается при изменении суммы скидки
//		
// Аргументы:
//		
Процедура ИзмСкидка()
	ИтогПоДокументу = Итог("СуммаСНДС");
	Если Скидка > ИтогПоДокументу Тогда
		// скидка не может быть больше, чем итог по документу 
		Скидка = ИтогПоДокументу;	    
	КонецЕсли;
	Сдача();
КонецПроцедуры //ИзмСкидка

// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования валюты в документе
	ОткрытьФормуМодально("Обработка.ИнформацияОВалюте", Контекст);
КонецПроцедуры	

// ===============================
Процедура ИзмМестоХранения()
	// Инициализируем список действий по кнопке "Действия"
	СписокДействий = глПолучитьСписокДействий(
		?((МестоХранения.ВидСклада = Розничный) И (МестоХранения.СуммовойУчет=1),"","ТоварныйСостав,") + "
		|ОбновлениеЦен,
		|СтруктураПодчиненности,
		|ДвиженияДокумента,
		|ВводНаОсновании,
		|ОткрытьВЖурнале,
		|Подчиненные");
КонецПроцедуры //ИзмМестоХранения()

// ===============================
Процедура ИзмКонтрагент()	// Процедура при выборе Контрагента в докумнете
    Если Контрагент.Выбран()=1 Тогда                                      
		Если СтарыйКонтрагент <> Контрагент Тогда
			// изменили Контрагента
			Если (глВосстановитьЗначение(,"ПодставлятьОсновнойДоговор") = Да)
			И (ПустоеЗначение(Контрагент.БазДоговор)=0) 
			И (ПустоеЗначение(Договор)=1) Тогда
				Если Фирма = Контрагент.БазДоговор.Фирма Тогда
					// подставим договор по умолчанию
					Договор = Контрагент.БазДоговор;    
				КонецЕсли;	
			Иначе
				// очистим договор
				Договор = ПолучитьПустоеЗначение("Документ");
			КонецЕсли;
			ДокументОснование = ПолучитьПустоеЗначение("Документ");			
			// установим флаг конечного потребителя
			Если (Контрагент.ВидКонтрагента = Перечисление.ВидыКонтрагентов.ЧастноеЛицо)
			И (ПустоеЗначение(Контрагент.ИНН) = 1) Тогда
				КонечныйПотребитель = 1;
			Иначе	
				КонечныйПотребитель = 0;
			КонецЕсли;			
		КонецЕсли;
	Иначе
		//Не выбран Контрагент - не имеет смысла показывать договор и документ-основание
		Договор = ПолучитьПустоеЗначение("Документ");
		ДокументОснование = ПолучитьПустоеЗначение("Документ");			
	КонецЕсли;
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры	  

// ===============================
Процедура ЗаполнениеШапкиНаОсновании(ДокОснование)
	Если ПустоеЗначение(ДокОснование)=1 Тогда
		Возврат;
	КонецЕсли;
	Если глЕстьРеквизитШапки("Валюта",ДокОснование.Вид()) = Да Тогда
		Если ПустоеЗначение(Валюта) = 1 Тогда
			Валюта=ДокОснование.Валюта;
			Если глЕстьРеквизитШапки("Дата_Курса",ДокОснование.Вид()) = Да Тогда
				Дата_Курса = ДокОснование.Дата_Курса;
			Иначе
				Дата_Курса = ДокОснование.ДатаДок;
			КонецЕсли;
			Курс = ДокОснование.Курс;
		КонецЕсли;	
	Иначе
		Валюта = Гривня;
		Дата_Курса=ДатаДок;
		Курс = глКурсДляВалюты(Валюта,ДатаДок);
	КонецЕсли;	
	Если глЕстьРеквизитШапки("Подразделение",ДокОснование.Вид()) = Да Тогда
		Подразделение = ДокОснование.Подразделение;
	КонецЕсли;
	Если глЕстьРеквизитШапки("ПодразделениеПроизв",ДокОснование.Вид()) = Да Тогда
		ПодразделениеПроизв = ДокОснование.ПодразделениеПроизв;
	КонецЕсли;
	глЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
	УстКассу();
	ВыбранВидТорговли();
КонецПроцедуры

// ===============================
Процедура Заполнить()
	Если ПустоеЗначение(ДокументОснование)=1 Тогда
	    Возврат;
	КонецЕсли;
	
	Если ДокументОснование.Вид()= "Счет" Тогда
		Если ДокументОснование.Проведен()=1 Тогда
			// Если вводим на основании Счета, то спецификацию записываем
			// исходя из остатков зарезервированных ТМЦ
			ВремРегистры=СоздатьОбъект("Регистры");
			Если Выбран()>0 Тогда // документ не новый, а существующий
				Если (СравнитьТА()<0)  Тогда
			 	// если итоги не актуальны, то Долги по Кредиту берем из временногно расчета Регистра
					ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());				
				КонецЕсли;
			Иначе // документ новый
				Дат=ПолучитьДатуТА();
			 	Если Дат>ДатаДок Тогда
			 	// если итоги не актуальны, то остатки квоты берем из временногно расчета Регистра
					ВремРегистры.РассчитатьРегистрыНа(ДатаДок+1);
				КонецЕсли;
			КонецЕсли;      
			РегРезерв=ВремРегистры.Резервы;
			// Нужно вычислить скидку 
			Скидка = 0;
			СписокТоваров=СоздатьОбъект("СписокЗначений");
			ДокументОснование.ВыбратьСтроки();
			Пока ДокументОснование.ПолучитьСтроку()=1 Цикл
				Если ДокументОснование.ТМЦ.Выбран()=0 Тогда
					Продолжить;
				КонецЕсли;
				текСуммаСкидки = ДокументОснование.Скидка;
				Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаБезНДС Тогда
					текСуммаСкидки = текСуммаСкидки * (100 + глПроцентНДС(ДокументОснование.ТМЦ.СтавкаНДС,ДокументОснование.ДатаДок))/100; 
				КонецЕсли;
				Скидка = Скидка + текСуммаСкидки;
				Если ДокументОснование.ТМЦ.ВидТМЦ=Перечисление.ВидыТМЦ.Услуга Тогда
					// услуги
					НоваяСтрока();
					ТМЦ			= ДокументОснование.ТМЦ;
					ЦенаСНДС	= ДокументОснование.ЦенаСНДС;
					Кво			= ДокументОснование.Кво;
					Ед			= ДокументОснование.Ед;
					Коэффициент	= ДокументОснование.Коэффициент;
					глВыч_суммы_накл(Контекст,2);
					Продолжить;
				Иначе
					Если СписокТоваров.НайтиЗначение(ДокументОснование.ТМЦ)=0 Тогда
						// не вносим строку с ТМЦ, если такую уже внесли
						// таким образом отсекаем возможное
						// дублирование строк по ТМЦ в Счете
						РезервПоСчету=РегРезерв.Остаток(Фирма,ДокументОснование.ТМЦ,ДокументОснование,"Резерв");
						Если РезервПоСчету>0 Тогда
							НоваяСтрока();
							ТМЦ			= ДокументОснование.ТМЦ;
							ЦенаСНДС	= ДокументОснование.ЦенаСНДС;
							Коэффициент = ?(ДокументОснование.Коэффициент=0,1,ДокументОснование.Коэффициент);
							Кво			= РезервПоСчету/Коэффициент;
							Ед			= ДокументОснование.Ед;
							Коэффициент = Коэффициент;
							Набор		= ДокументОснование.Набор;
							глВыч_суммы_накл(Контекст,2);
							СписокТоваров.ДобавитьЗначение(ТМЦ);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			РегРезерв=0;
		КонецЕсли;
	ИначеЕсли ДокументОснование.Вид()= "РасходнаяРозничная" Тогда
		глСкопироватьТовСостав(Контекст,ДокументОснование);
	ИначеЕсли ДокументОснование.Вид()= "ВыпускПродукции" Тогда
		тбВыпускПродукции = СоздатьОбъект("ТаблицаЗначений");
		ДокументОснование.ВыгрузитьТабличнуюЧасть(тбВыпускПродукции);
		// заполним реквизиты шапки, которые еще не заполнены
		ДокументОснование = ПолучитьПустоеЗначение("Документ");
		Контрагент=глВосстановитьЗначение(,"БазЧастноеЛицо");
		ИзмКонтрагент(); 	
		// попробуем определить Договор/Заказ
	    тбВыпускПродукции.ВыбратьСтроки();
		Пока тбВыпускПродукции.ПолучитьСтроку() = 1 Цикл
			Если Договор <> тбВыпускПродукции.Заказ Тогда
				Если тбВыпускПродукции.НомерСтроки <> 1 Тогда
					// в табличной части несколько заказов, договор не заполняем
					Договор = ПолучитьПустоеЗначение("Документ");
					Прервать;
				Иначе
					Договор = тбВыпускПродукции.Заказ;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;		
		Если ПустоеЗначение(Договор) = 0 Тогда
			// можно определить контрагента
			Контрагент = Договор.Контрагент;
			ИзмКонтрагент();
		КонецЕсли;
		тбВыпускПродукции.Свернуть("Продукция,Ед,Коэффициент","Кво");
		// заполним табличную часть
	    тбВыпускПродукции.ВыбратьСтроки();
		Пока тбВыпускПродукции.ПолучитьСтроку() = 1 Цикл
			глДобавитьТоварВДокумент(Контекст,тбВыпускПродукции.Продукция,тбВыпускПродукции.Кво,);
			Ед 			= тбВыпускПродукции.Ед;
			глИзмЕдин(Контекст);
		КонецЦикла;		
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	СтарДоговор = Договор;
	СтарОснование = ДокументОснование;
	
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если (ДокументОснование <> СтарОснование)
		И (ДокументОснование.Вид() <> "РасходыНаПриобретение") Тогда
			УдалитьСтроки();
			ЗаполнениеШапкиНаОсновании(ДокументОснование);
			Заполнить();
		КонецЕсли;	
	КонецЕсли;	          
	Если Договор <> СтарДоговор Тогда
		ЗаполнениеШапкиНаОсновании(Договор);
	КонецЕсли;	
КонецПроцедуры	

// ===============================
Процедура ВыборПредпочтения()
	спОтбор = СоздатьОбъект("СписокЗначений"); 
	спСчета=СоздатьОбъект("СписокЗначений");
	Если МестоХранения.ВидСклада = Перечисление.ВидыСкладов.Оптовый Тогда
		спСчета.ДобавитьЗначение(ТМЦ.Счет);
		Если глПартионныйУчетПоСкладам = Да Тогда
		    спОтбор.Установить("МестоХранения",МестоХранения);
		КонецЕсли;
	Иначе
		спСчета.ДобавитьЗначение(СчетПоКоду("28.2.1"));
		спСчета.ДобавитьЗначение(СчетПоКоду("28.2.2"));
		спОтбор.Установить("МестоХранения",МестоХранения);
	КонецЕсли;
	спОтбор.Установить("Счет",спСчета);
	спОтбор.Установить("Контекст",Контекст);  
	ОткрытьФормуМодально("Справочник.Контрагенты.ВыборПредпочтения", спОтбор);
КонецПроцедуры

// ===============================
Процедура ВыборКассы()
	спКассы = глЗаполнитьСпКассы(Фирма,Валюта);
	спКассы.ВыбратьЗначение(Касса,,,,2);
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Флаг=0;
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ИзмВидНДС();
	ИначеЕсли Рекв="Предпочтение" Тогда
	    ВыборПредпочтения(); 
	ИначеЕсли Рекв="Касса" Тогда
		ВыборКассы();		
	ИначеЕсли Рекв="ЭККА" Тогда
		глВыбратьЭККА(Контекст,"Чек",1);
	Иначе
		Флаг=1;
	КонецЕсли;
КонецПроцедуры
                        
// ===============================
// функции и процедуры, вызываемые из формул элементов диалога

// ===============================
Процедура Печать()
	
	Таб = СоздатьОбъект("Таблица");    

	ПечФорма = "Накладная";
	Язык = глЯзык(ПечФорма); 	
	Таб.ИсходнаяТаблица(ПечФорма);
	глУстПропись(Валюта,Язык);
	                                                                            
	ПечОснование=глСтрокаОснование(Контекст,Договор,ДокументОснование);
	Таб.ВывестиСекцию("Шапка");
	
	Ном = 1;
	ВСоставе = 0;
	
	СуммаНДС = 0; 
	ОсталосьСкидка = Скидка;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл                
		Если Набор.Выбран()=1 Тогда
			Если (ПустоеЗначение(ВСоставе)=1) или (ВСоставе<>Набор) Тогда
				Таб.ВывестиСекцию("ВСоставе");
				ВСоставе=Набор;
			КонецЕсли;
		Иначе
			Если ПустоеЗначение(ВСоставе)=0 Тогда
				ВСоставе=0;
			КонецЕсли;
		КонецЕсли;         
		
		ПечЦена = глФРМТ(ЦенаСНДС,Валюта,0);
		ПечСумма = глФРМТ(СуммаСНДС,Валюта,0);
		
		Таб.ВывестиСекцию("Строка");
		Ном = Ном+1;
		
		Если НомерСтроки = КоличествоСтрок() Тогда
			СкидкаНаСтроку = ОсталосьСкидка;
		Иначе
			СкидкаНаСтроку = ?(Итог("СуммаСНДС")=0,0,Окр(Скидка* СуммаСНДС/Итог("СуммаСНДС"),2));
			ОсталосьСкидка = ОсталосьСкидка - СкидкаНаСтроку;
		КонецЕсли;
		
		ПроцНДС = глПроцентНДС(ТМЦ.СтавкаНДС,ДатаДок);
		СуммаНДС = СуммаНДС + (СуммаСНДС - СкидкаНаСтроку)* ПроцНДС/(100 + ПроцНДС);
	КонецЦикла;
	
	Если Скидка<>0 Тогда
		ПечСкидка = глФРМ(Скидка,Валюта,0);
		Таб.ВывестиСекцию("Скидка");
	КонецЕсли;
	
	ПечНДС = глФРМТ(СуммаНДС,Валюта,0);
	ПечСНДС = глФРМТ(Итог("СуммаСНДС")-Скидка,Валюта,0);
	
	ПечНДСПропись = " "+глФРМТ(СуммаНДС,Валюта,0);
	ПечСНДСПропись = Формат(Итог("СуммаСНДС")-Скидка,"ЧПДС");
	
	Таб.ВывестиСекцию("Дно");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: Расходная накладная","");
	
КонецПроцедуры

// ===============================
// Предопределенные процедуры

Процедура ПриВыбореЗакладки(Номер,Значение)
	Форма.ИспользоватьСлой("Совместный,"+Значение,2);
КонецПроцедуры

// ===============================
Процедура ВводНового(Скопирован)
	Если Скопирован=1 Тогда	//копирование документа
		ЧекПробит=0;
		Возврат;
	КонецЕсли;
	глЗаполнитьШапку(Контекст);
	Автор=глПользователь;
	глВыбратьЭККА(Контекст,"Чек",0);
	Если ЭККА.Выбран()=1 Тогда
		Если (ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Регистрация) или
			(ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Автономный) Тогда
			МестоХранения = ЭККА.Магазин;    
		КонецЕсли;
	КонецЕсли;
	
	Если ПустоеЗначение(МестоХранения)=1 Тогда
		МестоХранения = глВосстановитьЗначение(Контекст,"ОсновнойМагазин");
	КонецЕсли;
                                         
	ДатаДок=РабочаяДата();
	Валюта = Гривня;
	Дата_Курса=ДатаДок;
	Курс=глКурсДляВалюты(Валюта,Дата_Курса);

	ЧекПробит=0;

	Контрагент=глВосстановитьЗначение(,"БазЧастноеЛицо");
	ИзмКонтрагент(); 	
	
	Если ПустоеЗначение(ВидТорговли)=1 Тогда
	    ВидТорговли = Перечисление.ВидыТорговли.Нал;
	КонецЕсли;
	УстКассу();
	ВыбранВидТорговли();
	
	Отпустил = глВосстановитьЗначение(,"БазОтпустил");
	Подразделение = глВосстановитьЗначение(,"БазПодразделениеПродажи");
	СубконтоВалДох = глВосстановитьЗначение(Контекст,"СубконтоВалДох",Константа.БазВаловыйДоход);	
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(ДокОснование)

	СинонимДокумента	= ПредставлениеВида();
	СинонимОснования	= ДокОснование.ПредставлениеВида();

	Если глЕстьРеквизитШапки("Валюта",ДокОснование.Вид()) = Да Тогда
		Если ДокОснование.Валюта <> Гривня Тогда
			СтатусВозврата(0);
			Предупреждение("Документ """+СинонимДокумента+""" нельзя вводить на основании документа """+СинонимОснования+""" с валютой "+ДокОснование.Валюта+".");
			Возврат;
		КонецЕсли;	
	КонецЕсли;
		
	Автор=глПользователь;
	Если ДокОснование.Вид()="Счет" Тогда
		Если ДокОснование.ЧтоПродаем = Перечисление.ЧтоПродаем.НеоборотныеАктивы Тогда
			Предупреждение("Для реализации основных средств используйте документ ЛиквидацияНеоборАктивов");
			СтатусВозврата(0);
			Возврат; 
		КонецЕсли;
		МестоХранения = глВосстановитьЗначение(Контекст,"ОсновнойМагазин");
	КонецЕсли;  	                      
	глВыбратьЭККА(Контекст,"Чек",0);
	ЗаполнениеШапкиНаОсновании(ДокОснование);
	Заполнить();	
	ВыбранВидТорговли();
	
	ЧекПробит=0; 
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	
	глПроверкаДатыДок(Контекст,"Открытие");
	
	УстДоступностьКнопок();

	Если Проведен()=1 Тогда
	    Форма.кПредварительно.Видимость(0);
	КонецЕсли;
	
	СтараяДата = ДатаДок;
	ПересчетРегистров();
	//Если документ еще не проведен, тогда 
	//Предварительное проведение делаем только в потоке
	Если ( Проведен() = 0 ) Тогда
		ПроводитьПослеТА(1,1);
	КонецЕсли;                  
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Дополнительно","Дополнительно");
	Форма.ИспользоватьСлой("Основной, Совместный",2); 
	
	Если ПустоеЗначение(ВидТорговли)=1 Тогда
	    ВидТорговли = Перечисление.ВидыТорговли.Нал;
		ВыбранВидТорговли();
	КонецЕсли;		          
	
	Если Константа.ПоказыватьОстаткиТМЦ = Нет Тогда
	    Форма.тОстатокПолный.Видимость(0);
	    Форма.тОстатокПоСкладу.Видимость(0);
	КонецЕсли;

	Если Константа.ИспользоватьСкидку = Нет Тогда
	    Форма.тСкидка.Доступность(0);
	    Форма.Скидка.Доступность(0);
	КонецЕсли;           

	// Инициализируем список действий по кнопке "Действия"
	СписокДействий = глПолучитьСписокДействий(
		?((МестоХранения.ВидСклада = Розничный) И (МестоХранения.СуммовойУчет=1),"","ТоварныйСостав,") + "
		|ОбновлениеЦен,
		|СтруктураПодчиненности,
		|ДвиженияДокумента,
		|ВводНаОсновании,
		|ОткрытьВЖурнале,
		|Подчиненные");
КонецПроцедуры

// ===============================
Процедура ПриВводеСтроки()
	Сдача();
КонецПроцедуры
                                 
// ===============================
Процедура ПриНачалеРедактированияСтроки()
	Сдача();
	Если ПустоеЗначение(Предпочтение)=1 Тогда
		Форма.Предпочтение.НазначитьТип("Справочник.Контрагенты");
		Предпочтение=ПолучитьПустоеЗначение("Справочник.Контрагенты");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриРедактированииНовойСтроки()
	Сдача();
	Форма.Предпочтение.НазначитьТип("Справочник.Контрагенты");
	Предпочтение=ПолучитьПустоеЗначение("Справочник.Контрагенты");
КонецПроцедуры

// ===============================
Процедура ОбработкаПодбора(Выб)  
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры

// ===============================
Процедура ОбработкаВнешнегоСобытия(Источник,Событие,Данные)
	глДобавитьТоварПоШтрихКоду(Контекст,Событие,Данные);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	ОтборСчетаКонтрагент = ?(ПустоеЗначение(ДокументОснование)=1, Контрагент, 0);
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли; 
	// заполним реквизиты БазаНДС1,БазаНДС2,БазаНДС3,НДС1,НДС2,НДС3
	БазаНДС1 = 0;
	БазаНДС2 = 0;
	БазаНДС3 = 0;
	НДС1 = 0;
	НДС3 = 0;
	НДС2 = 0;
	ПроцБезНДС			  = глПроцентНДС(БезНДС,ДатаДок);
	ПроцЛьготнаяСтавкаНДС = глПроцентНДС(ЛьготнаяСтавкаНДС,ДатаДок);
	ПроцОсновнаяСтавкаНДС = глПроцентНДС(ОсновнаяСтавкаНДС,ДатаДок);
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ТМЦ.СтавкаНДС = БезНДС Тогда
			БазаНДС1 = БазаНДС1 + СуммаСНДС; 							
		ИначеЕсли ТМЦ.СтавкаНДС = ЛьготнаяСтавкаНДС Тогда
			БазаНДС2 = БазаНДС2 + СуммаСНДС; 							
		ИначеЕсли ТМЦ.СтавкаНДС = ОсновнаяСтавкаНДС Тогда
			БазаНДС3 = БазаНДС3 + СуммаСНДС; 							
	    КонецЕсли;
	КонецЦикла;	
	НДС1 = БазаНДС1*ПроцБезНДС/(100+ПроцБезНДС);
	НДС2 = БазаНДС2*ПроцЛьготнаяСтавкаНДС/(100+ПроцЛьготнаяСтавкаНДС);
	НДС3 = БазаНДС3*ПроцОсновнаяСтавкаНДС/(100+ПроцОсновнаяСтавкаНДС);
	глСохранитьЗначение(Контекст,"СубконтоВалДох",СубконтоВалДох);
КонецПроцедуры
                  
// ===============================
// При входе в Форму запомним промежуточные переменные

// ===============================

ЗаполнитьСпВидТорговли();
ВыбранВидТорговли();