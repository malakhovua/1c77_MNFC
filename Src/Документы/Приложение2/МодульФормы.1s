Перем СтарЧтоПродаем;
Перем СтарНалоговаяНакладная;
Перем СтарыйКонтрагент;
Перем НачальнаяДатаДокумента;
Перем СтараяДата;

// ===============================
Процедура РассчитатьБазуНДС()
    НачВалюта = ?(ПустоеЗначение(Валюта)=1,Гривня,Валюта);
	ИзмСуммыНДС = глПересчет(Итог("ИзмНДС"),НачВалюта,Гривня,Курс,ДатаДок);
	ИзмБазыНДС  = глПересчет(Итог("ИзмСуммыБезНДС"),НачВалюта,Гривня,Курс,ДатаДок);
КонецПроцедуры

// ===============================
Процедура ВводНового()
	Предупреждение("Этот документ можно вводить только на основании Налоговой накладной!");
	СтатусВозврата(0);
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(ДокОсн)
	Если ДокОсн.Вид() <> "НалоговаяНакладная" Тогда
		// вводить документ можно только на основании НалоговойНакладной
		// остальные документы, отмеченные в "вводить на основании" используются
		// при выборе реквизита ДокументОснование
		Предупреждение("Документ """+Вид()+""" нельзя вводить на основании документа """+ДокОсн.Вид()+"""."+РазделительСтрок+"Возможно только выбирать его в качестве документа-основания в диалоге !");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Фирма = ДокОсн.Фирма;
	глУстановитьНомер(Контекст);
	
	Договор = ДокОсн.Договор;
	ДокументОснование = ДокОсн.ДокументОснование;
	НалоговаяНакладная = ДокОсн;
	
	УсловиеПродажи = ДокОсн.УсловиеПродажи;
	ВидНДС = ДокОсн.ВидНДС;
	КатегорияЦен = ДокОсн.КатегорияЦен;
	ЧтоПродаем = ДокОсн.ЧтоПродаем;
	
	Валюта = ДокОсн.Валюта;
	Дата_Курса= ДокОсн.Дата_Курса;
	Курс = ДокОсн.Курс;
	
	Контрагент = ДокОсн.Контрагент;
	ФормаРасчетов = ДокОсн.ФормаРасчетов;
	Выписал = глВосстановитьЗначение(,"БазВыписалНН");
	ДокОсн.ВыбратьСтроки();
	Пока ДокОсн.ПолучитьСтроку() <> 0 Цикл
		НоваяСтрока();
		ТМЦ = ДокОсн.ТМЦ;
		Ед = ДокОсн.Ед;
		Кво = ДокОсн.Кво;
		ЦенаБезНДС = ДокОсн.ЦенаБезНДС;
		ЦенаСНДС = ДокОсн.ЦенаСНДС;
		Коэффициент = ДокОсн.Коэффициент;
		СуммаБезНДС = ДокОсн.СуммаБезНДС;
	КонецЦикла;
КонецПроцедуры
                         
// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кФирма.Доступность(0);
		Форма.кВалюта.Доступность(0);
		Форма.кОснование.Доступность(0);
		Форма.кОчиститьРодительскийДокумент.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	НачальнаяДатаДокумента = ДатаДок;СтараяДата = ДатаДок;
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	СтарЧтоПродаем = ЧтоПродаем;	
	СтарыйКонтрагент = Контрагент;
	глУстВидимостьЦен(Контекст);
КонецПроцедуры
                                 
// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры //

// ===============================
Процедура ИзмИзмКво()
	// При изменении количества
	Если ИзмКво <> 0 Тогда
		Если ИзмЦены <> 0 Тогда
		    ИзмЦены = 0;
		    Предупреждение("Разрешается корректировать или цену или количество!");
		КонецЕсли;
	КонецЕсли;
	глВыч_суммы_накл(Контекст,0);
КонецПроцедуры

// ===============================
Процедура ИзмИзмЦены()
	Если ИзмЦены <> 0 Тогда
		Если ИзмКво <> 0 Тогда
		    ИзмКво = 0;
		    Предупреждение("Разрешается корректировать или цену или количество!");
		КонецЕсли;
	КонецЕсли;
	глВыч_суммы_накл(Контекст,0);
КонецПроцедуры

// ===============================
Процедура ИзмТМЦ()
	Если ТМЦ.Вид()="ТМЦ" Тогда
		глПриИзмененииТовара(Контекст);
	Иначе // необоротные активы
		глУстановкаБазЕд(Контекст, , ТМЦ.БазЕдиница);
		ЦенаБезНДС = глПересчет(ТМЦ.Цена_Прих,Гривня,Валюта,ДатаДок);
		глВыч_суммы_накл(Контекст,-1);		
	КонецЕсли;
КонецПроцедуры //
                                           
// ======================================
Процедура ИзмВидНДС()
	// пересчитаем суммы НДС в строках табличной части
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаБезНДС Тогда
			глВыч_Суммы_Накл(Контекст,-1);
		Иначе
			глВыч_Суммы_Накл(Контекст,1);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ======================================
Процедура ИзмНалоговаяНакладная()
	Если ПустоеЗначение(НалоговаяНакладная) = 0 Тогда
		ВводНаОсновании(НалоговаяНакладная);
	Иначе
		НалоговаяНакладная = СтарНалоговаяНакладная;
	КонецЕсли;	
КонецПроцедуры                                                      

// ===============================
Процедура ИзмДоговор()
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Вид() = "Договор" Тогда
			Валюта_Прежн = Валюта;
			Валюта = Договор.Валюта;
			Если Валюта <> Валюта_Прежн Тогда
				Дата_Курса=ДатаДок;
				Курс_Прежн = Курс;
				Курс = глКурсДляВалюты(Валюта,ДатаДок);
				Если ((Валюта_Прежн <> Валюта) Или (Курс_Прежн <> Курс)) Тогда
					// что-то изменилось, надо пересчитать цены
					глУстан_Вал(Контекст, Валюта, Валюта_Прежн, Курс, Курс_Прежн);
				КонецЕсли;
			КонецЕсли;
			ВидТорговли = Договор.ВидТорговли;
			ВидНДС = Договор.ВидНДС;
		КонецЕсли;
	КонецЕсли;    
КонецПроцедуры

// ======================================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;	
	Если СтарыйКонтрагент <> Контрагент Тогда
		// изменили Контрагента
		Если Договор.Выбран() = 1 Тогда
			Если Договор.Контрагент <> Контрагент Тогда
				Договор = 0;
			КонецЕсли;
		КонецЕсли;    
		Если Договор.Выбран() = 0 Тогда
			Если (Фирма = Контрагент.БазДоговор.Фирма) и (глВосстановитьЗначение(,"ПодставлятьОсновнойДоговор")=Да) Тогда
				Договор = Контрагент.БазДоговор;
			КонецЕсли;
			Если ПустоеЗначение(Контрагент.ВидТорговли) = 0 Тогда
				ВидТорговли = Контрагент.ВидТорговли;
			ИначеЕсли ПустоеЗначение(ВидТорговли) = 1 Тогда
				ВидТорговли = глВосстановитьЗначение(,"ОсновнойВидТорговли");
			КонецЕсли;	
			ИзмДоговор();
		КонецЕсли;       
		// проверим документ-основание
		Если ПустоеЗначение(ДокументОснование) = 0 Тогда
			Если ДокументОснование.Контрагент <> Контрагент Тогда
				ДокументОснование = 0;
			КонецЕсли;	
		КонецЕсли;
		// проверим налоговую
		Если ПустоеЗначение(НалоговаяНакладная) = 0 Тогда
			Если НалоговаяНакладная.Контрагент <> Контрагент Тогда
				НалоговаяНакладная = 0;
				ИзмНалоговаяНакладная();
			КонецЕсли;	
		КонецЕсли;
		Если ПустоеЗначение(Контрагент.КатегорияЦен.Получить(ДатаДок)) = 0 Тогда
			КатегорияЦен = Контрагент.КатегорияЦен.Получить(ДатаДок);
		Иначе
			КатегорияЦен = глВосстановитьЗначение(,"ОсновнаяКатегорияЦен");
		КонецЕсли;
	КонецЕсли;     
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры
                                
// ===============================
Функция ФРМЦена(ЧислЗнач)
	// без разделения на триады, не меньше двух и не больше пяти знаков после запятой
    Стр=СокрЛ(Формат(Окр(ЧислЗнач,5),"Ч15.5"));
	Для Инд=1 по 3 Цикл
		Если Прав(Стр,1)="0" Тогда
		    Стр = Лев(Стр,СтрДлина(Стр)-1);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Стр;
КонецФункции

// ===============================
Процедура Печать()
	ФирмаНалогНомер = Фирма.ИНН;
	КонтрагентНалогНомер = Контрагент.ИНН;
	                           
	НН_НомерДок = глНомерБезПрефикса(НалоговаяНакладная.НомерДок);
	НН_ДатаДок = НалоговаяНакладная.ДатаДок;
	П2_НомерДок = глНомерБезПрефикса(НомерДок);

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица_Укр");
	Для Страница = 1 По 2 Цикл
		Таб.ВывестиСекцию( "Пустота" );
		Если Страница = 1 Тогда
			Таб.ВывестиСекцию( "Оригинал" );
		ИначеЕсли Страница = 2 Тогда
			Таб.ВывестиСекцию( "Копия" );
		КонецЕсли;
		Таб.ВывестиСекцию( "Шапка" );
		ВыбратьСтроки();
		Пока ( ПолучитьСтроку() > 0 ) Цикл
			Ст1 = ДатаДок;
			Ст2 = Причина;
			Ст3 = ТМЦ.ПолнНаименование;
			Ст4 = ?(ТМЦ.Вид()="ТМЦ",Ед,ТМЦ.БазЕдиница);
			Ст5 = "Х";
			Ст6 = "Х";
			Ст7 = "Х";
			Ст8 = "Х";
			ПроцНДС = глПроцентНДС(?((ВидНДС=ОсновнаяСтавкаНДС)и(ТМЦ.Вид()="ТМЦ"),ТМЦ.СтавкаНДС,ВидНДС),ДатаДок);
			Если ИзмКво<>0 Тогда
				Ст5 = Формат(ИзмКво,"Ч13.3");
				Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС Тогда
					ВремЦенаБезНДС = ЦенаСНДС*100/(100+ПроцНДС);
				Иначе
					ВремЦенаБезНДС = ЦенаБезНДС;
				КонецЕсли;
				Ст6 = ФРМЦена(глПересчет(ВремЦенаБезНДС,Валюта,Курс,Гривня,ДатаДок,Дата_Курса));
			Иначе
				Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС Тогда
					ВремИзмЦены = ИзмЦены*100/(100+ПроцНДС);
				Иначе
					ВремИзмЦены = ИзмЦены;
				КонецЕсли;
				Ст7 = ФРМЦена(глПересчет(ВремИзмЦены,Валюта,Курс,Гривня,ДатаДок,Дата_Курса));
				Ст8 = Формат(Кво,"Ч13.3");
			КонецЕсли;
			Ст9 = "Х";
			Ст10 = "Х";
			Ст11 = "Х";
			Ст12 = "Х";
			Ст13 = "Х";
			Ст14 = "Х";
			Ст15 = "Х";
			ПечИзмСуммыБезНДС = глФРМ3(глПересчет(ИзмСуммыБезНДС,Валюта,Курс,Гривня,ДатаДок,Дата_Курса),Гривня,0);
			Если Сокрлп(ВидНДС.Код) = "НДС20" Тогда
				Ст9 = ПечИзмСуммыБезНДС;
			ИначеЕсли Сокрлп(ВидНДС.Код) = "НДС0" Тогда
				Ст10 = ПечИзмСуммыБезНДС;
			ИначеЕсли Сокрлп(ВидНДС.Код) = "БезНДС" Тогда
				Ст11 = ПечИзмСуммыБезНДС;
			КонецЕсли;
			ПечИзмНДС = глФРМ3(глПересчет(ИзмНДС,Валюта,Курс,Гривня,ДатаДок,Дата_Курса),Гривня,0);
			Если (Сокрлп(ВидНДС.Код) = "НДС20") и (ИзмСуммыБезНДС<0) Тогда
				Ст12 = ПечИзмНДС;
				Ст13 = ПечИзмНДС;
			ИначеЕсли (Сокрлп(ВидНДС.Код) = "НДС20") и (ИзмСуммыБезНДС>0) Тогда
				Ст14 = ПечИзмНДС;
				Ст15 = ПечИзмНДС;
			КонецЕсли;
			Таб.ВывестиСекцию("Строка");
		КонецЦикла;
		Таб.ВывестиСекцию("Подвал");

		Если Страница <> 2 Тогда
			Таб.НоваяСтраница();
		КонецЕсли;

	КонецЦикла;

	Таб.Опции(0, 0, 0, 0);
	Таб.ПараметрыСтраницы(2,,,,,,,,);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать расчета корретировки", "");
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
    // пустой договор должен строго иметь тип "Документ", важно для отчета АнализНДС
    Если ПустоеЗначение(Договор) = 1 Тогда
        Договор = ПолучитьПустоеЗначение("Документ");
    КонецЕсли;
	РассчитатьБазуНДС();
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Приложение № 2 к налоговой накладной"));
	Если (ЧтоПродаем = Перечисление.ЧтоПродаем.НеоборотныеАктивы) Тогда
		НазначитьВид(ТМЦ,"НеоборотныеАктивы");
	Иначе
		НазначитьВид(ТМЦ,"ТМЦ");
	КонецЕсли;
	Форма.кОчиститьРодительскийДокумент.Доступность(РодительскийДокумент.Выбран());
	Форма.ТМЦ.НеИзменятьВид(1);
	РассчитатьБазуНДС();
	Возврат "";
КонецФункции
                                                   
// ===============================
Процедура ИзмЧтоПродаем()
	Если (КоличествоСтрок()<>0) и (СтарЧтоПродаем<>ЧтоПродаем) и (СокрЛП(СтарЧтоПродаем)<>"") Тогда
		Рез = Вопрос("Для изменения нужно очистить табличную часть. Удалить строки?","Да+Нет");
		Если Рез = "Да" Тогда
		    УдалитьСтроки();
		Иначе
			ЧтоПродаем = СтарЧтоПродаем;
		КонецЕсли;
	КонецЕсли;
	СтарЧтоПродаем = ЧтоПродаем;
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ИзмВидНДС();
		ФлагСтандОбр = 0;
	ИначеЕсли Рекв = "Выписал" Тогда
		ФлагСтандОбр = 0;
		КонтФирма = Фирма;
		ОткрытьФорму("Справочник.Сотрудники.ДляВыбора",КонтФирма);
	ИначеЕсли Рекв = "НалоговаяНакладная" Тогда
		СтарНалоговаяНакладная = НалоговаяНакладная;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в документе
	ОткрытьФормуМодально("Обработка.ИнформацияОВалюте", Контекст);
КонецПроцедуры	


//====================================================================== УМК Сандомирский В.Ю. (*) //--- заголовок FormEx_ПланРаскраски
Функция Раскраска()
	ТекстПлана = "(BRUSH_S[" + Строка(100*255*100) + "])()()"; //--- добавляя колонку вначало документа до "раскрашек" часов добавить "()"
	Возврат  ТекстПлана;	
КонецФункции


// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");
