Перем ПересчКво, ПервыйДок, ПоЗаказу;
Перем Товар;
Перем СчетНДС,СубконтоНДС1,СубконтоНДС2; // для бартера
Перем ВремРег;       
Перем спОтбор;
Перем НДСПервоеСобытие, НДСВтороеСобытие, ВзаиморасчетыГрн; // для исправления ошибок при округлениях
Перем АвансВал,АвансОсн; // для погашения аванса в валюте

// ===========================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Фирма,"Фирма");
    глВыбранЛи(Контрагент,"Контрагент");
    глВыбранЛи(СчетКонтрагента,"Счет контрагента");
	Если глВсеВыбрано = 1 Тогда
		Если глПроверитьСчетВзаиморасчетов(СчетКонтрагента) = 0 Тогда
			глКомментарий("Счет контрагента ("+СчетКонтрагента+") не является счетом учета взаиморасчетов!",1,,"!");
		    глВсеВыбрано = 0;
		ИначеЕсли (Валюта = Гривня)
		И (СчетКонтрагента.Валютный = 1) Тогда
			глКомментарий("Нельзя выбирать валютный счет, если валюта документа "+Гривня+" !",1,,"!");
			глВсеВыбрано = 0;       
		ИначеЕсли (Валюта <> Гривня)
		И (СчетКонтрагента.Валютный = 0) Тогда
			глКомментарий("Нельзя выбирать не валютный счет, если валюта документа "+Валюта+" !",1,,"!");
			глВсеВыбрано = 0;
		КонецЕсли;	
	КонецЕсли;	
    глВыбранЛи(МестоХранения,"Место хранения");
    глВыбранЛи(Валюта,"Валюта");
    глВыбранЛи(ВидТорговли,"Вид торговли");
	глВыбранЛи(ВидТары,"Вид тары");
    глВыбранЛи(СубконтоВалДох,"Субконто валовых доходов");
    глВыбранЛи(Подразделение,"Подразделение сбыта");
	глВыбранЛи(ВидНДС,"Вид НДС");
  	глВсеВыбрано = ?(глВсеВыбрано = 0, 0, глПроверкаДублейСтрок(Контекст));
	Если Валюта <> Гривня Тогда
	    Если ВидТорговли <> Перечисление.ВидыТорговли.Предоплата Тогда
			глКомментарий("Продавать за валюту можно только на условиях предоплаты!",0,,"!");
    		глВсеВыбрано = 0;
		КонецЕсли;               		
		Если ВидНДС = ОсновнаяСтавкаНДС Тогда
			// это явная ошибка, запрещаем проведение 
        	глКомментарий("При продаже за валюту нельзя указывать НДС!",0,,"!");
    		глВсеВыбрано = 0;
		ИначеЕсли ВидНДС <> ЛьготнаяСтавкаНДС Тогда
			// просто предупредим, но жестко не запрещаем
		    глКомментарий("Отгрузка за валюту вероятно должна проходить по ставке НДС """+ЛьготнаяСтавкаНДС+"""!",2)
		КонецЕсли;	
    КонецЕсли;    
    Если МестоХранения.ВидСклада = Розничный Тогда
        глКомментарий("Накладная предназначена для отгрузки ТМЦ с ОПТОВОГО склада, а не РОЗНИЧНОГО (магазина)!",0,,"!");
        глКомментарий("Проверьте параметры склада!",2);
        глВсеВыбрано = 0;
	КонецЕсли;
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		глВыбранЛи(Касса,"Касса");
    КонецЕсли;
	глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС"));
    Возврат глВсеВыбрано;
КонецФункции

// ===========================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
    глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);
    глВыбранЛи(ТМЦ.ВидДеятельности,"Вид деятельности в карточке ТМЦ",НомерСтроки);  
	глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,ТМЦ,НомерСтроки,1)=Да, глВсеВыбрано, 0);
	ЕстьКалькулируемыеУслуги = 0;
	Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
		Если глПолучитьНормы(ТМЦ,,1,ДатаДок,ПоЗаказу,,1)=1 Тогда // калькулируемая услуга
			ЕстьКалькулируемыеУслуги = 1;
			Если глВсеВыбрано = 1 Тогда
				Если (ТМЦ.ВидДеятельности.ПозаказноеПроизводство = Да) И (ПустоеЗначение(ПоЗаказу) = 1) Тогда
					глКомментарий("В строке "+НомерСтроки+" выбрана услуга, в карточке которой указан вид деятельности с позаказным производством!",0,,"!");
					глВсеВыбрано = 0;                         
				ИначеЕсли (ТМЦ.ВидДеятельности.ПозаказноеПроизводство = Нет) И (ПустоеЗначение(ПоЗаказу) = 0) Тогда
					глКомментарий("В строке "+НомерСтроки+" выбрана услуга, в карточке которой указан вид деятельности с непозаказным производством!",0,,"!");
					глВсеВыбрано = 0;                         
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если ЕстьКалькулируемыеУслуги = 1 Тогда
		глВыбранЛи(ПодразделениеПроизв,"Подразделение производства");
		глПроверитьТипПодразделения(ПодразделениеПроизв,"подразделения производства");
	КонецЕсли;
    Возврат глВсеВыбрано;
КонецФункции
                       

// ===============================
Процедура ОбработкаПроведения(ЧастичноПровести = 0)
	глКомментарий("Начало",2,Контекст);
	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки()=0 Тогда
			глНеПроводить(Контекст);
		    Возврат;
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

// ===============================
Процедура ОбработкаУдаленияПроведения()	
	РежимПроведения=0;
КонецПроцедуры
