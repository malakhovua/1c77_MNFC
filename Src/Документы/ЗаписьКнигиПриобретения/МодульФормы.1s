Перем НачальнаяДатаДокумента;
Перем СтарыйКонтрагент;

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
КонецПроцедуры	

// ===============================
Процедура ЗаполнитьВидыОпераций()
	спВидОперации.УдалитьВсе();
	Если Импорт = 1 Тогда
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.Импорт);
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.ИмпортПоВекселю);
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.РаботыОтНерезидента);
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.РаботыОтНерезидентаПрошлогоПериода);
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.ВексельПрошлогоПериода);
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.ПогашениеНалоговогоВекселя);
	Иначе
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.ПокупкаВУкраине);
		спВидОперации.ДобавитьЗначение(Перечисление.ВидыОперацийКнигиПриобретения.РасчетКорректировки);
	КонецЕсли;
	// если вид операции есть в новом списке - спозиционируемся на него
	Инд = спВидОперации.НайтиЗначение(ВидОперации);
	Если Инд = 0 Тогда
	    // нет такого в списке
		спВидОперации.ТекущаяСтрока(1);
		ВидОперации = спВидОперации.ПолучитьЗначение(1);
	Иначе
		спВидОперации.ТекущаяСтрока(Инд);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура УстНалоговыйКредит()
	фНалоговыйКредит = 1;
	Если ВидНДС.Код = "БезНДС" Тогда
		// приобретение без НДС - нет налогового кредита
	    фНалоговыйКредит = 0;
	КонецЕсли;
	Если ВаловыеРасходы = 0 Тогда
	    // не относятся на валовые расходы и не подлежат амортизации - нет налогового кредита
		фНалоговыйКредит = 0;
	КонецЕсли;
	Если ДляОблагаемыхОпераций = 0 Тогда
	    // для совершения операций, кот. не подлежат или освобождены от налогообложения - нет налогового кредита
		фНалоговыйКредит = 0;
	КонецЕсли;
	Если фНалоговыйКредит <> НалоговыйКредит Тогда
	    НалоговыйКредит = фНалоговыйКредит;
	КонецЕсли;
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Запись книги приобретения"));
	    
	Слои = "Основной,";
	Если Импорт = 1 Тогда
		Слои = Слои + "Импорт";
	Иначе
		Слои = Слои + "НеИмпорт";
	КонецЕсли;
	Форма.ИспользоватьСлой(Слои);
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
КонецФункции

// ===============================
Процедура ИзмВидОперации()
	Инд = спВидОперации.ТекущаяСтрока();
	Если Инд <> 0 Тогда
		ТекВидОперации = спВидОперации.ПолучитьЗначение(Инд);
		Если ТекВидОперации <> ВидОперации Тогда
		    ВидОперации = ТекВидОперации;
		КонецЕсли;
	КонецЕсли;
	УстНалоговыйКредит();
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кФирма.Доступность(0);
		Форма.кОснование.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	НачальнаяДатаДокумента = ДатаДок;
	СтарыйКонтрагент = Контрагент;
	ЗаполнитьВидыОпераций();
	Инд = спВидОперации.НайтиЗначение(ВидОперации);
	Если Инд <> 0 Тогда
	    спВидОперации.ТекущаяСтрока(Инд);
	КонецЕсли;
	ИзмВидОперации();
КонецПроцедуры
                                                              
// ===============================
Процедура ИзмДатаДок()
КонецПроцедуры
	
// ===============================
Процедура ИзмДляОблагаемыхОпераций()
	УстНалоговыйКредит();
КонецПроцедуры

// ===============================
Процедура ИзмИмпорт()
	ЗаполнитьВидыОпераций();
	УстНалоговыйКредит();
КонецПроцедуры

// ===============================
Процедура ИзмВаловыеРасходы()
	УстНалоговыйКредит();
КонецПроцедуры

// ===============================
Процедура ИзмНДС()
	СуммаСНДС = СуммаБезНДС + НДС;
КонецПроцедуры

// ===============================
Процедура ИзмСуммаБезНДС()
	Если ВидНДС.Выбран() = 0 Тогда
	    НДС = 0;
	Иначе
		Ставка = ВидНДС.Ставка.Получить(ДатаДок);
		НДС = СуммаБезНДС * Ставка;
	КонецЕсли;
	ИзмНДС();
КонецПроцедуры

// ===============================
Процедура ИзмВидНДС()
	УстНалоговыйКредит();
	ИзмСуммаБезНДС();
КонецПроцедуры

// ===============================
Процедура ИзмДоговор()
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Вид() = "Договор" Тогда
			ВидТорговли = Договор.ВидТорговли;
			ВидНДС = Договор.ВидНДС;
			ИзмВидНДС();
		КонецЕсли;
	КонецЕсли;    
КонецПроцедуры

// ======================================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;	
	Если СтарыйКонтрагент <> Контрагент Тогда
		// изменили Контрагента
		Если Договор.Выбран() = 1 Тогда
			Если Договор.Контрагент <> Контрагент Тогда
				Договор = 0;
			КонецЕсли;
		КонецЕсли;    
		Если Договор.Выбран() = 0 Тогда
			Если (Фирма = Контрагент.БазДоговор.Фирма) и (глВосстановитьЗначение(,"ПодставлятьОсновнойДоговор")=Да) Тогда
				Договор = Контрагент.БазДоговор;
			КонецЕсли;
			Если ПустоеЗначение(Контрагент.ВидТорговли) = 0 Тогда
				ВидТорговли = Контрагент.ВидТорговли;
			ИначеЕсли ПустоеЗначение(ВидТорговли) = 1 Тогда
				ВидТорговли = глВосстановитьЗначение(,"ОсновнойВидТорговли");
			КонецЕсли;	
			ИзмДоговор();
		КонецЕсли;       
		// проверим документ-основание
		Если ПустоеЗначение(ДокументОснование) = 0 Тогда
			Если глЕстьРеквизитШапки("Контрагент",ДокументОснование.Вид()) = Да Тогда
				Если ДокументОснование.Контрагент <> Контрагент Тогда
					ДокументОснование = 0;
				КонецЕсли; 
			ИначеЕсли глЕстьРеквизитШапки("Субконто1",ДокументОснование.Вид()) = Да Тогда
				Если ДокументОснование.Субконто1 <> Контрагент Тогда
					ДокументОснование = 0;
				КонецЕсли; 
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;     
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры

// ======================================
Процедура ИзмВидДокумента()
	Если ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.РасчетКорректировки Тогда
		Импорт = 0;
		ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РасчетКорректировки;
	ИначеЕсли (ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.Чек)
	или (ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.НалоговаяНакладная) Тогда
		Импорт = 0;
		ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.ПокупкаВУкраине;
	ИначеЕсли ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.ГТД Тогда
		Импорт = 1;
		ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.Импорт;
	КонецЕсли;
	ИзмИмпорт();
	ИзмВидОперации();
КонецПроцедуры 

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	          
	глУстановитьФирму(Контекст);
	Контрагент = глВосстановитьЗначение(,"ОсновнойПоставщик");
	ИзмКонтрагент();
	ВидНДС = глВосстановитьЗначение(,"БазНДС");
	
	ДляОблагаемыхОпераций = 1;
	ВаловыеРасходы = 1;
	Импорт = 0;
	ИзмИмпорт();
	
	ВидОперации = спВидОперации.ПолучитьЗначение(1);
	ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.НалоговаяНакладная;
	ИзмВидДокумента();
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(ДокОснование)
	Если ДокОснование.Вид() = "РасходныйКассовый" Тогда
		Если ДокОснование.ВидОплаты = Перечисление.ВидыОплаты.Возврат Тогда
		    Предупреждение("По документу " + ДокОснование +  " не возникают налоговые обязательства!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
	ДляОблагаемыхОпераций = 1;
	
	ДокументОснование = глПолучитьДокументВзаиморасчетов(ДокОснование);
	
	глЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
	// в зависимости от вида документа основания
	Если (ДокОснование.Вид() = "РасходыНаПриобретение")
	ИЛИ (ДокОснование.Вид() = "БанковскаяВыписка") Тогда
		// в документе несколько строк, предложим выбор, по какому 
		// контрагенту/Договору формировать запись книги приобретения
		Табл = СоздатьОбъект("ТаблицаЗначений");
		Табл.НоваяКолонка("Счет",,,,"Счет",10);
		Табл.НоваяКолонка("Контрагент",,,,"Контрагент",20);
		Табл.НоваяКолонка("Договор",,,,"Договор",40);
		Табл.НоваяКолонка("ДокументОснование",,,,"Документ-основание",40);
		Табл.НоваяКолонка("СуммаСНДС",,,,"Сумма с НДС");
		Табл.НоваяКолонка("НомерСтрокиДок");
		Табл.ВидимостьКолонки("НомерСтрокиДок",0);
		ДокОснование.ВыбратьСтроки();
		Пока ДокОснование.ПолучитьСтроку()=1 Цикл
			Если ДокОснование.Счет.ВидСубконто(2) = ВидыСубконто.Договора Тогда
				Табл.НоваяСтрока();
				Табл.Счет = ДокОснование.Счет;
				Табл.Контрагент = ДокОснование.Субконто1;
				Табл.Договор = ДокОснование.Субконто2;
				Табл.ДокументОснование = ДокОснование.ДокументОснование;
				Табл.СуммаСНДС = ДокОснование.СуммаСНДС;
				Табл.НомерСтрокиДок = ДокОснование.НомерСтроки;
			КонецЕсли;
		КонецЦикла;
		Если Табл.КоличествоСтрок() = 0 Тогда
			глКомментарий("В документе-основании "+ДокОснование+" нет строк, которые проводятся по взаиморасчетам. Сформировать документ ""Запись книги приобретения"" невозможно.",1);
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		// выберем строку
		ВыбСтрока = 0;
		Пока ВыбСтрока = 0 Цикл
			Если Табл.ВыбратьСтроку(ВыбСтрока,"Выберите строку для заполнения книги приобретения") <> 1 Тогда
				// не выбрана строка
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		КонецЦикла;
		Табл.ПолучитьСтрокуПоНомеру(ВыбСтрока);
		ДокОснование.ПолучитьСтрокуПоНомеру(Табл.НомерСтрокиДок);
			
		// контрагент и договор из выбранной строки документа
		Контрагент = ДокОснование.Субконто1;
		Договор = ДокОснование.Субконто2;
		ДокументОснование = ДокОснование.ДокументОснование;
	Иначе
		// все остальные виды документов
		Попытка 
			Контрагент = ДокОснование.Контрагент;
		Исключение КонецПопытки;
		
		Если ДокОснование.Вид() = "РасходныйКассовый" Тогда
			Если ДокОснование.Субконто1.Вид() = "Контрагенты" Тогда
				Контрагент = ДокОснование.Субконто1;
			КонецЕсли;
		КонецЕсли;

		// определим договор, если он не заполнен
		Если ПустоеЗначение(Договор) = 1 Тогда
			Если глЕстьРеквизитШапки("Договор",ДокОснование.Вид()) = Да Тогда
				Если ДокОснование.Договор.Выбран() = 1 Тогда
					Договор = ДокОснование.Договор;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Попытка
		ВидНДС = ДокОснование.ВидНДС;
	Исключение
		ВидНДС = глВосстановитьЗначение(,"БазНДС");
	КонецПопытки;
	
	Попытка
		Валюта = ДокОснование.Валюта;
	Исключение
		Валюта = Гривня;
	КонецПопытки;

	// вид операции
	Если Валюта <> Гривня Тогда
		Импорт = 1;
		ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.ГТД;
	ИначеЕсли ДокОснование.Вид() = "ВозвратПоставщику" Тогда
		ВаловыеРасходы = 1;
		ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.РасчетКорректировки
	ИначеЕсли ДокОснование.Вид()="ПриходнаяНакладнаяГТД" Тогда
	    Импорт = 1;
		ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.ГТД;
	Иначе
		ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.НалоговаяНакладная;
	КонецЕсли;

	ЗаполнитьВидыОпераций();
	ИзмВидДокумента();
	ВаловыеРасходы = 1;
	Если (Найти(ДокОснование.Вид(),"ПриходнаяНакладная")>0) Или (ДокОснование.Вид() = "УслугиСтороннихОрганизаций") Тогда
		// если НДС за счет прибыли, то это не налоговый кредит
		ВаловыеРасходы = ВаловыеРасходы - ДокОснование.НДСнаЗатраты;
	КонецЕсли;
	Если ДокОснование.Вид() = "РасходыНаПриобретение" Тогда
		// если в приходной накладной НДС за счет прибыли, то это не налоговый кредит
		// в РасходыНаПриобретение нет нужного реквизита, возьмем его из ПН:
		ВаловыеРасходы = ВаловыеРасходы - ДокОснование.ПриходнаяНакладная.НДСнаЗатраты;
	КонецЕсли;
	УстНалоговыйКредит();
		
	Если ДокОснование.Вид() = "ВозвратПоставщику" Тогда
		ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РасчетКорректировки;
	Иначе
		ВидОперации = спВидОперации.ПолучитьЗначение(1);
	КонецЕсли;
	// дата оплаты
	Если глЕстьРеквизитШапки("ВидТорговли",ДокОснование.Вид()) = Да Тогда
		Если ДокОснование.ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
			ДатаОплаты = Формат(ДокОснование.ДатаДок,"ДДДММГГ");
			ФормаОплаты = "Оплата готівкою";
		КонецЕсли;
	КонецЕсли;
	Если ПустоеЗначение(ДатаОплаты) = 1 Тогда
		// если документ - не приходная накладная за наличные
		СписокДат = СоздатьОбъект("СписокЗначений");
		
		Рег = СоздатьОбъект("Регистр.ВзаиморасчетыПоставщиков");
		Рег.УстановитьЗначениеФильтра("Счет",ДокументОснование,1);
		Рег.ВыбратьДвижения(,,);
		Пока Рег.ПолучитьДвижение()=1 Цикл
			Если (Рег.Приход = 1) и (Найти(АвансоваяОплата+ПостОплата+ВтороеСобытиеБартерРасход,Рег.КодОперации)<>0) Тогда
				// нашли оплату
				Док = Рег.ТекущийДокумент();
				Если (Док.Вид() = "БанковскаяВыписка") Тогда
					СписокДат.ДобавитьЗначение(Док.ДатаДок);
					ФормаОплаты = "Оплата з розрахункового рахунку";
				ИначеЕсли (Док.Вид() = "ПриходныйКассовый") Тогда
					СписокДат.ДобавитьЗначение(Док.ДатаДок);
					ФормаОплаты = "Оплата готівкою";
				ИначеЕсли (Док.Вид() = "РасходныйКассовый") Тогда
					СписокДат.ДобавитьЗначение(Док.ДатаДок);
					ФормаОплаты = "Оплата готівкою";
				ИначеЕсли (Док.Вид() = "РасходнаяНакладная") Тогда
					// бартерный документ, расходная накладная - вместо оплаты
					СписокДат.ДобавитьЗначение(Док.ДатаДок);
					ФормаОплаты = "Бартер";
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		СтрокаДат = "";
		Если СписокДат.РазмерСписка() = 3 Тогда
			СписокДат.УдалитьЗначение(3);
			СтрокаДат = СписокДат.ВСтрокуСРазделителями()+" ...";
		Иначе
			СтрокаДат = СписокДат.ВСтрокуСРазделителями();
		КонецЕсли;
		ДатаОплаты = СтрЗаменить(СтрокаДат,"""","");
	КонецЕсли;
	
	// определим сумму
	Если (ДокОснование.Вид() = "ПриходнаяНакладнаяЗапасы") или (ДокОснование.Вид() = "ПриходнаяНакладнаяПрочие")
		или (ДокОснование.Вид() = "СчетВходящий") Тогда
		СуммаБезНДС = ДокОснование.Итог("СуммаБезНДС");
		НДС = ДокОснование.Итог("НДС");
		СуммаСНДС = СуммаБезНДС + НДС;
	ИначеЕсли (ДокОснование.Вид() = "ПриходнаяНакладнаяГТД") Тогда
	    СуммаБезНДС = ДокОснование.Итог("СуммаБезНДС")+ДокОснование.Итог("Акциз")+ДокОснование.Итог("Пошлина")+ДокОснование.Итог("Перевозка");
		НДС = ДокОснование.Итог("НДС") + ДокОснование.ДопНДС;
		СуммаСНДС = СуммаБезНДС + НДС;
	ИначеЕсли Найти(ДокОснование.Вид(),"УслугиСтороннихОрганизаций") > 0 Тогда
		СуммаБезНДС = ДокОснование.Итог("СуммаБезНДС");
		НДС = ДокОснование.Итог("НДС");
		СуммаСНДС = Окр(ДокОснование.Итог("СуммаСНДС"),2);
	ИначеЕсли ДокОснование.Вид() = "ВозвратПоставщику" Тогда
		// берем отрицательную сумму - проводки "сторно"
		СуммаБезНДС = -ДокОснование.Итог("СуммаБезНДС");
		НДС = -ДокОснование.Итог("НДС");
		СуммаСНДС = -Окр(ДокОснование.Итог("СуммаСНДС"),2);
	ИначеЕсли ДокОснование.Вид() = "РасходыНаПриобретение" Тогда
		// определим сумму по документу 
		// отбираем только те строки документа, которые содержат Контрагента, Договор и ДокументОснование
		СуммаБезНДС = 0;
		НДС = 0;
		СуммаСНДС = 0;
		ДокОснование.ВыбратьСтроки();
		Пока ДокОснование.ПолучитьСтроку()=1 Цикл
			Если (ДокОснование.Субконто1 = Контрагент) и (ДокОснование.Субконто2 = Договор) и 
				(ДокОснование.ДокументОснование = ДокументОснование) Тогда
				СуммаБезНДС = СуммаБезНДС + ДокОснование.СуммаБезНДС;
				НДС = НДС + ДокОснование.НДС;
				СуммаСНДС = СуммаСНДС + ДокОснование.СуммаСНДС;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ДокОснование.Вид() = "БанковскаяВыписка" Тогда
	    // требуемая строка в банковской выписке уже выбрана выше
		Если ДокОснование.ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Списание Тогда
			Если ДокОснование.ВидОплаты = Перечисление.ВидыОплаты.Оплата Тогда
				Знак = 1;   
				ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.НалоговаяНакладная;
			Иначе // на всякий случай
			    Предупреждение("Выбран возврат денежных средств покупателю. Документ ""Запись книги приобретения"" сформировать невозможно!");
				СтатусВозврата(0);
				Возврат;
			КонецЕсли
		Иначе
			Если ДокОснование.ВидОплаты = Перечисление.ВидыОплаты.Оплата Тогда // на всякий случай
			    Предупреждение("Выбрана оплата заказа покупателем. Документ ""Запись книги приобретения"" сформировать невозможно!");
				СтатусВозврата(0);
				Возврат;
			Иначе
				Знак = -1;
				ВидДокумента = Перечисление.ВидыДокументовКнигиПриобретения.РасчетКорректировки;
			КонецЕсли
		КонецЕсли;
		НДС 		= Знак * ДокОснование.НДС;
		СуммаСНДС 	= Знак * ДокОснование.СуммаСНДС;
		СуммаБезНДС = СуммаСНДС 	- НДС;
	ИначеЕсли ДокОснование.Вид() = "РасходныйКассовый" Тогда
		СуммаБезНДС = ДокОснование.СуммаВал - ДокОснование.НДС;
		НДС = ДокОснование.НДС;
		СуммаСНДС = ДокОснование.СуммаВал;
	КонецЕсли;
КонецПроцедуры //ВводНаОсновании 

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
    // пустой договор должен строго иметь тип "Документ", важно для отчета АнализНДС
    Если ПустоеЗначение(Договор) = 1 Тогда
        Договор = ПолучитьПустоеЗначение("Документ");
    КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ИзмВидНДС();
		ФлагСтандОбр = 0;
	КонецЕсли;
КонецПроцедуры
                                                       
// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
