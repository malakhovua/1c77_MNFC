// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(Сотрудник,"Сотрудник");
	глВыбранЛи(ВидЗатрат,"Вид затрат");
	Если ИспользоватьСчетаРасходов <> Класс9 Тогда
		глВыбранЛи(ВидЗатрат.Счет,"Счет 8-го класса для вида затрат");
	КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
	глВсеВыбрано = 1;
	глВыбранЛи(Работа,"Работа",НомерСтроки);
	глВыбранЛи(ВидЗатрат,"Вид затрат");
	Если глПроверитьАналитикуПоЗатратам(Подразделение,ВидДеятельности,Заказ,Продукция,,,,,НомерСтроки) = 0 Тогда
		глВсеВыбрано = 0;
	КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Процедура ПроводкиДно()
Перем Таб, Таб1;
	Таб = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(Таб);
	Таб.Свернуть("Продукция,ВидДеятельности,Заказ","Сумма");
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку() = 1 Цикл
		Регистр.ПроизводственныеЗатраты.Фирма			= Фирма;
		Регистр.ПроизводственныеЗатраты.ВидДеятельности	= Таб.ВидДеятельности;
		Регистр.ПроизводственныеЗатраты.Подразделение	= Подразделение;
		Регистр.ПроизводственныеЗатраты.Продукция		= Таб.Продукция;
		Регистр.ПроизводственныеЗатраты.ВидЗатрат		= ВидЗатрат;
		Регистр.ПроизводственныеЗатраты.Заказ			= Таб.Заказ;
		Регистр.ПроизводственныеЗатраты.Материал		= 0;
		Регистр.ПроизводственныеЗатраты.Количество		= 0; // материалов нет
		Регистр.ПроизводственныеЗатраты.Сумма			= Таб.Сумма;
		Регистр.ПроизводственныеЗатраты.СуммаДав		= 0;
		Регистр.ПроизводственныеЗатраты.КодОперации		= ОтражениеЗатрат;
		Регистр.ПроизводственныеЗатраты.ДвижениеПриходВыполнить();
	КонецЦикла;

	Таб.Выгрузить(Таб1);
	Инд = Таб1.КоличествоСтрок();
	Пока Инд >= 1 Цикл
		// удалим из Таб1 не давальческие заказы
		Таб1.ПолучитьСтрокуПоНомеру(Инд);
		Если глЭтоДавальческийЗаказ(Таб1.Заказ) = 0 Тогда
			Таб1.УдалитьСтроку(Инд);
		КонецЕсли;
		Инд = Инд - 1;
	КонецЦикла;
	Таб1.Свернуть("ВидДеятельности","Сумма");
	Таб1.ВыбратьСтроки();
	Пока Таб1.ПолучитьСтроку() = 1 Цикл
		глПроводка(Контекст,"ДВ",,Таб1.Сумма,"Сдельный наряд: "+Сотрудник,, Таб1.ВидДеятельности,Подразделение,,
		,,, ,,"СН");
	КонецЦикла;

	Таб.Свернуть("ВидДеятельности","Сумма");
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку() = 1 Цикл
		глПроводкаПоЗатратам(Контекст,"231","661",Таб.Сумма,"Сдельный наряд: "+Сотрудник,, Таб.ВидДеятельности,Подразделение,ВидЗатрат,
		Сотрудник,НачМесяца(ДатаДок),, ,,"СН");
	КонецЦикла;
	Таб = 0;
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);
	
	Если ПроверкаШапки() = 0 Тогда
		глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
	КонецЦикла;

	ПроводкиДно();

	Операция.СуммаОперации = Итог("Сумма");
	Операция.Содержание = Примечание;
	Операция.Записать();
	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры