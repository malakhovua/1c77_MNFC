Перем Касса;        
Перем СчетНДС, СубконтоНДС1, СубконтоНДС2;
Перем Счет, Субк1, Субк2, Субк3; // счет и аналитика по ТМЦ
Перем ПересчКво;
Перем ВремРег;
Перем НДСВсего, ВзаиморасчетыВсего, ИтогСуммаСНДС, ИтогНДС; // для исправления ошибок при округлениях

// ===============================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Контрагент,"Поставщик");
    глВыбранЛи(СчетКонтрагента,"Счет контрагента");
	Если глВсеВыбрано = 1 Тогда
		Если (глПроверитьСчетВзаиморасчетов(СчетКонтрагента) = 0)
		И (СчетКонтрагента.ВидСубконто(1) <> ВидыСубконто.Сотрудники) Тогда
			глКомментарий("Счет контрагента ("+СчетКонтрагента+") не является счетом учета взаиморасчетов!",1,,"!");
		    глВсеВыбрано = 0;
		ИначеЕсли (Валюта = Гривня)
		И (СчетКонтрагента.Валютный = 1) Тогда
			глКомментарий("Нельзя выбирать валютный счет, если валюта документа "+Гривня+" !",1,,"!");
			глВсеВыбрано = 0;       
		ИначеЕсли (Валюта <> Гривня)
		И (СчетКонтрагента.Валютный = 0) Тогда
			глКомментарий("Нельзя выбирать не валютный счет, если валюта документа "+Валюта+" !",1,,"!");
			глВсеВыбрано = 0;
		КонецЕсли;	
	КонецЕсли;	
	глВыбранЛи(Валюта,"Валюта");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(МестоХранения,"Место хранения");
	глВыбранЛи(ВидТорговли,"Вид торговли");
	глВыбранЛи(ЧтоПриходуем,"Что приходуем");
	глВыбранЛи(СубконтоВалРасх,"Субконто валовых расходов");
	Если НДСнаЗатраты = 0 Тогда
		глВыбранЛи(ВидНДС,"Вид НДС");
	КонецЕсли;
	Если Валюта = Гривня Тогда  
		Если (ЧерезПодотчетноеЛицо = 1) Тогда
			глВыбранЛи(ПодотчетноеЛицо,"Подотчетное лицо");
		КонецЕсли;
	КонецЕсли;
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		Касса = глПолучитьКассу(Фирма,Гривня);

		// накладную за наличные можно создать только за гривни
		Ит = СоздатьОбъект("БухгалтерскиеИтоги");
		Ит.ИспользоватьРазделительУчета(Фирма);
		Ит.ИспользоватьСубконто(ВидыСубконто.НашиДенежныеСчета, Касса, 2);
		Ит.ВыполнитьЗапрос(,ТекущийДокумент(),"30.1",,,,,"С");
		Остаток = Ит.СНД();
		Ит = 0;
		Если Итог("СуммаСНДС") > Остаток Тогда
		    Если Константа.РазрешитьОтрицОстатки = Нет Тогда
			    глКомментарий("В кассе "+Касса+" недостаточно средств для проведения операции! Остаток "+Остаток,0,,"!");
				глВсеВыбрано = 0;
			Иначе
				// разрешены отрицательные остатки,
				// выведем сообщение с другим уровнем детальности
			    глКомментарий("В кассе "+Касса+" недостаточно средств для проведения операции! Остаток "+Остаток,1,,"!");
		    КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если (ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности) и
	((Валюта <> Гривня) или (ВидТорговли = Перечисление.ВидыТорговли.Бартер)) Тогда
	    глКомментарий("Нельзя приходовать бланки строгой отчетности таким способом! Проверьте валюту и вид торговли!",0,,"!");
		глВсеВыбрано = 0;
	КонецЕсли;
	глВсеВыбрано = ?(глВсеВыбрано=0,0,глПроверкаДублейСтрок(Контекст));
	глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС"));
	Возврат глВсеВыбрано;
КонецФункции
                      
// ===============================
Процедура РассчитатьШапку()
	// параметры для проводки по НДС
	СчетНДС = СчетПоКоду("6415");
	СубконтоНДС1 = ВидНДС;
	СубконтоНДС2 = 0;
	// налоговый кредит
	Если Константа.НДСпоВходящимНН = Да Тогда
		// проводку по налоговому кредиту сделаем в 
		// документе ЗаписьКнигиПриобретения
		СчетНДС = СчетПоКоду("6442");
		СубконтоНДС1 = Контрагент;
		СубконтоНДС2 = Договор;
	КонецЕсли;
	ИтСуммаБезНДС = Итог("СуммаБезНДС");
КонецПроцедуры

// ===============================
// Назначение:
//		Отражает движения документа по регистру ВзаиморасчетыПоставщиков
// ===============================
Процедура ДвиженияВзаиморасчеты()
	// расчет итогов по взаиморасчетам
	ВремРег = СоздатьОбъект("Регистры");
	
	Если СчетКонтрагента = СчетПоКоду("3721") Тогда
		ПервыйДок = Договор;
	Иначе
		ПервыйДок = ?(Контрагент.УчетВРазрезеДоговоров.Получить(ДатаДок)= 1,Договор, Контрагент.БазДоговор); //  + umk
	КонецЕсли;

	ФлагОтгрузки = 1; ЗнакОплаты = 1; ФлагВозврата = 0;
	глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,Фирма,?(ВидТорговли=Перечисление.ВидыТорговли.Бартер,-ЗнакОплаты,ЗнакОплаты),Контрагент,ПервыйДок,Валюта);
	тбСуммыПогашения = глПолучитьСуммыДляПогашения(Контекст,Фирма,Валюта);
	тбДолги = глПолучитьИтогиВзаиморасчетов(Контекст,ВремРег,Фирма,?(ВидТорговли=Перечисление.ВидыТорговли.Бартер,-ЗнакОплаты,ЗнакОплаты),Контрагент,ПервыйДок,Валюта);
	
	// проводим по регистрам взаиморасчетов
	глПровестиПоВзаиморасчетам(Контекст,ФлагОтгрузки,ЗнакОплаты,ФлагВозврата,Фирма,тбДолги,тбСуммыПогашения,Валюта,Контрагент,ПервыйДок,ВидТорговли,ДокументОснование);
	// погашение аванса подотчета
	СуммаПогашенияАванса = 0; СуммаАвансаПодотчета = 0;
	Если (ВидТорговли <> Перечисление.ВидыТорговли.Нал) И (ВидТорговли <> Перечисление.ВидыТорговли.Бартер) 
			И (ЧерезПодотчетноеЛицо = 1) И (СуммаПодотчета > 0) Тогда
		ПроцНДС = глПроцентНДС(ВидНДС,ДатаДок);
		глПогашениеДолга(Контекст, 1, тбДолги, Фирма, Контрагент, Перечисление.ВидыОплаты.Оплата,
			?(ПустоеЗначение(ДокументОснование)=1,ТекущийДокумент(),ДокументОснование), ПервыйДок, Перечисление.РежимыОплаты.ПоСчету, 
			СуммаПодотчета*1, СуммаПодотчета*ПроцНДС/(ПроцНДС+100), Валюта, ВидНДС);
	КонецЕсли;

	// формируем проводки по движениям регистров
	НДСВтороеСобытие = 0;
	НДСВсего		   = 0;
	ВзаиморасчетыВсего = 0;	
	РегПоставщики=ВремРег.ВзаиморасчетыПоставщиков;	
	РегПокупатели=ВремРег.ВзаиморасчетыПокупателей;	
	// по регистру поставщиков
	Если (РегПоставщики.ВыбратьДвиженияДокумента(ТекущийДокумент()) = 0)
	И (Итог("СуммаБезНДС") = 0) Тогда
		// по регистру взаиморасчетов нет движений. Данная ситуация может возникнуть если 
		// сумма по документу 0. В этом случае нужно сделать одну проводку по счету взаиморасчетов
		глПроводка(Контекст,,СчетКонтрагента,РегПоставщики.Долг,"Взаиморасчеты с поставщиком.",Итог("Кво"), ,,,
		Контрагент,ПервыйДок,, ,,"ПХ",1,"Взаиморасчеты",1);
	КонецЕсли;	
	// проводки по взаиморасчетам
	Пока РегПоставщики.ПолучитьДвижение() = 1 Цикл
		Если (Константа.ПроводкиПоКассеТолькоОрдерами = Нет) И (ВидТорговли = Перечисление.ВидыТорговли.Нал) 
				И (РегПоставщики.Приход = 1) Тогда
			// при оплате наличными формируются 2 движения, надо взять только одно. Возьмем расход, приход пропускаем
			Продолжить;
		КонецЕсли;

		Если Найти(АвансоваяОтгрузка+ПостОтгрузка+АвансоваяОплата+ПостОплата+ПервоеСобытиеБартерПриход+ВтороеСобытиеБартерПриход,РегПоставщики.КодОперации) = 0 Тогда
			// для формирования проводок по взаиморасчетам нам нужны только эти коды операций
			Продолжить;	
		КонецЕсли;      
		// главная часть сложной проводки по взаиморасчетам
		Если (РегПоставщики.КодОперации <> ПостОплата) И (РегПоставщики.КодОперации <> АвансоваяОплата) Тогда
			глПроводка(Контекст,,СчетКонтрагента,РегПоставщики.Долг,?(ВидТорговли = Перечисление.ВидыТорговли.Бартер,"Бартер: ","")+"Взаиморасчеты с поставщиком.",, ,,,
			Контрагент,ПервыйДок,, ,,"ПХ",1,"Взаиморасчеты",1);
		КонецЕсли;
		ВзаиморасчетыВсего = ВзаиморасчетыВсего + РегПоставщики.Долг;
		// погашение аванса
		Если ((РегПоставщики.КодОперации = АвансоваяОплата) Или (РегПоставщики.КодОперации = ПостОплата)) 
				И (ЧерезПодотчетноеЛицо = 1) И (СуммаПодотчета > 0) Тогда
			// дополнительные проводки для подотчета
			глПроводка(Контекст,СчетКонтрагента,"3721",РегПоставщики.Долг,"Списано с подотчета сотрудника",, Контрагент,ПервыйДок,,
			ПодотчетноеЛицо,,, ,,"ПХ",1,"Подотчет");
			Если РегПоставщики.КодОперации = АвансоваяОплата Тогда 
				СуммаАвансаПодотчета = СуммаАвансаПодотчета + РегПоставщики.Долг;
			КонецЕсли;
			ВзаиморасчетыВсего 	= ВзаиморасчетыВсего - РегПоставщики.Долг;
			Если глДелатьПроводкиПоНалогам(РегПоставщики,"НДС") = 1 Тогда
				НДСВсего = НДСВсего - РегПоставщики.НДС;			
			КонецЕсли;	
		ИначеЕсли (глВыделятьЛиАвансыПоСчету(СчетКонтрагента) = 1) И (РегПоставщики.КодОперации = ПостОтгрузка) Тогда
			СуммаПогашенияАванса = СуммаПогашенияАванса + РегПоставщики.Долг;
		КонецЕсли;      
		// проводки по НДС
		Если глДелатьПроводкиПоНалогам(РегПоставщики,"НДС") = 1 Тогда
			НДСВсего = НДСВсего + РегПоставщики.НДС;			
			Если (ВидТорговли = Перечисление.ВидыТорговли.Нал) И (НДСнаЗатраты = 0) Тогда                             
			    глПроводка(Контекст,СчетНДС,,РегПоставщики.НДС,"НДС",, СубконтоНДС1,СубконтоНДС2,,
		    	,,, ,,"ПХ",1,"Взаиморасчеты");
			ИначеЕсли ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда   					
	        	глПроводка(Контекст,СчетНДС,,РегПоставщики.НДС,"Бартер: НДС",, СубконтоНДС1,СубконтоНДС2,,
				,,, ,,"ПХ",1,"Взаиморасчеты");                                                                         
			Иначе
				Если НДСнаЗатраты = 0 Тогда                             
					Если РегПоставщики.КодОперации = АвансоваяОтгрузка Тогда
				    	глПроводка(Контекст,СчетНДС,,РегПоставщики.НДС,"НДС (первое событие)",, СубконтоНДС1,СубконтоНДС2,,
				    	,,, ,,"ПХ",1,"Взаиморасчеты");                                                                                 
					ИначеЕсли РегПоставщики.КодОперации = ПостОтгрузка Тогда
						// эта операция в регистре проходит без флага НУ, а проводки сделать надо ...
				    	глПроводка(Контекст,"6441",,РегПоставщики.НДС,"НДС (налоговый кредит)",, Контрагент,ПервыйДок,,
				    	,,, ,,"ПХ",1,"Взаиморасчеты");
					ИначеЕсли (РегПоставщики.КодОперации = АвансоваяОплата) И (ЧерезПодотчетноеЛицо = 1) И (СуммаПодотчета > 0) Тогда
				    	глПроводка(Контекст,СчетНДС,"6441",РегПоставщики.НДС,"НДС (первое событие)",, СубконтоНДС1,СубконтоНДС2,,
				    	Контрагент,ПервыйДок,, ,,"ПХ",1);                                                                                 
					КонецЕсли;	        
				Иначе
					// возможно была предоплата, в которой указали сумму НДС.
					Если РегПоставщики.КодОперации = ПостОтгрузка Тогда          
						НДСВтороеСобытие = НДСВтороеСобытие + РегПоставщики.НДС; 
					ИначеЕсли (РегПоставщики.КодОперации = АвансоваяОплата) И (ЧерезПодотчетноеЛицо = 1) И (СуммаПодотчета > 0) Тогда
						НДСВтороеСобытие = НДСВтороеСобытие - РегПоставщики.НДС; 
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
		
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		//// проводки по ВД/ВР
		//Если (глДелатьПроводкиПоНалогам(РегПоставщики,"ВД/ВР") = 1) И (СубконтоВалРасх <> Константа.НиДоходНиРасход)
		//		И (ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности) Тогда
		//	Если ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда
		//    	глПроводка(Контекст,"ВР","ВР",РегПоставщики.СуммаСНДС_НУ-РегПоставщики.НДС,"Бартер: валовые расходы",, Контрагент,Договор,Константа.БартерВалДох,
		//    	Контрагент,Договор,Константа.БартерВалДох, ,,"ПХ",1);             
		//	Иначе
		//		глПроводка(Контекст,"ВР","ВР",РегПоставщики.СуммаСНДС_НУ-РегПоставщики.НДС,"Вал. расходы (поставщик)",, Контрагент,Договор,СубконтоВалРасх,
		//		Контрагент,Договор,СубконтоВалРасх,,,"ПХ",1);
		//	КонецЕсли;
		//КонецЕсли;
		// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		// дополнительные проводки
		Если (ВидТорговли = Перечисление.ВидыТорговли.Нал) И (Константа.ПроводкиПоКассеТолькоОрдерами = Нет) Тогда
			// по кассе при оплате наличными
    		глПроводка(Контекст,СчетКонтрагента,"301",РегПоставщики.Долг,"Оплата наличными",, Контрагент,ПервыйДок,,
    		Касса,,, ,,"ПХ");
		ИначеЕсли (ВидТорговли = Перечисление.ВидыТорговли.Бартер) И (РегПоставщики.КодОперации = ПервоеСобытиеБартерПриход) Тогда
			// для бартерных операций первое событие показываем как аванс в счет встречной поставки
			Сч=?(глВыделятьЛиАвансыПоСчету(СчетКонтрагента)=1,"681","36") + ?(Валюта=Гривня,"1","2");
			глПроводка(Контекст,СчетКонтрагента,Сч,РегПоставщики.Долг,"Бартер: первое событие",, Контрагент,ПервыйДок,,
			Контрагент,ПервыйДок,, ,,"ПХ");						
		КонецЕсли;	
	КонецЦикла;
	// для бартера просмотрим другой регистр		
	Если ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда
		РегПокупатели.ВыбратьДвиженияДокумента(ТекущийДокумент());
		Пока РегПокупатели.ПолучитьДвижение() = 1 Цикл            
			Если Найти(ПервоеСобытиеБартерПриход+ВтороеСобытиеБартерПриход,РегПокупатели.КодОперации) = 0 Тогда
				// для формирования проводок по взаиморасчетам нам нужны только эти коды операций
				Продолжить;	
			КонецЕсли;
			ВзаиморасчетыВсего = ВзаиморасчетыВсего + РегПокупатели.Долг;
			Если (РегПокупатели.КодОперации = ПервоеСобытиеБартерПриход) 
					И (глБартерНаОбщихОснованиях(РегПокупатели.Договор,РегПокупатели.Счет,РегПокупатели.КредДокумент) = 0) Тогда
				// проводки по НДС
				Если глДелатьПроводкиПоНалогам(РегПокупатели,"НДС") = 1 Тогда
					НДСВсего = НДСВсего + РегПокупатели.НДС;			
    				глПроводка(Контекст,"6441",,РегПокупатели.НДС,"Бартер: НДС (кредит)",, Контрагент,ПервыйДок,,
        			,,, ,,"ПХ",1,"Взаиморасчеты");               
				    глПроводка(Контекст,"643","6415",РегПокупатели.НДС,"Бартер: НДС",, Контрагент,ПервыйДок,,
				    ВидНДС,,, ,,"ПХ",1); // налоговые обязательства возникаю ВСЕГДА по первому событию
				КонецЕсли;
				
				// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
				//// проводки по ВД/ВР
				//Если (глДелатьПроводкиПоНалогам(РегПокупатели,"ВД/ВР") = 1) И (Константа.БартерВалДох <> Константа.НиДоходНиРасход)
				//		И (ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности) Тогда
			    //	глПроводка(Контекст,"ВД","ВД",РегПокупатели.СуммаСНДС_НУ-РегПокупатели.НДС,"Бартер: валовые доходы",, Контрагент,Договор,Константа.БартерВалДох,
			    //	Контрагент,Договор,Константа.БартерВалДох, ,,"ПХ",1);
				//КонецЕсли; 
				// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
				
			ИначеЕсли РегПокупатели.КодОперации = ВтороеСобытиеБартерПриход Тогда
				// главная часть сложной проводки по взаиморасчетам
				глПроводка(Контекст,,СчетКонтрагента,РегПокупатели.Долг,"Бартер: Взаиморасчеты с поставщиком.",, ,,,
				Контрагент,ПервыйДок,, ,,"ПХ",1,"Взаиморасчеты",1);                                             
				// дополнительные проводки для бартера 
				Если глВыделятьЛиАвансыПоСчету(СчетКонтрагента)=1 Тогда
					// закрываем встречную поставку
					Сч = "371" + ?(Валюта=Гривня,"1","2");
					глПроводка(Контекст,СчетКонтрагента,Сч,РегПокупатели.Долг,"Бартер: закрываем аванс",, Контрагент,ПервыйДок,,
					Контрагент,ПервыйДок,, ,,"ПХ");						                                                        
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если НДСнаЗатраты = 1 Тогда
		
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		//Если (СубконтоВалРасх <> Константа.НиДоходНиРасход)	И (ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности) Тогда
		//	// необходимо увеличить ВР на сумму НДС
		//	глПроводка(Контекст,"ВР","ВР",Итог("НДС"),"Вал. расх. увеличены на сумму НДС, не относящуюся к нал. кредиту",, Контрагент,Договор,СубконтоВалРасх,
		//	Контрагент,Договор,СубконтоВалРасх,,,"ПХ",1);
		//КонецЕсли;
		
		// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		// если была предоплата ...
		Если НДСВтороеСобытие <> 0 Тогда
			// ... необходимо сторнировать НДС
	    	глПроводка(Контекст,СчетНДС,"6441",-НДСВтороеСобытие,"НДС: Сторно ранее учтенного налогового кредита",, СубконтоНДС1,СубконтоНДС2,,
	    	Контрагент,ПервыйДок,, ,,"ПХ");
		КонецЕсли;					
	КонецЕсли;	
	Если СуммаПогашенияАванса > Макс(0,СуммаАвансаПодотчета) Тогда
		глПроводка(Контекст,СчетКонтрагента,"3711",СуммаПогашенияАванса - Макс(0,СуммаАвансаПодотчета),"Прих: авансовый платеж",, Контрагент,ПервыйДок,,
		Контрагент,ПервыйДок,, ,,"ПХ");				
	КонецЕсли;
КонецПроцедуры

// ===============================
Функция ПроверкаСтроки()
	глВсеВыбрано = 1;
    глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);
   	глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,ТМЦ,НомерСтроки,1)=Да, глВсеВыбрано, 0);
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция РассчитатьСтроку()
	ПересчКво = Кво * Коэффициент;
	Счет = ТМЦ.Счет;
	Если Счет.Выбран()=0 Тогда
	    глКомментарий("Не выбран счет, на который приходуем! Строка "+НомерСтроки, 0,,"!");
	    Возврат 0;
	КонецЕсли;
	// определим субконто по счету ТМЦ
	Если Счет.КоличествоСубконто() = 3 Тогда
		Субк1 = МестоХранения;
		Если ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности Тогда
		    Субк2 = ТМЦ.Субконто;
		Иначе
		    Субк2 = ТМЦ;
		КонецЕсли;    
		Субк3 = ТекущийДокумент();
	ИначеЕсли Счет.КоличествоСубконто() = 2 Тогда
		Если ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности Тогда
			// 33 счет
			Субк1 = МестоХранения;
			Субк2 = ТМЦ.Субконто; Субк3 = 0;
		Иначе
			глКомментарий("Неверно указан счет, на который приходуем ("+Счет+")! Строка "+НомерСтроки, 0,,"!");
			Возврат 0;
		КонецЕсли;
	Иначе
		Субк1 = ТМЦ;
		Субк2 = 0; Субк3 = 0;
	КонецЕсли;
КонецФункции

// ===============================
Процедура ПроводкиСтрока()                                
	ПострСуммаСНДС	= глОкрКорр("Взаим", ?(ИтогСуммаСНДС = 0, 0, ВзаиморасчетыВсего * СуммаСНДС / ИтогСуммаСНДС),2);
	ПострНДС		= глОкрКорр("НДС", ?(ИтогНДС = 0, 0, НДСВсего * НДС / ИтогНДС),2);
	ПострСуммаБезНДС= ПострСуммаСНДС - ПострНДС;
	Себестоимость 	= ?(НДСнаЗатраты=1,ПострСуммаСНДС,ПострСуммаБезНДС);
	
	Если СчетКонтрагента =  СчетПоКоду("3721") Тогда // + umk
		глПроводка(Контекст,Счет,СчетКонтрагента,СуммаСНДС,"Прих:Себестоимость",ПересчКво, Субк1,Субк2,Субк3,
		Контрагент,Договор,, ,,"ПХ",1);
		Возврат;
	КонецЕсли;
	
	Если ЧтоПриходуем = Перечисление.ЧтоПриходуем.БланкиСтрогойОтчетности Тогда
		// бланки строгой отчетности - не запасы, 
	    // на забалансовый
	    глПроводка(Контекст,"08",,ПересчКво*ТМЦ.Цена_Прих,"Прих:бланки строгой отч-сти",ПересчКво, МестоХранения,ТМЦ,,
		,,, ,,"НУ");
		Если ТМЦ.ВидБланкаСтрогойОтчетности = Перечисление.ВидыБланковСтрогойОтчетности.МаркаГербовогоСбора Тогда
		    // через 64
			глПроводка(Контекст,"6425",,Себестоимость,"Прих:марки герб. сбора", ,,,,
			Контрагент,Договор,, ,,"НУ",1,"Взаиморасчеты");
			глПроводка(Контекст,Счет,"6425",Себестоимость,"Прих:марки герб. сбора",ПересчКво, Субк1,Субк2,Субк3,
			,,, ,,"НУ",1,0);
		Иначе
			глПроводка(Контекст,Счет,,Себестоимость,"Прих:бланки строгой отч-сти", ПересчКво,Субк1,Субк2,Субк3,
			Контрагент,Договор,, ,,"НУ",1,"Взаиморасчеты");
		КонецЕсли;
		Если Счет = СчетПоКоду("2091") Тогда                                    
			КодОперации = Закупка;
			глПровестиПартию(Контекст, 1, 0, Фирма, Субк2, Счет, МестоХранения, Контрагент,
				ТекущийДокумент(), ТекущийДокумент(), ПересчКво, Себестоимость, 0,
				КодОперации, 1, Себестоимость, 0);
			// движения по регистру Остатки                                                                          
			глПровестиОстатки(Контекст, ВремРег, Фирма, МестоХранения, Субк2, ,ПересчКво, 1, 0);
		КонецЕсли;	
	Иначе
		Если ЧтоПриходуем = Перечисление.ЧтоПриходуем.ОСиНМАиДрНеобМатАктивы Тогда
			Если ТМЦ.Счет = СчетПоКоду("153") Тогда
				// в составе приходуемых на 153 счет могут быть малоценнные НМА
				ВывестиКомментарийПоМБП = 1;
			КонецЕсли;
		КонецЕсли;
		глПроводка(Контекст,Счет,,Себестоимость,"Прих:Себестоимость",ПересчКво, Субк1,Субк2,Субк3,
		Контрагент,Договор,, ,,"ПХ",1,"Взаиморасчеты");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);
	ВывестиКомментарийПоМБП = 0;
	
	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	РассчитатьШапку();
	
	Если СчетКонтрагента <>  СчетПоКоду("3721") Тогда // + umk
		ДвиженияВзаиморасчеты();
	КонецЕсли;
	
	ОкрПриходСтоимостьВсего = 0;
	глОчиститьКлючОкр("Взаим");
	глОчиститьКлючОкр("НДС");
	ИтогСуммаСНДС 	= Итог("СуммаСНДС");
	ИтогНДС		  	= Итог("НДС");
	// расчитаем базы распределения 
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки()=0 Тогда
		    глНеПроводить(Контекст);
		    Возврат;
		КонецЕсли;
		Если РассчитатьСтроку() = 0 Тогда
		    глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
		ПроводкиСтрока();
		Если ТМЦ.Вид() = "Инвестиции" Тогда //--- УМК Сандомирский В.Ю. (01.12.14)
			СпрТМЦ = СоздатьОбъект("Справочник.Инвестиции");
			СпрТМЦ.НайтиЭлемент(ТМЦ);
			СпрТМЦ.Поставщик = Контрагент;
			СпрТМЦ.ДатаПокупки = ДатаДок;
			
			СпрТМЦ.Цена_Прих	 = СуммаБезНДС; 
			СпрТМЦ.Цена_Прих_Вал = СуммаБезНДС; 
			СпрТМЦ.Валюта		 = Гривня;
			
			СпрТМЦ.Записать();
		КонецЕсли; //... УМК Сандомирский В.Ю. (01.12.14)
	КонецЦикла;

	Если ВывестиКомментарийПоМБП = 1 Тогда
		глКомментарий(""+ТекущийДокумент()+" - если в накладной приходуются малоценные необоротные активы, проводку по валовым расходам нужно сделать вручную.",0,,"!");
	КонецЕсли;
	                   
	глЗаписатьПроводкиВОперацию(Контекст);
	
	Операция.СуммаОперации = Итог("СуммаСНДС");
	Операция.Содержание = "Приходование: "+Строка(ЧтоПриходуем)+?(Валюта=Гривня,""," Импорт") + " " + Примечание;
	Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
