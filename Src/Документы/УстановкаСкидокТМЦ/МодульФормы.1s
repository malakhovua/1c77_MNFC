Перем ТекСпис;
Перем СписокДействий;
Перем СтараяДата;              
Перем НачальнаяДатаДокумента;

Перем Расшифровка;
Перем КэшОборотов;



Процедура ОбновитьОбороты()
	КэшОборотов.УдалитьСтроки();

	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с НДата по КДата;
	|ТМЦ = Регистр.Обороты.ТМЦ;
	|РасходКво = Регистр.Обороты.РасходКво;
	|Функция РасходКвоСумма = Сумма(РасходКво);
	|Группировка ТМЦ без групп;
	|Условие(ТМЦ в списТМЦ);
	|"//}}ЗАПРОС
	;
    
	КДата = Мин(ПолучитьДатуТА(), ТекущаяДата());
	//НДата = Мин(ТекущаяДата() - 7, КДата);
	НДата = КДата - 7;
	
	
	списТМЦ = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(списТМЦ, "ТМЦ");
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

	Пока Запрос.Группировка(1) = 1 Цикл
		КэшОборотов.НоваяСтрока();
		КэшОборотов.ТМЦ = Запрос.ТМЦ;
		КэшОборотов.Кво = Запрос.РасходКвоСумма;
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
	СтараяДата 		 = ДатаДок;	
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
		
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
		Форма.кПодбор.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	
	СписокДействий = глПолучитьСписокДействий("
		|СтруктураПодчиненности,
		|ТоварныйСостав,
		|ВводНаОсновании,
		|ОткрытьВЖурнале,
		|Подчиненные");		

	Если КоличествоСтрок() > 0 Тогда
		ОбновитьОбороты();
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ИзмДата()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);	
КонецПроцедуры // ИзмДата

Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;	
КонецПроцедуры

Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;

	глУстановитьФирму(Контекст);	
КонецПроцедуры 

Процедура Подбор()
	ТекСпис = "";
	ОткрытьПодбор("Справочник.ТМЦ", "ФормаСписка",,1);
КонецПроцедуры

Процедура ОбработкаПодбора(Элемент, Конт)
	Если ТекСпис = "" Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если Элемент = ТМЦ Тогда
			    Возврат;
			КонецЕсли;
		КонецЦикла;
	
		НоваяСтрока();
		ТМЦ = Элемент;
		ОкруглятьДо = 2;
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура Обороты()
	Если ПустоеЗначение(ТекущийДокумент()) = 1 Тогда
		Предупреждение("Документ необходимо записать");
		Возврат;
	КонецЕсли;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Документ", ТекущийДокумент());
	
	ОткрытьФормуМодально("Отчет.УстановленныеСкидки", Расшифровка);
	Расшифровка.Установить("Дата1", 		ДатаДок);
    Расшифровка.Установить("Дата2", 		?(ПустоеЗначение(ДатаПо) = 1, ТекущаяДата(), ДатаПо));
	Расшифровка.Установить("ВыбПокупатель", "");
	Расшифровка.Установить("ВыбНоменклатура", "");
	ТаблицаМФ = Расшифровка.Получить("ТаблицаМФ");
	
	ВыгрузитьТабличнуюЧасть(ТаблицаМФ.ПолучитьЗначение(1, "СписокЭлементов"), "ТМЦ");
	ТаблицаМФ.УстановитьЗначение(1, "ФлВкл", 2);
	
	ТЗПолучателей = Расшифровка.Получить("ТЗПолучателей");
	
	ТЗПолучателей.ВыбратьСтроки();
	Пока ТЗПолучателей.ПолучитьСтроку() = 1 Цикл
		Если ТаблицаМФ.ПолучитьЗначение(6, "СписокЭлементов").НайтиЗначение(ТЗПолучателей.Контрагент) = 0 Тогда
			ТаблицаМФ.ПолучитьЗначение(6, "СписокЭлементов").ДобавитьЗначение(ТЗПолучателей.Контрагент);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаМФ.УстановитьЗначение(6, "ФлВкл", 2);			
	Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ);
	
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 2;
	ОткрытьФорму("Отчет.Обороты");
	
КонецПроцедуры // Обороты

//======================================================================
Функция ПечатьСырьё()
	ТЗВ = СоздатьОбъект("ТаблицаЗначений");
	ТЗП = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТЗП);
	
	глСводПоНормам(0, Контекст, "Норма", 100, ТЗВ);
	СписокВыведенныхРодителей = СоздатьОбъект("СписокЗначений");
	
	Выводимые = СоздатьОбъект("СписокЗначений");
	ТЗВ.ВыбратьСтроки();
	Пока ТЗВ.ПолучитьСтроку() = 1 Цикл
		глВывестиРодителей(ТЗВ, СписокВыведенныхРодителей, Выводимые);
	КонецЦикла;	
	Родитель = "";
	Выводимые.ВыбратьЗначение(Родитель, "Выберите группу");
				
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ПотребностьСырье");
	Таб.ВывестиСекцию("Шапка");
	
	Ном = 1;
	КвоВсего = 0;
	ТЗВ.ВыбратьСтроки();
	Пока ТЗВ.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(Родитель) = 0 Тогда
			Если ТЗВ.ТМЦ.ПринадлежитГруппе(Родитель) = 0 Тогда
				Продолжить;					
			КонецЕсли;
		КонецЕсли;
		
		Таб.ВывестиСекцию("Строка");
		КвоВсего = КвоВсего + ТЗВ.Кво;
		Ном = Ном + 1;
	КонецЦикла;
	
	Таб.ВывестиСекцию("Дно");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать();
КонецФункции

//======================================================================
Процедура ИзмТМЦ()
	ОкруглятьДо = 2;	
КонецПроцедуры // ИзмТМЦ

Функция ПродажиНеделя()
	Если ПустоеЗначение(ТМЦ) = 0 Тогда
		Стр = 0;	
		Если КэшОборотов.НайтиЗначение(ТМЦ, Стр, "ТМЦ") = 1 Тогда
			Возврат КэшОборотов.ПолучитьЗначение(Стр, "Кво");
		КонецЕсли;
	КонецЕсли;
	Возврат 0;
КонецФункции  

КэшОборотов = СоздатьОбъект("ТаблицаЗначений");
КэшОборотов.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
КэшОборотов.НоваяКолонка("Кво", "Число", 15, 3);