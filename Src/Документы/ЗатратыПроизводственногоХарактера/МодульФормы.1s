// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================
Перем СтарыйСчет; 			// значение элемента диалога до выбора
Перем СтароеПодразделениеДок; 	// значение элемента диалога до выбора
Перем НачальнаяДатаДокумента;

// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
Процедура УстДоступностьПоСчету(СтарыйСчет=0)
	Если Счет.Выбран()=0 Тогда // на всякий случай
	    Возврат;
	КонецЕсли;

	Форма.Валюта.Доступность(Счет.Валютный);
	Форма.СуммаВал.Доступность(Счет.Валютный);
	Форма.Кво.Доступность(Счет.Количественный);

	Если ПустоеЗначение(СтарыйСчет) = 0 Тогда // изменили значение счета
		КвоСубконтоОбр = Мин(Счет.КоличествоСубконто(),СтарыйСчет.КоличествоСубконто()); // к-во субконто, которые надо проверить
		Для Инд=1 По КвоСубконтоОбр Цикл
			Если Счет.ВидСубконто(Инд) <> СтарыйСчет.ВидСубконто(Инд)  Тогда
				// разные виды субконто: значение удаляем
				УстановитьАтрибут("Субконто"+СокрЛП(Строка(Инд)),0);
			КонецЕсли;
		КонецЦикла;			
		Для Инд=КвоСубконтоОбр+1 По 3 Цикл
			// удаляем значения оставшихся субконто
			УстановитьАтрибут("Субконто"+СокрЛП(Строка(Инд)),0);
		КонецЦикла;			
		
		Если Счет.Валютный < СтарыйСчет.Валютный Тогда // только 0 < 1
			Валюта=0;
			СуммаВал=0;
		КонецЕсли;

		Если Счет.Количественный < СтарыйСчет.Количественный Тогда // только 0 < 1
			Кво=0;
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры //УстДоступностьВСтроке

// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Затраты производственного характера"));
	Возврат "";
КонецФункции
                                      
// ===============================
Процедура ИзмСчет()
	Если Счет.Выбран()=0 Тогда
		Счет=СтарыйСчет;
		Возврат;
	КонецЕсли;

	Для Инд = 1 По 3 Цикл
		НазначитьТип("Субконто"+Инд,Счет.ВидСубконто(Инд));
	КонецЦикла;
	УстДоступностьПоСчету(СтарыйСчет);
	СтарыйСчет=Счет;
КонецПроцедуры //ИзмСчет

// ===============================
Процедура ИзмПодразделениеДок()
	Если ПодразделениеДок.Выбран()=0 Тогда
		СтароеПодразделениеДок = 0;
		Форма.Подразделение.Доступность(1);
		Возврат;
	КонецЕсли;

	Если ПодразделениеДок = СтароеПодразделениеДок Тогда
		Возврат;
	КонецЕсли;

	Если КоличествоСтрок()>0 Тогда
		Если Вопрос("Подразделение в табличной части будет изменено!","Да+Нет")="Нет" Тогда
			ПодразделениеДок = СтароеПодразделениеДок;
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Подразделение = ПодразделениеДок;
	КонецЦикла;
	СтароеПодразделениеДок = ПодразделениеДок;
	Форма.Подразделение.Доступность(0);

КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ОбработкаВыбораЗначения(ВыбСчет,ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "Счет" Тогда
		Если ВыбСчет.Забалансовый = 1 Тогда
		    Предупреждение("Выбранный счет '"+ВыбСчет.Код+" "+ВыбСчет.Наименование+"' является забалансовым.");
			ФлагСтандОбр = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриРедактированииНовойСтроки()
	СтарыйСчет="";
	// как для пустого счета
	Форма.Валюта.Доступность(0);
	Форма.СуммаВал.Доступность(0);
	Форма.Кво.Доступность(0);
	Подразделение = ПодразделениеДок;
КонецПроцедуры

// ===============================
Процедура ПриНачалеРедактированияСтроки()
	СтарыйСчет=Счет;
	УстДоступностьПоСчету();
КонецПроцедуры

// ===============================
Процедура ВводНового(Признак)
	Если Признак = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	ПроводитьПоЭлементамЗатрат=?(ИспользоватьСчетаРасходов <> Класс9,1,0);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.					Доступность(0);
		Форма.кПровести.			Доступность(0);
		Форма.кДействия.			Доступность(0);
		Форма.кФирма.				Доступность(0);               		
		Форма.кХПодразделениеДок.	Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
	Форма.Счет.ВыполнятьФормулуТолькоПриИзменении(1);
	СтароеПодразделениеДок = ПодразделениеДок;
КонецПроцедуры

// ===============================
// ТЕЛО МОДУЛЯ
// ===============================
СтарыйСчет			= 0;

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");