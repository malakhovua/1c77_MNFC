Перем ОбъектныйУчет[4];

// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(Подразделение,"Подразделение");
	ноАмортГр = 0;
	Спис = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По 4 Цикл
		Если глПолучитьНиО(ноАмортГр,"АмортГр"+Инд,"о ставках амортизации "+Инд+" группы!") = 0 Тогда
			Возврат 0;
		КонецЕсли;
		глПарсить(ноАмортГр.Дополнительно.Получить(ДатаДок),Спис);
		ОбъектныйУчет[Инд] = Число(Спис.Получить("Пообъектно"));
	КонецЦикла;
	Если Производственные=1 Тогда
		Если ВидНеоборотныхАктивов <> НМА Тогда
			глВыбранЛи(Группа,"Группа");
			Если глВсеВыбрано = 1 Тогда
				Если ОбъектныйУчет[Группа]=0 Тогда // пообъектный учет не ведется
					глВыбранЛи(БалСтоимостьГруппы,"Балансовая стоимость группы");
				КонецЕсли;				
			КонецЕсли; 
		КонецЕсли;		
	КонецЕсли;		
	Возврат глВсеВыбрано;
КонецФункции

// ===========================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
    глВыбранЛи(НеоборотныйАктив,"НеоборотныйАктив",НомерСтроки);
	Если Счет.Валютный = 1 Тогда
		глВыбранЛи(Валюта, "Валюта", НомерСтроки);
	Иначе
		Если ПустоеЗначение(Валюта) = 0 Тогда
			Сообщить("В строке " + Строка(НомерСтроки) + " выбран не валютный счёт. Валюта не должна выбираться");
		КонецЕсли;
	КонецЕсли;
	Если глВсеВыбрано = 1 Тогда
		Если НеоборотныйАктив.ВидНеоборотногоАктива <> ВидНеоборотныхАктивов Тогда
			глКомментарий("Документ "+ТекущийДокумент()+" нельзя проводить с необоротными активами вида """+НеоборотныйАктив.ВидНеоборотногоАктива+""". Строка "+НомерСтроки+".",1);		
			глВсеВыбрано=0;
		    Возврат глВсеВыбрано;			
		КонецЕсли;	
		Если НеоборотныйАктив.Производственное <> Производственные Тогда
			глКомментарий("Документ "+ТекущийДокумент()+" нельзя проводить с необоротными активами, для которых "+?(Производственные=1,"не","")+" начисляется амортизация в налоговом учете. Строка "+НомерСтроки+".",1);		
			глВсеВыбрано=0;
		    Возврат глВсеВыбрано;			
		КонецЕсли;	
		Если (Производственные = 1)
		И (ВидНеоборотныхАктивов <> НМА) Тогда
			// группы важны только для производственных ОС/других матер. активов
			Если НеоборотныйАктив.Группа <> Группа Тогда
				глКомментарий("Документ "+ТекущийДокумент()+" нельзя проводить с необоротными активами, не относящимися к группе "+Группа+". Строка "+НомерСтроки+".",1);
				глВсеВыбрано=0;
			    Возврат глВсеВыбрано;			
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
    глВыбранЛи(Счет,"Счет",НомерСтроки);                                      
    Возврат глВсеВыбрано;
КонецФункции

// ===============================
Процедура РассчитатьШапку()
КонецПроцедуры

// ===============================
Процедура РассчитатьСтроку()
КонецПроцедуры

// ===============================
Процедура ПроводкиСтрока()
	// для налогового учета
	СчетГруппы 	= СчетПоКоду("ОС"+Группа);
	СчетУчета 	= НеоборотныйАктив.Счет.		Получить(ТекущийДокумент());
	СчетЗатрат	= НеоборотныйАктив.СчетЗатрат.	Получить(ТекущийДокумент());

	Если Сред(СчетУчета.Код,1,2) = "10" Тогда
		СчетИзноса = СчетПоКоду("131");
	ИначеЕсли Сред(СчетУчета.Код,1,2) = "12" Тогда
		СчетИзноса = СчетПоКоду("133");
		СчетГруппы = СчетПоКоду("НА1");
	Иначе
		СчетИзноса = СчетПоКоду("132");
	КонецЕсли;
	
	глПроводка(Контекст,Счет,"00",ПервСтоимость,"Ввод остатков необоротных активов: первоначальная стоимость",Кво, Подразделение,МестоХранения,НеоборотныйАктив,
	,,,Валюта,ВалСтоимость,"Вв");             
	глПроводка(Контекст,"00",СчетИзноса,Износ,"Ввод остатков необоротных активов: износ",, ,,,
	НеоборотныйАктив,,, ,,"Вв");
    
	ПроводитьПоНУ = 0;
	Если Производственные=1 Тогда
		Если ВидНеоборотныхАктивов <> НМА Тогда
			Если ОбъектныйУчет[Группа]=1 Тогда // пообъектно
				глПроводка(Контекст,СчетГруппы,,БалСтоимость,"Ввод остатков необоротных активов",Кво, НеоборотныйАктив,,,
				,,, ,,"Вв");
				Если БалСтоимость = 0 Тогда
					глКомментарий("Необоротный актив "+НеоборотныйАктив+" оприходовался по налоговому учету с нулевой балансовой стоимостью.",2,,"!");
				КонецЕсли;
				ПроводитьПоНУ = 1;
			КонецЕсли;	
		Иначе
			глПроводка(Контекст,"НА1",,ПервСтоимостьНал,"Ввод остатков НМА: первоначальная стоимость",Кво, НеоборотныйАктив,,,
			,,, ,,"Вв");
			глПроводка(Контекст,,"НА2",ИзносНал,"Ввод остатков НМА: износ",, ,,,
			НеоборотныйАктив,,, ,,"Вв");         
			ПроводитьПоНУ = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если (ПроводитьПоНУ = 0) И 
	((ПустоеЗначение(ПервСтоимостьНал)=0)	
	ИЛИ ((ПустоеЗначение(ИзносНал)=0))) Тогда 
		// по НУ проводить не надо, а суммы указаны
		глКомментарий(""+ТекущийДокумент()+" - для необоротного актива """+НеоборотныйАктив+""", проводки по забалансовым счетам нужно сделать вручную.",1,,"!");
	КонецЕсли;	                    
	
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);

	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	РассчитатьШапку();
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки()=0 Тогда
		    глНеПроводить(Контекст);
		    Возврат;
		КонецЕсли;    		
		РассчитатьСтроку();
		ПроводкиСтрока();				
	КонецЦикла;       
	Если Производственные=1 Тогда
		Если ВидНеоборотныхАктивов <> НМА Тогда
			Если ОбъектныйУчет[Группа]=0 Тогда // пообъектный учет не ведется
				// сделаем одну проводку на сумму бал. стоимости группы
				СчетГруппы 	= СчетПоКоду("ОС"+Группа);
				глПроводка(Контекст,СчетГруппы,,БалСтоимостьГруппы,"Ввод остатков необоротных активов по группе",Итог("Кво"), ,,,
				,,, ,,"Вв");
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	Операция.Содержание = Примечание;
    Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры