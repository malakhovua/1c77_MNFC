// Реализация - продажная стоимость Реализованных ТМЦ
Перем Реализация, Себестоимость;

// ===============================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Фирма,"Фирма");
    глВыбранЛи(Константа.ТоварыВАссортименте,"Константа ""Товары в ассортименте""");
    глВыбранЛи(МестоХранения,"Место хранения"); 
    глВыбранЛи(Подразделение,"Подразделение"); 	
	Если глВсеВыбрано = 1 Тогда
		// место хранения выбрано
		// проверим, чтобы по нему был суммовой учет
		Если МестоХранения.СуммовойУчет = 0 Тогда
			глКомментарий("По месту хранения " + МестоХранения + " не ведется суммовой учет!",0,,"!");
			глВсеВыбрано = 0;
		КонецЕсли;
	КонецЕсли;
	Если глВсеВыбрано = 1 Тогда
		// все выбрано, проверим, нет ли по данному магазину в текущем месяце 
		// уже проведенного документа ОпределениеТорговойНаценки
		Док = СоздатьОбъект("Документ.ОпределениеТорговойНаценки");
		Док.ВыбратьДокументы(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
		Пока Док.ПолучитьДокумент() = 1 Цикл
			Если (Док.Проведен() = 1) И (Док.ТекущийДокумент() <> ТекущийДокумент())
			И (Док.МестоХранения = МестоХранения) И (Док.Фирма = Фирма) Тогда
				глВсеВыбрано = 0;
				Прервать;
			КонецЕсли;
		КонецЦикла;
        Если глВсеВыбрано = 0 Тогда
        	глКомментарий("За " + ПериодСтр(НачМесяца(ДатаДок),КонМесяца(ДатаДок)) + " уже есть проведенный документ " + Док.ТекущийДокумент() + "!", 0,,"!");
		КонецЕсли;
		Док = 0;
	КонецЕсли;
	Возврат глВсеВыбрано;    
КонецФункции

// ======================================
Функция РассчитатьПоБух()                                                
	
	// Запрос для определения сумм наценки
	Ит28 = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит28.ИспользоватьРазделительУчета(Фирма);
	Ит28.ИспользоватьСубконто(ВидыСубконто.МестаХранения, МестоХранения, 2);
	Ит28.ВыполнитьЗапрос(НачМесяца(ДатаДок), КонМесяца(ДатаДок), "2821;2851");
	// Запрос для определения сумм Реализацияизации
	Ит70 = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит70.ИспользоватьРазделительУчета(Фирма);
	Ит70.ИспользоватьСубконто(ВидыСубконто.Подразделения, Подразделение, 2);
	Ит70.ИспользоватьСубконто(ВидыСубконто.МестаХранения, МестоХранения, 2);
	Ит70.ВыполнитьЗапрос(НачМесяца(ДатаДок), КонМесяца(ДатаДок), "702;704");
	// Реализация - продажная стоимость Реализованных ТМЦ
	Реализация = 0; СреднийКоэф = 0;
	Ит70.ВыбратьСчета();
	Если Ит70.ПолучитьСчет(,"702")=1 Тогда
		Реализация = Ит70.КО("С")
	КонецЕсли;
	Если Ит70.ПолучитьСчет(,"704")=1 Тогда
		// Отнимем стоимость возвращенных ТМЦ
		// Знак "+", т.к. при возврате используется сторно
		Реализация = Реализация + Ит70.КО("С")
	КонецЕсли;
	Если Ит28.ПолучитьСчет(,"2851")=1 Тогда    
		// остаток торговой наценки на конец периода
		СреднийКоэф = Ит28.СКК("С");
	КонецЕсли;  
	Остаток = 0;
	Если Ит28.ПолучитьСчет(,"2821")=1 Тогда
		Остаток = Ит28.СКД("С");
	КонецЕсли;
	Если Реализация > Остаток Тогда
		глКомментарий("Сумма реализации по "+МестоХранения+" за "+ПериодСтр(НачМесяца(ДатаДок),КонМесяца(ДатаДок))+
					  " превышает стоимость ТМЦ на конец месяца. Проверьте правильность оформления "+
					  "операций по этому складу.",0);
		Возврат 0;
	КонецЕсли;
	СреднийКоэф = СреднийКоэф / ?(Остаток=0,1,Остаток);
	Себестоимость = (1 - СреднийКоэф)*Реализация;
	Ит = 0;
	Возврат 1;
КонецФункции 

// ======================================
Процедура РассчитатьШапку()                  
	Если РассчитатьПоБух() = 0 Тогда
		глНеПроводить(Контекст);
    	Возврат;
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура РегистрыПартии(Счет)       
	Если ((ПустоеЗначение(Себестоимость) = 0) ИЛИ (ПустоеЗначение(Реализация) = 0)) Тогда
		Регистр.Партии.Фирма = Фирма;
		Регистр.Партии.ТМЦ = Константа.ТоварыВАссортименте;
		Регистр.Партии.Счет = Счет;
		Регистр.Партии.МестоХранения = МестоХранения;
		Регистр.Партии.Поставщик = ПолучитьПустоеЗначение("Справочник.Контрагенты");
		Регистр.Партии.Поставка = ПолучитьПустоеЗначение("Документ");
		Регистр.Партии.ПрихДокумент = ПолучитьПустоеЗначение("Документ");
		Регистр.Партии.КодОперации = РозничнаяПродажа;
		Регистр.Партии.ОстатокТовара = 0;
		Регистр.Партии.Стоимость = Себестоимость;
		Регистр.Партии.ПродСтоимость = Реализация;
		Регистр.Партии.ДвижениеРасходВыполнить();
	КонецЕсли;	
КонецПроцедуры 

// ======================================
Процедура ПроводкиШапка()
	Перем Партия;
	
	СчРеализация = ?(ИспользоватьСчетаРасходов=Класс8, "791", "902");
	Товар = Константа.ТоварыВАссортименте;
	СчетУчета = СчетПоКоду("2821");
	СчетНаценки = СчетПоКоду("2851");
	глПроводка(Контекст,СчРеализация,СчетУчета,Себестоимость,"Опр.торг.наценки: Себестоимость",, Товар.ВидДеятельности,Подразделение,,
	МестоХранения,Товар,, ,,"ОН",1,0);
	глПроводка(Контекст,СчетУчета,СчетНаценки,-(Реализация-Себестоимость),"Опр.торг.наценки: Наценка (сторно)",, МестоХранения,Товар,,
	МестоХранения,Товар,, ,,"ОН",1,0);
	РегистрыПартии(СчетУчета);
КонецПроцедуры 

// ======================================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);
	
	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	РассчитатьШапку();
	ПроводкиШапка();
	глЗаписатьПроводкиВОперацию(Контекст);
	Операция.СуммаОперации = Себестоимость;
	Операция.Содержание = Примечание;
	Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
