//--- УМК Сандомирский В.Ю. (15.04.15) Документ разработан для отражения цен конкурентов у наших клиентов

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение)
	ОтображаемыеСлои = "Общий," + Значение;           
	Форма.ИспользоватьСлой(ОтображаемыеСлои,2);		
КонецПроцедуры

// ===============================
Процедура ЗаполнитьФайлы()
//	Перем КомлектующиеНабора;
//	Перем Индекс;
//	
//	Если Выбран()=0 Тогда
//	    Возврат;
//	КонецЕсли;
//
//	ФЛ = СоздатьОбъект("Справочник.Файлы");
//	ФЛ.ИспользоватьВладельца(ТекущийЭлемент());
//	ФЛ.ВыбратьЭлементы();
//	Индекс = 0; 
//	Файлы.УдалитьСтроки();
//	Пока ФЛ.ПолучитьЭлемент()=1 Цикл
//		Если ФЛ.ПометкаУдаления()=1 Тогда
//		    Продолжить;
//		КонецЕсли;
//
//	    Индекс = Индекс + 1;
//		Файлы.НоваяСтрока(Индекс);
//		Файлы.Ссылка = ФЛ.ТекущийЭлемент();
//		Файлы.Категория = ФЛ.Категория;
//		Файлы.Описание = ФЛ.Наименование;
//	КонецЦикла;
//	
//	Файлы.Сортировать("Категория,Описание");
КонецПроцедуры	// ЗаполнитьФайлы 

//======================================================================
Процедура ПриЗаписи()
	
	//--- записываем ссылки на файлы в документ спутник
	
	Если ТабФайлы.КоличествоСтрок() > 0 Тогда
	
		ДокФайлыОбъект = СоздатьОбъект("Документ.УМК_Файлы");
		Если ПустоеЗначение(ДокФайлы) <> 1 Тогда
			ДокФайлыОбъект.НайтиДокумент(ДокФайлы);
			Если ДокФайлыОбъект.ПометкаУдаления() = 1 Тогда
				ДокФайлыОбъект.СнятьПометкуУдаления();
			КонецЕсли;
			ДокФайлыОбъект.УдалитьСтроки();
			ДокФайлыОбъект.ДатаДок = ДатаДок;
		Иначе	
			ДокФайлыОбъект.Новый();		
			ДокФайлыОбъект.ДатаДок = ДатаДок;
		КонецЕсли;
		
		// Цикл по строкам ТабФайлы
		ТабФайлы.ВыбратьСтроки();
		Пока ТабФайлы.ПолучитьСтроку() = 1 Цикл
				
			ДокФайлыОбъект.НоваяСтрока();
			ДокФайлыОбъект.Картинка 		= ТабФайлы.Картинка;
			ДокФайлыОбъект.Путь 			= ТабФайлы.Путь;
			ДокФайлыОбъект.Описание 		= ТабФайлы.Описание;
				
		КонецЦикла;	
			
		ДокФайлыОбъект.Записать();
	
		ДокФайлы = ДокФайлыОбъект.ТекущийДокумент();
	
	КонецЕсли;
	
	
КонецПроцедуры // ПриЗаписи


//======================================================================
Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Файлы","Файлы");
	Форма.ИспользоватьСлой("Общий,Основной",2);
	
	//--- Заполняем таблицу файлы из документа спутника
	Если ПустоеЗначение(ДокФайлы) <> 1 Тогда
		ТабФайлы.УдалитьСтроки();
		ДокФайлы.ВыбратьСтроки();
		Пока ДокФайлы.ПолучитьСтроку() = 1 Цикл
			
			ТабФайлы.НоваяСтрока();
			ТабФайлы.Картинка 	= ДокФайлы.Картинка;
			ТабФайлы.Путь 		= ДокФайлы.Путь;
			ТабФайлы.Описание 	= ДокФайлы.Описание;
		
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

//======================================================================
Процедура ИзмФирма()
	Если Фирма <> глВосстановитьЗначение(Контекст,"Фирма") Тогда
		Если КоличествоСтрок()>0 Тогда
			Рез = Вопрос("Фирма была изменена. Удалить строки?","Да+Нет");
			Если Рез ="Да" Тогда
				УдалитьСтроки();
			Иначе
				Фирма = глВосстановитьЗначение(Контекст,"Фирма");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		глСохранитьЗначение(Контекст,"Фирма",Фирма);
	КонецЕсли;
КонецПроцедуры      

//======================================================================
Процедура ВводНового(ПризнакКопирования)
	
	Если ПризнакКопирования = 1 Тогда
		
		ДокФайлы = ""; //--- УМК Сандомирский В.Ю. (24.04.15) при копировании
		
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	ИзмФирма();
	
КонецПроцедуры

//======================================================================
Процедура ИзмТорговыйПредставитель()
	
	Если ПустоеЗначение(ТорговыйПредставитель) <> 1 Тогда
		Если ТорговыйПредставитель.ЭтоГруппа() <> 1 Тогда
			Предупреждение(" Торговый представитель должен быть группой справочника ""Контрагенты""");
			ТорговыйПредставитель = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ИзмТорговыйПредставитель()

//======================================================================
Процедура ИзмКлиентУМК()
	Если ПустоеЗначение(КлиентУМК) <> 1 Тогда
		ТорговыйПредставитель = КлиентУМК.Родитель;
		ТипЦенКлиента         = КлиентУМК.КатегорияЦен.Получить(ДатаДок);
		ТипКлиента			  = КлиентУМК.ТипКлиента;
	КонецЕсли;
КонецПроцедуры // ИзмКлиентУМК()

//======================================================================
Процедура ИзмПроцентПоСТроке()
	
		ЦенаРеализацииУМК    = ЦенаПриходаУМК * (1 + ПроцентНаценкиСтрока/100);	
	
КонецПроцедуры // ИзмПроцентПоСТроке()

//======================================================================
Процедура ИзмТМЦ()
	
	Если ПустоеЗначение(ТМЦ) <> 1 Тогда
		
		ЦенаПриходаУМК = глВернутьЦену(ТМЦ, ТипЦенКлиента, ДатаДок);
		ПроцентНаценкиСтрока = ПроцентНаценкиКлиента;
	 	ИзмПроцентПоСТроке();
		
	КонецЕсли;
	
КонецПроцедуры // ИзмТМЦ()

//======================================================================
Процедура ИзмТМЦКонкурентов()

	Если ПустоеЗначение(ТМЦКонкурентов) <> 1 Тогда
		
		Если ПустоеЗначение(ТМЦКонкурентов.ТМЦ.Получить(ДатаДок)) <> 1 Тогда
			ТМЦ = ТМЦКонкурентов.ТМЦ.Получить(ДатаДок);
			ИзмТМЦ();
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры // ИзмТМЦКонкурентов

//======================================================================
Процедура ИзмЦенаПриходаКонкурента()
	//ЦенаРеализацииКонкурента    = ЦенаПриходаКонкурента * (1 + ПроцентНаценкиСтрока/100);	
КонецПроцедуры // ИзмЦенаПриходаКонкурента()

//======================================================================
Функция ВернутьПроцентРазницыПриходныхЦен()
	Если ЦенаПриходаКонкурента > 0 Тогда
		ПроцентРазницыПрихода =  (ЦенаПриходаКонкурента - ЦенаПриходаУМК) / ЦенаПриходаКонкурента * 100;
		Возврат Окр(ПроцентРазницыПрихода,2);
	КонецЕсли;
КонецФункции // ВернутьПроцентРазницыПриходныхЦен

//======================================================================
Функция ВернутьПроцентРазницыРасходныхЦен()
	Если ЦенаРеализацииКонкурента > 0 Тогда
		ПроцентРазницыПрихода =  (ЦенаРеализацииКонкурента - ЦенаРеализацииУМК) / ЦенаРеализацииКонкурента* 100;
		Возврат Окр(ПроцентРазницыПрихода,2);
	КонецЕсли;
КонецФункции // ВернутьПроцентРазницыПриходныхЦен

//======================================================================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ТМЦКонкурентов" Тогда
		Если ПустоеЗначение(Конкурент) = 1 Тогда
			ФлагСтандОбр = 0;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//======================================================================
Функция УстДоступность()
	
	Если Форма.АктивныйЭлемент() = "ТабФайлы" Тогда //--- УМК Сандомирский В.Ю. (17.02.15) расстормаживаю перемещение по списку
		Если ТабФайлы.ТекущаяСтрока() <> 0 Тогда
			Если ТабФайлы.Картинка = 1 Тогда
			    Путь = СокрЛП(ТабФайлы.Путь);
				Попытка
					сКартинка.Загрузить(Путь);
				Исключение
				КонецПопытки;			
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
КонецФункции // УстДоступность()

//======================================================================
Процедура Печать()
	
	//--- УМК Сандомирский В.Ю. (08.11.14)
	ПриЗаписиПерепроводить(0);
	Распечатана = 1;
	Записать();
	ПриЗаписиПерепроводить(1);	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ЦеныКонкурентов"); 
	ТаблТМЦ = СоздатьОбъект("ТаблицаЗначений");

	ВыгрузитьТабличнуюЧасть(ТаблТМЦ);		
	Таб.ВывестиСекцию("Шапка");
	Таб.ПовторятьПриПечатиСтроки(11, 12);    
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		  
		ПечРазницаПриход			= ЦенаПриходаКонкурента - ЦенаПриходаУМК;
		ПечПроцентРазницыПриход		= ВернутьПроцентРазницыПриходныхЦен();	
		ПечРазницаРасход			= ЦенаРеализацииКонкурента - ЦенаРеализацииУМК;	
		ПечПроцентРазницыРасход		= ВернутьПроцентРазницыРасходныхЦен();
		
		Таб.ВывестиСекцию("Строка");			
		
	КонецЦикла;
	
	Таб.ВывестиСекцию("Дно");	
	
	
	
	Таб.ПараметрыСтраницы(2,,,5,5,5,5,4,,1);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: цены конкурентов","");
	
КонецПроцедуры // Печать()


//******************************************************************************
// ОБРАБОТКА СПИСКА ФАЙЛОВ

//======================================================================
Процедура ДобавитьФайл()
 	
	Пт = "";
	Фл = "";
	Если ФС.ВыбратьФайл(0, Фл, Пт, "Выберите файл") = 1 Тогда
	    Путь = СокрЛП(Пт) + Фл;
	Иначе
		Возврат;
	КонецЕсли;
	Если СокрЛП(Путь) <> "" Тогда
		расш = ВРЕГ(Прав(СокрЛП(Путь), 3));
	    Если (расш = "BMP") или (расш = "JPG") Тогда
	        Картинка = 1;
	    КонецЕсли;
	КонецЕсли;
	ТекОписание = "";
	ВвестиСтроку(ТекОписание,"Введите описание файла",200);
	ТабФайлы.НоваяСтрока();
	ТабФайлы.Картинка  = Картинка;
	ТабФайлы.Путь	= Путь;
	ТабФайлы.Описание  = ТекОписание;
	
КонецПроцедуры	// ДобавитьКомпл

//======================================================================
// Модально открывается форма текущего в списке файлов для редактирования
Процедура ИзменитьФайл()
	
	глВвестиЗначениеВТаблицу(ТабФайлы,ТабФайлы.ТекущаяСтрока(),ТабФайлы.ТекущаяКолонка(),,);	
	
КонецПроцедуры	// ИзменитьКомпл

//======================================================================
// Удаляет текущий файл в списке 
Процедура УдалитьФайл()
	
	Если Вопрос("Удалить файл?",1)=2 Тогда
		Возврат;
	КонецЕсли;
	Поз=ТабФайлы.ТекущаяСтрока();
	
	Если Поз = 0 Тогда
	    // не выбрана строка таблицы
		Возврат;
	КонецЕсли;

	ТабФайлы.УдалитьСтроку(Поз);
	
	Если Поз <= ТабФайлы.КоличествоСтрок() Тогда
		ТабФайлы.ТекущаяСтрока(Поз);
	Иначе
		ТабФайлы.ТекущаяСтрока(ТабФайлы.КоличествоСтрок());
	КонецЕсли;
	
КонецПроцедуры

//======================================================================
Процедура ОткрытьФайл()
	Поз = ТабФайлы.ТекущаяСтрока();
	Если Поз <> 0 Тогда
	    ЗапуститьПриложение(СокрЛП(ТабФайлы.ПолучитьЗначение(Поз, "Путь")));
	КонецЕсли;
КонецПроцедуры

//******************************************************************************

// ===============================
ТабФайлы.НоваяКолонка("Картинка","Число",1,,"Картинка",10);
ТабФайлы.НоваяКолонка("Путь","Строка",,,"Путь к файлу",30);
ТабФайлы.НоваяКолонка("Описание","Строка",,,"Описание",30);

ТабФайлы.ВыводитьПиктограммы("Картинка");

ТорговыйПредставитель.ВыборГруппы(1);