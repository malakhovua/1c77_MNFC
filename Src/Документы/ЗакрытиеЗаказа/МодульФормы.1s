// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Срок;
Перем СписокДействий;
Перем СтараяДата;

Перем НачальнаяДатаДокумента;


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Снятие резерва"));     
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмДата()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

// ===============================
Процедура Заполнить(Зап = 0)
	Перем Резервы;
	Перем Итоги;
	
	ВыбТовар = ПолучитьПустоеЗначение("Справочник.ТМЦ");		
	
	// Если это начальное заполнение, то ТМЦ выбирать не надо
	Если Зап = 0 Тогда
		Если ВвестиЗначение(ВыбТовар,"Выберите ТМЦ, по которому снимаем заказ","Справочник.ТМЦ",,) = 0 Тогда
			Если Вопрос("Заполнить по всем заказ?","Да+Нет") = "Нет" Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если КоличествоСтрок() <> 0 Тогда	
		Ответ = Вопрос("Заполнение по заказам. Очистить табличную часть?","Да+Нет+Отмена");
		Если Ответ = "Отмена" Тогда
			Возврат;
		ИначеЕсли Ответ = "Да" Тогда
			УдалитьСтроки();
		КонецЕсли;	
	КонецЕсли;	
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьСубконто(ВидыСубконто.ЗаказыПоставщикам, , 1);
	Ит.Опции(1);
	Ит.ВыполнитьЗапрос(, ?(ДатаЗапроса = Дата(0), ДатаДок, ДатаЗапроса), "ЗП");
	
	Ит.ВыбратьСубконто(1);
	Пока Ит.ПолучитьСубконто(1) = 1 Цикл
		ДокРезерва = Ит.Субконто(1);
		Если ДокРезерва.ДатаДок > ДатаДок Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока();
		ДокументРезерва = ДокРезерва;
	КонецЦикла;
КонецПроцедуры	

// ===============================
Процедура ИзмФирма()
	СтараяФирма = Фирма;
	глУстФирма(Контекст);	
	Если СтараяФирма = Фирма Тогда
		Возврат;
	КонецЕсли;
	
	// фирма изменилась
	Если ПустоеЗначение(Фирма) = 0 Тогда
		Если КоличествоСтрок() <> 0 Тогда
			Ответ = Вопрос("Перезаполнить табличную часть?","Да+Нет+Отмена");
			Если Ответ = "Отмена" Тогда
				Фирма = "";
			Иначе
				УдалитьСтроки();
				Если Ответ = "Да" Тогда
					Заполнить(1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
			
	Если ПустоеЗначение(Фирма) = 1 Тогда
		Фирма = СтараяФирма;
		глУстановитьНомер(Контекст);
	КонецЕсли;
КонецПроцедуры //ИзмФирма

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в документе
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
КонецПроцедуры	


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ВводНаОсновании(ДокОснование)
	Автор = глПользователь;
	ДатаДок=РабочаяДата();
	глЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
	НоваяСтрока();
	ДокументРезерва=ДокОснование;
КонецПроцедуры

// ===============================
Процедура ВводНового(Скопирован)
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	глЗаполнитьШапку(Контекст);
	ДатаДок=РабочаяДата();
	Основание="Истек срок резервирования ТМЦ.";
//	Заполнить(1);
КонецПроцедуры

// ===============================
Процедура ОбработкаВыбораЗначения(Выб,ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "ДокументРезерва" Тогда
		Если Выб.Фирма <> Фирма Тогда
			Предупреждение("Выбран документ, принадлежащий другой фирме!");
			ФлагСтандОбр = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;

	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.			Доступность(0);
		Форма.кПровести.	Доступность(0);
		Форма.кДействия.	Доступность(0);
		Форма.кЗаполнить.	Доступность(0);
		Форма.кФирма.		Доступность(0);               		
		Форма.кОсн.			Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;

	СтараяДата = ДатаДок;
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

Процедура Ведомость()
	Если ПустоеЗначение(ДокументРезерва) = 0 Тогда
		Спис = СоздатьОбъект("СписокЗначений");
		Расшифровка = СоздатьОбъект("СписокЗначений");
		Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
	    Расшифровка.Установить("РазделительУчета", Фирма);
	   	Расшифровка.Установить("Дата1", ДокументРезерва.ДатаДок);
	   	Расшифровка.Установить("Дата2", ДатаЗапроса);
	   	Расшифровка.Установить("Счет", СчетПоКоду("ЗП"));
	   	Расшифровка.Установить("ДанныеПоСубсчетам", 0);
   		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.ЗаказыПоставщикам);
   		Расшифровка.Установить("ВыбСубконто1", Спис);
   		Расшифровка.Установить("Субконто1", ДокументРезерва);
   		Расшифровка.Установить("ОтборСубконто1", 1);
   		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.Контрагенты);
   		Расшифровка.Установить("ВыбСубконто2", Спис);
   		Расшифровка.Установить("Субконто2", "");
   		Расшифровка.Установить("ОтборСубконто2", 1);
   		Расшифровка.Установить("ВидСубконто3", ВидыСубконто.ТМЦ);
   		Расшифровка.Установить("ВыбСубконто3", Спис);
   		Расшифровка.Установить("Субконто3", "");
   		Расшифровка.Установить("ОтборСубконто3", 1);
   		
   		глРасшифровка = Расшифровка;
   		глФлагРасшифровки = 1; глОбновить = 1;
   		ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
   		глФлагРасшифровки = 0; глОбновить = 0;
   	КонецЕсли;
КонецПроцедуры

// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");