Перем СтараяДата;

//======================================================================
Процедура УстВидимостьКолонкиВалюта() Далее

//===============================
Процедура ПриОткрытии()     
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	Если НазваниеНабораПрав() = "ПроизводствоПросмотр" Тогда
		Если ПустоеЗначение(Автор) = 0 Тогда
		    Если Автор <> глПользователь Тогда
		        Форма.ТолькоПросмотр(1);
		    КонецЕсли;		    
		КонецЕсли;
	КонецЕсли;
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
		Форма.кПодбор.Доступность(0);          
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	СтараяДата = ДатаДок;
	
	ПриЗаписиПерепроводить(1);
	Если ТипЗначенияСтр(Форма.Параметр) = "ТаблицаЗначений" Тогда		
		ЗагрузитьТабличнуюЧасть(Форма.Параметр);
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			глПриИзмененииТовара(Контекст);
		КонецЦикла;
	КонецЕсли;
	
	Если НазваниеНабораПрав() = "Склады" Тогда
		Форма.ЦенаСНДС.Видимость(0);
		Форма.СуммаСНДС.Видимость(0);
		Форма.кПечать.Видимость(0);
	КонецЕсли;	
	УстВидимостьКолонкиВалюта();
КонецПроцедуры
               
// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

// ===============================
Процедура ВводНового(Копируем)
	ДатаСозд = ТекущаяДата();
	Автор = "";
	Если Копируем = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);	
	НеФормироватьПроводки = глПользователь.НеФПУмолчаниеЗаказ;
КонецПроцедуры   

// ===============================
Процедура ОбработкаПодбора(Выб) //Предопределенная процедура
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры //Обработка подбора

Процедура Печать()
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Печать");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0,0,0);
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Заказ поставщику","");
КонецПроцедуры

// ===============================
Процедура Подбор()
	глПодбор(Контекст,"ТМЦ","ДляПодбора")
КонецПроцедуры

// ======================================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, СтараяДата)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Если КоличествоСтрок() > 0 Тогда
		ПолучитьСтрокуПоНомеру(1);
		НГруппа = ТМЦ.НГруппа;	    
	КонецЕсли;
	
	Попытка
		АвтоВремяОтключить();
		УстановитьВремя(0,0,1);
	Исключение КонецПопытки;
	Если ПустоеЗначение(Автор) = 1 Тогда
	    Автор = глПользователь;
		АвторЗ = глПользователь;
	КонецЕсли;	
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Идент, Флаг)
	Если (Идент = "Поставщик") или (Идент = "ТМЦ") Тогда
		глВыборИзЗаказа = 1;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьЦены()
	Если КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Вопрос("Заполнить цены заново?", "Да+Нет") = "Да" Тогда
	    ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ЦенаСНДС = глВернутьЦену(ТМЦ, Константа.ЗакупЦена, ДатаДок);
			глВыч_Суммы_Накл(Контекст, 1);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ВводНаОсновании(ДОсн)
	Для Инд = 1 По Метаданные.ОбщийРеквизитДокумента() Цикл
		Атр = Метаданные.ОбщийРеквизитДокумента(Инд).Идентификатор;
		УстановитьАтрибут(Атр, ДОсн.ПолучитьАтрибут(Атр));
	КонецЦикла;
	Для Инд = 1 По Метаданные.Документ("ЗаказПоставщикуП").РеквизитШапки() Цикл
		Атр = Метаданные.Документ("ЗаказПоставщикуП").РеквизитШапки(Инд).Идентификатор;
		УстановитьАтрибут(Атр, ДОсн.ПолучитьАтрибут(Атр));
	КонецЦикла;
	ТЗУже = СоздатьОбъект("ТаблицаЗначений");
	ТЗУже.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗУже.НоваяКолонка("Кво", "Число", 15, 3);
	Док = СоздатьОбъект("Документ");
	Док.ВыбратьПодчиненныеДокументы(,,ДОсн);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если (Док.ПометкаУдаления() = 0) И (Док.Вид() = "ЗаказПоставщику") Тогда
			Док.ВыбратьСтроки();
			Пока Док.ПолучитьСтроку() = 1 Цикл
				ТЗУже.НоваяСтрока();
				ТЗУже.ТМЦ = Док.ТМЦ;
				ТЗУже.Кво = Док.Кво;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ТЗУже.Свернуть("ТМЦ", "Кво");
	ДОсн.ВыбратьСтроки();
	Пока ДОсн.ПолучитьСтроку() = 1 Цикл
		НоваяСтрока();
		Для Инд = 1 По Метаданные.Документ("ЗаказПоставщикуП").РеквизитТабличнойЧасти() Цикл
			Атр = Метаданные.Документ("ЗаказПоставщикуП").РеквизитТабличнойЧасти(Инд).Идентификатор;
			УстановитьАтрибут(Атр, ДОсн.ПолучитьАтрибут(Атр));
		КонецЦикла;
		Стр = 0;
		Склад = ТМЦ.НГруппа.МХ;
		Если ТЗУже.НайтиЗначение(ТМЦ, Стр, "ТМЦ") = 1 Тогда
			Кво = Кво - ТЗУже.ПолучитьЗначение(Стр, "Кво");			
			Если Кво <= 0 Тогда
				УдалитьСтроку();
			Иначе
				глВыч_суммы_накл(Контекст);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ДокументОснование = ДОсн;
КонецПроцедуры

Процедура ИзмТМЦ()
	Склад = ТМЦ.НГруппа.МХ;
	глПриИзмененииТовара(Контекст)
КонецПроцедуры

//======================================================================
Процедура УстВидимостьКолонкиВалюта()
	Форма.ЦенаВал.Видимость(?((ВалютаЗ.Выбран() = 1) И (ВалютаЗ <> Гривня), 1, 0));
КонецПроцедуры // УстВидимостьКолонкиВалюта();

//======================================================================
Процедура ИзмЦенаВал()
	ЦенаСНДС = ЦенаВал * Курс;
	глВыч_Суммы_Накл(Контекст, 1);
КонецПроцедуры // ИзмЦенаВал

//======================================================================
Процедура ИзмКурс()
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ЦенаВал = 0 Тогда
			ЦенаВал = ЦенаСНДС;
		КонецЕсли;
		
		ИзмЦенаВал();
	КонецЦикла;	
КонецПроцедуры // ИзмКурс

//======================================================================
Процедура ИзмВалюта()
	УстВидимостьКолонкиВалюта();
	Курс = ВалютаЗ.Курс.Получить(ДатаДок) / ВалютаЗ.Кратность.Получить(ДатаДок);
	ИзмКурс();
КонецПроцедуры // ИзмВалюта

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|ТоварныйСостав"); //--- УМК Сандомирский В.Ю. (22.12.14)