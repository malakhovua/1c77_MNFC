Перем Фрм;
Перем НачальнаяДатаДокумента;

// ===============================
Процедура ИзмДатаДок()
КонецПроцедуры //ИзмДатаДок

// ===============================
Процедура ИзмРСчет()
	Если РСчет.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если РСчет.Владелец <> Фирма Тогда
		РСчет = Фирма.РС;
		Предупреждение("Выбранный расчетный счет принадлежит другой фирме!");
	КонецЕсли;
	Номер = РСчет.ПоследнийРасхДок + 1;
	Валюта = РСчет.Валюта;
	Счет = ?(Валюта=Гривня,СчетПоКоду("63.1"),СчетПоКоду("63.2"));
КонецПроцедуры

// ===============================
Процедура ИзмСуммаСНДС()
	Если ОплатаНалога = 0 Тогда
		ВидНДС = глВосстановитьЗначение(Контекст,"БазНДС");
		Если ПустоеЗначение(ВидНДС) = 0 Тогда
			Ст = ВидНДС.Ставка.Получить(ДатаДок);
			НДС = СуммаСНДС*Ст/(1+Ст);
		Иначе
			НДС = 0;
		КонецЕсли;
	Иначе
		НДС = 0;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмОплатаНалога()
	Если ОплатаНалога = 1 Тогда
		Договор = 0;
		НазначениеПлатежа = 0;
	Иначе
		ВидНалога = 0;
	КонецЕсли;
	ИзмСуммаСНДС();
КонецПроцедуры 

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Платежное поручение"));
	Форма.тВидНалога.Доступность(ОплатаНалога);
	Форма.ВидНалога.Доступность(ОплатаНалога);
	Форма.ИспользоватьСлой("Основной,Основание",2);
    Форма.кВзаиморасчеты.Доступность(1-ОплатаНалога);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		
		Форма.кФирма.Доступность(0);
		Форма.кОсн.Доступность(0);
	Иначе
		Форма.кОсн.Доступность(1-ОплатаНалога);
	КонецЕсли;	
	Форма.Фирма.Видимость(0);
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмДокументОснование()
	Если ПустоеЗначение(ДокументОснование) = 1 Тогда
	    Возврат;
	КонецЕсли;
    
	Если ДокументОснование.Вид() = "ЗаявлениеНаАккредитив" Тогда
		Предупреждение("Выбранный документ " +ДокументОснование+ " не может быть основанием!");
	    ДокументОснование = 0;
	КонецЕсли;
	глЗаполнитьШапкуНаОсн(Контекст,ДокументОснование);
	Если ПустоеЗначение(СуммаСНДС) = 1 Тогда
		СуммаСНДС = глДолгКонтрагента(Контекст ,+1, Фирма, Контрагент, Договор,ДокументОснование);
		Если глЕстьРеквизитМнЧ("СуммаСНДС",ДокументОснование.Вид()) = Да Тогда
			Если СуммаСНДС = 0 Тогда
				СуммаСНДС = ДокументОснование.Итог("СуммаСНДС");
			КонецЕсли;
		КонецЕсли;
		Попытка
			// возьмем сумму НДС из документа-основания
			НДС = ДокументОснование.Итог("НДС");
		Исключение
			// нет такого реквизита в док-основании, 
			Попытка
				// возьмем сумму НДС из документа-основания 
				// как разницу между "с НДС" и "Без НДС
				НДС = ДокументОснование.Итог("СуммаСНДС") 
					- ДокументОснование.Итог("СуммаБезНДС");
			Исключение
				// ничего не вышло... - рассчитаем из ставки по умолчанию
				ИзмСуммаСНДС();
			КонецПопытки;
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры //ИзмДокументОснование

// ======================================
Процедура УстРСчетКонтрагента()
	Если РСчетКонтрагента.Выбран() = 0 Тогда
	    СпрДС = СоздатьОбъект("Справочник.ДенежныеСчета");
		СпрДС.ИспользоватьВладельца(Контрагент);
		СпрДС.ВыбратьЭлементы();
		ДС = 0; КвоДС = 0;
		Пока СпрДС.ПолучитьЭлемент() = 1 Цикл
			Если СпрДС.Валюта <> Рсчет.Валюта Тогда
			    Продолжить;
			КонецЕсли;
			ДС = СпрДС.ТекущийЭлемент();
			КвоДС = КвоДС + 1;
		КонецЦикла;
		Если КвоДС = 1 Тогда
		    РСчетКонтрагента = ДС;
		КонецЕсли;
	КонецЕсли;      
КонецПроцедуры 

// ===============================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;
	Если Договор.Выбран() = 1 Тогда
	    Если Договор.Контрагент <> Контрагент Тогда
			Договор = 0;
	    КонецЕсли;
	КонецЕсли;     
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если ДокументОснование.Контрагент <> Контрагент Тогда
			ДокументОснование = 0;
		КонецЕсли;	
	КонецЕсли;	
	                            
	Если (Константа.ПодставлятьОсновнойДоговор=Да)
	И (ПустоеЗначение(Контрагент.БазДоговор)=0) 
	И (ПустоеЗначение(Договор)=1) Тогда
		Если Фирма = Контрагент.БазДоговор.Фирма Тогда
			Договор = Контрагент.БазДоговор;
		КонецЕсли;	
	КонецЕсли;                              
	
	Если РСчетКонтрагента.Владелец <> Контрагент Тогда
		РСчетКонтрагента = 0;
		УстРСчетКонтрагента();
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(Док)
	Фирма = Док.Фирма;
	глУстановитьНомер(Контекст);
	РСчет = Фирма.РС;
	ИзмРСчет();
	Контрагент = Док.Контрагент;
	ИзмКонтрагент();
	глЗаполнитьШапкуНаОсн(Контекст, Док);
	Если Док.Вид() = "ЗаявлениеНаАккредитив" Тогда
		Если Док.Фирма.РС.Выбран() = 1 Тогда
			Если Док.Фирма.РС.Валюта <> Гривня Тогда
		    	Предупреждение("Предусмотрен ввод только гривневых платежек!");
	    		СтатусВозврата(0);
			КонецЕсли;
		КонецЕсли;
		СуммаСНДС = Док.Сумма;
		ИзмСуммаСНДС();
	ИначеЕсли Док.Вид() = "Договор" Тогда
		Договор = Док;			
		ИзмДокументОснование();
	Иначе
		ДокументОснование = Док;
		ИзмДокументОснование();
	КонецЕсли;
	Если (Док.Вид() = "УслугиСтороннихОрганизаций") или (Найти(Док.Вид(),"ПриходнаяНакладная") > 0) Тогда
		Счет = Док.СчетКонтрагента;
	Иначе		
		Счет = ?(Валюта=Гривня,СчетПоКоду("63.1"),СчетПоКоду("63.2"));
	КонецЕсли;
	АвтоПодстСуммыНДС = Перечисление.ПодстановкаСуммыНДС.БезСуммыНДС;
КонецПроцедуры

// ===============================
Процедура УстСальдоНалога()
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Если ВидНалога.Выбран() = 1 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.ШкалаСтавок, ВидНалога, 2);
	КонецЕсли;
	Ит.ВыполнитьЗапрос(?(Выбран()=1,ТекущийДокумент(),ДатаДок),,Счет,,,,,"С");
	СуммаСНДС = Ит.СНК();
	НДС = 0;
КонецПроцедуры

// ===============================
Процедура ИзмСчет()
	Если (Счет.Выбран() = 0) Или (ОплатаНалога = 0) Тогда
	    Возврат;
	КонецЕсли;
	УстСальдоНалога();
КонецПроцедуры

// ===============================
Процедура ОчиститьЗаказ()
	Если Договор.Выбран() = 1 Тогда
	    Договор = 0;
		СуммаСНДС = 0;
		НДС = 0;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВыборОснования()  
	// Процедура по кнопке редактирования основания в докумнете
	СтарДоговор = Договор;
	СтарОснование = ДокументОснование;
	
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если (ДокументОснование <> СтарОснование)
		И (ДокументОснование.Вид() <> "РасходыНаПриобретение") Тогда
			ВводНаОсновании(ДокументОснование);	
		КонецЕсли;	
	КонецЕсли;	          
КонецПроцедуры	

// ===============================
Процедура ИзмВидНалога()
	Если ВидНалога.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;
	Счет = ВидНалога.Счет;
	ИзмСчет();
	Контрагент = ВидНалога.Получатель;
	РСчетКонтрагента = ВидНалога.РасчетныйСчет;
	ИзмКонтрагент();
КонецПроцедуры

// ===============================
Процедура ИзмНазначениеПлатежа()
	Если ПустоеЗначение(НазначениеПлатежа) = 0 Тогда
		Если НазначениеПлатежа.Шаблон = 1 Тогда
			Содержание = Шаблон("[]"+НазначениеПлатежа.ПолнНаименование);
		Иначе
			Содержание = НазначениеПлатежа.ПолнНаименование;
		КонецЕсли;
	Иначе
		Содержание = "";
	КонецЕсли;
	
	Если ОплатаНалога = 1 Тогда
		Возврат;
	КонецЕсли;
	
	// определим документ, который уместно подставить в содержанин
	ДокументДляСодержания = 0;
    Если ПустоеЗначение(ДокументОснование) = 0 Тогда
        ДокументДляСодержания = ДокументОснование;
	ИначеЕсли ПустоеЗначение(Договор) = 0 Тогда
		ДокументДляСодержания = Договор;
	КонецЕсли;
	
	Если ПустоеЗначение(ДокументДляСодержания) = 0 Тогда
	    Если ДокументДляСодержания.Вид() = "Договор" Тогда
	        Содержание = Содержание + " згідно договору № ";
			Содержание = Содержание + ?(ПустоеЗначение(ДокументДляСодержания.НомерДоговора)=1
				,СокрЛП(ДокументДляСодержания.НомерДок)
				,СокрЛП(ДокументДляСодержания.НомерДоговора));
			Содержание = Содержание + " від " + ДокументДляСодержания.ДатаДок;
	    ИначеЕсли ДокументДляСодержания.Вид() = "СчетВходящий" Тогда
	        Содержание = Содержание + " згідно рахунку № ";
			Содержание = Содержание + ?(ПустоеЗначение(ДокументДляСодержания.НомерСчета)=1
				,СокрЛП(ДокументДляСодержания.НомерДок)
				,СокрЛП(ДокументДляСодержания.НомерСчета));
			Содержание = Содержание + " від " + ДокументДляСодержания.ДатаДок;
	    ИначеЕсли Найти(ДокументДляСодержания.Вид(),"ПриходнаяНакладная")<>0 Тогда
	        Содержание = Содержание + " згідно накладної № ";
			Содержание = Содержание + ?(ПустоеЗначение(ДокументДляСодержания.НомерРасходнойНакладной)=1
				,СокрЛП(ДокументДляСодержания.НомерДок)
				,СокрЛП(ДокументДляСодержания.НомерПриходнойНакладной));
			Содержание = Содержание + " від " + ДокументДляСодержания.ДатаДок;
	    ИначеЕсли Найти(ДокументДляСодержания.Вид(),"УслугиСтороннихОрганизаций")<>0 Тогда
	        Содержание = Содержание + " згідно акту № ";
			Содержание = Содержание + ?(ПустоеЗначение(ДокументДляСодержания.НомерАктаВыполненныхРабот)=1
				,СокрЛП(ДокументДляСодержания.НомерДок)
				,СокрЛП(ДокументДляСодержания.НомерАктаВыполненныхРабот));
			Содержание = Содержание + " від " + ДокументДляСодержания.ДатаДок;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	Если РСчет.Владелец <> Фирма Тогда
		РСчет = Фирма.РС;
		ИзмРСчет();
	КонецЕсли;
КонецПроцедуры 

// ===============================
Функция ФормированиеСуммыТекстом(Подст)
	Перем Номер;
	
	СтрПодст="";
    СтрСумма="";
	СтрНДС="";
	СтрКон="";

	Номер=Подст.ПорядковыйНомер();
	Если (Номер=1) ИЛИ (Номер=3) Тогда
	   	СтрСумма="Сума "+СокрЛ(Формат(СуммаСНДС,"Ч18.2,'"))+" грн";
	КонецЕсли;
	Если (Номер=1) ИЛИ (Номер=2) Тогда
		Если НДС=0 Тогда
			СтрНДС=", без ПДВ";
		Иначе
			СтрНДС=", у т.ч. ПДВ("+Константа.БазНДС+") "+СокрЛ(Формат(НДС,"Ч18.2,'"))+" грн";
		КонецЕсли;
	КонецЕсли;
	СтрПодст=СтрСумма+СтрНДС;
	Если ПустаяСтрока(СтрПодст)=0 Тогда
		СтрПодст=СтрПодст+"."
	КонецЕсли;
	Возврат СтрПодст;
КонецФункции

// ===============================
Процедура Печать()
	Если РСчет.Валюта <> Гривня Тогда
	    Предупреждение("Предусмотрена печать только гривневых платежек!");
	    Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	ИмяФайлаПечатнойФормы = КаталогИБ()+"ExtForms\PrnForms\PP_ukr.mxl";
	Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы) = 1 Тогда
		Таб.ИсходнаяТаблица(ИмяФайлаПечатнойФормы);
	Иначе
		Таб.ИсходнаяТаблица("Таблица_Укр");
	КонецЕсли;
	// строго на украинском!
	глУстПропись(Гривня, "у"); 
	Эк = 2;
	СуммаСНДС_стр = Сред(Формат(Цел(СуммаСНДС), "ЧПД"),1,Найти(Формат(Цел(СуммаСНДС), "ЧПД"),"гр") - 2) + " грн. " + Формат((СуммаСНДС - Цел(СуммаСНДС))*100,"Ч(0)2") + " коп.";
	Если ВвестиЧисло(Эк,"Введите количество экземпляров",1,0) = 0 Тогда
		Возврат;
	КонецЕсли;   
	Если ПустоеЗначение(ДатаДок) = 0 Тогда
		ЧислоДаты = ?(ДатаЧисло(ДатаДок)<10,""""+"0"+ДатаЧисло(ДатаДок)+"""",""""+ДатаЧисло(ДатаДок)+"""");
		МесяцДаты = Прав(Формат(ДатаДок,"Д ДДММММГГГГ"),СтрДлина(Формат(ДатаДок,"Д ДДММММГГГГ"))-2);
	КонецЕсли;
    Для Ном = 1 По Эк Цикл
		Таб.ВывестиСекцию("Форма");
		Если Ном/2 = Окр(Ном/2) Тогда
		    Таб.НоваяСтраница();
		КонецЕсли;
	КонецЦикла;
	Таб.Опции(0,0,0,0,"");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Платежное поручение","");
КонецПроцедуры

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Если РСчет.Выбран() = 1 Тогда
			Номер = РСчет.ПоследнийРасхДок + 1;
		КонецЕсли;
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	РСчет = Фирма.РС;
	ИзмРСчет();
	Счет = СчетПоКоду("63.1");
	АвтоПодстСуммыНДС = Перечисление.ПодстановкаСуммыНДС.БезСуммыНДС;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		//Форма.кДействия.Доступность(0);
		Форма.кФирма.Доступность(0);               		
		Форма.кОсн.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	

	Форма.Счет.ВыборГруппы(0);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	Если РСчет.ПоследнийРасхДок < Номер Тогда
		Сч = СоздатьОбъект("Справочник.НашиДенежныеСчета");
		Сч.НайтиЭлемент(РСчет);
		Сч.ПоследнийРасхДок = Номер;
		Сч.Записать();
	КонецЕсли;

	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры       

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");
