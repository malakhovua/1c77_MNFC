Перем НачальнаяДатаДокумента;

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Начисление амортизации ОС и НМА"));
	Форма.КПечать.Доступность(Проведен()*(1-Модифицированность()));
	Форма.Фирма.Видимость(0);
    Возврат "";
КонецФункции

// ===============================
Функция Период()
	Если НалоговыйУчет = 1 Тогда
	    // за квартал
		Возврат "За "+ПериодСтр(НачКвартала(ДатаДок),КонКвартала(ДатаДок));
	Иначе
		// за месяц
		Возврат "За "+ПериодСтр(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
	КонецЕсли;
КонецФункции

// ===============================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кФирма.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	НачальнаяДатаДокумента = ДатаДок;
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	УстДоступность();
КонецПроцедуры

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	НалоговыйУчет = 2;
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры                    

// ===============================
Процедура Печать()
Перем ОбъектныйУчет[4], АмортГр[4];
	глУстПропись(Гривня);

	Таб = СоздатьОбъект("Таблица");
	
	// главный бухгалтер
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить ("Дата",ДатаДок);
	Расшифровка.Установить ("Фирма",Фирма);
	Расшифровка.Установить("АмортГр1","");
	Расшифровка.Установить("АмортГр2","");
	Расшифровка.Установить("АмортГр3","");
	Расшифровка.Установить("АмортГр4","");
	Расшифровка.Установить("ГлавныйБухгалтер","");
	
	глПолучитьДанные(Расшифровка);

	АмортГр[1] = Расшифровка.Получить("АмортГр1");
	АмортГр[2] = Расшифровка.Получить("АмортГр2");
	АмортГр[3] = Расшифровка.Получить("АмортГр3");
	АмортГр[4] = Расшифровка.Получить("АмортГр4");
	ГлавныйБухгалтер = глФИО(Расшифровка.Получить("ГлавныйБухгалтер"),1);

	ноАмортГр = 0;
	Спис = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По 4 Цикл
		Если глПолучитьНиО(ноАмортГр,"АмортГр"+Инд,"о ставках амортизации "+Инд+" группы!") = 1 Тогда
			глПарсить(ноАмортГр.Дополнительно.Получить(ДатаДок),Спис);
			ОбъектныйУчет[Инд] = Число(Спис.Получить("Пообъектно"));
		Иначе
			ОбъектныйУчет[Инд] = 0;
		КонецЕсли;
	КонецЦикла;
	    
	ПонижающийКоэффициент = 0;
	НиО = СоздатьОбъект("Справочник.ШкалаСтавок");
	Если НиО.НайтиПоКоду("ПонижКоэф") = 1 Тогда
	    ПонижающийКоэффициент = НиО.ТекущийЭлемент().Ставка.Получить(ДатаДок);
	КонецЕсли;
	
	Если НалоговыйУчет = 1 Тогда
		// налоговый учет
		Таб.ПараметрыСтраницы(1); // портрет
		Если Константа.ФормыНаУкраинском = Да Тогда
			Таб.ИсходнаяТаблица("НалоговыйУчет_Укр");
		Иначе
			Таб.ИсходнаяТаблица("НалоговыйУчет");
		КонецЕсли;
		
		ТекПериодСтр = ПериодСтр(НачКвартала(ДатаДок),КонКвартала(ДатаДок));

		Ит1 = СоздатьОбъект("БухгалтерскиеИтоги");
		Ит1.ИспользоватьРазделительУчета(Фирма);
		Ит1.ВключатьСубсчета(1);
		Ит1.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
		Ит1.ВыполнитьЗапрос(НачКвартала(ДатаДок),,"ОС,НА1");

		Для НомерГруппы = 1 По 4 Цикл
			Если НомерГруппы > 1 Тогда
				Таб.НоваяСтраница();
			КонецЕсли;

			Группа = ?(Константа.ФормыНаУкраинском = Да,"Група ","Группа ")+НомерГруппы;
			СчетНА = "ОС."+НомерГруппы;
			КоэффициентАмортизации = АмортГр[НомерГруппы];
			Таб.ВывестиСекцию("ОСШапка");
			Ит = СоздатьОбъект("БухгалтерскиеИтоги");
			Ит.ИспользоватьРазделительУчета(Фирма);
			Если ОбъектныйУчет[НомерГруппы]=1 Тогда // пообъектно
				Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
				Ит.ВыполнитьЗапрос(ТекущийДокумент(),ТекущийДокумент(),СчетНА);
				
				ИтСумма = 0;
				Ит.ВыбратьСубконто(1,,,,,"ИнвенНомер"); // упорядочим по инвентарным номерам
				Пока Ит.ПолучитьСубконто(1) = 1 Цикл
					НА = Ит.Субконто(1);
					НА.ИспользоватьДату(НачКвартала(ДатаДок),1);
					
					БалСтоимость = 0;
					Если Ит1.ПолучитьСубконто(1,,НА)=1 Тогда
						Если Ит1.ПолучитьСчет(,СчетНА)=1 Тогда
					    	БалСтоимость = Ит1.СНД("С");
						КонецЕсли;
					КонецЕсли;
					
					Кво = 1;
					Сумма = Ит.КО();
			
				    Наименование = "Инв. № "+Сокрлп(НА.ИнвенНомер)+", "+НА.ПолнНаименование;
		
					Таб.ВывестиСекцию("ОССтрока");
					ИтСумма = ИтСумма + Сумма;
				КонецЦикла;
				Таб.ВывестиСекцию("ОСДно");
			Иначе
				НА = 0;
				Наименование = ?(Константа.ФормыНаУкраинском = Да,"В цiлому по групi","В целом по группе");
				Ит.ВыполнитьЗапрос(ТекущийДокумент(),ТекущийДокумент(),СчетНА);
		    	БалСтоимость = Ит.СНД("С");
		    	Кво = Ит.СНД("К");
				Сумма = Ит.КО();
				ИтСумма = Сумма;
				Таб.ВывестиСекцию("ОССтрока_Группа");
				Таб.ВывестиСекцию("ОСДно");
			КонецЕсли;
		КонецЦикла;
			
		// НМА
		Таб.НоваяСтраница();
		СчетНА = "НА.1";
		
		Таб.ВывестиСекцию("НМАШапка");
		
		Ит.ИспользоватьРазделительУчета(Фирма);
		Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
		Ит.ВыполнитьЗапрос(ТекущийДокумент(),ТекущийДокумент(),"НА.2");
		
		ИтСумма = 0;
		Ит.ВыбратьСубконто(1);
		Пока Ит.ПолучитьСубконто(1) = 1 Цикл
			НА = Ит.Субконто(1);
			НА.ИспользоватьДату(НачКвартала(ДатаДок),1);
			
			ПервСтоимость = 0; Кво = 0;
			Если Ит1.ПолучитьСубконто(1,,НА)=1 Тогда
				Если Ит1.ПолучитьСчет(,СчетНА)=1 Тогда
					ПервСтоимость = Ит1.СНД("С");
					Кво = Ит1.СНД("К");
				КонецЕсли;
			КонецЕсли;
			Сумма = Ит.КО();
	
		    Наименование = НА.ПолнНаименование;
			Таб.ВывестиСекцию("НМАСтрока");
			ИтСумма = ИтСумма + Сумма;
		КонецЦикла;
		Таб.ВывестиСекцию("НМАДно");
	Иначе
		// бухгалтерский учет
		Таб.ПараметрыСтраницы(2); // ландшафт
		Если Константа.ФормыНаУкраинском = Да Тогда
			Таб.ИсходнаяТаблица("БухгалтерскийУчет_Укр");
		Иначе
			Таб.ИсходнаяТаблица("БухгалтерскийУчет");
		КонецЕсли;
		
		// подготовим данные для формирования запроса
		// счет износа и период
		ТекПериодСтр = ПериодСтр(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
		
		Ит = СоздатьОбъект("БухгалтерскиеИтоги");
		Ит.ИспользоватьРазделительУчета(Фирма);
		Ит.ВключатьСубсчета(-1,1); // без групп
		Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
		Ит.ВыполнитьЗапрос(ТекущийДокумент(),ТекущийДокумент(),"13,191",,,2);
		
		Ит1 = СоздатьОбъект("БухгалтерскиеИтоги");
		Ит1.ИспользоватьРазделительУчета(Фирма);
		Ит1.ВключатьСубсчета(1);
		Ит1.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
		Ит1.ВыполнитьЗапрос(НачМесяца(ДатаДок),,"10,11,12,13,191");
		
		ИтКво = 0;ИтСумма = 0;ИтПерв = 0;ИтОст = 0;
		Ит.ВыбратьСчета();
		Пока Ит.ПолучитьСчет() = 1 Цикл
			СчетИзноса = Ит.Счет.Код;
			Если СчетИзноса = "131" Тогда
				Ит.ВыбратьСубконто(1,,,,,"ИнвенНомер"); // по инвентарным номерам
				ВидНеоборотныхАктивовСтр = "Основные средства";
				СчетНА = "10";
			ИначеЕсли СчетИзноса = "132" Тогда
				Ит.ВыбратьСубконто(1);
				ВидНеоборотныхАктивовСтр = "Другие необоротные материальные активы";
				СчетНА = "11";
			ИначеЕсли СчетИзноса = "191" Тогда
				Ит.ВыбратьСубконто(1);
				ВидНеоборотныхАктивовСтр = "Гудвил";
				СчетНА = "191";
			Иначе
				Ит.ВыбратьСубконто(1);
				ВидНеоборотныхАктивовСтр = "Нематериальные активы";
				СчетНА = "12";
			КонецЕсли;
			
			Если ИтСумма <> 0 Тогда
				// уже был минимум один проход
				Таб.НоваяСтраница();
				ИтКво = 0;
				ИтСумма = 0;
				ИтПерв = 0;
				ИтОст = 0;
			КонецЕсли;
			Таб.ВывестиСекцию("Шапка");
			
			Пока Ит.ПолучитьСубконто(1) = 1 Цикл
				НА = Ит.Субконто(1);
				НА.ИспользоватьДату(НачМесяца(ДатаДок),1);
								
				ПервСтоимость = 0; Износ = 0;
				Если Ит1.ПолучитьСубконто(1,,НА)=1 Тогда
					Если Ит1.ПолучитьСчет(,СчетНА)=1 Тогда
						ПервСтоимость = Ит1.СНД("С");
						Кво = Ит1.СНД("К");
					КонецЕсли;
					Если Ит1.ПолучитьСчет(,СчетИзноса)=1 Тогда
						Износ = Ит1.СНК("С");
					КонецЕсли;
				КонецЕсли;
				
				ОстСтоимость = ПервСтоимость - Износ;
				
				Если СчетНА = "10" Тогда
				    Наименование = "Инв. № "+Сокрлп(НА.ИнвенНомер)+", "+НА.ПолнНаименование;
				Иначе
				    Наименование = НА.ПолнНаименование;
				КонецЕсли;
				Сумма = Ит.КО();
				
				МетодРасчетаИзноса = НА.МетодРасчетаИзноса; 
				ЛиквидационнаяСтоимость = "-";
				СрокИспользования = "-";
				НормаАмортизации = "-";
				РасчетныйОбъемПроизводства = "-";
				ТекущийОбъемПроизводства = "-";
				
				Если МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПрямолинейноеСписание Тогда
					ЛиквидационнаяСтоимость = НА.ЛиквидационнаяСтоимость;
					СрокИспользования = НА.СрокИспользования;
				ИначеЕсли (МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.УменьшениеОстатка) или
			              (МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.УскоренноеУменьшениеОстатка) Тогда
					НормаАмортизации = НА.НормаАмортизации * 12;
					ЛиквидационнаяСтоимость = НА.ЛиквидационнаяСтоимость;
					СрокИспользования = НА.СрокИспользования;
				ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.СуммЕдиниц Тогда
					ЛиквидационнаяСтоимость = НА.ЛиквидационнаяСтоимость;
					РасчетныйОбъемПроизводства = НА.РасчетныйОбъемПроизводства;
					НА.ИспользоватьДату(КонМесяца(ДатаДок),1);
    				ТекущийОбъемПроизводства = НА.ТекущийОбъемПроизводства;
    				НА.ИспользоватьДату(НачМесяца(ДатаДок),1);
				ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйМесяц Тогда
					НормаАмортизации = 1;
				ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйИПоследнийМесяц Тогда
					НормаАмортизации = 0.5;
				ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Налоговый Тогда
					НормаАмортизации = АмортГр[НА.Группа];
					Если НА.ВидНеоборотногоАктива = НМА Тогда
				    	СрокИспользования = НА.СрокИспользования;
					КонецЕсли;
				КонецЕсли;	
			    
				Если НА.ВидНеоборотногоАктива = Перечисление.ВидыНеоборотныхАктивов.Гудвил Тогда
					ПечПервСтоимость = "-";
				Иначе
					ПечПервСтоимость = Формат(ПервСтоимость, "Ч12.2-,");
				КонецЕсли;
				
				Таб.ВывестиСекцию("Строка");
				ИтКво = ИтКво + Кво;
				ИтСумма = ИтСумма + Сумма;
				ИтПерв = ИтПерв + ПервСтоимость;
				ИтОст  = ИтОст + ОстСтоимость;
			КонецЦикла;
			
			Если НА.ВидНеоборотногоАктива = Перечисление.ВидыНеоборотныхАктивов.Гудвил Тогда
				ПечИтПерв = "-";
			Иначе
				ПечИтПерв = Формат(ИтПерв, "Ч12.2-,");
			КонецЕсли;
			
			Таб.ВывестиСекцию("Дно");
		КонецЦикла;
	КонецЕсли;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,0,0);
	Таб.Показать("ПЕЧАТЬ: Ведомость начисления амортизации","");
КонецПроцедуры

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
