// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(Подразделение,"Подразделение");
	глПроверитьТипПодразделения(Подразделение);
	
	докНезавершенка = СоздатьОбъект("Документ."+Вид());
	докНезавершенка.ВыбратьДокументы(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
	Пока докНезавершенка.ПолучитьДокумент() = 1 Цикл
		Если (докНезавершенка.ПометкаУдаления() = 1) или (докНезавершенка.Проведен() = 0) Тогда
		    Продолжить; // нерабочий документ
		КонецЕсли;
		Если докНезавершенка.ТекущийДокумент() = ТекущийДокумент() Тогда
		    Продолжить;
		КонецЕсли;
		Если (докНезавершенка.Фирма <> Фирма) или (докНезавершенка.Подразделение <> Подразделение) Тогда
		    Продолжить; // другие фирма/подразделение
		КонецЕсли;
		глКомментарий("По подразделению "+Подразделение+" уже введено незавершенное производство документом "+докНезавершенка.НомерДок+" от "+докНезавершенка.ДатаДок+"!",0);
		глВсеВыбрано = 0;
	КонецЦикла;

    Возврат глВсеВыбрано;
КонецФункции //ПроверкаШапки

// ===============================
Функция ПроверкаСтроки()
	глВсеВыбрано = 1;
	глВыбранЛи(Продукция,"Продукция");
	глВыбранЛи(ВидДеятельности,"Вид деятельности");
	Если глВсеВыбрано = 1 Тогда
		Если ВидДеятельности.ПозаказноеПроизводство = Нет Тогда
			Если ПустоеЗначение(Заказ) = 0 Тогда
		    	глКомментарий("Для вида деятельности """+ВидДеятельности+""" заказ не может быть указан!",0,,"!");
				глВсеВыбрано = 0;
			КонецЕсли;
		Иначе
			глВыбранЛи(Заказ,"Заказ");
		КонецЕсли;
	КонецЕсли;
КонецФункции //ПроверкаСтроки

// ===============================
// Назначение:
//		Выполняет движения по регистру НормативныеЗатраты
// Аргументы:
//		Конт	- документ незавершенки
//		Знак	- знак движений (1 - обычные, -1 - сторно)
// Возвращает:
//		1 - успешно, 0 - ошибка
Функция ДвиженияНормативныеЗатраты(Конт, Знак)
	
	Регистр.НормативныеЗатраты.Фирма = Фирма;
	Регистр.НормативныеЗатраты.Подразделение = Подразделение;
	Регистр.НормативныеЗатраты.ТипЗатрат = ?(Знак = 1, НормыНаНезаверш, НормыНаНезавершПрош);
	
	Конт.ВыбратьСтроки();
	Пока Конт.ПолучитьСтроку() = 1 Цикл
		тбНормы = 0;
		Если глПолучитьНормы(Конт.Продукция,Конт.Ед,Конт.Кво,Конт.ДатаДок,Конт.Заказ,тбНормы) = 0 Тогда
		    Возврат 0;
		КонецЕсли;
		
		Регистр.НормативныеЗатраты.ВидДеятельности = Конт.ВидДеятельности;
		Регистр.НормативныеЗатраты.Продукция = Конт.Продукция;
		Регистр.НормативныеЗатраты.Заказ = Конт.Заказ;
		
		тбНормы.ВыбратьСтроки();
		Пока тбНормы.ПолучитьСтроку() = 1 Цикл
			Регистр.НормативныеЗатраты.СтатьяКалькуляции = тбНормы.СтатьяКалькуляции;
			Регистр.НормативныеЗатраты.Материал = тбНормы.Материал;
			
			Коэф = 1;
			Если ПустоеЗначение(тбНормы.Материал) = 1 Тогда
			    // нематериальные затраты включаем с учетом коэффициента завершенности
				// предполагается: материалы списаны сразу, производственный процесс не закончен
				Коэф = Конт.КоэфЗавершенности;
			КонецЕсли;
			
			Регистр.НормативныеЗатраты.Количество = тбНормы.Кво * Знак * ?(тбНормы.ВидЭлемента = Перечисление.ВидыЭлементовСоставаПродукции.ВозвратныйОтход,-1,1);
			Регистр.НормативныеЗатраты.Сумма = тбНормы.Сумма * Коэф * Знак;
			
			Если (Регистр.НормативныеЗатраты.Количество <> 0) или (Регистр.НормативныеЗатраты.Сумма <> 0) Тогда
				Регистр.НормативныеЗатраты.ДвижениеВыполнить();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
    Возврат 1;
КонецФункции //ДвиженияНормы

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);
	
	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
		    глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// найдем незавершенку прошлого месяца
	глКомментарий("Выполняется сторнирование незавершенки прошлого месяца",2);
	
	докНезавершенкаПрошлого = СоздатьОбъект("Документ."+Вид());
	докНезавершенкаПрошлого.ВыбратьДокументы(НачМесяца(ДобавитьМесяц(ДатаДок,-1)), КонМесяца(ДобавитьМесяц(ДатаДок,-1)));
	Пока докНезавершенкаПрошлого.ПолучитьДокумент() = 1 Цикл
		Если (докНезавершенкаПрошлого.ПометкаУдаления() = 1) или (докНезавершенкаПрошлого.Проведен() = 0) Тогда
		    Продолжить; // нерабочий документ
		КонецЕсли;
		Если (докНезавершенкаПрошлого.Фирма <> Фирма) или (докНезавершенкаПрошлого.Подразделение <> Подразделение) Тогда
		    Продолжить; // другие фирма/подразделение
		КонецЕсли;
		// нашли незавершенку прошлого месяца
		// нормы по ней отражены в прошлом месяце и в текущем (при выпуске)
		// нужно сторнировать движения за прошлый месяц
		Если ДвиженияНормативныеЗатраты(докНезавершенкаПрошлого, -1) = 0 Тогда
		    глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
		Прервать; // документ с незавершенкой только один за месяц
	КонецЦикла;
	
	// выполним движения по незавершенке текущего
	глКомментарий("Выполняются движения по незавершенке текущего месяца",2);
	Если ДвиженияНормативныеЗатраты(Контекст, 1) = 0 Тогда
	    глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
