Перем НачальнаяДатаДокумента;

// ===============================
Процедура ВводНового(Копируем)
	Если Копируем = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;  	
	глУстановитьФирму(Контекст);
КонецПроцедуры

// ===============================
Процедура ВыбратьСтатью()
	спрСтатьиКалькуляции = СоздатьОбъект("Справочник.СтатьиКалькуляции");
	Если спрСтатьиКалькуляции.Выбрать("Выберите статью расчета завершенности","ФормаСписка") = 1 Тогда
		СтатьяРасчетаЗавершенности = спрСтатьиКалькуляции.ТекущийЭлемент();
		//тСтатьяРасчетаЗавершенности = СтатьяРасчетаЗавершенности.Наименование;
	Конецесли;	
КонецПроцедуры

// ===============================
Процедура Рассчитать()
	Если ПустоеЗначение(СтатьяРасчетаЗавершенности) = 1 Тогда
		спрСтатьиКалькуляции = СоздатьОбъект("Справочник.СтатьиКалькуляции");
		Если спрСтатьиКалькуляции.Выбрать("",) = 1 Тогда
			СтатьяРасчетаЗавершенности = спрСтатьиКалькуляции.ТекущийЭлемент();
		Иначе
			Предупреждение("Не выбрана статья расчета завершенности, расчет не может быть выполнен!");
			Возврат;
		КонецЕсли;	
	КонецЕсли;	                 
	                
	спПродукция 		= СоздатьОбъект("СписокЗначений");
	спЗаказ 			= СоздатьОбъект("СписокЗначений");
	спВидДеятельности 	= СоздатьОбъект("СписокЗначений");

	ВыгрузитьТабличнуюЧасть(спПродукция,		"Продукция");
	ВыгрузитьТабличнуюЧасть(спЗаказ,			"Заказ");
	ВыгрузитьТабличнуюЧасть(спВидДеятельности,	"ВидДеятельности");

	ПозицияИтогов = "";
	Если Выбран()=1 Тогда // документ существует
		Если СравнитьТА()<0 Тогда // и находится до ТА
			ПозицияИтогов = СформироватьПозициюДокумента(ТекущийДокумент(),-1); 
		КонецЕсли;
	Иначе // документ новый
	 	Если ДатаДок<ПолучитьДатуТА() Тогда // и находится до ТА
	 		ПозицияИтогов = ДатаДок;
		КонецЕсли;
	КонецЕсли;      
	тПериодПроизв = ?(ПустоеЗначение(ПозицияИтогов)=1,"","Период с ПозицияИтогов по ПозицияИтогов;");

	ТекстЗапросаПроизв = тПериодПроизв + "
		|Обрабатывать НеПомеченныеНаУдаление;
		|Без Итогов;
		|Фирма 				= Регистр.ПроизводственныеЗатраты.Фирма;
		|ВидДеятельности 	= Регистр.ПроизводственныеЗатраты.ВидДеятельности;
		|Подразделение 		= Регистр.ПроизводственныеЗатраты.Подразделение;
		|Продукция 			= Регистр.ПроизводственныеЗатраты.Продукция;
		|СтатьяКалькуляции 	= Регистр.ПроизводственныеЗатраты.ВидЗатрат.СтатьяКалькуляции;
		|Заказ 				= Регистр.ПроизводственныеЗатраты.Заказ;
		|Сумма 				= Регистр.ПроизводственныеЗатраты.Сумма;

		|Функция СуммаПроизв = КонОст(Сумма);

		|Группировка Продукция 			Без Упорядочивания Без Групп ;
		|Группировка ВидДеятельности 	Без Упорядочивания;
		|Группировка Заказ 				Без Упорядочивания;

		|Условие (Фирма = Фирма);
		|Условие (Подразделение = Подразделение);
		|Условие (Продукция в спПродукция);
		|Условие (ВидДеятельности в спВидДеятельности);	
		|Условие (СтатьяКалькуляции = СтатьяРасчетаЗавершенности);
		|Условие (Заказ в спЗаказ);
		|";

	ПозицияНачМесяца = Мин(НачМесяца(ДатаДок),ПолучитьДатуТА());
	тПериодНормыНаВыпуск = "Период с ПозицияНачМесяца" + ?(ПустоеЗначение(ПозицияИтогов)=1,""," по ПозицияИтогов") + ";";

	ТекстЗапросаНормыНаВыпуск = тПериодНормыНаВыпуск + "
		|Обрабатывать НеПомеченныеНаУдаление;
		|Без Итогов;
		|Фирма 				= Регистр.НормативныеЗатраты.Фирма;
		|ВидДеятельности 	= Регистр.НормативныеЗатраты.ВидДеятельности;
		|Подразделение 		= Регистр.НормативныеЗатраты.Подразделение;
		|Продукция 			= Регистр.НормативныеЗатраты.Продукция;
		|СтатьяКалькуляции 	= Регистр.НормативныеЗатраты.СтатьяКалькуляции;
		|Заказ 				= Регистр.НормативныеЗатраты.Заказ;
		|Сумма 				= Регистр.НормативныеЗатраты.Сумма;

		|Функция СуммаНормыНаВыпуск = Сумма(Сумма);

		|Группировка Продукция 			Без Упорядочивания Без Групп ;
		|Группировка ВидДеятельности 	Без Упорядочивания;
		|Группировка Заказ 				Без Упорядочивания;

		|Условие (Фирма = Фирма);
		|Условие (Подразделение	= Подразделение);
		|Условие (Продукция в спПродукция);
		|Условие (ВидДеятельности в спВидДеятельности);
		|Условие (СтатьяКалькуляции = СтатьяРасчетаЗавершенности);
		|Условие (Заказ в спЗаказ);
		|";

	ЗапросПроизв = СоздатьОбъект("Запрос");
	Если ЗапросПроизв.Выполнить(ТекстЗапросаПроизв) = 0 Тогда
		Возврат;
	КонецЕсли;                

	ЗапросНормыНаВыпуск = СоздатьОбъект("Запрос");
	Если ЗапросНормыНаВыпуск.Выполнить(ТекстЗапросаНормыНаВыпуск) = 0 Тогда
		Возврат;
	КонецЕсли;

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл

		ЗатратПроизв = 0;
		Если ЗапросПроизв.Получить(Продукция,ВидДеятельности,Заказ) = 1 Тогда
			ЗатратПроизв = ЗапросПроизв.СуммаПроизв;
		КонецЕсли;

		ЗатратНормыНаВыпуск = 0;
		Если ЗапросНормыНаВыпуск.Получить(Продукция,ВидДеятельности,Заказ) = 1 Тогда
			ЗатратНормыНаВыпуск = ЗапросНормыНаВыпуск.СуммаНормыНаВыпуск;
		КонецЕсли;

		ЗатратПоНормам = 0;
		тбНормативныеЗатраты = СоздатьОбъект("ТаблицаЗначений");
		Если глПолучитьНормы(Продукция,Ед,?(ПустоеЗначение(Кво)=1,1,Кво),ДатаДок,Заказ,тбНормативныеЗатраты) = 1 Тогда
			текНомерСтроки = 0;
			Если тбНормативныеЗатраты.НайтиЗначение(СтатьяРасчетаЗавершенности,текНомерСтроки,"СтатьяКалькуляции") = 1 Тогда
				ЗатратПоНормам 	= тбНормативныеЗатраты.ПолучитьЗначение(текНомерСтроки,"Сумма");
			КонецЕсли;	
		КонецЕсли;	
		                                   
		Если ПустоеЗначение(ЗатратПоНормам) = 1 Тогда
			глКомментарий("Статья калькуляции " + СтатьяРасчетаЗавершенности 
							+ " не вошла в нормы на производство продукции " + Продукция
							+ " по виду деятельности " + ВидДеятельности
							+ ?(ПустоеЗначение(Заказ)=1,""," по заказу " + Заказ)
							+ ". Коэффициент завершенности по строке " + НомерСтроки + " рассчитан не будет.",1);			
			Продолжить;
		КонецЕсли;             

		Если ЗатратПроизв - ЗатратНормыНаВыпуск < 0 Тогда
		    КоэфЗавершенности = 0;
			глКомментарий("Фактические затраы меньше затрат по нормам (возможно экономия). Коэффициент завершенности в строке " + НомерСтроки + " установлен в значение 0.",2);			 
		Иначе
			КоэфЗавершенности = (ЗатратПроизв - ЗатратНормыНаВыпуск) / ЗатратПоНормам;
		КонецЕсли;
		
		Если КоэфЗавершенности > 1 Тогда
			глКомментарий("Коэффициент завершенности  по заказу "+Заказ+" больше 1. 
							|Проверьте правильность указания нормы затрат и правильность отражения фактических затрат.",1);			
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Форма.Заголовок(глЗаголовок(Контекст,"Незавершенное производство"));
	
	Если Форма.ТолькоПросмотр()=0 Тогда
		Если КоличествоСтрок() > 0 Тогда
		    Форма.кРассчитать.Доступность(1);
		Иначе
		    Форма.кРассчитать.Доступность(0);
		КонецЕсли;
	КонецЕсли;	
		
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмПродукция()                        
	Если ПустоеЗначение(Продукция) = 0 Тогда
		Ед = Продукция.ЕдиницаПоУмолчанию; 
		Коэффициент = Ед.Коэффициент;
		ВидДеятельности = Продукция.ВидДеятельности;
		КоэфЗавершенности = 1;
	КонецЕсли;	
КонецПроцедуры

// ===============================
Процедура ИзмКоэфЗавершенности()
	Если КоэфЗавершенности > 1 Тогда
		глКомментарий("Коэффициент завершенности  по заказу "+Заказ+" больше 1. 
		|Проверьте правильность указания нормы затрат и правильность отражения фактических затрат.",1);			
	КонецЕсли;	
КонецПроцедуры
    
// ===============================
Процедура ОбработкаПодбора(Выб) //Предопределенная процедура
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры //Обработка подбора

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"открытие");
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1); 
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кРассчитать.Доступность(0);
		Форма.кПодбор.Доступность(0);          
		
		Форма.кФирма.Доступность(0);
		Форма.кСтатьяРасчетаЗавершенности.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
КонецПроцедуры


// ===============================
Процедура ПриЗаписи() //Предопределенная процедура
	Автор = глПользователь;             
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриЗакрытии()
КонецПроцедуры	

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
    |СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
