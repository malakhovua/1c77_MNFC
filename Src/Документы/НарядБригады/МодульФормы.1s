Перем СписокДействий;      
Перем НачальнаяДатаДокумента;

// ===============================
Процедура ОбработкаВыбораЗначения(ВыбЗнач,ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "Заказ" Тогда
		//Если глПроверитьФирму(Контекст,ВыбЗнач) = 0 Тогда
		//	ФлагСтандОбр = 0;
		//КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВводНового()
	глЗаполнитьШапку(Контекст);
КонецПроцедуры //ВводНового

Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);

	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кЗаполнить.Доступность(0);
		Форма.кРассчитать.Доступность(0);
		Форма.кФирма.Доступность(0);               		
		Форма.кОчиститьПодразделение.Доступность(0);               		
		Форма.кОчиститьПродукция.Доступность(0);               		
		Форма.кОчиститьВидДеятельности.Доступность(0);               		
		Форма.кОчиститьЗаказ.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	

КонецПроцедуры

Процедура Рассчитать()
	Если КоличествоСтрок() > 0 Тогда
		Если Вопрос("Удалить строки?", "Да+Нет") = "Да" Тогда
			УдалитьСтроки();
		Иначе
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	// определим с какой даты начинать начисление з/п. Для этого возьмем предыдущий документ
	Док = СоздатьОбъект("Документ.НарядБригады");
	НДата = Дата(0);
	Док.ОбратныйПорядок();
	Док.УстановитьФильтр(1,0);
	Док.ВыбратьДокументы(,ДатаДок-1);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		НДата = Док.ДатаДок + 1;
		Прервать;
	КонецЦикла;
	
	ТЗСотр = СоздатьОбъект("ТаблицаЗначений");
	ТЗСотр.НоваяКолонка("Сотрудник", "Справочник.Сотрудники");
	ТЗСотр.НоваяКолонка("Результат", "Число");

	ТЗВР = СоздатьОбъект("ТаблицаЗначений");
	
	ДокБ = СоздатьОбъект("Документ.Бригада");
	ДокБ.УстановитьФильтр(1,0);
	ДокБ.ВыбратьДокументы(НДата, ДатаДок);
	Пока ДокБ.ПолучитьДокумент() = 1 Цикл
		// вначале определим к-во человек, выполнявших работу
		ДокБ.ВыгрузитьТабличнуюЧасть(ТЗВР, "ВидРабот");
		ТЗВР.НоваяКолонка("КвоСотр", "Число");
		ТЗВР.Заполнить(1,,,"КвоСотр");
		ТЗВР.Свернуть("ВидРабот", "КвоСотр");
		ОбщМасса = ДокБ.ДокОснование.ДокОснование.Итог("Кво");
		
		ДокБ.ВыбратьСтроки();
		Пока ДокБ.ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			ТЗВР.НайтиЗначение(ДокБ.ВидРабот, Стр, "ВидРабот");
			ТЗВР.ПолучитьСтрокуПоНомеру(Стр);
			Ст = ТЗВР.ВидРабот.Ставка.Получить(ДокБ.ДатаДок);
			ТЗСотр.НоваяСтрока();
			ТЗСотр.Сотрудник = ДокБ.Сотрудник;
			ТЗСотр.Результат = (ОбщМасса / ТЗВР.КвоСотр) * Ст;
		КонецЦикла;
	КонецЦикла;
	
	ТЗСотр.Свернуть("Сотрудник", "Результат");
	ЗагрузитьТабличнуюЧасть(ТЗСотр);
КонецПроцедуры
// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;
КонецПроцедуры //ПриЗаписи

// ===============================
Функция УстДоступность()
	Форма.кОчиститьПодразделение.Доступность(Подразделение.Выбран());
//	Форма.кРассчитать.Доступность(Мин(КоличествоСтрок(),1)*?(Сумма = 0,0,1));
	Форма.Фирма.Видимость(0);
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Форма.Заголовок(глЗаголовок(Контекст,"Наряд бригады"));

	Возврат "";
КонецФункции //УстДоступность

// ===============================
Процедура Печать()
	Таб = СоздатьОбъект("Таблица");
	СуффиксТаблицы = ?(глВосстановитьЗначение(,"ФормыНаУкраинском")= Да, "_Укр", "");
	Таб.ИсходнаяТаблица("Таблица"+СуффиксТаблицы);
	глУстПропись(Гривня);
	
	Таб.ВывестиСекцию("Шапка");

    ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	Таб.ВывестиСекцию("Дно");
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.Опции(0,0,,);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Наряд бригады",);
	Таб = 0;
КонецПроцедуры //Печать

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|ДвиженияДокумента,
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");