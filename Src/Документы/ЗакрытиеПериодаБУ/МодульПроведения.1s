Перем Счет905;
Перем Период;
Перем ТаблицаОстатков;

// ===============================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Фирма,"Фирма");
	Возврат глВсеВыбрано;    
КонецФункции

//
//Расчетная балансовая стоимость остатка ТМЦ
Функция ПолучитьРасчетнуюСтоимость(СчетБУ, ТМЦ, Количество, Инверсия, ПозицияНайдена)
	
	сзКлюч = СоздатьОбъект("СписокЗначений");
	сзКлюч.ДобавитьЗначение(ТМЦ);
	ТаблицаОстатков.Подмножество(сзКлюч, 1,"Индекс");
	ТаблицаОстатков.ВыбратьСтроки("Индекс");
	
	Пока ТаблицаОстатков.ПолучитьСтроку("Индекс") = 1 Цикл
		
		ПозицияНайдена = 1;
		
		Если ТаблицаОстатков.Счет = СчетБУ Тогда
			
			Инверсия = ?((ТаблицаОстатков.Сумма = 0) И (ТаблицаОстатков.Количество = 0), 1, 0); //после свертки таблицы остатков сумма и количество свернулась в "0".
			
			Если (Инверсия = 1) ИЛИ (ТаблицаОстатков.Количество = 0) Тогда
				Возврат 0; 
			КонецЕсли;
			
			Возврат  Окр(Количество * ?(ТаблицаОстатков.Сумма<=0, 0, ТаблицаОстатков.Сумма/ТаблицаОстатков.Количество),2);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат 0;

КонецФункции

// ===============================
// Корректировка балансовой стоимости счетов ТМЦ бухгалтерского учета
Процедура ПроводкиКорректировкиСчетовТМЦ_БУ()
	
    СписокСчетов = "20,22,25,26,28";
	
    //подготовим таблицу остатков ТМЦ
	
    ТаблицаОстатков = СоздатьОбъект("ИндексированнаяТаблица");
	ТаблицаОстатков.НоваяКолонка("Счет");
	ТаблицаОстатков.НоваяКолонка("ТМЦ");
	ТаблицаОстатков.НоваяКолонка("Сумма");
	ТаблицаОстатков.НоваяКолонка("Количество");
	
	ТаблицаОстатков.ДобавитьИндекс("Индекс", "ТМЦ");
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,, 1);
	Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,, 1);
	Ит.ВыполнитьЗапрос(, Период, СписокСчетов,,, 1,, "СК");
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Ит.ВыбратьСубконто(ВидыСубконто.МестаХранения);
		Пока Ит.ПолучитьСубконто(ВидыСубконто.МестаХранения) = 1 Цикл
			Ит.ВыбратьСубконто(ВидыСубконто.ТМЦ);
			Пока Ит.ПолучитьСубконто(ВидыСубконто.ТМЦ) = 1 Цикл
				
				Если Ит.СКД(3) < 0 Тогда
					Продолжить; // отрицательные остатки в расчет не берем
				КонецЕсли;
				
				ТаблицаОстатков.НоваяСтрока();
				ТаблицаОстатков.Счет =Ит.Счет;
				ТаблицаОстатков.ТМЦ =Ит.Субконто(ВидыСубконто.ТМЦ);
				ТаблицаОстатков.Сумма =Ит.СКД();
				ТаблицаОстатков.Количество =Ит.СКД(3);
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаОстатков.Свернуть("Счет,ТМЦ", "Количество, Сумма");
	
	//Подготовим таблицу корректировок.
	
	ТаблицаКорректировки = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаКорректировки.НоваяКолонка("Счет");
	ТаблицаКорректировки.НоваяКолонка("МестаХранения");
	ТаблицаКорректировки.НоваяКолонка("ТМЦ");
	ТаблицаКорректировки.НоваяКолонка("Сумма");
	ТаблицаКорректировки.НоваяКолонка("Количество");
	ТаблицаКорректировки.НоваяКолонка("СуммаКорректироки");
	
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,, 1);
	Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,, 1);
	Ит.ВыполнитьЗапрос(, Период, СписокСчетов,,, 1,, "СК");
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Ит.ВыбратьСубконто(ВидыСубконто.МестаХранения);
		Пока Ит.ПолучитьСубконто(ВидыСубконто.МестаХранения) = 1 Цикл
			Ит.ВыбратьСубконто(ВидыСубконто.ТМЦ);
			Пока Ит.ПолучитьСубконто(ВидыСубконто.ТМЦ) = 1 Цикл
				
				Инверсия = 0; // если остались суммы без количества.
				ПозицияНайдена = 0;
				
				РасчетнаяСтоимость = ПолучитьРасчетнуюСтоимость(Ит.Счет, Ит.Субконто(ВидыСубконто.ТМЦ), Ит.СКД(3), Инверсия, ПозицияНайдена);
				
				//Если сумма осталась в остатке, а количество "0" и позиция присутствует в таблице остатов, то необходимо списать сумму
				Если (Ит.СКД() <> 0) И (Ит.СКД(3) = 0) И (ПозицияНайдена = 1) Тогда
					Инверсия = 1;
				КонецЕсли;
				
				Если ((РасчетнаяСтоимость <= 0) ИЛИ( РасчетнаяСтоимость = Ит.СКД())) И (Инверсия = 0) Тогда 
					Продолжить;
				КонецЕсли;
				
				ТаблицаКорректировки.НоваяСтрока();
				ТаблицаКорректировки.Счет =Ит.Счет;
				ТаблицаКорректировки.ТМЦ = Ит.Субконто(ВидыСубконто.ТМЦ);
				ТаблицаКорректировки.МестаХранения =Ит.Субконто(ВидыСубконто.МестаХранения);
				ТаблицаКорректировки.Сумма = Ит.СКД();
				ТаблицаКорректировки.Количество =Ит.СКД(3);
				ТаблицаКорректировки.СуммаКорректироки = ?(Инверсия = 1, - Ит.СКД(), РасчетнаяСтоимость - Ит.СКД());
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаКорректировки.Сортировать("ТМЦ");
	
	ТаблицаКорректировки.ВыбратьСтроки();
	
	Пока ТаблицаКорректировки.ПолучитьСтроку() = 1 Цикл
		глПроводка(Контекст, ТаблицаКорректировки.Счет, Счет905,ТаблицаКорректировки.СуммаКорректироки,
		  "Корректировка балансовой стоимости ТМЦ. Закрытие периода.",,ТаблицаКорректировки.МестаХранения,ТаблицаКорректировки.ТМЦ,,Константа.БазВидДеятельности,,,,,"ФР");		
    КонецЦикла;
   
	Отклонение =  ТаблицаКорректировки.Итог("СуммаКорректироки");
 
    Если Отклонение <> 0 Тогда
       Сообщить("Корректировка балансовой стоимости ТМЦ. Отклонение суммы на счете 905: " + Строка(ТаблицаКорректировки.Итог("СуммаКорректироки")),"!");
    КонецЕсли;
   
	Если ВыводитьРезультирующиеТаблицы = 1 Тогда
		ПоказатьТЗ(ТаблицаКорректировки, "Корректировка балансовой стоимости ТМЦ");
	КонецЕсли;
	
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	
	глКомментарий("Начало",2,Контекст);
	
	Счет905 = СчетПоКоду("905");
	Период = КонМесяца(ДатаДок);

	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	
	Если КорректировкаБСЗапасов = 1 Тогда
		Сообщить("Корректировка балансовой стоимости ТМЦ...");
		ПроводкиКорректировкиСчетовТМЦ_БУ();
	КонецЕсли;
	
	Операция.СуммаОперации = 0;	
	Операция.Содержание = Примечание;
	Операция.Записать();
	
	глКомментарий("Окончание",2,Контекст);
	
КонецПроцедуры
