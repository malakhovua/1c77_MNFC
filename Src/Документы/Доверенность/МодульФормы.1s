Перем НачальнаяДатаДокумента;

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмТМЦ()
	Если ТМЦ.Выбран()=1 Тогда
		ЕдИзм = СоздатьОбъект("Справочник.Единицы");
		ЕдИзм.ИспользоватьВладельца(ТМЦ);
		ЕдИзм.ВыбратьЭлементы();
		Пока ЕдИзм.ПолучитьЭлемент() = 1 Цикл
			Если ЕдИзм.Единица = ТМЦ.БазоваяЕдиница Тогда
	   		 	Ед = ЕдИзм.ТекущийЭлемент();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ИзмСотрудник()
	Если Сотрудник.Выбран()=1 Тогда
		ПодтверждающийДокумент = "паспорт";
		СерияПодтверждающегоДокумента = Сотрудник.ПаспортСерия;
		НомерПодтверждающегоДокумента = Сотрудник.ПаспортНомер;
		ДатаПодтверждающегоДокумента = Сотрудник.ВыданДата;
		КемВыданПодтверждающийДокумент = Сотрудник.ВыданКем;
		Если ПустаяСтрока(КомуВыдана) = 1 Тогда
			КомуВыдана = глФИО(Сотрудник);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	ПоДокументу = "счету-фактуре №";
	СрокДействия = ДатаДок + 10;
КонецПроцедуры 

// ======================================
Процедура ВводНаОсновании(Док)
	// Документ основание - СчетВходящий
	Если Док.ЧтоПриходуем <> Перечисление.ЧтоПриходуем.ТМЦ Тогда
		Предупреждение("Нельзя выписывать доверенность, когда приходуются "+Док.ЧтоПриходуем+" .");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;	
	Фирма = Док.Фирма;
	глЗаполнитьШапкуНаОсн(Контекст,Док);
	глУстановитьНомер(Контекст);
	Контрагент = Док.Контрагент;
	ПоДокументу = "счету-фактуре № "+СокрЛП(Строка(Док.НомерСчета))+" от "+СокрЛП(Строка(Док.ДатаДок));
	СрокДействия = ДатаДок + 10;
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку()=1 Цикл
		Если Док.ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
			// услуги в доверенность не вставляем
			Продолжить;
		КонецЕсли;
		НоваяСтрока();
		ТМЦ = Док.ТМЦ;
		Ед = Док.Ед;
		Кво = Док.Кво;
	КонецЦикла;
КонецПроцедуры 

// ======================================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;	
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	ПриЗаписиПерепроводить(1);
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);

		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
	
КонецПроцедуры

// ======================================
Процедура Печать()
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
    
	глУстПропись(Гривня);

	Таб.ВывестиСекцию("ЛицеваяСторона");
	
	Таб.НоваяСтраница();
	
	Таб.ВывестиСекцию("ОбратнаяСторонаШапка");
	
	Если КоличествоСтрок() > 6 Тогда
		СекцияСтроки = "Строка";
		глКомментарий("Количество строк в табличной части больше шести."+
					   "Типовая форма М-2 ""Доверенность"" рассчитана на шесть строк!",1,,"!");
	Иначе
		СекцияСтроки = "СтрокаВысокая";
	КонецЕсли;
	Ном = 1;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Таб.ВывестиСекцию(СекцияСтроки);
		Ном = Ном+1;
	КонецЦикла;
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: Доверенность","");
КонецПроцедуры 

// ======================================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "Сотрудник" Тогда
		ФлагСтандОбр = 0;
		КонтФирмы = Фирма;
		ОткрытьФорму("Справочник.Сотрудники.ДляВыбора",КонтФирмы);
	КонецЕсли;
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Доверенность"));
	Возврат "";
КонецФункции
           
// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|ОткрытьВЖурнале");
