Перем НачальнаяДатаДокумента;

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Заявление на аккредитив"));
	Форма.ЧейАкцепт.Видимость(Акцепт);
	Если НаправлятьУсловия = 4 Тогда
		Форма.ДругоеУсловие.Видимость(1);
	Иначе
		Форма.ДругоеУсловие.Видимость(0);
	КонецЕсли;
    Возврат "";
КонецФункции

// ======================================
Процедура УстРСчетКонтрагента()
	Если РСчетКонтрагента.Выбран() = 0 Тогда
	    СпрДС = СоздатьОбъект("Справочник.ДенежныеСчета");
		СпрДС.ИспользоватьВладельца(Контрагент);
		СпрДС.ВыбратьЭлементы();
		ДС = 0; КвоДС = 0;
		Пока СпрДС.ПолучитьЭлемент() = 1 Цикл
			Если СпрДС.Валюта <> Рсчет.Валюта Тогда
			    Продолжить;
			КонецЕсли;
			ДС = СпрДС.ТекущийЭлемент();
			КвоДС = КвоДС + 1;
		КонецЦикла;
		Если КвоДС = 1 Тогда
		    РСчетКонтрагента = ДС;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	Если РСчетКонтрагента.Владелец <> Контрагент Тогда
	     РСчетКонтрагента = 0;
	КонецЕсли;
	УстРСчетКонтрагента();
	Если Договор.Контрагент <> Контрагент Тогда
	    Договор = 0;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмРСчет()
	Если РСчет.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	Если РСчет.Выбран() = 0 Тогда
		РСчет = Фирма.РС;
		ИзмРСчет();
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ИзмВыполнитьАккредитив()
	ВыполнитьАккредитив = спВыполнитьАккредитив.ТекущаяСтрока();
КонецПроцедуры

// ======================================
Процедура ИзмНаправлятьУсловия()
	НаправлятьУсловия = спНаправлятьУсловия.ТекущаяСтрока();
	Если НаправлятьУсловия <> 4 Тогда
		ДругоеУсловие = "";
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ИзмАкцепт()
	Если Акцепт <> 1 Тогда
		ЧейАкцепт = "";
	КонецЕсли;
КонецПроцедуры 
                                                  
// ======================================
Процедура ИзмКво()
	СуммаСНДС = Кво * ЦенаСНДС;
КонецПроцедуры 
    
// ======================================
Процедура ИзмЦенаСНДС()
	СуммаСНДС = Кво * ЦенаСНДС;
КонецПроцедуры 

// ======================================
Процедура ИзмТМЦ()
	Если ТМЦ.Выбран() = 1 Тогда         
		ЦенаСНДС = глВернутьЦену(ТМЦ,КатегорияЦен,ДатаДок);
		Кво = ?(ПустоеЗначение(Кво)=1,1,Кво);
		ИзмЦенаСНДС();  
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	РСчет = Фирма.РС;
	ИзмРСчет();
	
	ВыполнитьАккредитив = глВосстановитьЗначение(Контекст,"ВыполнитьАккредитив",1);
	НаправлятьУсловия = глВосстановитьЗначение(Контекст,"НаправлятьУсловия",1);
	КатегорияЦен = глВосстановитьЗначение(,"ОсновнаяКатегорияЦен");
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кОсн.Доступность(0);

		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
	глПроверкаДатыДок(Контекст,"Открытие");

	спВыполнитьАккредитив.ДобавитьЗначение(1,"За счет средств плательщика, депонированных в исполняющем банке");
	спВыполнитьАккредитив.ДобавитьЗначение(2,"Инкассацией документов в банк-эмитент");
	спВыполнитьАккредитив.ДобавитьЗначение(3,"Через корреспондирующий счет банка-эмитента");
	спВыполнитьАккредитив.ТекущаяСтрока(ВыполнитьАккредитив);

	спНаправлятьУсловия.ДобавитьЗначение(1,"Спецсвязью");
	спНаправлятьУсловия.ДобавитьЗначение(2,"По электронной почте");
	спНаправлятьУсловия.ДобавитьЗначение(3,"Телетайпом");
	спНаправлятьУсловия.ДобавитьЗначение(4,"Другим путем");
	спНаправлятьУсловия.ТекущаяСтрока(НаправлятьУсловия);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;
	глСохранитьЗначение(Контекст,"ВыполнитьАккредитив",ВыполнитьАккредитив);
	глСохранитьЗначение(Контекст,"НаправлятьУсловия",НаправлятьУсловия);
КонецПроцедуры

// ===============================
Процедура Печать()
	Если РСчет.Валюта <> Гривня Тогда
	    Предупреждение("Предусмотрена печать только гривневых заявлений на аккредитив!");
	    Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	ИмяФайлаПечатнойФормы = КаталогИБ()+"ExtForms\PrnForms\ZNA_ukr.mxl";
	Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы) = 1 Тогда
		Таб.ИсходнаяТаблица(ИмяФайлаПечатнойФормы);
	Иначе
		Таб.ИсходнаяТаблица("Таблица_Укр");
	КонецЕсли;
	// строго на украинском!
	глУстПропись(Гривня, "у"); 
	Эк = 2;
	Сумма_стр = Сред(Формат(Цел(Сумма), "ЧПД"),1,Найти(Формат(Цел(Сумма), "ЧПД"),"гр") - 2) + " грн. " + Формат((Сумма - Цел(Сумма))*100,"Ч(0)2") + " коп.";
	
	Если ВвестиЧисло(Эк,"Введите количество экземпляров",1,0) = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(ДатаДок) = 0 Тогда
		ЧислоДаты = ?(ДатаЧисло(ДатаДок)<10,""""+"0"+ДатаЧисло(ДатаДок)+"""",""""+ДатаЧисло(ДатаДок)+"""");
		МесяцДаты = Прав(Формат(ДатаДок,"Д ДДММММГГГГ"),СтрДлина(Формат(ДатаДок,"Д ДДММММГГГГ"))-2);
	КонецЕсли;
	Если ПустоеЗначение(СрокДействия) = 0 Тогда
		ЧислоДатыСрока = ?(ДатаЧисло(СрокДействия)<10,""""+"0"+ДатаЧисло(СрокДействия)+"""",""""+ДатаЧисло(СрокДействия)+"""");
		МесяцДатыСрока = Прав(Формат(СрокДействия,"Д ДДММММГГГГ"),СтрДлина(Формат(СрокДействия,"Д ДДММММГГГГ"))-2);
	Иначе
		ЧислоДатыСрока = """     """;
		МесяцДатыСрока = "                              р.";
	КонецЕсли;
	ДатаДог = Договор.ДатаДок;
	Если ПустоеЗначение(ДатаДог) = 0 Тогда
		НомерДог = Договор.НомерДок;
		ЧислоДатыДог = ?(ДатаЧисло(ДатаДог)<10,""""+"0"+ДатаЧисло(ДатаДог)+"""",""""+ДатаЧисло(ДатаДог)+"""");
		МесяцДатыДог = Прав(Формат(ДатаДог,"Д ДДММММГГГГ"),СтрДлина(Формат(ДатаДог,"Д ДДММММГГГГ"))-2);
	Иначе         
		НомерДог = "_______________";
		ЧислоДатыДог = """      """;
		МесяцДатыДог = "                              р.";
	КонецЕсли;
	
    Для Ном = 1 По Эк Цикл
		Если Ном > 1 Тогда
		    Таб.НоваяСтраница();
		КонецЕсли;
		Таб.ВывестиСекцию("Шапка");
		Таб.ВывестиСекцию("ВыполнитьАккредитив"+ВыполнитьАккредитив);
		Таб.ВывестиСекцию("НаправлятьУсловия"+НаправлятьУсловия);
		//Таб.ВывестиСекцию("ДругоеУсловие");
		Таб.ВывестиСекцию("Акцепт"+Акцепт);
		Таб.ВывестиСекцию("Договор");
		ВыбратьСтроки();
		КвоСтрокТМЦ = КоличествоСтрок();
		КвоСтрокДок = СтрКоличествоСтрок(СписокДокументов);
		МаксКвоСтрок = ? (КвоСтрокТМЦ > КвоСтрокДок, КвоСтрокТМЦ, КвоСтрокДок);
		НСтроки = 0;
		Пока НСтроки < МаксКвоСтрок  Цикл
			НСтроки = НСтроки + 1;
			Если Нстроки <= КвоСтрокТМЦ Тогда
				ПолучитьСтроку();     
				ФТМЦ = ТМЦ;
				ФКво = Формат(Кво,"Ч10.3");
				ФЦенаСНДС = Формат(ЦенаСНДС,"Ч12.2");
				ФСуммаСНДС = Формат(СуммаСНДС,"Ч12.2");
			Иначе
				ФТМЦ = "";
				ФКво = "";
				ФЦенаСНДС = "";
				ФСуммаСНДС = "";
			КонецЕсли;
			Если Нстроки <= КвоСтрокДок Тогда
				СтрокаСпискаДокументов = СтрПолучитьСтроку(СписокДокументов,НСтроки);
			Иначе
				СтрокаСпискаДокументов = "";
			КонецЕсли;
			Таб.ВывестиСекцию("Строка");
		КонецЦикла;
		Таб.ВывестиСекцию("Дно");
	КонецЦикла;
	Таб.Опции(0,0,0,0,"");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Заявление на аккредитив","");
КонецПроцедуры
                          
// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");