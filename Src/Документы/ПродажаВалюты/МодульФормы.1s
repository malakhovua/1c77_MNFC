Перем НачальнаяДатаДокумента;

// ===============================
Процедура ИзмСуммаГРНУМВБ()
	СуммаГРН = СуммаГРНУМВБ - СуммаКомГРН;
КонецПроцедуры
                  
// ===============================
Процедура ИзмСуммаКомГРН()
	СуммаГРН = СуммаГРНУМВБ - СуммаКомГРН;
КонецПроцедуры

// ===============================
Процедура ИзмВалюта()
	Если Валюта.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;  
	КурсНБУ = Валюта.Курс.Получить(ДатаДок);
КонецПроцедуры

// ===============================
Процедура ИзмРСчетВал()
	Если РСчетВал.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;  
	Валюта = РСчетВал.Валюта;
	ИзмВалюта();
КонецПроцедуры
              
// ===============================
Процедура ИзмРСчет()
	Если РСчет.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если РСчет.Валюта <> Гривня Тогда
	    Предупреждение("В качестве расчетного счета должен быть выбран счет с валютой Гривня!");
	    РСчет = 0;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмСчетЗатратКом()
	Если СчетЗатратКом.Выбран() = 1 Тогда
		Если глПроверитьСчетЗатрат(СчетЗатратКом,,0)=0 Тогда
	        СчетЗатратКом = 0;
	    КонецЕсли; 
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	РСчет = Фирма.РС;
	ВидДеятельности = глВосстановитьЗначение(,"БазВидДеятельности");
	СчетЗатратКом = глВосстановитьЗначение(Контекст,"СчетЗатратКом");
	Если СчетЗатратКом.Выбран() = 0 Тогда
		СчетЗатратКом = СчетПоКоду(?(ИспользоватьСчетаРасходов=Класс9,"949","84"));
	КонецЕсли;
	СубконтоЗатратКом = глВосстановитьЗначение(Контекст,"СубконтоЗатратКом");
	СубконтоВалДох = глВосстановитьЗначение(Контекст,"СубконтоВалДох");
	Если СубконтоВалДох.Выбран()=0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Спр.НайтиПоКоду("1/2/3");
	    СубконтоВалДох = Спр.ТекущийЭлемент();
	КонецЕсли;                                
	СубконтоВалРасхКом = глВосстановитьЗначение(Контекст,"СубконтоВалРасхКом");
	Если СубконтоВалРасхКом.Выбран()=0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Спр.НайтиПоКоду("2/2/11");
	    СубконтоВалРасхКом = Спр.ТекущийЭлемент();
	КонецЕсли;                                
	СубконтоВалРасхБал = глВосстановитьЗначение(Контекст,"СубконтоВалРасхБал");
	Если СубконтоВалРасхБал.Выбран()=0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Спр.НайтиПоКоду("2/2/12");
	    СубконтоВалРасхБал = Спр.ТекущийЭлемент();
	КонецЕсли;
	СубконтоЗатратСебестВал = Константа.ВидЗатратСебестРеалВалюты;
КонецПроцедуры

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение)
	Форма.ИспользоватьСлой("Общий,"+Значение,2);
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	НачальнаяДатаДокумента = ДатаДок;   
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);          
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
	Форма.ИспользоватьСлой("Основной, Общий",2);
КонецПроцедуры
              
// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глСохранитьЗначение(Контекст,"СубконтоВалДох",СубконтоВалДох);
	глСохранитьЗначение(Контекст,"СубконтоВалРасхКом",СубконтоВалРасхКом);
	глСохранитьЗначение(Контекст,"СубконтоВалРасхБал",СубконтоВалРасхБал);
	глСохранитьЗначение(Контекст,"СчетЗатратКом",СчетЗатратКом);
	глСохранитьЗначение(Контекст,"СубконтоЗатратКом",СубконтоЗатратКом);
	
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ОбработкаВыбораЗначения(Значение,Рекв,Флаг)
	Если Рекв="СчетЗатратКом" Тогда
	    Если глПроверитьСчетЗатрат(Значение,,0)=0 Тогда
	        Флаг = 0;
	    КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Форма.Фирма.Видимость(0);
	Форма.Заголовок(глЗаголовок(Контекст,"Продажа валюты"));
	ДоступностьСебестВал = ?(ИспользоватьСчетаРасходов=Класс9,0,1);
	Форма.СубконтоЗатратСебестВал.Доступность(ДоступностьСебестВал);
	Форма.тСубконтоЗатратСебестВал1.Доступность(ДоступностьСебестВал);
	Форма.тСубконтоЗатратСебестВал2.Доступность(ДоступностьСебестВал);
	Форма.тСубконтоЗатратСебестВал3.Доступность(ДоступностьСебестВал);
	Возврат "";
КонецФункции

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
