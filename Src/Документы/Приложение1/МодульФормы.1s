Перем СтарНалоговаяНакладная;
Перем СтарЧтоПродаем;
Перем СтарыйКонтрагент;

Перем НачальнаяДатаДокумента;
Перем СтараяДата;
Перем СтарВидНДС;
Перем СтарКатегорияЦен;
          
// ===============================
Процедура РассчитатьБазуНДС()
    НачВалюта = ?(ПустоеЗначение(Валюта)=1,Гривня,Валюта);
	СуммаНДС = глПересчет(Итог("НДСОтгр"),НачВалюта,Гривня,Курс,ДатаДок);
	БазаНДС  = глПересчет(Итог("СуммаБезНДСОтгр"),НачВалюта,Гривня,Курс,ДатаДок);
КонецПроцедуры

// ===============================
Процедура ВводНового()
	Предупреждение("Этот документ можно вводить только на основании Налоговой накладной!");
	СтатусВозврата(0);
КонецПроцедуры
              
// ===============================
Процедура ВводНаОсновании(ДокОсн)
	Если ДокОсн.Вид() <> "НалоговаяНакладная" Тогда
		// вводить документ можно только на основании НалоговойНакладной
		// остальные документы, отмеченные в "вводить на основании" используются
		// при выборе реквизита ДокументОснование
		Предупреждение("Документ """+Вид()+""" нельзя вводить на основании документа """+ДокОсн.Вид()+"""."+РазделительСтрок+"Возможно только выбирать его в качестве документа-основания в диалоге !");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Фирма = ДокОсн.Фирма;
	глУстановитьНомер(Контекст);
	Договор = ДокОсн.Договор;
	ДокументОснование = ДокОсн.ДокументОснование;
	НалоговаяНакладная = ДокОсн;
	
	УсловиеПродажи = ДокОсн.УсловиеПродажи;
	ФормаРасчетов = ДокОсн.ФормаРасчетов;
	ВидНДС = ДокОсн.ВидНДС;
	КатегорияЦен = ДокОсн.КатегорияЦен;
	ЧтоПродаем = ДокОсн.ЧтоПродаем;
	
	Валюта = ДокОсн.Валюта;
	Дата_Курса= ДокОсн.Дата_Курса;
	Курс = ДокОсн.Курс;
	
	Контрагент = ДокОсн.Контрагент;
	Выписал = глВосстановитьЗначение(,"БазВыписалНН");
	ДокОсн.ВыбратьСтроки();
	Пока ДокОсн.ПолучитьСтроку() <> 0 Цикл
		НоваяСтрока();
		ТМЦ = ДокОсн.ТМЦ;
		Ед = ДокОсн.Ед;
		Кво = ДокОсн.Кво;  
		КвоОтгр = ДокОсн.Кво;
		ЦенаБезНДС = ДокОсн.ЦенаБезНДС;
		ЦенаСНДС = ДокОсн.ЦенаСНДС;
		Коэффициент = ДокОсн.Коэффициент;
		СуммаБезНДС = ДокОсн.СуммаБезНДС;
		глВыч_суммы_накл(Контекст,0);
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;СтараяДата = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кФирма.Доступность(0);
		Форма.кВалюта.Доступность(0);
		Форма.кОснование.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	СтарыйКонтрагент = Контрагент;
	СтарВидНДС		 = ВидНДС;
	СтарКатегорияЦен = КатегорияЦен;
	глУстВидимостьЦен(Контекст);
КонецПроцедуры

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докуменете
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
КонецПроцедуры

// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры //

// ===============================
// Назначение: Пересчет сумм в строке документа
//		
// Аргументы: НоваяЦена - новая цена для текущей строки документа
//		
Процедура ПересчетСтроки(НоваяЦена=0)
	Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаБезНДС Тогда
		Если ПустоеЗначение(НоваяЦена) = 0 Тогда                                  
			// определим ставку НДС
			Если ВидНДС = ОсновнаяСтавкаНДС Тогда
				СтавкаНДС = ТМЦ.СтавкаНДС;
			Иначе
				СтавкаНДС = ВидНДС;
			КонецЕсли;
			// устанавливаем цену без НДС
			ЦенаБезНДС = НоваяЦена*100/(глПроцентНДС(СтавкаНДС,ДатаДок)+100);
		КонецЕсли;
		глВыч_Суммы_Накл(Контекст,-1);
	Иначе                     
		Если ПустоеЗначение(НоваяЦена) = 0 Тогда
			ЦенаСНДС = НоваяЦена;
		КонецЕсли;
		глВыч_Суммы_Накл(Контекст,1);
	КонецЕсли;
КонецПроцедуры //ПересчетСтроки

// Назначение: пересчитывает суммы в строках документа при изменении реквизитов шапки
//		
// Аргументы: Реквизит - наименование измененного реквизита
//		
// ===============================
Процедура ИзмРеквизитШапки(Реквизит)
	// пересчитаем суммы НДС в строках табличной части
	Если (СтарВидНДС=ВидНДС) И (СтарКатегорияЦен=КатегорияЦен) Тогда
		// ничего не изменилось
	    Возврат;     
	Иначе
		СтарВидНДС		=ВидНДС;
		СтарКатегорияЦен=КатегорияЦен;
	КонецЕсли;
	Если КоличествоСтрок() = 0 Тогда
	    Возврат;
	КонецЕсли;      
	текОтвет = Вопрос("В документе изменен реквизит """+Реквизит+""". Перезаполнить цены заново?","Да+Нет");
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если текОтвет = "Да" Тогда
			// изменим цены
			ЦенаТовара = глВернутьЦену(ТМЦ, КатегорияЦен);
			Если ПустоеЗначение(ЦенаТовара) = 0 Тогда
				// получим параметры цены
				ЦенаТовара.ИспользоватьДату(ДатаДок);
				ЦенаЦены   = ЦенаТовара.Цена;
				ВалютаЦены = ЦенаТовара.Валюта;
				ЕдЦены	   = ЦенаТовара.Единица;
				ЦенаЦены   = ЦенаЦены * Коэффициент / ЕдЦены.Коэффициент;			
				ЦенаЦены   = глПересчет(ЦенаЦены,ВалютаЦены,ДатаДок,Валюта,Курс,Дата_курса);
				ПересчетСтроки(ЦенаЦены);                                                   
			Иначе
				ПересчетСтроки();
			КонецЕсли;
		ИначеЕсли Реквизит = "ВидНДС" Тогда
			ПересчетСтроки();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ===============================
Процедура ИзмТМЦ()
	Если ТМЦ.Вид()="ТМЦ" Тогда
		глПриИзмененииТовара(Контекст);
	Иначе // необоротные активы
		глУстановкаБазЕд(Контекст, , ТМЦ.БазЕдиница);
		ЦенаБезНДС = глПересчет(ТМЦ.Цена_Прих,Гривня,Валюта,ДатаДок);
		глВыч_суммы_накл(Контекст,-1);		
	КонецЕсли;
КонецПроцедуры
                         
// ===============================
Процедура ИзмДоговор()
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Вид() = "Договор" Тогда
			Валюта_Прежн = Валюта;
			Валюта = Договор.Валюта;
			Если Валюта <> Валюта_Прежн Тогда
				Дата_Курса=ДатаДок;
				Курс_Прежн = Курс;
				Курс = глКурсДляВалюты(Валюта,ДатаДок);
				Если ((Валюта_Прежн <> Валюта) Или (Курс_Прежн <> Курс)) Тогда
					// что-то изменилось, надо пересчитать цены
					глУстан_Вал(Контекст, Валюта, Валюта_Прежн, Курс, Курс_Прежн);
				КонецЕсли;
			КонецЕсли;
			ВидТорговли = Договор.ВидТорговли;
			ВидНДС = Договор.ВидНДС;
		КонецЕсли;
	КонецЕсли;    
КонецПроцедуры

// ======================================
Процедура ИзмНалоговаяНакладная()
	Если ПустоеЗначение(НалоговаяНакладная) = 0 Тогда
		ВводНаОсновании(НалоговаяНакладная);
	Иначе
		НалоговаяНакладная = СтарНалоговаяНакладная;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;	
	Если СтарыйКонтрагент <> Контрагент Тогда
		// изменили Контрагента
		Если Договор.Выбран() = 1 Тогда
			Если Договор.Контрагент <> Контрагент Тогда
				Договор = 0;
			КонецЕсли;
		КонецЕсли;    
		Если Договор.Выбран() = 0 Тогда
			Если (Фирма = Контрагент.БазДоговор.Фирма) и (глВосстановитьЗначение(,"ПодставлятьОсновнойДоговор")=Да) Тогда
				Договор = Контрагент.БазДоговор;
			КонецЕсли;
			Если ПустоеЗначение(Контрагент.ВидТорговли) = 0 Тогда
				ВидТорговли = Контрагент.ВидТорговли;
			ИначеЕсли ПустоеЗначение(ВидТорговли) = 1 Тогда
				ВидТорговли = глВосстановитьЗначение(,"ОсновнойВидТорговли");
			КонецЕсли;	
			ИзмДоговор();
		КонецЕсли;       
		// проверим документ-основание
		Если ПустоеЗначение(ДокументОснование) = 0 Тогда
			Если ДокументОснование.Контрагент <> Контрагент Тогда
				ДокументОснование = 0;
			КонецЕсли;	
		КонецЕсли;
		// проверим налоговую
		Если ПустоеЗначение(НалоговаяНакладная) = 0 Тогда
			Если НалоговаяНакладная.Контрагент <> Контрагент Тогда
				НалоговаяНакладная = 0;
				ИзмНалоговаяНакладная();
			КонецЕсли;	
		КонецЕсли;
		Если ПустоеЗначение(Контрагент.КатегорияЦен.Получить(ДатаДок)) = 0 Тогда
			КатегорияЦен = Контрагент.КатегорияЦен.Получить(ДатаДок);
		Иначе
			КатегорияЦен = глВосстановитьЗначение(,"ОсновнаяКатегорияЦен");
		КонецЕсли;
	КонецЕсли;     
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры

// ===============================
Функция ФРМЦена(ЧислЗнач)
	// без разделения на триады, не меньше двух и не больше пяти знаков после запятой
    Стр=СокрЛ(Формат(Окр(ЧислЗнач,5),"Ч15.5"));
	Для Инд=1 по 3 Цикл
		Если Прав(Стр,1)="0" Тогда
		    Стр = Лев(Стр,СтрДлина(Стр)-1);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Стр;
КонецФункции

// ===============================
Процедура Печать()
	
	глУстПропись(Гривня, "у");
	ФДата = Лев(Формат(ДатаДок, "ДДДММММГГГГ"), (СтрДлина(Формат(ДатаДок, "ДДДММММГГГГ"))-2));//отсёк "г."
	
	НН_НомерДок = глНомерБезПрефикса(НалоговаяНакладная.НомерДок);
	НН_ДатаДок = НалоговаяНакладная.ДатаДок;
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица_Укр");
	
	Для Страница = 1 По 2 Цикл
		Таб.ВывестиСекцию( "Пустота" );
		Если Страница = 1 Тогда
			Таб.ВывестиСекцию( "Оригинал" );
		ИначеЕсли Страница = 2 Тогда
			Таб.ВывестиСекцию( "Копия" );
		КонецЕсли;
		
		Таб.ВывестиСекцию( "Шапка" );
		Ном = 1;
		Ист7 = 0;
		ВыбратьСтроки();
		Пока ПолучитьСтроку() > 0 Цикл
			Ст1 = Ном;
			Ном = Ном + 1;
			
			Ст3 = ТМЦ.ПолнНаименование;
			Ст4 = Ед;
			Ст6 = Формат(Кво,"Ч13.3");
			Ст8 = ТМЦ.ПолнНаименование;
			Ст9 = Ед;
			Ст10 = Формат(КвоОтгр,"Ч13.3");
			Ст11 = ТМЦ.ПолнНаименование;
			Ст12 = Ед;
			Ст13 = Формат(КвоОст,"Ч13.3");
			Ст2 = ?(Договор.Выбран()=1,Строка(Договор),СокрЛП(Контрагент.ПолнНаименование));
			
			Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС Тогда
				// если основная цена с НДС, то для печати цену нужно рассчитывать
				ПроцНДС = глПроцентНДС(?((ВидНДС=ОсновнаяСтавкаНДС)и(ТМЦ.Вид()="ТМЦ"),ТМЦ.СтавкаНДС,ВидНДС),ДатаДок);
				ВремЦенаБезНДС = ЦенаСНДС*100/(100+ПроцНДС);
			Иначе
				ВремЦенаБезНДС = ЦенаБезНДС;
			КонецЕсли;
			Ст5 = ФРМЦена(глПересчет(ВремЦенаБезНДС,Валюта,Курс,Гривня,ДатаДок,Дата_Курса));
			
			СуммаБезНДСГрн = глПересчет(СуммаБезНДС,Валюта,Курс,Гривня,ДатаДок,Дата_Курса);
			Ст7 = глФРМ3(СуммаБезНДСГрн,Гривня,0);
			Ист7 = Ист7 + СуммаБезНДСГрн;
			Таб.ВывестиСекцию("Строка");
		КонецЦикла;                                        
		Ист7 = Формат(Ист7,"Ч13.2");
		Таб.ВывестиСекцию("Подвал");
		
		Если Страница <> 2 Тогда
			Таб.НоваяСтраница();
		КонецЕсли;
		
	КонецЦикла;
	
	Таб.Опции(0, 0, 0, 0);
	Таб.ПараметрыСтраницы(2,,,,,,,,);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать расчета корректировки", "");
	глУстПропись(Гривня);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
    глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	РассчитатьБазуНДС();
КонецПроцедуры
                                                   
// ===============================
Процедура ИзмЧтоПродаем()
	Если (КоличествоСтрок()<>0) и (СтарЧтоПродаем<>ЧтоПродаем) и (СокрЛП(СтарЧтоПродаем)<>"") Тогда
		Рез = Вопрос("Для изменения нужно очистить табличную часть. Удалить строки?","Да+Нет");
		Если Рез = "Да" Тогда
		    УдалитьСтроки();
		Иначе
			ЧтоПродаем = СтарЧтоПродаем;
		КонецЕсли;
	КонецЕсли;
	СтарЧтоПродаем = ЧтоПродаем;
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Приложение № 1 к налоговой накладной"));
	Если ЧтоПродаем = Перечисление.ЧтоПродаем.НеоборотныеАктивы Тогда
		НазначитьВид(ТМЦ,"НеоборотныеАктивы");
	Иначе
		НазначитьВид(ТМЦ,"ТМЦ");
	КонецЕсли;
	РассчитатьБазуНДС();
	Форма.ТМЦ.НеИзменятьВид(1);
	Возврат "";
КонецФункции

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ИзмРеквизитШапки("ВидНДС");
		ФлагСтандОбр = 0;
	ИначеЕсли Рекв = "Выписал" Тогда
		ФлагСтандОбр = 0;
		КонтФирма = Фирма;
		ОткрытьФорму("Справочник.Сотрудники.ДляВыбора",КонтФирма);
	ИначеЕсли Рекв = "НалоговаяНакладная" Тогда
		СтарНалоговаяНакладная = НалоговаяНакладная;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в документе
	ОткрытьФормуМодально("Обработка.ИнформацияОВалюте", Контекст);
КонецПроцедуры


//====================================================================== УМК Сандомирский В.Ю. (*) //--- заголовок FormEx_ПланРаскраски
Функция Раскраска()
	ТекстПлана = "(BRUSH_S[" + Строка(100*255*100) + "])()()"; //--- добавляя колонку вначало документа до "раскрашек" часов добавить "()"
	Возврат  ТекстПлана;	
КонецФункции



// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");