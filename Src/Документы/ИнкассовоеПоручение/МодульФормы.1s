Перем Фрм;
Перем НачальнаяДатаДокумента;
                        
// ===============================
Процедура ИзмРСчет()
	Если РСчет.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмСуммаСНДС()
	Ст = Константа.БазНДС.Ставка.Получить(ДатаДок);
	НДС = СуммаСНДС*Ст/(1+Ст);
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Инкассовое поручение"));
	Возврат "";
КонецФункции
            
// ======================================
//Процедура УстРСчетКонтрагента()
Процедура УстРСчетКонтрагента()
	Если РСчетКонтрагента.Выбран() = 0 Тогда
	    СпрДС = СоздатьОбъект("Справочник.ДенежныеСчета");
		СпрДС.ИспользоватьВладельца(Контрагент);
		СпрДС.ВыбратьЭлементы();
		ДС = 0; КвоДС = 0;
		Пока СпрДС.ПолучитьЭлемент() = 1 Цикл

			Если СпрДС.Валюта <> Рсчет.Валюта Тогда
			    Продолжить;
			КонецЕсли;
			ДС = СпрДС.ТекущийЭлемент();
			КвоДС = КвоДС + 1;
		КонецЦикла;
		Если КвоДС = 1 Тогда
		    РСчетКонтрагента = ДС;
		КонецЕсли;
	КонецЕсли;      
КонецПроцедуры 

// ===============================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	Если РСчетКонтрагента.Владелец <> Контрагент Тогда
	     РСчетКонтрагента = 0;
	КонецЕсли;
	УстРСчетКонтрагента();
КонецПроцедуры

// ===============================
Процедура ИзмНазначениеПлатежа()
	Если НазначениеПлатежа.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;
	Содержание = НазначениеПлатежа.ПолнНаименование;
	Если НДС = 0 Тогда
	    Содержание = Содержание+"
	    |Без ПДВ.";
	Иначе
	    Содержание = Содержание+"
		|В т.ч. ПДВ "+СокрЛП(Формат(НДС,"Ч14.2,'"))+" грн.";
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	Если РСчет.Выбран() = 0 Тогда
		РСчет = Фирма.РС;
		ИзмРСчет();
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура Печать()
	Если РСчет.Валюта <> Гривня Тогда
	    Предупреждение("Предусмотрена печать только гривневых поручений!");
	    Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица_Укр");

	// строго на украинском!
	глУстПропись(Гривня, "у"); 
	Эк = 2;
	СуммаСНДС_стр = Сред(Формат(Цел(СуммаСНДС), "ЧПД"),1,Найти(Формат(Цел(СуммаСНДС), "ЧПД"),"гр") - 2) + " грн. " + Формат((СуммаСНДС - Цел(СуммаСНДС))*100,"Ч(0)2") + " коп.";
	// номер инкассового поручения возьмем равным номеру,
	// стоящему в номере документа после префикса
	Номер = Сред(НомерДок, Найти(НомерДок, "-")+1);

	Если ВвестиЧисло(Эк,"Введите количество экземпляров",1,0) = 0 Тогда
		Возврат;
	КонецЕсли;
    Для Ном = 1 По Эк Цикл
		Если Ном > 1 Тогда
		    Таб.НоваяСтраница();
		КонецЕсли;
		Таб.ВывестиСекцию("Форма");
	КонецЦикла;
	Таб.Опции(0,0,0,0,"");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Инкассовое поручение","");
КонецПроцедуры

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	РСчет = Фирма.РС;
	ИзмРСчет();
	Счет = СчетПоКоду(?(РСчет.Валюта = Гривня, "36.1", "36.2"));
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
                                      
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кДействия.Доступность(0);
		
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
	
	Форма.Счет.ВыборГруппы(0);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры       
                
// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");
