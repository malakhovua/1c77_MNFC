// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Срок;
Перем СписокДействий;
Перем СтараяДата;

Перем НачальнаяДатаДокумента;


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Снятие резерва"));     
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмДата()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

// ===============================
Процедура Заполнить(Зап = 0)
	Перем Резервы;
	Перем Итоги;
	
	ВыбТовар = ПолучитьПустоеЗначение("Справочник.ТМЦ");		
	
	// Если это начальное заполнение, то ТМЦ выбирать не надо
	Если Зап = 0 Тогда
		Если ВвестиЗначение(ВыбТовар,"Выберите ТМЦ, по которому снимаем резерв","Справочник.ТМЦ",,) = 0 Тогда
			Если Вопрос("Заполнить по всем резервам?","Да+Нет") = "Нет" Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если КоличествоСтрок() <> 0 Тогда	
		Ответ = Вопрос("Заполнение по резервам. Очистить табличную часть?","Да+Нет+Отмена");
		Если Ответ = "Отмена" Тогда
			Возврат;
		ИначеЕсли Ответ = "Да" Тогда
			УдалитьСтроки();
		КонецЕсли;	
	КонецЕсли;	
	
	Резервы = СоздатьОбъект("Регистр.Резервы");
	Резервы.УстановитьЗначениеФильтра("Фирма",Фирма);
	Если ПустоеЗначение(ВыбТовар) = 0 Тогда
		Резервы.УстановитьЗначениеФильтра("ТМЦ",ВыбТовар);
	КонецЕсли;

	Итоги = СоздатьОбъект("ТаблицаЗначений");
	Резервы.ВыгрузитьИтоги(Итоги, 1);
	Итоги.Свернуть("3","4");
	
	Найдено=0;
	Для Счетчик=1 По Итоги.КоличествоСТрок() Цикл
		Док = Итоги.ПолучитьЗначение(Счетчик,1);
		Если глБанковскаяДата(Док.ДатаДок,Док.СрокРезервирования)<ДатаДок Тогда
			Если Фирма = Док.Фирма тогда
				НоваяСтрока();
				ДокументРезерва=Док;
				Найдено=1;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;

	Если Найдено=0 Тогда
		Предупреждение ("Не найдено просроченных Счетов с зарезервированными ТМЦ!");
	КонецЕсли;
КонецПроцедуры	

// ===============================
Процедура ИзмФирма()
	СтараяФирма = Фирма;
	глУстФирма(Контекст);	
	Если СтараяФирма = Фирма Тогда
		Возврат;
	КонецЕсли;
	
	// фирма изменилась
	Если ПустоеЗначение(Фирма) = 0 Тогда
		Если КоличествоСтрок() <> 0 Тогда
			Ответ = Вопрос("Перезаполнить табличную часть?","Да+Нет+Отмена");
			Если Ответ = "Отмена" Тогда
				Фирма = "";
			Иначе
				УдалитьСтроки();
				Если Ответ = "Да" Тогда
					Заполнить(1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
			
	Если ПустоеЗначение(Фирма) = 1 Тогда
		Фирма = СтараяФирма;
		глУстановитьНомер(Контекст);
	КонецЕсли;
КонецПроцедуры //ИзмФирма

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в документе
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
КонецПроцедуры	


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ВводНаОсновании(ДокОснование)
	Автор = глПользователь;
	ДатаДок=РабочаяДата();
	глЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
	НоваяСтрока();
	ДокументРезерва=ДокОснование;
КонецПроцедуры

// ===============================
Процедура ВводНового(Скопирован)
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	глЗаполнитьШапку(Контекст);
	ДатаДок=РабочаяДата();
	Основание="Истек срок резервирования ТМЦ.";
	Заполнить(1);
КонецПроцедуры

// ===============================
Процедура ОбработкаВыбораЗначения(Выб,ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "ДокументРезерва" Тогда
		Если Выб.Фирма <> Фирма Тогда
			Предупреждение("Выбран документ, принадлежащий другой фирме!");
			ФлагСтандОбр = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;

	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.			Доступность(0);
		Форма.кПровести.	Доступность(0);
		Форма.кДействия.	Доступность(0);
		Форма.кЗаполнить.	Доступность(0);
		Форма.кФирма.		Доступность(0);               		
		Форма.кОсн.			Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;

	СтараяДата = ДатаДок;
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура Отчет()
	Рег = СоздатьОбъект("Регистры");
	Рег.ВременныйРасчет(1);
	РегОст = Рег.Остатки;
	РегРез = Рег.Резервы;
	
	РегОст.УстановитьЗначениеФильтра("МестоХранения", Фирма.ОсновнойСклад);
	Если СравнитьТА() = -1 Тогда
		Рег.РассчитатьРегистрыНа(ТекущийДокумент());
	ИначеЕсли СравнитьТА() = -2 Тогда
		Если ДатаДок < ПолучитьДатуТА() Тогда
			Рег.РассчитатьРегистрыПо(ДатаДок);
		КонецЕсли;
	КонецЕсли;
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	РегОст.ВыгрузитьИтоги(ТЗ);
	ТЗ.Свернуть("ТМЦ", "ОстатокТовара");
	ТЗРез = СоздатьОбъект("ТаблицаЗначений");
	РегРез.ВыгрузитьИтоги(ТЗРез);
	ТЗРез.Свернуть("ТМЦ", "Резерв");
	
	ТЗ.НоваяКолонка("Резерв", "Число", 15, 3);
	
	Инд = 1;
	Пока Инд <= ТЗ.КоличествоСтрок() Цикл
		Стр = 0;
		ТЗ.ПолучитьСтрокуПоНомеру(Инд);
		Если ТЗРез.НайтиЗначение(ТЗ.ТМЦ, Стр, "ТМЦ") = 0 Тогда
			ТЗ.УдалитьСтроку(Стр);
		Иначе
			ТЗ.Резерв = ТЗРез.ПолучитьЗначение(Стр, "Резерв");
			Инд = Инд + 1;
		КонецЕсли;
	КонецЦикла;
	
	ТЗ.Сортировать("ТМЦ");
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Шапка");
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");		
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчёт по резервам и ост.");
КонецПроцедуры // Отчет

// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");