// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
	глВыбранЛи(ДокументРезерва,"Счет",НомерСтроки);
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Процедура ОбработкаПроведения()
	Перем ТаблИтогов;
	глКомментарий("Начало",2,Контекст);
	
	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	Если КоличествоСтрок()=0 Тогда
	    глНеПроводить(Контекст,"В документе должна быть хотя бы одна строка!");
		Возврат;
	КонецЕсли;
	// по Регистру Резервы (снимаем с резерва все ТМЦ по выбранным Счетам)

	ВремРегистры=СоздатьОбъект("Регистры");
	Рег=ВремРегистры.Резервы;

	спСчетов=СоздатьОбъект("СписокЗначений");
	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;

		глДобавитьЗначениеБезПовторения(спСчетов, ДокументРезерва, Строка(НомерСтроки));
	КонецЦикла;

	Рег.УстановитьЗначениеФильтра("Фирма", Фирма);
	Рег.УстановитьЗначениеФильтра("ДокументРезерва",спСчетов,2);

	Если ИтогиАктуальны()=0  Тогда
	//	если итоги актуальны, то смотрим итоги на ТА
	 // если итоги не актуальны, то берем из временногно расчета Регистра
		Рег.ВременныйРасчет();
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	Рег.ВыгрузитьИтоги(ТаблИтогов,1);

	ТаблИтогов.ВыбратьСтроки();
	Пока ТаблИтогов.ПолучитьСтроку()>0 Цикл
		Поз=спСчетов.НайтиЗначение(ТаблИтогов.ДокументРезерва);
		ВыбНомСтр="";
		спСчетов.ПолучитьЗначение(Поз,ВыбНомСтр);
		Регистр.Резервы.ПривязыватьСтроку(Число(ВыбНомСтр));
		Регистр.Резервы.ДвижениеРасход(Фирма,ТаблИтогов.ТМЦ,ТаблИтогов.ДокументРезерва,ТаблИтогов.Резерв);
	КонецЦикла;
	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры                                                                                                                                          
