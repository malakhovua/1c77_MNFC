// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем НачальнаяДатаДокумента;
Перем СписокНеоборотныхАктивов;


// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
Функция УдалятьСтрокиПриИзмененииАтрибута(ИмяАтрибута, ТекстВопроса) Экспорт
Перем Атрибут;
	Атрибут = ПолучитьАтрибут(ИмяАтрибута);

	Если Атрибут.Выбран()=0 Тогда
	    Возврат 0;
	КонецЕсли;

	Если Атрибут = глВосстановитьЗначение(Контекст,ИмяАтрибута) Тогда
		Возврат 0;
	КонецЕсли;
		
	Если КоличествоСтрок()>0 Тогда
		Рез = Вопрос(ТекстВопроса+" Удалить строки?","Да+Нет");
		Если Рез ="Нет" Тогда
			Атрибут = глВосстановитьЗначение(Контекст,ИмяАтрибута);
			УстановитьАтрибут(ИмяАтрибута,Атрибут);
			Возврат 0;
		Иначе
			УдалитьСтроки();
		КонецЕсли;
	КонецЕсли;
	глСохранитьЗначение(Контекст,ИмяАтрибута,Атрибут);
	Возврат 1;
КонецФункции

// ======================================
Процедура РассчитатьСтроку()
	Разница = КвоПоФакту - КвоПоУчету;
КонецПроцедуры 

// ===============================
Функция ПроверитьКоличество(Кво)
	Если НеоборотныйАктив.Выбран()=0 Тогда
		Предупреждение("Сначала выберите необоротный актив!");
		Возврат 0;
	КонецЕсли;

	ВидНеоборотногоАктива = НеоборотныйАктив.ВидНеоборотногоАктива;
	Если ВидНеоборотногоАктива = НМА Тогда
		Возврат 1;
	КонецЕсли;
	
	Если (Кво < 0) Или (Кво > 1) Тогда
		Предупреждение("Для " 
			+ ?(ВидНеоборотногоАктива = ОсновныеСредства, "основного средства", "необоротного материального актива") 
			+ " количество должно быть либо 0, либо 1.");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
КонецФункции

// ======================================
Процедура ЗаполнитьПоБухгалтерии(ТолькоРассчитать=0)
	Перем Ит;
	
	СпНеоборотныеАктивы = СоздатьОбъект("СписокЗначений"); // список необоротных активов
	Если ТолькоРассчитать=1 Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			Если СпНеоборотныеАктивы.Принадлежит(НеоборотныйАктив) = 0 Тогда
			    СпНеоборотныеАктивы.ДобавитьЗначение(НеоборотныйАктив);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;

	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения,Подразделение,2);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,МестоХранения,2);
	Если ТолькоРассчитать=1 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,СпНеоборотныеАктивы,2);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,,1,0);
	КонецЕсли;

	Ит.Опции(0,0);
	Ит.ВключатьСубсчета(-1);
	Ит.ВыполнитьЗапрос(?(Выбран()=1,ТекущийДокумент(),ДатаДок),,"10;11;12;191",,,,,"К");

	Если ТолькоРассчитать=1 Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			КвоПоУчету = 0;
			Ит.ВыбратьСубконто(3);
			Если Ит.ПолучитьСубконто(3,,НеоборотныйАктив)=1 Тогда  
				Ит.ВыбратьСчета();
				Пока Ит.ПолучитьСчет()=1 Цикл
					КвоПоУчету = КвоПоУчету + Ит.СНД("К");
				КонецЦикла;
			КонецЕсли;
			РассчитатьСтроку();
		КонецЦикла;
	Иначе
		Ит.ВыбратьСубконто(3);
		Пока Ит.ПолучитьСубконто(3)=1 Цикл
			НоваяСтрока();
			НеоборотныйАктив = Ит.Субконто(3);
			КвоПоУчету = 0;
			Ит.ВыбратьСчета();
			Пока Ит.ПолучитьСчет()=1 Цикл
				КвоПоУчету = КвоПоУчету + Ит.СНД("К");
			КонецЦикла;
			КвоПоФакту = КвоПоУчету;
			РассчитатьСтроку();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры 


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ======================================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Форма.Заголовок(глЗаголовок(Контекст,"Акт инвентаризации необоротных активов"));
	Форма.Фирма.Видимость(0);
	Возврат "";
КонецФункции 

// ======================================
Процедура ИзмКвоПоУчету()
	Если ПроверитьКоличество(КвоПоУчету) = 0 Тогда
		КвоПоУчету = 0;
	КонецЕсли;
	РассчитатьСтроку();
КонецПроцедуры 

// ======================================
Процедура ИзмКвоПоФакту()
	Если ПроверитьКоличество(КвоПоФакту) = 0 Тогда
		КвоПоФакту = 0;
	КонецЕсли;
	РассчитатьСтроку();
КонецПроцедуры 
                                    
// ======================================
Процедура ИзмНеоборотныйАктив()
	КвоПоУчету = 0;
	КвоПоФакту = 0;
	РассчитатьСтроку();
КонецПроцедуры 

// ======================================
Процедура ИзмПодразделение()       
	УдалятьСтрокиПриИзмененииАтрибута("Подразделение", "Подразделение было изменено");
КонецПроцедуры 

// ======================================
Процедура ИзмМестоХранения()       
	УдалятьСтрокиПриИзмененииАтрибута("МестоХранения", "Место хранения было изменено");
КонецПроцедуры 

// ======================================
Процедура ИзмФирма()
	УдалятьСтрокиПриИзмененииАтрибута("Фирма", "Фирма была изменена");
КонецПроцедуры 

// ======================================
Процедура Заполнить(ТолькоРассчитать=0)	

	Если Подразделение.Выбран() = 0 Тогда
	    Предупреждение("Не выбрано подразделение, по которому делается инвентаризация!");
		Возврат;
	КонецЕсли;

	Если МестоХранения.Выбран() = 0 Тогда
	    Предупреждение("Не выбран склад, на котором делается инвентаризация!");
		Возврат;
	КонецЕсли;
	
	Если ТолькоРассчитать=0 Тогда
		Если КоличествоСтрок() > 0 Тогда
			Рез = Вопрос("Удалить строки?","Да+Нет+Отмена");
			Если Рез ="Да" Тогда
				УдалитьСтроки();
			ИначеЕсли Рез = "Отмена" Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если Выбран() = 0 Тогда
		Если Вопрос("Документ не записан. Записать?", "Да+Нет") = "Да" Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ЗаполнитьПоБухгалтерии(ТолькоРассчитать);

КонецПроцедуры 

// ======================================
Процедура Печать()

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица_Укр");
	глУстПропись(Гривня,"у");

	КоличествоНаименований = 0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл
		КоличествоНаименований = КоличествоНаименований + ?(КвоПоФакту <> 0,1,0);
	КонецЦикла;

	Таб.ПараметрыСтраницы(1,,,,,,,,,1);
	Таб.Опции(0,0,0,0);
	Таб.ВывестиСекцию("Шапка");

	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл
		ПечПартия = "";
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	Таб.ВывестиСекцию("Дно");

	Таб.ТолькоПросмотр(1);
	Таб.Показать("Акт инвентаризации необоротных активов №"+Строка(НомерДок)+" от "+Строка(ДатаДок));
	глУстПропись(Гривня,"р");
КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================
                        
// ======================================
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Если (Рекв="ПредседательКомиссии") Или (Рекв="ПровелПроверку")
			Или (Рекв="ЧленКомиссии1") Или (Рекв="ЧленКомиссии2") Или (Рекв="ЧленКомиссии3") Тогда
		КонтФирмы = Фирма;
		Флаг = 0;
		ОткрытьФорму("Справочник.Сотрудники.ДляВыбора",КонтФирмы);
	ИначеЕсли Рекв="НеоборотныйАктив" Тогда
		СписокНеоборотныхАктивов.УдалитьВсе();
		ВыгрузитьТабличнуюЧасть(СписокНеоборотныхАктивов,"НеоборотныйАктив");
        Флаг = 0;
		КонтФирмы = Фирма;
		ОткрытьФорму("Справочник.НеоборотныеАктивы.ФормаСписка",КонтФирмы);
		ИзмНеоборотныйАктив();
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ОбработкаВыбораЗначения(Значение, Рекв, Флаг)
	Если (Рекв="ПредседательКомиссии") Или (Рекв="ПровелПроверку")
			Или (Рекв="ЧленКомиссии1") Или (Рекв="ЧленКомиссии2") Или (Рекв="ЧленКомиссии3")  Тогда
		Если Значение.Фирма<>Фирма Тогда
			Предупреждение("Можно выбирать только тех сотрудников, которые работают в этой фирме");
		    Флаг = 0;
		КонецЕсли;
	ИначеЕсли Рекв="НеоборотныйАктив" Тогда
		Если Значение.Выбран()=0 Тогда
			Возврат;
		КонецЕсли;
		Поз = СписокНеоборотныхАктивов.НайтиЗначение(Значение);
		Если (Поз > 0) И (Поз <> НомерСтроки) Тогда
			Предупреждение("Необоротный актив "+Значение+" уже выбран в строке "+Поз+"!");
		    Флаг = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ПриВыбореЗакладки(Ном,Значен);
	Форма.ИспользоватьСлой(Значен+", Общий",2);
КонецПроцедуры  

// ======================================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
	Форма.ИспользоватьСлой("Основной,Общий",2);
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.			Доступность(0);
		Форма.кЗаписать.	Доступность(0);
		Форма.кЗаполнить.	Доступность(0);
		Форма.кРассчитать.	Доступность(0);
		Форма.кФирма.		Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	

	глСохранитьЗначение(Контекст,"МестоХранения",МестоХранения);
	глСохранитьЗначение(Контекст,"Подразделение",Подразделение);
	глСохранитьЗначение(Контекст,"Фирма",		 Фирма);
	
	Форма.Подразделение.	ВыполнятьФормулуТолькоПриИзменении();
	Форма.МестоХранения.	ВыполнятьФормулуТолькоПриИзменении();
	Форма.НеоборотныйАктив.	ВыполнятьФормулуТолькоПриИзменении();
КонецПроцедуры 

// ======================================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	МестоХранения = глВосстановитьЗначение(,"ОсновнойСклад");
	Подразделение = глВосстановитьЗначение(,"ОсновноеПодразделение");
	ИзмФирма();
	ИзмМестоХранения();
	ИзмПодразделение();
КонецПроцедуры 

// ======================================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;
КонецПроцедуры

СписокНеоборотныхАктивов = СоздатьОбъект("СписокЗначений");
