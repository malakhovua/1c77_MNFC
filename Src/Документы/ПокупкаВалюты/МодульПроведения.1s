Перем СуммаБал, СуммаНБУ, СуммаПенс, ноПенсионныйПокупкаВал;

// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(РСчет,"Валютный расчетный счет");
	глВыбранЛи(Валюта,"Валюта расчетного счета");
	глВыбранЛи(Банк,"Банк");
	глВыбранЛи(СчетЗатратКР,"Счет затрат по разнице между бал. стоим. валюты и курсом НБУ");
	глВыбранЛи(СчетДоходовКР,"Счет доходов по разнице между бал. стоим. валюты и курсом НБУ");
	глВсеВыбрано = ?(глВсеВыбрано = 0,0, глПроверитьСчетЗатрат(СчетЗатратКР,2,0));
	глВыбранЛи(СубконтоЗатратКР,"Вид затрат по разнице между бал. стоим. валюты и курсом НБУ");
	Если КомБанкаГрн <> 0 Тогда
		глВыбранЛи(СчетЗатратКом,"Счет затрат по комиссионным");
		глВсеВыбрано = ?(глВсеВыбрано = 0,0, глПроверитьСчетЗатрат(СчетЗатратКом,2,0));
		глВыбранЛи(СубконтоЗатратКом,"Вид затрат по комиссионным");
		глВыбранЛи(СубконтоВалРасх,"Субконто валовых расходов по комиссионным");
	КонецЕсли;
	глВыбранЛи(СчетЗатратПенс,"Счет затрат по Пенсионному");
	глВсеВыбрано = ?(глВсеВыбрано = 0,0, глПроверитьСчетЗатрат(СчетЗатратПенс,2,0));
	глВыбранЛи(СубконтоЗатратПенс,"Вид затрат по Пенсионному");
	глВыбранЛи(ВалРасхПенсионный,"Субконто валовых расходов по отчислениям в Пенсионный фонд");
	глВыбранЛи(ВидДеятельности,"Вид деятельности");
	Если глВсеВыбрано = 1 Тогда
		Если РСчет.СчетУчета.Выбран() = 0 Тогда
		    глКомментарий("Для расчетного счета "+Сокрлп(РСчет)+" не задан бухгалтерский счет!",0,,"!");
			глВсеВыбрано = 0;
		КонецЕсли;
	КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция РассчитатьШапку()
	// пенсионный
	Если глПолучитьНиО(ноПенсионныйПокупкаВал,"ПенсПокупВал","об отчислениях в ПФ с покупки валюты") = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	СуммаНБУ = Окр(глПересчет(СуммаВал,РСчет.Валюта,Гривня,КурсНБУ,ДатаДок), 2);
	СуммаБал = СуммаГРНУМВБ;
	СуммаПенс = Окр(СуммаГРНУМВБ*ноПенсионныйПокупкаВал.Ставка.Получить(ДатаДок), 2);
	
	Возврат 1;
КонецФункции

// ===============================
Процедура ПроводкиШапка()
	// проводки
	глПроводка(Контекст,РСчет.СчетУчета,"33.3",СуммаНБУ,"Зачислена валюта, приобретенная на УМВБ",, РСчет,,,
	Банк,РСчетГрн,, Рсчет.Валюта,СуммаВал,"ВЛ");
	глПроводка(Контекст,"ВЛ",,СуммаБал,"Балансовая стоимость приобретенной валюты",, РСчет,,,
	,,, Рсчет.Валюта,СуммаВал,"ВЛ");
	глПроводкаПоЗатратам(Контекст,СчетЗатратКом,"33.3",КомБанкаГрн,"Комиссионные банка",, ВидДеятельности,Подразделение,СубконтоЗатратКом,
	Банк,РСчетГрн,, ,,"ВЛ");
	глПроводкаПоЗатратам(Контекст,СчетЗатратПенс,"65.1",СуммаПенс,"Начисление сбора в ПФ",, ВидДеятельности,Подразделение,СубконтоЗатратПенс,
	ноПенсионныйПокупкаВал,,, ,,"ВЛ");
	глПроводка(Контекст,"65.1","33.3",СуммаПенс,"Уменьшение задолженности перед ПФ",, ноПенсионныйПокупкаВал,,,
	Банк,РСчетГрн,, ,,"ВЛ");
	
	// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
	//Если СубконтоВалРасх <> Константа.НиДоходНиРасход Тогда
	//	глПроводка(Контекст,"ВР","ВР",КомБанкаГрн,"Покупка валюты: Валовые расходы",, Банк,ТекущийДокумент(),СубконтоВалРасх,
	//	Банк,ТекущийДокумент(),СубконтоВалРасх, ,,"ВЛ");
	//КонецЕсли;
	//Если ВалРасхПенсионный <> Константа.НиДоходНиРасход Тогда
	//	глПроводка(Контекст,"ВР","ВР",СуммаПенс,"Валовые расходы: Пенсионный фонд",, Банк,ТекущийДокумент(),ВалРасхПенсионный,
	//	Банк,ТекущийДокумент(),ВалРасхПенсионный, ,,"ВЛ");
	//КонецЕсли;
	// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
	
	СуммаКР = СуммаГРНУМВБ - СуммаНБУ;
	Если СуммаКР > 0 Тогда
	    // уплатили на УМВБ больше, чем зачислили в качестве покрытия купленной валюты - расходы
		глПроводкаПоЗатратам(Контекст,СчетЗатратКР,"33.3",СуммаКР,"Расходы: разница между бал. стоим. валюты и курсом НБУ",, ВидДеятельности,Подразделение,СубконтоЗатратКР,
		Банк,РСчетГрн,, ,,"ВЛ");
	Иначе
		// уплатили на УМВБ меньше, чем зачислили в качестве покрытия купленной валюты - доходы
		глПроводка(Контекст,"33.3",СчетДоходовКР,-СуммаКР,"Доходы: разница между бал. стоим. валюты и курсом НБУ",, Банк,РСчетГрн,,
		ВидДеятельности,Подразделение,, ,,"ВЛ");
	КонецЕсли;
	// переоценим бух. счет учета денежных средств
	глТаблицаСчетов.УдалитьСтроки();
	глТаблицаСчетов.НоваяСтрока();
	глТаблицаСчетов.Счет = РСчет.СчетУчета;
	глТаблицаСчетов.Субконто1 = РСчет;
	глТаблицаСчетов.Валюта = РСчет.Валюта;
	// курсовую разницу всегда считаем операционной
	глТаблицаСчетов.ОперационнаяКР = 1;
	глТаблицаСчетов.ВидДеятельности = Константа.БазВидДеятельности;
    глПереоценкаСчетов(Контекст, глТаблицаСчетов);
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);

	Если ПроверкаШапки()=0 Тогда
		глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	
	Если РассчитатьШапку() = 0 Тогда
	    глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	ПроводкиШапка();

	Операция.СуммаОперации = СуммаВал;
	Операция.Содержание = Примечание;
	Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры