Перем НачальнаяДатаДокумента;
Перем График;                 

// ===============================
Процедура ИзмГрафикФормированияПартий()   
	Если ГрафикФормированияПартий.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ГрафикФормированияПартий.ЧастотаФормированияПартий = Перечисление.СпособыФормированияПартий.Ежедневно Тогда
		График = "За дату " + Формат(ДатаДок,"ДДДММММГГГГ");
	ИначеЕсли ГрафикФормированияПартий.ЧастотаФормированияПартий = Перечисление.СпособыФормированияПартий.Ежемесячно Тогда		
		График = "За " + Формат(ДатаДок,"ДММММГГГГ");
	Иначе            
		//конечная дата либо дата следующей партии по данному графику, либо не выводится.
		фДатаИзДокумента = 0;
		Док = СоздатьОбъект("Документ.ПартияПродукции");
		Док.ВыбратьДокументы(ДатаДок+1);
		Пока Док.ПолучитьДокумент() = 1 Цикл
			Если Док.ГрафикФормированияПартий = ГрафикФормированияПартий Тогда
				ДатаКон = Док.ДатаДок;
				фДатаИзДокумента = 1;
				Прервать;  
			КонецЕсли;
		КонецЦикла;
		Если фДатаИзДокумента = 0 Тогда
			График = "За период с " + Формат(ДатаДок,"ДДДММГГ") + " по ...";   
		Иначе
			График = "За период с " + Формат(ДатаДок,"ДДДММГГ") + " по " +Формат(ДатаКон,"ДДДММГГ");
		КонецЕсли;
		
	КонецЕсли; 
	Форма.ТекстГрафика.Заголовок(График);	
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()   
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	
	ИзмГрафикФормированияПартий();
КонецПроцедуры
         
// ===============================
Процедура ВводНового(Копируем)
	Если Копируем = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
КонецПроцедуры          

//===============================
Функция УстДоступность()  
	Форма.Заголовок(глЗаголовок(Контекст,"Партия продукции"));
	Возврат "";
КонецФункции
                        
// ======================================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
    
	глВсеВыбрано = 1;
    глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(ГрафикФормированияПартий,"График формирования партий");
	Если глВсеВыбрано = 1 Тогда
		Док = СоздатьОбъект("Документ.ПартияПродукции");
        Если (ГрафикФормированияПартий.ЧастотаФормированияПартий = Перечисление.СпособыФормированияПартий.Ежедневно)
		или (ГрафикФормированияПартий.ЧастотаФормированияПартий = Перечисление.СпособыФормированияПартий.Произвольно) Тогда
			Док.ВыбратьДокументы(ДатаДок,ДатаДок);
			Пока Док.ПолучитьДокумент() = 1 Цикл
				Если (Док.ГрафикФормированияПартий = ГрафикФормированияПартий) и (Док.ТекущийДокумент() <> ТекущийДокумент())	Тогда
					глВсеВыбрано = 0;
					глКомментарий("На указанную дату уже есть документ с графиком формирования партий """+ГрафикФормированияПартий+"""",1,,"!");
					Прервать;  
				КонецЕсли;
			КонецЦикла;   
		ИначеЕсли ГрафикФормированияПартий.ЧастотаФормированияПартий = Перечисление.СпособыФормированияПартий.Ежемесячно Тогда		
			Если ДатаДок <> НачМесяца(ДатаДок) Тогда
				глВсеВыбрано = 0;                   
				глКомментарий("Дата документа должа быть началом месяца",1,,"!");
			Иначе
				Док.ВыбратьДокументы(ДатаДок,КонМесяца(ДатаДок));
				Пока Док.ПолучитьДокумент() =1 Цикл
					Если (Док.ГрафикФормированияПартий = ГрафикФормированияПартий) и (Док.ТекущийДокумент() <> ТекущийДокумент()) Тогда
						глВсеВыбрано = 0;
						глКомментарий("В этом месяце уже есть документ с графиком формирования партий """ +ГрафикФормированияПартий+"""",1,,"!");
						Прервать;  
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;  
	КонецЕсли;
	
	Если глВсеВыбрано = 0 Тогда
		глКомментарий("Документ не может быть записан!",0,,"!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;     

КонецПроцедуры    
             
АвтоВремяНачалоДня();
