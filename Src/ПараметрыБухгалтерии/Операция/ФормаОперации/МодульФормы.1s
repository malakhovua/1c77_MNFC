// ======================================
Функция УстДоступность()
	Заголовок = "Операция "+Документ.НомерДок;
	Если Выбрана() = 1 Тогда  
		Заголовок = Заголовок + ". Проведена";
	Иначе
		Заголовок = Заголовок + ". Новая";
	КонецЕсли;          
	Если  ПустоеЗначение(Документ.Автор)=0 Тогда
		Заголовок = Заголовок + ". Автор: "+Документ.Автор;
	КонецЕсли;
	Форма.Заголовок(Заголовок);
	Возврат "";
КонецФункции

//============================
Процедура ПриРедактированииНовойСтроки()
	НомерЖурнала = "0";
	НашаФирма = глВосстановитьЗначение(,"БазФирма");
КонецПроцедуры 

// ===============================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	Если Документ.Вид() = "СписаниеТМЦВПроизводство" Тогда
		СтатусВозврата(глПроверитьДоступКНормам(Документ.ПродукцияШ));
	КонецЕсли;
КонецПроцедуры
                  
// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	
	Документ.Автор = глПользователь;
	ВыбФирма = 0;
	ОднаФирма = 1;
	
	глВсеВыбрано = 1;
	ВыбратьПроводки();
	Пока ПолучитьПроводку() = 1 Цикл
        
		глВыбранЛи(НашаФирма,"Фирма", НомерПроводки());
		
		Если НашаФирма <> ВыбФирма Тогда
			Если ПустоеЗначение(ВыбФирма) = 1 Тогда
				ВыбФирма = НашаФирма;
			Иначе
				ОднаФирма = 0;
			КонецЕсли;
		КонецЕсли;

		Если Дебет.Счет.ВидСубконто(2) = ВидыСубконто.МесяцНачисленияЗП Тогда
		    Дебет.МесяцНачисленияЗП = НачМесяца(Дебет.МесяцНачисленияЗП);
		КонецЕсли;
		Если Кредит.Счет.ВидСубконто(2) = ВидыСубконто.МесяцНачисленияЗП Тогда
		    Кредит.МесяцНачисленияЗП = НачМесяца(Кредит.МесяцНачисленияЗП);
		КонецЕсли;
		// если в проводке есть торговый счет
		СчетаПоставщиковПокупателей = глСчетаПоставщиковПокупателей.Принадлежит(Число(Дебет.Счет.Код))+глСчетаПоставщиковПокупателей.Принадлежит(Число(Кредит.Счет.Код));
		СчетаУчетаТоваров = глСчетаУчетаТоваров.Принадлежит(Число(Дебет.Счет.Код))+глСчетаУчетаТоваров.Принадлежит(Число(Кредит.Счет.Код));
		Если ((СчетаПоставщиковПокупателей>0) ИЛИ (СчетаУчетаТоваров>0)) Тогда
			// проверим, можно ли выполнять ручные проводки по торговым счетам
			Если Документ.Выбран() = 0 Тогда
			    текДокумент = ""+Документ.Вид()+" "+СокрЛ(Документ.НомерДок)+" ("+Документ.ДатаДок+")";
			Иначе
			    текДокумент = Документ;
			КонецЕсли;
			// проверим, можно ли выполнять ручные проводки по торговым счетам
			Если Константа.ЗапретитьПроводкиПоТорговымСчетам = 2 Тогда
				// запрещаем				
				глКомментарий("Запрещено формирование ручных операций по счетам "+?(СчетаУчетаТоваров>0,"учета товаров.","учета взаиморасчетов.")+"",0,,"!!");
				глКомментарий(" "+текДокумент+" не проводится!",0,,"!!");
				СтатусВозврата(0);
			ИначеЕсли Константа.ЗапретитьПроводкиПоТорговымСчетам = 1 Тогда
				// разрешаем, но предупреждаем
				глКомментарий("Формирование ручных операций по счетам "+?(СчетаУчетаТоваров>0,"учета товаров ","учета взаиморасчетов")+" может привести к расхождению с данными по регситрам оперативного учета.",0,,"!!");
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	Если глВсеВыбрано = 0 Тогда
		глКомментарий(" "+Документ+" от "+ДатаОперации+" не проводится!",0,,"!!");
		СтатусВозврата(0);
	КонецЕсли;
	
	// если во всех проводках указана одна и та же фирма,
	// запишем ее в реквизит шапки Фирма, иначе очистим его
	Если ОднаФирма = 1 Тогда
		Документ.Фирма = ВыбФирма;
	Иначе
		Документ.Фирма = 0;
	КонецЕсли;
КонецПроцедуры


                     