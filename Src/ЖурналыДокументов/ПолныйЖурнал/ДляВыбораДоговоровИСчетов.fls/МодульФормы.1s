Перем спРасшифровка,РазрешитьОтбор,РазрешитьВидОтбора, фРазрешатьПустогоКонтрагента;

Перем СписокВидовДокументов;

Перем КонтрагентДляОтбора,ВидДокументаДляОтбора;

Перем ЗначениеВВидеСтроки;

Перем  РегВзаиморасчетыПоставщиков; //--- УМК Сандомирский В.Ю. (12.09.14)

Функция РассчитатьДолг(ПоКнопке=0)  Экспорт
    Перем ЕстьДатаОплаты;
    
	ТекДок = ТекущийДокумент;
	Контрагент = КонтрагентДляОтбора;
	ТипОперации = спПоставщикПокупатель.ПолучитьЗначение(спПоставщикПокупатель.ТекущаяСтрока());
	
	Если ПустоеЗначение(Контрагент)=1 Тогда
		Предупреждение("Не выбран контрагент.");
		Возврат "";
	КонецЕсли;

	// В зависимости от вида документа
	// определим РегистрУчета и
	// определим текущий курс валюты Кредита
	ВремРегистры=СоздатьОбъект("Регистры");
	Если ТипОперации="Продажа" Тогда
		ИмяРегистрУчета="ВзаиморасчетыПокупателей";
		Рег=ВремРегистры.ВзаиморасчетыПокупателей;
		Знак=1;
		// значит кредит мы даем клиенту
		// значит при продаже товара мы начисляем Долг (на клиента),
		ВалютаКредита=Контрагент.ВалютаКредита;
		СуммаКред=Контрагент.СуммаКредита.Получить(ТекДок.ДатаДок);
		Если СуммаКред<>0 Тогда
			Глубина=Контрагент.Глубина.Получить(ТекДок.ДатаДок);
		Иначе
			Глубина=0;
		КонецЕсли;
	ИначеЕсли ТипОперации="Закупка" Тогда
		ИмяРегистрУчета="ВзаиморасчетыПоставщиков";
		Рег=ВремРегистры.ВзаиморасчетыПоставщиков;
		Знак=-1;
		// значит кредит нам дает клиент
		ВалютаКредита=Контрагент.ВалютаКредитаПоставщика;
		СуммаКред=Контрагент.СуммаКредитаПоставщика.Получить(ТекДок.ДатаДок);
		
		Если СуммаКред<>0 Тогда
			Глубина=Контрагент.ГлубинаКредитаПоставщика.Получить(ТекДок.ДатаДок);
		Иначе
			Глубина=0;
		КонецЕсли;
	КонецЕсли;
	
	Если ВалютаКредита.Выбран()=0 Тогда
		Предупреждение("Не найдена валюта кредита клиента!");
	КонецЕсли;      
	
	Если ВидОтбора.ТекущаяСтрока() = 2 Тогда
		//Договора
		Рег.УстановитьФильтр(ТекДок.Фирма,Контрагент,ТекДок,,,,);
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 3 Тогда            
		//Счета
		Рег.УстановитьФильтр(ТекДок.Фирма,Контрагент,,,ТекДок,,);
	КонецЕсли;
	
	Просрочено=0;
	Срок=0;
	ТекущийДолг=0;
	СрочныйДолг=0; // Это тот долг,для оплаты которого осталось 7 дней сроку.
	ПросроченныйДолг=0;
	Рег.ВыбратьИтоги(); //Берем итоги на точку актуальности
	
	//Рассчитываем остатки для показа в строке или в списке
	Если ПоКнопке = 0 Тогда
		Пока Рег.ПолучитьИтог()>0 Цикл
			Если ПустоеЗначение(Рег.КредДокумент)=1 Тогда
				// пропускаем пустые документы
				Продолжить;
			КонецЕсли;
			ТекущийДолг=ТекущийДолг+Рег.ДолгОсн;
		КонецЦикла;
		Возврат ТекущийДолг;
	КонецЕсли;
	
	Пока Рег.ПолучитьИтог()>0 Цикл
		Если ПустоеЗначение(Рег.КредДокумент)=1 Тогда
			// пропускаем пустые документы
			Продолжить;
		КонецЕсли;

		ВидКредДок=Рег.КредДокумент.Вид();
		
		Если глЕстьРеквизитШапки("ДатаОплаты",ВидКредДок) = ДА Тогда
			ДатаОплаты=Рег.КредДокумент.ДатаОплаты;
			Если Число(ДатаОплаты)=0 Тогда
				ДатаОплаты=Рег.КредДокумент.ДатаДок;
			КонецЕсли;
		Иначе
			ДатаОплаты=Рег.КредДокумент.ДатаДок;
		КонецЕсли;

		ТекущийДолг=ТекущийДолг+Рег.ДолгОсн;
		Если ((Знак*Рег.ДолгОсн)>0) И (Просрочено<(ТекДок.ДатаДок-ДатаОплаты)) Тогда
			// если еще не погашено
			Просрочено=ТекДок.ДатаДок-ДатаОплаты;
			Если ДатаОплаты < ТекДок.ДатаДок Тогда
				ПросроченныйДолг=ПросроченныйДолг+Рег.ДолгОсн;
			КонецЕсли;
			Если ДатаОплаты > (ТекДок.ДатаДок-7) Тогда
				СрочныйДолг=СрочныйДолг+Рег.ДолгОсн;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Стр0="Контрагент: "+?(ПустоеЗначение(Контрагент.ПолнНаименование)=0,СокрЛП(Контрагент.ПолнНаименование),СокрЛП(Контрагент.Наименование))+". ";
	Стр001="Договор: "+СокрЛП(ТекДок)+". ";
	Стр01="    на "+ПолучитьТА();
	Стр1=?(Знак>0,"Кредит контрагенту= ","Кредит нам= ")+?(СуммаКред=0,"0",глФРМ(СуммаКред,ВалютаКредита,1))+" , на "+СокрЛ(Глубина)+" дней. ";
	Стр2=?(Знак>0,"Долг контрагента= ","Наш долг= ")+?(ТекущийДолг=0,"0",глФРМ(Знак*ТекущийДолг,ВалютаКредита,1))+" . ";
	Стр3=?(Знак*ТекущийДолг>СуммаКред,"КРЕДИТ ИСЧЕРПАН! ","Остаток кредита= "
	+?(Знак*ТекущийДолг=СуммаКред,"0",глФРМ(СуммаКред-Знак*ТекущийДолг,ВалютаКредита,1))+" . ");
	Стр4=?(Знак*ПросроченныйДолг>0,"ПРОСРОЧЕНА ОПЛАТА! "+глФРМ(Знак*ПросроченныйДолг,ВалютаКредита,1)+" на "
	+СокрЛ(Просрочено)+" дней.","Не просрочено.");
	Стр5=?(Знак*СрочныйДолг>0,"Срочный долг= "+глФРМ(Знак*СрочныйДолг,ВалютаКредита,1),"");

	Предупреждение(Стр0+РазделительСтрок+Стр001+РазделительСтрок+Стр01+РазделительСтрок+Стр1+РазделительСтрок+Стр2+РазделительСтрок+Стр3+РазделительСтрок+Стр4+?(ПустоеЗначение(Стр5)=1,"",РазделительСтрок+Стр5));

    Возврат "";
КонецФункции

// ===============================
Процедура ИзмСпособОтображенияОстатков()
   	Если спСпособОтображенияОстатков.ТекущаяСтрока() = 2 Тогда
	    Форма.стлбОстаток.Видимость(1);
	Иначе
		Форма.стлбОстаток.Видимость(0);
	КонецЕсли;
	глСохранитьЗначение(,"спСпособОтображенияОстатков",спСпособОтображенияОстатков.ТекущаяСтрока());
	глСохранитьЗначение(,"спПоставщикПокупатель",спПоставщикПокупатель.ТекущаяСтрока());
КонецПроцедуры

// ===============================
Функция Остаток(Тип)
	Если (спСпособОтображенияОстатков.ТекущаяСтрока() <> Тип) или (ПустоеЗначение(ВидДок)=1) Тогда
		// нужен остаток в списке, а установлено - в инф. строке (или наоборот)
		Возврат "";
	КонецЕсли;
	
	Если (ВидДок = "ПриходнаяНакладнаяГТД") ИЛИ (ВидДок = "ПриходнаяНакладная") ИЛИ (ВидДок = "УМК_ЗаказКлиента") Тогда //--- УМК Сандомирский В.Ю. (29.09.14) или (27.10.14)
		Возврат "";		
	Иначе	
		Ост = РассчитатьДолг(0);		
	КонецЕсли;
	
    Возврат Формат(Ост,"Ч21.3.'");
КонецФункции   

// ===============================
Процедура кНовыйЗаказ()
	ОткрытьФорму("Документ.Заказ",КонтрагентДляОтбора);
КонецПроцедуры //кНовыйЗаказ

// ===============================
Процедура кНовыйДоговор()           
	ОткрытьФорму("Документ.Договор",КонтрагентДляОтбора);
КонецПроцедуры //кНовыйЗаказ

// ===============================
Функция НомДоговора()
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если глЕстьРеквизитШапки("НомерДоговора", ТекущийДокумент.Вид()) = Да Тогда
			Возврат ТекущийДокумент.НомерДоговора;
		КонецЕсли;	
	КонецЕсли;	
КонецФункции		
	
// ===============================
Функция ВидТорг()
	Док = СоздатьОбъект("Документ");
	Док = ТекущийДокумент;
	Если Док.Выбран() = 1 Тогда
		Если глЕстьРеквизитШапки("ВидТорговли", Док.Вид()) = Да Тогда
			Возврат Док.ВидТорговли;
		КонецЕсли;	
	КонецЕсли;	
КонецФункции		

// ===============================
Процедура ПриВыбореСтроки()
	Если (Форма.РежимВыбора()=0) и (Форма.МодальныйРежим()=1) Тогда
		Док = СоздатьОбъект("Документ");
		Док = ТекущийДокумент;
		Если Док.ПометкаУдаления() = 1 тогда
			спРасшифровка.УдалитьВсе();
			Форма.Закрыть();
		Иначе	
			спРасшифровка.УдалитьВсе();
			спРасшифровка.ДобавитьЗначение(Информация,"Контрагент");
			спРасшифровка.ДобавитьЗначение(ТекущийДокумент,"Документ");
			Форма.Закрыть();
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры                               

// ===============================
Процедура ПриУстановкеБыстрогоОтбора() Далее
	
// ===============================
Процедура ВыборЗначения(ПоКнопке = 0)  
	Перем ЗначениеИзменилось,СтароеЗначение,ВыбЗначение;                     
	
	ЗначениеИзменилось=0;    
		
	СтароеЗначение=КонтрагентДляОтбора;
	ВыбЗначение = СоздатьОбъект("Справочник.Контрагенты");
	Если ВыбЗначение.Выбрать("Выбор контрагента","Форма списка")=1 Тогда
		КонтрагентДляОтбора = ВыбЗначение.ТекущийЭлемент();
		Если ВыбЗначение<>СтароеЗначение Тогда
			ЗначениеИзменилось=1;    
		КонецЕсли;	
	КонецЕсли;	
	        
	Если (ЗначениеИзменилось=1) и (ПоКнопке=1) Тогда
		ПриУстановкеБыстрогоОтбора();
	КонецЕсли;	

КонецПроцедуры                                         

// ===============================
Процедура ПриУстановкеБыстрогоОтбора()
	Перем ТекущДок;                           
	
	ТекущДок = ТекущийДокумент;
	
	Форма.кЗначение.Доступность(1);
	
	Если (ПустоеЗначение(КонтрагентДляОтбора)=1) и (фРазрешатьПустогоКонтрагента <> 1) Тогда
		ВыборЗначения();
	КонецЕсли;           
	
	Если ВидОтбора.ТекущаяСтрока() = 1 Тогда
		УстановитьОтбор("Контрагент", КонтрагентДляОтбора);
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 2 Тогда
		УстановитьОтбор("ДоговораКонтрагентов", КонтрагентДляОтбора);
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 3 Тогда    
		УстановитьОтбор("СчетаКонтрагентов", КонтрагентДляОтбора);
		
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 4 Тогда   // --- УМК Сандомирский В.Ю. (27.10.14) приходная в отбор 
		
		УстановитьОтбор("ГТДКонтрагентов", КонтрагентДляОтбора);	
		
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 5 Тогда   // --- УМК Сандомирский В.Ю. (27.10.14) приходная в отбор 
		
		УстановитьОтбор("ПриходныеКонтрагентов", КонтрагентДляОтбора);
		
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 6 Тогда   // --- УМК Сандомирский В.Ю. (28.11.14) приходная в отбор 
		
		УстановитьОтбор("ЗаказыКлиентов", КонтрагентДляОтбора);
		
	ИначеЕсли ВидОтбора.ТекущаяСтрока() = 7 Тогда   // --- УМК Сандомирский В.Ю. (08.01.15) РН в отбор 
		
		УстановитьОтбор("РНКонтрагентов", КонтрагентДляОтбора);	
		
	КонецЕсли;                              
	
	Если ПустоеЗначение(КонтрагентДляОтбора)=0 Тогда
		ЗначениеВВидеСтроки=Строка(КонтрагентДляОтбора);
	Иначе                                               
		ЗначениеВВидеСтроки="Выберите контрагента!";
	КонецЕсли;
	
	Попытка
		АктивизироватьОбъект(ТекущДок);
	Исключение	
	КонецПопытки;	
КонецПроцедуры // ПриУстановкеБыстрогоОтбора
                                
// ===============================
Процедура ПриОткрытии()
	ВидОтбора.ДобавитьЗначение("по контрагенту"); 
	ВидОтбора.ДобавитьЗначение("договора контрагента"); 
	ВидОтбора.ДобавитьЗначение("счета контрагента");   
	
	ВидОтбора.ДобавитьЗначение("ГТД контрагента"); 			// --- УМК Сандомирский В.Ю. (27.10.14) приходная в отбор
	ВидОтбора.ДобавитьЗначение("Приходные контрагента"); 	// --- УМК Сандомирский В.Ю. (27.10.14) приходная в отбор
	ВидОтбора.ДобавитьЗначение("Заказы клиента"); 			// --- УМК Сандомирский В.Ю. (28.11.14) приходная в отбор
	
	ВидОтбора.ДобавитьЗначение("РН клиента"); 				// --- УМК Сандомирский В.Ю. (08.01.15) РН в отбор
	
	спСпособОтображенияОстатков.ДобавитьЗначение(1,"Не показывать");
	спСпособОтображенияОстатков.ДобавитьЗначение(2,"В списке");
	спСпособОтображенияОстатков.ДобавитьЗначение(3,"В инф. строке");
	спСпособОтображенияОстатков.ТекущаяСтрока(глВосстановитьЗначение(,"спСпособОтображенияОстатков"));
	спПоставщикПокупатель.ДобавитьЗначение("Закупка","по поставщикам");
	спПоставщикПокупатель.ДобавитьЗначение("Продажа","по покупателям");
	спПоставщикПокупатель.ТекущаяСтрока(глВосстановитьЗначение(,"спПоставщикПокупатель"));
	ИзмСпособОтображенияОстатков();
	
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		РазрешитьВидОтбора = 0;
		РазрешитьОтбор = 0;
		Если ТипЗначенияСтр(Форма.Параметр)="СписокЗначений" Тогда
			спРасшифровка = Форма.Параметр;
            КонтрагентДляОтбора = спРасшифровка.Получить("Контрагент");
			ГрафаОтбора = спРасшифровка.Получить("ГрафаОтбора");
			фРазрешатьПустогоКонтрагента = спРасшифровка.Получить("фРазрешатьПустогоКонтрагента");
			
			Если ГрафаОтбора = "ДоговораКонтрагентов" Тогда
				ВидОтбора.ТекущаяСтрока(2);
			ИначеЕсли ГрафаОтбора = "СчетаКонтрагентов" Тогда
				ВидОтбора.ТекущаяСтрока(3);
			ИначеЕсли ГрафаОтбора = "ГТДКонтрагентов" Тогда  //--- УМК Сандомирский В.Ю. (28.11.14)
				ВидОтбора.ТекущаяСтрока(4);	
			ИначеЕсли ГрафаОтбора = "ПриходныеКонтрагентов" Тогда  //--- УМК Сандомирский В.Ю. (28.11.14)
				ВидОтбора.ТекущаяСтрока(5);	
			ИначеЕсли ГрафаОтбора = "ЗаказКлиентов" Тогда  //--- УМК Сандомирский В.Ю. (28.11.14)
				ВидОтбора.ТекущаяСтрока(6);		
			ИначеЕсли ГрафаОтбора = "РНКонтрагентов" Тогда  //--- УМК Сандомирский В.Ю. (28.11.14)
				ВидОтбора.ТекущаяСтрока(7);	
			Иначе                          
				//Разрешить менять вид отбора
				РазрешитьВидОтбора = 1;
				ВидОтбора.ТекущаяСтрока(1);
			КонецЕсли;                   
			
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр)="Справочник" Тогда
			КонтрагентДляОтбора = Форма.Параметр;    
			ВидОтбора.ТекущаяСтрока(1);
		КонецЕсли;
		
		Если (ПустоеЗначение(КонтрагентДляОтбора)=1) или (спРасшифровка.Получить("РазрешитьОтбор") = 1) Тогда
			//Разрешать менять значение отбора
			РазрешитьОтбор = 1;
		КонецЕсли;
		
		ПриУстановкеБыстрогоОтбора();
		Форма.ОбработкаВыбораСтроки(1);
	Иначе
		ВидОтбора.ТекущаяСтрока(2);
		ПриУстановкеБыстрогоОтбора();
	КонецЕсли;
	
	Форма.ВидОтбора.ВыполнятьФормулуТолькоПриИзменении(1);
	
	Если РазрешитьОтбор=0 Тогда
	    // нельзя менять отбор
		Форма.кЗначение.Доступность(0);
	КонецЕсли;
	Если РазрешитьВидОтбора=0 Тогда
	    // нельзя менять вид отбора
		Форма.ВидОтбора.Доступность(0);
	КонецЕсли;
	
	
	
	Если ВидДок = "ПриходнаяНакладнаяГТД" Тогда
		ВрРегистры = СоздатьОбъект("Регистры");							  //--- УМК Сандомирский В.Ю. (12.09.14) 	
		РегВзаиморасчетыПоставщиков = ВрРегистры.ВзаиморасчетыПоставщиков;  //--- УМК Сандомирский В.Ю. (12.09.14)
		РегВзаиморасчетыПоставщиков.УстановитьФильтр(ТекущийДокумент.Фирма,ТекущийДокумент.Контрагент);
		РегВзаиморасчетыПоставщиков.ВременныйРасчет();
		ВрРегистры.РассчитатьРегистрыПо(РабочаяДата());
	КонецЕсли;
	
КонецПроцедуры              

// ===============================
Процедура ПриУстановкеОтбора(ИмяРекв,ЗначРекв)
	Если ( не ((ИмяРекв="ДоговораКонтрагентов") или (ИмяРекв="СчетаКонтрагентов")
	или (ИмяРекв="Контрагент"))) или (РазрешитьОтбор = 0) Тогда
	    СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры   

// ===============================
Функция УстДоступность()
	Если ВидОтбора.ТекущаяСтрока() = 2 Тогда
		//Договора
		Форма.кНовыйДоговор.Доступность(1);
		Форма.кНовыйЗаказ.Доступность(1);
	Иначе                          
		Форма.кНовыйДоговор.Доступность(0);
		Форма.кНовыйЗаказ.Доступность(0);
	КонецЕсли;
    
КонецФункции //УстДоступность()

Функция Адрес()
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если ТекущийДокумент.Вид() = "Договор" Тогда
			Возврат глАдресСтрокой(ТекущийДокумент.Адрес);
		КонецЕсли;
	КонецЕсли;
КонецФункции



//====================================================================== УМК Сандомирский В.Ю. (07.09.14)
Функция Продавец()
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если ТекущийДокумент.Вид() = "Договор" Тогда
			Возврат ТекущийДокумент.Продавец;
		КонецЕсли;
	КонецЕсли;
КонецФункции // Продавец


//====================================================================== УМК Сандомирский В.Ю. (07.09.14)
Функция ВернутьВалютуДокумента()
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если ТекущийДокумент.Вид() = "ПриходнаяНакладнаяГТД" Тогда
			Возврат ТекущийДокумент.Валюта;
		КонецЕсли;
	КонецЕсли;
КонецФункции 

//====================================================================== УМК Сандомирский В.Ю. (07.09.14)
Функция ВернутьСуммаВалютнаяДокумента()
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если ТекущийДокумент.Вид() = "ПриходнаяНакладнаяГТД" Тогда
			Возврат ТекущийДокумент.Итог("СуммаБезНДСВал");
		КонецЕсли;
	КонецЕсли;
КонецФункции 

//====================================================================== УМК Сандомирский В.Ю. (07.09.14)
Функция ВернутьВалютныйВзаиморасчет()
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если ТекущийДокумент.Вид() = "ПриходнаяНакладнаяГТД" Тогда
			Возврат РегВзаиморасчетыПоставщиков.СводныйОстаток(ТекущийДокумент.Фирма,ТекущийДокумент.Контрагент,,,,ТекущийДокумент,ТекущийДокумент.Валюта,"Долг");
		КонецЕсли;
	КонецЕсли;
КонецФункции 


РазрешитьОтбор=1;
РазрешитьВидОтбора = 1;
фРазрешатьПустогоКонтрагента = 0;

КонтрагентДляОтбора = СоздатьОбъект("Справочник.Контрагенты");

ЗначениеВВидеСтроки = "";
УстановитьИнтервал('01.01.1980', ТекущаяДата());