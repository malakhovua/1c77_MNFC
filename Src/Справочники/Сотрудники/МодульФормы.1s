Перем НачФирма;

Процедура ЗаполнитьСписокСпецодежды() Далее		//--- УМК Сандомирский В.Ю. (10.06.15) спецодежда

// ===============================
Процедура ИзмФИО()
	Если ПустаяСтрока(Фамилия) = 0 Тогда
	    Наименование = глФИО(СокрЛП(Фамилия) + " " + СокрЛП(Имя) + " " + СокрЛП(Отчество), 1);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИсторияЗначений()
	глРедактироватьИсториюЗначений(Контекст, "",
	"История периодических реквизитов (" + ТекущийЭлемент() + ")", 1, 0);
КонецПроцедуры //ИсторияЗначений

// ======================================
Функция УстДоступность()
	Если (ПустоеЗначение(Фирма) <> 1) и (Фирма = НачФирма) Тогда
	    Форма.Фирма.Доступность(0);
	КонецЕсли;
	
	Если НазваниеНабораПрав() = "Администратор" Тогда //--- УМК Сандомирский В.Ю, (04.11.14)
		Форма.тСупервайзер.Доступность(1);
		Форма.Супервайзер.Доступность(1);
	Иначе
		Форма.тСупервайзер.Доступность(0);
		Форма.Супервайзер.Доступность(0);
	КонецЕсли;
	
	Возврат "";
КонецФункции

// ======================================
Процедура ИзмФирма()
	Если Фирма.Выбран() = 1 Тогда
		УстановитьНовыйКод(СокрЛП(Фирма.Префикс)+"-");
	КонецЕсли;
КонецПроцедуры 
                    
// ======================================
Процедура ИзмСчетЗатрат()
	Если СчетЗатрат.Выбран() = 1 Тогда
		Если ИспользоватьСчетаРасходов = Класс8 Тогда
			Если (СчетЗатрат <> СчетПоКоду("231")) и (СчетЗатрат <> СчетПоКоду("24")) Тогда
		   		Предупреждение("Если используется только 8-й класс счетов, счет затрат не должен указываться или же может быть указан счет ""231"" либо счет ""24""!");
			СчетЗатрат = глВосстановитьЗначение("Сотрудники","СчетЗатрат");
			КонецЕсли;
		ИначеЕсли глПроверитьСчетЗатрат(СчетЗатрат,1)=0 Тогда
			СчетЗатрат = глВосстановитьЗначение("Сотрудники","СчетЗатрат");
		КонецЕсли;	
	КонецЕсли;
	глСохранитьЗначение("Сотрудники","СчетЗатрат", СчетЗатрат);
КонецПроцедуры 

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
	    Возврат;
	КонецЕсли;
	ИспользоватьДату('01.01.1980');
	ТипСотрудника = 1; // основной
	Фирма = глВосстановитьЗначение(,"БазФирма");
	ИзмФирма();
	ВидДеятельности = глВосстановитьЗначение(,"БазВидДеятельности");
	КТУ = 1;
КонецПроцедуры //ВводНового

// ===============================
Процедура ПриОткрытии()
	Если Константа.ЗапретитьРедактированиеСправочников = Да Тогда
		Форма.ТолькоПросмотр(1);
	КонецЕсли;
	СохранениеПериодическихРеквизитов(2, "*");
	глСохранитьЗначение("Сотрудники","СчетЗатрат", СчетЗатрат);
	Если (НазваниеНабораПрав() = "ПрочиеЗаказ") ИЛИ (НазваниеНабораПрав() = "МастерЭкспедиции") Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Общие");
	Форма.Закладки.ДобавитьЗначение("Паспорт","Паспортные, налоговые данные");
	Форма.Закладки.ДобавитьЗначение("Зарплата","Данные по з/п");
	Форма.Закладки.ДобавитьЗначение("Спецодежда","Спецодежда");

	Форма.ИспользоватьСлой("Основной, Общий",2);
	
	Активизировать("Фамилия",0);
	Если Выбран() = 1 Тогда
		НачФирма = Фирма;
	КонецЕсли;
	Если НазваниеНабораПрав() <> "Администратор" Тогда
		Форма.История.Доступность(0);
	КонецЕсли;	

	Для инд = 1 По 7 Цикл					//--- УМК Сандомирский В.Ю. (17.07.14) 
		Чис = Число(Сред(ДниОплаты, Инд, 1));
		Если Инд = 1 Тогда
		    ф1 = Чис;
		ИначеЕсли Инд = 2 Тогда
		    ф2 = Чис;
		ИначеЕсли Инд = 3 Тогда
		    ф3 = Чис;
		ИначеЕсли Инд = 4 Тогда
		    ф4 = Чис;
		ИначеЕсли Инд = 5 Тогда
		    ф5 = Чис;
		ИначеЕсли Инд = 6 Тогда
		    ф6 = Чис;
		ИначеЕсли Инд = 7 Тогда
		    ф7 = Чис;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьСписокСпецодежды(); //--- УМК Сандомирский В.Ю. (10.06.15) спецодежда
	
КонецПроцедуры

// ===============================
Процедура ПриВыбореЗакладки(Ном,Значение);
	Форма.ИспользоватьСлой("Общий,"+Значение,2);
	Если НазваниеНабораПрав() <> "Администратор" Тогда
	    Форма.Оклад.Видимость(0);
		Форма.тТариф.Видимость(0); //--- УМК Сандомирский В.Ю, (07.10.14) добавил текстовое поле
		Форма.Аванс.Видимость(0);
		Форма.История.Доступность(0);
	КонецЕсли;	
КонецПроцедуры 

// ===============================
Процедура ПриЗаписи()
	Если (глЗаписьПериодическихРеквизитов(Контекст) = 0)  Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Если СчетЗатрат.Выбран() = 1  Тогда                    
		Если (ИспользоватьСчетаРасходов = Класс8) Тогда
			Если (СчетЗатрат <> СчетПоКоду("231")) и (СчетЗатрат <> СчетПоКоду("24")) Тогда
		   		глКомментарий("Если используется только 8-й класс счетов, счет затрат не должен указываться или же может быть указан счет ""231"" либо счет ""24""!",0,,"!");
		   		СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		ИначеЕсли глПроверитьСчетЗатрат(СчетЗатрат,2)=0 Тогда
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;	
	ИначеЕсли ИспользоватьСчетаРасходов <> Класс8 Тогда
	    глКомментарий("Не выбран счет затрат!",0,,"!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	//--- УМК Сандомирский В.Ю, (24.04.14) Закоментил
	//глОтправитьУведомлениеЭП("Справочник.Сотрудники", "Сотрудники: уведомление об изменении", ?(ТекущийЭлемент().Выбран() = 1, "Изменен","Добавлен новый") + " элемент справочника Сотрудники: " + Наименование + " пользователь: " + Строка(глПользователь));	

КонецПроцедуры //ПриЗаписи

// ===============================
Процедура ОткрытьВР()
	Если ТекущийЭлемент().Выбран() = 1 Тогда
		КФормы = ТекущийЭлемент();
		ОткрытьФорму("Справочник.ВидыРаботСотрудников.ФормаСписка", КФормы);
	КонецЕсли;
КонецПроцедуры

// =============================== //--- УМК Сандомирский В.Ю. (17.07.14) 
Процедура ИзмКН(Чис) 
	
	ДОплаты = "";
	Для Инд = 1 По 7 Цикл
		ДОплаты = ДОплаты + Шаблон("[ф" + Строка(Инд) + "]");
	КонецЦикла;
	ДниОплаты = ДОплаты;
КонецПроцедуры

//======================================================================
Процедура ЗаполнитьПятидневку()
	
	Ф1 = 1;
	Ф2 = 1;
	Ф3 = 1;
	Ф4 = 1;
	Ф5 = 1;


	//
	//Для Инд = 1 По 5 Цикл		
	//	УстановитьАтрибут("Ф"+Инд,1);
	//	
	//КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПятидневку()


//====================================================================== //--- УМК Сандомирский В.Ю, (01.09.14) Формируем должность строкой
Процедура ВыбВидТарифа()
	//--- УМК Сандомирский В.Ю, (01.09.14) Формируем должность строкой
	ТекДолжность = "";
	Если ПустоеЗначение(ВидТарифа) <> 1 Тогда
		ТекДолжность = ТекДолжность + СокрЛП(ВидТарифа.Наименование) + ", ";
	КонецЕсли;
	
	Если Выбран() = 1 Тогда
		СпрВидыРабот = СоздатьОбъект("Справочник.ВидыРаботСотрудников");
		СпрВидыРабот.ИспользоватьВладельца(ТекущийЭлемент());
		СпрВидыРабот.ВыбратьЭлементы();
		Пока СпрВидыРабот.ПолучитьЭлемент() = 1 Цикл
			Если СпрВидыРабот.ПометкаУдаления() = 1 Тогда
				Продолжить;	
			КонецЕсли;		
			ТекДолжность = ТекДолжность + СокрЛП(СпрВидыРабот.ВидРабот.Наименование) + ", ";
		КонецЦикла;
	КонецЕсли;
	
	Если ТекДолжность <> "" Тогда
		Если Должность <> ТекДолжность Тогда		
			Должность = ТекДолжность;		
		КонецЕсли;	
	КонецЕсли;
	//... УМК Сандомирский В.Ю, (01.09.14) 
КонецПроцедуры // ВыбВидТарифа

//====================================================================== //--- УМК Сандомирский В.Ю, (07.10.14)
Функция ВернутьТариф()
	
	Если ПустоеЗначение(ВидТарифа) <> 1 Тогда
		Возврат ВидТарифа.Тариф.Получить(РабочаяДата());
	КонецЕсли;
	
КонецФункции // ВернутьТариф()

// --- ЗАКЛАДКА СПЕЦОДЕЖДА

//====================================================================== //--- УМК Сандомирский В.Ю. (10.06.15) спецодежда
Процедура ЗаполнитьСписокСпецодежды()
	Перем ЭлементСпецодежды;
	Перем Индекс;
	
	Если Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;

	ЭлементСпецодежды = СоздатьОбъект("Справочник.УМК_СпецодеждаСотрудника");
	ЭлементСпецодежды.ИспользоватьВладельца(ТекущийЭлемент());
	ЭлементСпецодежды.ПорядокКодов();
	ЭлементСпецодежды.ВыбратьЭлементы();
	Индекс = 0; 
	СписокСпецодежды.УдалитьСтроки();
	Пока ЭлементСпецодежды.ПолучитьЭлемент()=1 Цикл
		Если ЭлементСпецодежды.ПометкаУдаления()=1 Тогда
		    Продолжить;
		КонецЕсли;

	    Индекс = Индекс + 1;
		СписокСпецодежды.НоваяСтрока(Индекс);
		СписокСпецодежды.Ссылка 					= ЭлементСпецодежды.ТекущийЭлемент();
		СписокСпецодежды.ВидСпецодежды				= ЭлементСпецодежды.ВидСпецодежды;
		СписокСпецодежды.FormEx_ПланРаскраски = "(BRUSH["+глПолучитьЦвет(186,251,162)+"])";
		
		
	КонецЦикла;
	
КонецПроцедуры	// ЗаполнитьСписокСпецодежды

//====================================================================== 
Процедура ДобавитьСпецодежду()
	Перем ФормаЗаписи;
	Если Выбран() = 0 Тогда
		Предупреждение("Элемент сначала следует записать!");
		Возврат;
	КонецЕсли;	
	ОткрытьФормуМодально("Элемент.УМК_СпецодеждаСотрудника",Контекст,,,ТекущийЭлемент());
	ЗаполнитьСписокСпецодежды();
КонецПроцедуры // Добавить


//====================================================================== //--- УМК Сандомирский В.Ю. (09.09.14)
Процедура ИзменитьСпецодежду()
	Перем ФормаЗаписи;
	Поз=СписокСпецодежды.ТекущаяСтрока();	
	Если Поз = 0 Тогда
	    // не выбрана строка таблицы
		Возврат;
	КонецЕсли;               	
	Эл=СписокСпецодежды.ПолучитьЗначение(Поз,"Ссылка");
	ОткрытьФормуМодально(Эл,Контекст);
	ЗаполнитьСписокСпецодежды();
КонецПроцедуры // Изменить

//====================================================================== //--- УМК Сандомирский В.Ю. (09.09.14)
Процедура УдалитьСпецодежду()
	
	Перем Поз;
	
	Поз = СписокСпецодежды.ТекущаяСтрока();
	
	Если Поз = 0 Тогда
	    // не выбрана строка таблицы
		Возврат;
	КонецЕсли;
	
	Эл = СписокСпецодежды.ПолучитьЗначение(Поз,"Ссылка");

	Если Вопрос("Удалить вид упаковки ?",1)=2 Тогда
		Возврат;
	КонецЕсли;
	Спр=СоздатьОбъект("Справочник.УМК_СпецодеждаСотрудника");
	Спр.ИспользоватьВладельца(ТекущийЭлемент());
	Если Спр.НайтиЭлемент(Эл)=1 Тогда
		Спр.Удалить(0);
		СписокСпецодежды.УдалитьСтроку(Поз);
		СписокСпецодежды.ТекущаяСтрока(?(Поз>1,Поз-1,1));
		Форма.Обновить();
	КонецЕсли;
КонецПроцедуры // Удалить

//====================================================================== //--- УМК Сандомирский В.Ю. (09.09.14)
Процедура ИзменениеПорядка(Направление)
	
	ТекСтрока = СписокСпецодежды.ТекущаяСтрока();
	СписокСпецодежды.СдвинутьСтроку(Направление,ТекСтрока);
	
	ТекСтрока = ТекСтрока + Направление;
	Если ТекСтрока > СписокСпецодежды.КоличествоСтрок() Тогда
		ТекСтрока = СписокСпецодежды.КоличествоСтрок(); 
	КонецЕсли; 
	
	СписокСпецодежды.ТекущаяСтрока(ТекСтрока);
	
	//--- Сдвигаем номерацию на 100
	ТЗ_Элементы = СоздатьОбъект("ТаблицаЗначений");
	ТЗ_Элементы.НоваяКолонка("Ссылка");
	СпрСпецодежда	= СоздатьОбъект("Справочник.УМК_СпецодеждаСотрудника");
	СпрСпецодежда.ИспользоватьВладельца(ТекущийЭлемент());
	СпрСпецодежда.ВыбратьЭлементы();
	Пока СпрСпецодежда.ПолучитьЭлемент() = 1 Цикл
		ТЗ_Элементы.НоваяСтрока();
		ТЗ_Элементы.Ссылка = СпрСпецодежда.ТекущийЭлемент();
	КонецЦикла;
	
	ТЗ_Элементы.ВыбратьСтроки();
	Пока ТЗ_Элементы.ПолучитьСтроку() = 1 Цикл
		СпрСпецодежда.НайтиЭлемент(ТЗ_Элементы.Ссылка);
		СпрСпецодежда.Код = Число(СпрСпецодежда.Код) + 100;
		СпрСпецодежда.Записать();
	КонецЦикла;
	//--- Сдвигаем номерацию на 100
	
	ТекКод = 0;
	СписокСпецодежды.ВыбратьСтроки();
	Пока СписокСпецодежды.ПолучитьСтроку() = 1 Цикл
		ТекКод = ТекКод + 1;
		СпрСпецодежда.НайтиЭлемент(СписокСпецодежды.Ссылка);
		СпрСпецодежда.Код = ТекКод;
		СпрСпецодежда.Записать();
	КонецЦикла;
	
КонецПроцедуры // ИзменениеПорядка(1)

//====================================================================== //--- УМК Сандомирский В.Ю. (09.09.14)
Процедура ЗаполнитьСпецодеждуИзШаблона()
	
	Если Выбран() <> 1 Тогда
		Сообщить("Запишите текущий элемент !","!");
		Возврат;
	КонецЕсли;
	
	Записать();
	

	Если ПустоеЗначение(ШаблонСпецодежды) <> 1 Тогда
		
	
		
		ТекСпецодежда 	= СоздатьОбъект("Справочник.УМК_Спецодежда");
		ТекСпецодеждаСотрудника	= СоздатьОбъект("Справочник.УМК_СпецодеждаСотрудника");
		

		ТекСпецодеждаСотрудника.ИспользоватьВладельца(ТекущийЭлемент());
		
		ТекСпецодеждаСотрудника.ВыбратьЭлементы();
		Пока ТекСпецодеждаСотрудника.ПолучитьЭлемент() = 1 Цикл
			ТекСпецодеждаСотрудника.Удалить(0);
		КонецЦикла;
		
		ТекСпецодежда.ИспользоватьВладельца(ШаблонСпецодежды);
		ТекСпецодежда.ПорядокКодов();
		ТекСпецодежда.ВыбратьЭлементы();
		Пока ТекСпецодежда.ПолучитьЭлемент() = 1 Цикл
			
			Если ТекСпецодежда.ПометкаУдаления() = 1 Тогда
				Продолжить;
			КонецЕсли;
						
			Если ТекСпецодеждаСотрудника.НайтиПоРеквизиту("ВидСпецодежды",ТекСпецодежда.ВидСпецодежды,0) <> 1 Тогда
				ТекСпецодеждаСотрудника.Новый();
				ТекСпецодеждаСотрудника.ВидСпецодежды = ТекСпецодежда.ВидСпецодежды;
				ТекСпецодеждаСотрудника.Записать();
			Иначе
				Если ТекСпецодеждаСотрудника.ПометкаУдаления() = 1 Тогда
					ТекСпецодеждаСотрудника.СнятьПометкуУдаления();				
				КонецЕсли;
				ТекСпецодеждаСотрудника.Записать();
			КонецЕсли;
			
					
		КонецЦикла;
		
		//--- Сдвигаем номерацию на 100
		ТЗ_Элементы = СоздатьОбъект("ТаблицаЗначений");
		ТЗ_Элементы.НоваяКолонка("Ссылка");
		ТекСпецодеждаСотрудника	= СоздатьОбъект("Справочник.УМК_СпецодеждаСотрудника");
		ТекСпецодеждаСотрудника.ИспользоватьВладельца(ТекущийЭлемент());
		ТекСпецодеждаСотрудника.ВыбратьЭлементы();
		Пока ТекСпецодеждаСотрудника.ПолучитьЭлемент() = 1 Цикл
			ТЗ_Элементы.НоваяСтрока();
			ТЗ_Элементы.Ссылка = ТекСпецодеждаСотрудника.ТекущийЭлемент();
		КонецЦикла;
		ТЗ_Элементы.ВыбратьСтроки();
		Пока ТЗ_Элементы.ПолучитьСтроку() = 1 Цикл
			ТекСпецодеждаСотрудника.НайтиЭлемент(ТЗ_Элементы.Ссылка);
			ТекСпецодеждаСотрудника.Код = Число(ТекСпецодеждаСотрудника.Код) + 100;
			ТекСпецодеждаСотрудника.Записать();
		КонецЦикла;
		
		//--- перенумероввываем
		ТекКод = 0;
		ТекСпецодежда.ИспользоватьВладельца(ШаблонСпецодежды);
		ТекСпецодежда.ПорядокКодов();
		ТекСпецодежда.ВыбратьЭлементы();
		Пока ТекСпецодежда.ПолучитьЭлемент() = 1 Цикл
			
			Если ТекСпецодеждаСотрудника.НайтиПоРеквизиту("ВидСпецодежды",ТекСпецодежда.ВидСпецодежды,0) = 1 Тогда
			
				ТекКод = ТекКод + 1;
				ТекСпецодеждаСотрудника.Код = ТекКод;
				ТекСпецодеждаСотрудника.Записать();
				
			КонецЕсли;
			
		КонецЦикла;
			
		ЗаполнитьСписокСпецодежды();
		
	КонецЕсли;
	
	
	
КонецПроцедуры // ЗаполнитьВидыУпаковкиИзГруппировки


//====================================================================== //--- УМК Сандомирский В.Ю. (09.09.14)
СписокСпецодежды.НоваяКолонка("FormEx_ПланРаскраски");										
СписокСпецодежды.НоваяКолонка("Ссылка","Справочник.УМК_СпецодеждаСотрудника",,,,0);
СписокСпецодежды.НоваяКолонка("ВидСпецодежды","Справочник.УМК_ВидыСпецодежды",,,"Вид Спецодежды");
СписокСпецодежды.ВидимостьКолонки("Ссылка",0);
СписокСпецодежды.ВидимостьКолонки("FormEx_ПланРаскраски",0);					