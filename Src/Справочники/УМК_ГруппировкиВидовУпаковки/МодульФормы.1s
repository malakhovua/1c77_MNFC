
// =============================== //--- УМК Сандомирский В.Ю, (31.08.14)
Процедура ЗаполнитьСписокВидовУпаковки()
	Перем РазрешенныйВидУпаковки;
	Перем Индекс;
	
	Если Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;

	РазрешенныйВидУпаковки = СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковки");
	РазрешенныйВидУпаковки.ИспользоватьВладельца(ТекущийЭлемент());
	РазрешенныйВидУпаковки.ВыбратьЭлементы();
	Индекс = 0; 
	СписокВидовУпаковки.УдалитьСтроки();
	Пока РазрешенныйВидУпаковки.ПолучитьЭлемент()=1 Цикл
		Если РазрешенныйВидУпаковки.ПометкаУдаления()=1 Тогда
		    Продолжить;
		КонецЕсли;

	    Индекс = Индекс + 1;
		СписокВидовУпаковки.НоваяСтрока(Индекс);
		СписокВидовУпаковки.Ссылка 			= РазрешенныйВидУпаковки.ТекущийЭлемент();
		СписокВидовУпаковки.ВидУпаковки		= РазрешенныйВидУпаковки.ВидУпаковки;
		
		СписокВидовУпаковки.НеВыгружатьВЗаказник = РазрешенныйВидУпаковки.НеВыгружатьВЗаказник;		//--- УМК Сандомирский В.Ю, (06.04.15)
		Если РазрешенныйВидУпаковки.НеВыгружатьВЗаказник = 1 Тогда									//--- УМК Сандомирский В.Ю, (06.04.15)
			СписокВидовУпаковки.FormEx_ПланРаскраски = "(BRUSH["+глПолучитьЦвет(243,188,240)+"])";
		КонецЕсли;																					//... УМК Сандомирский В.Ю, (06.04.15)
		
		СписокВидовУпаковки.Цена			= РазрешенныйВидУпаковки.Цена.Получить(РабочаяДата());
		СписокВидовУпаковки.КоэфУвВеса		= РазрешенныйВидУпаковки.КоэфУвВеса;
		СписокВидовУпаковки.СкидкаНаВес		= РазрешенныйВидУпаковки.СкидкаНаВес;	
		СписокВидовУпаковки.ВесУпаковки		= РазрешенныйВидУпаковки.ВесУпаковки.Получить(РабочаяДата());	//--- УМК Сандомирский В.Ю, (18.11.14)
		СписокВидовУпаковки.Примечание  	= РазрешенныйВидУпаковки.Примечание;
		СписокВидовУпаковки.ДляПечати		= РазрешенныйВидУпаковки.ДляПечати.Получить(РабочаяДата()); 	//--- УМК Сандомирский В.Ю, (06.11.14)
			
	КонецЦикла;
	
КонецПроцедуры	// ЗаполнитьСписокВидовУпаковки

//====================================================================== //--- УМК Сандомирский В.Ю, (31.08.14)
Процедура ДобавитьВидУпаковки()
	Перем ФормаЗаписи;
	Если Выбран() = 0 Тогда
		Предупреждение("Элемент сначала следует записать!");
		Возврат;
	КонецЕсли;	
	ОткрытьФормуМодально("Элемент.УМК_РазрешенныеВидыУпаковки",Контекст,,,ТекущийЭлемент());
	ЗаполнитьСписокВидовУпаковки();
КонецПроцедуры // Добавить

// ===============================
Процедура ИзменитьВидУпаковки()
	Перем ФормаЗаписи;
	Поз=СписокВидовУпаковки.ТекущаяСтрока();	
	Если Поз = 0 Тогда
	    // не выбрана строка таблицы
		Возврат;
	КонецЕсли;               	
	Эл=СписокВидовУпаковки.ПолучитьЗначение(Поз,"Ссылка");
	ОткрытьФормуМодально(Эл,Контекст);
	ЗаполнитьСписокВидовУпаковки();
КонецПроцедуры // Изменить

//======================================================================
Процедура УдалитьВидУпаковки()
	
	Перем Поз;
	
	Поз=СписокВидовУпаковки.ТекущаяСтрока();
	
	Если Поз = 0 Тогда
	    // не выбрана строка таблицы
		Возврат;
	КонецЕсли;
	
	Эл=СписокВидовУпаковки.ПолучитьЗначение(Поз,"Ссылка");

	Если Вопрос("Удалить вид упаковки ?",1)=2 Тогда
		Возврат;
	КонецЕсли;
	Спр=СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковки");
	Спр.ИспользоватьВладельца(ТекущийЭлемент());
	Если Спр.НайтиЭлемент(Эл)=1 Тогда
		Спр.Удалить(0);
		СписокВидовУпаковки.УдалитьСтроку(Поз);
		СписокВидовУпаковки.ТекущаяСтрока(?(Поз>1,Поз-1,1));
		Форма.Обновить();
	КонецЕсли;
КонецПроцедуры // Удалить

//======================================================================
Процедура ПриОткрытии()
	ЗаполнитьСписокВидовУпаковки();
КонецПроцедуры 

//======================================================================
Процедура ИзменениеПорядка(Направление)
	
	ТекСтрока = СписокВидовУпаковки.ТекущаяСтрока();
	СписокВидовУпаковки.СдвинутьСтроку(Направление,ТекСтрока);
	
	ТекСтрока = ТекСтрока + Направление;
	Если ТекСтрока > СписокВидовУпаковки.КоличествоСтрок() Тогда
		ТекСтрока = СписокВидовУпаковки.КоличествоСтрок(); 
	КонецЕсли; 
	
	СписокВидовУпаковки.ТекущаяСтрока(ТекСтрока);
	
	//--- Сдвигаем номерацию на 100
	ТЗ_Элементы = СоздатьОбъект("ТаблицаЗначений");
	ТЗ_Элементы.НоваяКолонка("Ссылка");
	СпрРазрешенныеВидыУпаковки	= СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковки");
	СпрРазрешенныеВидыУпаковки.ИспользоватьВладельца(ТекущийЭлемент());
	СпрРазрешенныеВидыУпаковки.ВыбратьЭлементы();
	Пока СпрРазрешенныеВидыУпаковки.ПолучитьЭлемент() = 1 Цикл
		ТЗ_Элементы.НоваяСтрока();
		ТЗ_Элементы.Ссылка = СпрРазрешенныеВидыУпаковки.ТекущийЭлемент();
	КонецЦикла;
	
	ТЗ_Элементы.ВыбратьСтроки();
	Пока ТЗ_Элементы.ПолучитьСтроку() = 1 Цикл
		СпрРазрешенныеВидыУпаковки.НайтиЭлемент(ТЗ_Элементы.Ссылка);
		СпрРазрешенныеВидыУпаковки.Код = СпрРазрешенныеВидыУпаковки.Код + 100;
		СпрРазрешенныеВидыУпаковки.Записать();
	КонецЦикла;
	//--- Сдвигаем номерацию на 100
	
	ТекКод = 0;
	СписокВидовУпаковки.ВыбратьСтроки();
	Пока СписокВидовУпаковки.ПолучитьСтроку() = 1 Цикл
		ТекКод = ТекКод + 1;
		СпрРазрешенныеВидыУпаковки.НайтиЭлемент(СписокВидовУпаковки.Ссылка);
		СпрРазрешенныеВидыУпаковки.Код = ТекКод;
		СпрРазрешенныеВидыУпаковки.Записать();
	КонецЦикла;
	
КонецПроцедуры // ИзменениеПорядка(1)

//======================================================================
Процедура ЗаполнитьСписокМатериаловУпаковки()
	Если СписокВидовУпаковки.КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокВидовУпаковки.Ссылка.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	
	глЗаполнитьСписокМатериаловУпаковки(СписокМатериаловУпаковки, СписокВидовУпаковки.Ссылка, "Групп", РабочаяДата());
КонецПроцедуры // ЗаполнитьСписокМатериаловУпаковки()

//====================================================================== //--- УМК Сандомирский В.Ю, (13.05.15)
Функция УстДоступность()

	Если СписокВидовУпаковки.ТекущаяСтрока() > 0 Тогда
		ЗаполнитьСписокМатериаловУпаковки();	
		Если ПустоеЗначение(СписокВидовУпаковки.Ссылка.Путь) <> 1 Тогда
			Попытка
				сКартинка.Загрузить(СокрЛП(СписокВидовУпаковки.Ссылка.Путь));
			Исключение
			КонецПопытки;
		Иначе
			ПустаяКартинка = СоздатьОбъект("Картинка");
			сКартинка.УстановитьКартинку(ПустаяКартинка);
		КонецЕсли;		
	КонецЕсли;
	
КонецФункции

//====================================================================== //--- УМК Сандомирский В.Ю, (12.11.14)
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидУпаковкиПоУмолчанию" Тогда
		ФлагСтандОбр = 0;
		
		ТекРазрешенныеВидыУпаковки 	= СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковки");
		СпВидыУпаковкиЛокальный 	= СоздатьОбъект("СписокЗначений");
		ТекРазрешенныеВидыУпаковки.ИспользоватьВладельца(ТекущийЭлемент());
		ТекРазрешенныеВидыУпаковки.ВыбратьЭлементы();
		Пока ТекРазрешенныеВидыУпаковки.ПолучитьЭлемент() = 1 Цикл
			Если ТекРазрешенныеВидыУпаковки.ПометкаУдаления() = 1 Тогда
				Продолжить;
			КонецЕсли;
			
			//Если ТекРазрешенныеВидыУпаковки.ВидУпаковки.Активна <> Перечисление.ДаНет.Да Тогда //--- пропускаем не активные
			//	Продолжить;
			//КонецЕсли;
			
			СпВидыУпаковкиЛокальный.ДобавитьЗначение(ТекРазрешенныеВидыУпаковки.ТекущийЭлемент().ВидУпаковки);	// --- из подчиненного справочника	 
		КонецЦикла;
				
		СтрВидУпаковки = "";
		Если СпВидыУпаковкиЛокальный.ВыбратьЗначение(СтрВидУпаковки,,,,2) = 1 Тогда
		    ВидУпаковкиПоУмолчанию = СтрВидУпаковки;
		КонецЕсли;			
			
	КонецЕсли;
	
КонецПроцедуры // Принача




// ===============================
СписокВидовУпаковки.НоваяКолонка("FormEx_ПланРаскраски");										//--- УМК Сандомирский В.Ю, (06.04.15)
СписокВидовУпаковки.НоваяКолонка("Ссылка","Справочник.УМК_РазрешенныеВидыУпаковки",,,,0);
СписокВидовУпаковки.НоваяКолонка("ВидУпаковки","Справочник.ВидыУпаковки",,,"Вид упаковки",20);
СписокВидовУпаковки.НоваяКолонка("НеВыгружатьВЗаказник","Число",1,1,"Индивидуальная упаковка",4);				//--- УМК Сандомирский В.Ю, (06.04.15)
СписокВидовУпаковки.НоваяКолонка("Цена","Число",12,2,"Цена",8);
СписокВидовУпаковки.НоваяКолонка("КоэфУвВеса","Число",5,2,"Коэф. увеличения веса");
СписокВидовУпаковки.НоваяКолонка("СкидкаНаВес","Число",5,2,"Скидка на вес");
СписокВидовУпаковки.НоваяКолонка("ВесУпаковки","Число",5,2,"Вес упаковки");  					//--- УМК Сандомирский В.Ю, (18.11.14)
СписокВидовУпаковки.НоваяКолонка("ДляПечати","Строка",20,,"Для печати",10);  					//--- УМК Сандомирский В.Ю, (06.11.14)
СписокВидовУпаковки.НоваяКолонка("Примечание","Строка",200,,"Примечание",20);

СписокВидовУпаковки.ВыводитьПиктограммы("НеВыгружатьВЗаказник"); 	//--- УМК Сандомирский В.Ю, (06.04.15)

СписокВидовУпаковки.ВидимостьКолонки("Ссылка",0);
СписокВидовУпаковки.ВидимостьКолонки("FormEx_ПланРаскраски",0);		//--- УМК Сандомирский В.Ю, (06.04.15)

СписокМатериаловУпаковки.НоваяКолонка("МатериалУпаковки","Справочник.ТМЦ",,,"Материал",20);
СписокМатериаловУпаковки.НоваяКолонка("НормаСписания","Число",15,5,"Норма ",5);


