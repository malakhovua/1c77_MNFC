Перем ДатаЗаписи;

// ===============================
// Название: ПоказатьИсторию()
// Параметры: 
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопка "История",.
// Описание:
// Открывается форма обработки для редактирования периодических реквизитов
Процедура ПоказатьИсторию()
	Если ПустоеЗначение(ТекущийЭлемент())=0 Тогда
		глРедактироватьИсториюЗначений(Контекст,
		"Цена",
		"История периодических реквизитов ("+Владелец+")");
	КонецЕсли;
КонецПроцедуры // ПоказатьИсторию

// ===============================
Процедура ВыбратьДату()
	ВвестиДату(ДатаЗаписи, "Введите дату цен");
	ИспользоватьДату(ДатаЗаписи);
КонецПроцедуры //ПриИзмененииДаты

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
//	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	ДатаЗаписи = ИспользоватьДату();
	СохранениеПериодическихРеквизитов(2, "*");
	ИерархическийСписок(1,0);
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
	    Влад = Владелец;
	КонецЕсли;	
КонецПроцедуры	// ПриОткрытии

// ===============================
Процедура ПриРедактированииНовойСтроки()
	ДатаЗаписи =Константа.ДатаНачалаРаботы;
	ИспользоватьДату(ДатаЗаписи);
	Форма.Обновить();
КонецПроцедуры

// ===============================
Процедура ПриНачалеРедактированияСтроки()
	СохранениеПериодическихРеквизитов(2, "*");
	//ВладелецЕдиницы = ТекущийЭлемент().Владелец;
КонецПроцедуры

// ===============================
Процедура ПриВыбореВладельца(ЭлементВладельца)
	Влад = ЭлементВладельца;
	ИспользоватьВладельца(Влад);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()	// Предопределенная процедура
	// в процедуре при записи проверятся заполнение обязательных реквизитов
	Перем Описание;
	Перем ФлагОшибки;
	Перем ВыборкаЦен;
	
	Описание = "Не заполнено поле ";
	                
	// изначально считаем, что не все обязательные поля заполнены
	ФлагОшибки = 0;
	
	Если ПустоеЗначение(ТМЦ) = 1 Тогда
	    Описание = Описание + "'ТМЦ'";
	Иначе
		// все обязательные поля заполнены
		ФлагОшибки = 1;
	КонецЕсли;
	
	Если ФлагОшибки=0 Тогда
	    
		// не заполнено обязательное поле
		Предупреждение(Описание);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	// проверяем, есть ли уже цена с такой категорией
	ВыборкаЦен = СоздатьОбъект("Справочник.ЦеныУпаковкиПоТМЦ");
	ВыборкаЦен.ИспользоватьВладельца(Владелец);
	ВыборкаЦен.ВыбратьЭлементы();
	Пока ВыборкаЦен.ПолучитьЭлемент()=1 Цикл
		Если ТМЦ = ВыборкаЦен.ТМЦ Тогда
			Если Выбран()=0 Тогда
				// для новой цена сравниваем только совпадение категории
				ФлагОшибки = 0;
				Прервать;
			Иначе
				// для сохраненного элемента проверить несовпадение категорий
				// недостаточно, т.к. из выборки можно получить редактируемый элемент
				// дублирование категорий может произойти только если совпадают
				// категории у разных элементов
				Если (ТекущийЭлемент() <> ВыборкаЦен.ТекущийЭлемент()) Тогда
					ФлагОшибки = 0;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ФлагОшибки = 0 Тогда
		Предупреждение("У вида упаковки уже есть цена для ТМЦ '"+ТМЦ+"' "+
		              ?(ВыборкаЦен.ПометкаУдаления()=1," (помечена на удаление).",""));
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Наименование = ТМЦ.Наименование;
	
	глЗаписьПериодическихРеквизитов(Контекст);
	
КонецПроцедуры	// ПриЗаписи

Процедура ДобавитьГруппу()
	СпрТМЦ = СоздатьОбъект("Справочник.ТМЦ");
	СпрТМЦ.ВыборГруппы(1);
	СпрЦеныУп = СоздатьОбъект("Справочник.ЦеныУпаковкиПоТМЦ");
	Влад = ИспользоватьВладельца();
	СпрЦеныУп.ИспользоватьВладельца(Влад);

	Если СпрТМЦ.Выбрать("Выберите группу", "ФормаСписка") = 1 Тогда
		Если СпрТМЦ.ЭтоГруппа() = 1 Тогда
		    СТМЦ = СоздатьОбъект("Справочник.ТМЦ");
			СТМЦ.ИспользоватьРодителя(СпрТМЦ.ТекущийЭлемент());
			СТМЦ.ВыбратьЭлементы();
			Пока СТМЦ.ПолучитьЭлемент() = 1 Цикл
				Если СТМЦ.ЭтоГруппа() = 0 Тогда
					Если СпрЦеныУп.НайтиПоРеквизиту("ТМЦ", СТМЦ.ТекущийЭлемент(), 0) = 0 Тогда
						СпрЦеныУп.Новый();
						СпрЦеныУп.Владелец = Влад;
						СпрЦеныУп.ТМЦ = СТМЦ.ТекущийЭлемент();
						СпрЦеныУп.Записать();
					ИначеЕсли СпрЦеныУп.ПометкаУдаления() = 1 Тогда
					    СпрЦеныУп.СнятьПометкуУдаления();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Форма.Обновить();
КонецПроцедуры

НоваяЦена = 0;