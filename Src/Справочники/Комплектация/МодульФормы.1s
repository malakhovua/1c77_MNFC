Перем ВводНового;
Перем Записали;
Перем ВнКонтекст;

// ===============================
Процедура ВводНового()	// предопределенная процедура
	Если Владелец.ВидТМЦ=Перечисление.ВидыТМЦ.Набор Тогда
		Кво=1;
	Иначе
		Предупреждение("Выбранный товар не является набором, поэтому не может содержать комплектующие");
	    СтатусВозврата(0);
	КонецЕсли;
	ВводНового = 1;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()	// предопределенная процедура
    ВнКонтекст=Форма.Параметр;
КонецПроцедуры

// ===============================
Процедура ОбработкаВыбораЗначения(ВыбЗнач,ИдентЭлемДиалога, ФлагСтандОбр)	// Предопределенная процедура
	
	Если ИдентЭлемДиалога = "Товар" Тогда
		Если ВыбЗнач.ВидТМЦ = Перечисление.ВидыТМЦ.Набор Тогда
			// набор не может вхотить в набор
			
			Предупреждение("Выбранный товар '"+СокрЛП(ВыбЗнач.Наименование)+"' является набором."+РазделительСтрок+
			               "");
		    // поле не заполняем
			ФлагСтандОбр = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры	// ОбработкаВыбораЗначения

// ===============================
Процедура ПриЗаписи()	// предопределенная процедура
	Перем КомплектующиеТовара;
	Перем Описани;
	Перем ФлагОшибки;
	Перем НашлиЭлемент;
	Перем НеЗаписывать;
	
	Описание = "Не заполнено поле ";
	                
	// изначально считаем, что не все обязательные поля заполнены
	ФлагОшибки = 1;
	
	// проверка заполнения обязательных полей
	Если Товар.Выбран()=0 Тогда
		Описание = Описание + "'Комплектующее'";
	ИначеЕсли ПустоеЗначение(Кво)=1 Тогда
		Описание = Описание + "'Количество'";
	Иначе
		ФлагОшибки = 0;
	КонецЕсли;
	
	Если ФлагОшибки = 1 Тогда
	    Предупреждение(Описание);
	    СтатусВозврата(0);
	    Возврат;
	КонецЕсли;

	// проверим, а есть ли такое комплектующее в наборе
	// если есть то отменим запись
	НашлиЭлемент = 0;
	НеЗаписывать = 0;

	КомплектующиеТовара = СоздатьОбъект("Справочник.Комплектация");
	КомплектующиеТовара.ИспользоватьВладельца(Владелец);
	КомплектующиеТовара.ВыбратьЭлементы();
	Пока КомплектующиеТовара.ПолучитьЭлемент()=1 Цикл
		Если КомплектующиеТовара.ПометкаУдаления()=1 Тогда
		    // помеченные к удалению пропускаем
			Продолжить;
		КонецЕсли;

		Если КомплектующиеТовара.Товар = Товар Тогда
		    НашлиЭлемент = 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НашлиЭлемент = 1 Тогда
	    Если Выбран()=0 Тогда
			// новый элемент
	        НеЗаписывать = 1;
		Иначе
			Если КомплектующиеТовара.ТекущийЭлемент() <> ТекущийЭлемент() Тогда
			    // найденный не совпадает с текущим
				НеЗаписывать = 1;
			КонецЕсли;
	    КонецЕсли;
	КонецЕсли;
	
	Если НеЗаписывать = 1 Тогда
	    Предупреждение("Комплектующее '"+Товар+"' уже имеется. Запись отменена");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Наименование=Товар.Наименование;
	Записали = 1;
КонецПроцедуры	// ПриЗаписи

// ===============================
Процедура ПриЗакрытии()	// предопределенная процедура
	Перем Позиция;
	Если ТипЗначенияСтр(ВнКонтекст)="ГрупповойКонтекст" Тогда
		Если Записали = 1 Тогда
			Если ВводНового=1 Тогда
				// добавляем строку в список комплектующих набора
				ВнКонтекст.СписокКомплектующих.НоваяСтрока();
				ВнКонтекст.СписокКомплектующих.Ссылка = ТекущийЭлемент();
				ВнКонтекст.СписокКомплектующих.Товар = Товар;
				ВнКонтекст.СписокКомплектующих.Количество = Кво;
				ВнКонтекст.СписокКомплектующих.ТекущаяСтрока(ВнКонтекст.СписокКомплектующих.КоличествоСтрок());
				ВнКонтекст.Форма.кИзменить.Доступность(1);
				ВнКонтекст.Форма.кУдалить.Доступность(1);
			Иначе
				// обновляем строку в списке комплектующих
				Позиция = 0;
				ВнКонтекст.СписокКомплектующих.НайтиЗначение(ТекущийЭлемент(),Позиция,"Ссылка");
				ВнКонтекст.СписокКомплектующих.УстановитьЗначение(Позиция,"Товар",Товар);
				ВнКонтекст.СписокКомплектующих.УстановитьЗначение(Позиция,"Количество",Кво);
				ВнКонтекст.СписокКомплектующих.ТекущаяСтрока(Позиция);
			КонецЕсли;
			ВнКонтекст.Форма.Обновить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры	// ПриЗакрытии