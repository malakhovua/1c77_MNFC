// ===============================
// Назначение: определяет признак розничной продажи конечному потребителю
// Аргументы:	КонечныйПотребитель -  признак продажи в розницу конечному потребителю из документа
//				Док - документ продажи в розницу
// Возвращает: 	1 - продажа конечному потребителю
//				0 - продажа не конечному потрбителю
Функция ЭтоКонечныйПотребитель(КонечныйПотребитель,Док)
	Если (ПустоеЗначение(КонечныйПотребитель) = 1) 
	И (Док.Вид() = "ОтчетКА") Тогда
		Возврат 1;
	Иначе
		Возврат КонечныйПотребитель;
	КонецЕсли;    
КонецФункции //ЭтоКонечныйПотребитель

// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Процедура Сформировать()
	Перем НДС_20, НДС_0, НДС_безНДС;
	
	Если глПроверкаИнтервалаОтчета(Дата1,Дата2,1) = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	Если глПолучитьНиО(НДС_20,"НДС20","о ставке НДС 20%") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если глПолучитьНиО(НДС_0,"НДС0","о ставке НДС 0%") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если глПолучитьНиО(НДС_безНДС,"БезНДС","о ставке НДС ""Без НДС""") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапр = 	"//{{ЗАПРОС(Запр)
		|Период с Дата1 по Дата2;
		|ОбрабатыватьДокументы Проведенные;

		|Дат 			= Документ.НалоговаяНакладная.ТекущийДокумент.ДатаДок,	Документ.Приложение2.ТекущийДокумент.ДатаДок,	Документ.Приложение1.ТекущийДокумент.ДатаДок;
		|НН 			= Документ.НалоговаяНакладная.ТекущийДокумент,			Документ.Приложение2.ТекущийДокумент,			Документ.Приложение1.ТекущийДокумент;
		|Фрм 			= Документ.НалоговаяНакладная.Фирма,					Документ.Приложение2.Фирма,						Документ.Приложение1.Фирма;
		|ВидНДС 		= Документ.НалоговаяНакладная.ВидНДС,					Документ.Приложение2.ВидНДС,					Документ.Приложение1.ВидНДС;
		|Контрагент 	= Документ.НалоговаяНакладная.Контрагент,				Документ.Приложение2.Контрагент,				Документ.Приложение1.Контрагент;
	
		|БезНДС			= Документ.НалоговаяНакладная.СуммаБезНДС;
		|НДС_ 			= Документ.НалоговаяНакладная.НДС;
		|СумСНДС 		= Документ.НалоговаяНакладная.СуммаСНДС;
	
		|ИзмСумБезНДС 	= Документ.Приложение2.ИзмСуммыБезНДС;
		|ИзмНДС_ 		= Документ.Приложение2.ИзмНДС;
	
		|СумБезНДСОтгр 	= Документ.Приложение1.СуммаБезНДСОтгр;
	    |НДСОтгр_ 		= Документ.Приложение1.НДСОтгр;             
		|Условие (Фрм=Фирма);
		|Функция СуммаБезНДС 		= Сумма(БезНДС);
		|Функция НДС 				= Сумма(НДС_);
		|Функция СуммаСНДС 			= Сумма(СумСНДС); 
		|Функция ИзмСуммыБезНДС 	= Сумма(ИзмСумБезНДС);
		|Функция ИзмНДС 			= Сумма(ИзмНДС_);
		|Функция СуммаБезНДСОтгр 	= Сумма(СумБезНДСОтгр);
	    |Функция НДСОтгр 			= Сумма(НДСОтгр_);
		|Группировка Дат;
		|Группировка НН;
		|"//}}ЗАПРОС
		;
	
	Запр = СоздатьОбъект("Запрос");
	Если Запр.Выполнить(ТекстЗапр) = 0 Тогда
		Предупреждение("Не выполнился запрос по налоговым накладным!");
		Возврат;
	КонецЕсли;                                  
	       
	Предоплата = Перечисление.ВидыТорговли.Предоплата;
	
	РознТорг = СоздатьОбъект("Запрос");
	ТекстЗапр = 	"//{{ЗАПРОС(РознТорг)
		|Период с Дата1 по Дата2;
		|ОбрабатыватьДокументы Проведенные; 

		|Док 				= Документ.РасходнаяРозничная.ТекущийДокумент, 			Документ.ОтчетКА.ТекущийДокумент;
		|Дат 				= Документ.РасходнаяРозничная.ТекущийДокумент.ДатаДок, 	Документ.ОтчетКА.ТекущийДокумент.ДатаДок;
		|Фрм 				= Документ.РасходнаяРозничная.Фирма, 					Документ.ОтчетКА.Фирма;
		|Скидка				= Документ.РасходнаяРозничная.Скидка;
		
		|КонечныйПотребитель= Документ.РасходнаяРозничная.КонечныйПотребитель;
		|ВидТорговли		= Документ.РасходнаяРозничная.ВидТорговли;	

		|ДокБазаНДС_БезНДС	= Документ.РасходнаяРозничная.БазаНДС1,	 				Документ.ОтчетКА.БазаНДС1;
		|ДокБазаНДС_0		= Документ.РасходнаяРозничная.БазаНДС2,	 				Документ.ОтчетКА.БазаНДС2;
		|ДокБазаНДС_20		= Документ.РасходнаяРозничная.БазаНДС3,	 				Документ.ОтчетКА.БазаНДС3;

		|ДокНДС_БезНДС		= Документ.РасходнаяРозничная.НДС1,	 					Документ.ОтчетКА.НДС1;
		|ДокНДС_0			= Документ.РасходнаяРозничная.НДС2,	 					Документ.ОтчетКА.НДС2;
		|ДокНДС_20			= Документ.РасходнаяРозничная.НДС3,	 					Документ.ОтчетКА.НДС3;

		|Функция БазаНДС_БезНДСВсего = Сумма(ДокБазаНДС_БезНДС);
		|Функция БазаНДС_0Всего 	 = Сумма(ДокБазаНДС_0);
		|Функция БазаНДС_20Всего  	 = Сумма(ДокБазаНДС_20);

		|Функция НДС_БезНДСВсего 	= Сумма(ДокНДС_БезНДС);
		|Функция НДС_0Всего 		= Сумма(ДокНДС_0);
		|Функция НДС_20Всего 		= Сумма(ДокНДС_20);

		|Функция БазаНДС_БезНДС 	= Сумма(ДокБазаНДС_БезНДС) 	Когда (ЭтоКонечныйПотребитель(КонечныйПотребитель,Док) = 1);
		|Функция БазаНДС_0 			= Сумма(ДокБазаНДС_0) 		Когда (ЭтоКонечныйПотребитель(КонечныйПотребитель,Док) = 1);
		|Функция БазаНДС_20 		= Сумма(ДокБазаНДС_20) 		Когда (ЭтоКонечныйПотребитель(КонечныйПотребитель,Док) = 1);

		|Функция НДС_БезНДС 		= Сумма(ДокНДС_БезНДС) 	Когда (ЭтоКонечныйПотребитель(КонечныйПотребитель,Док) = 1);
		|Функция НДС_0	 			= Сумма(ДокНДС_0) 		Когда (ЭтоКонечныйПотребитель(КонечныйПотребитель,Док) = 1);
		|Функция НДС_20 			= Сумма(ДокНДС_20) 		Когда (ЭтоКонечныйПотребитель(КонечныйПотребитель,Док) = 1);
		|Группировка Дат;
		
	    |Группировка Док;
		
		|Условие(Фрм = Фирма); 
		|Условие(ВидТорговли <> Предоплата);
		|"//}}ЗАПРОС
		;         
		
	Если РознТорг.Выполнить(ТекстЗапр)=0 Тогда
		Предупреждение("Не выполнился запрос по розничным накладным!");
		Возврат;
	КонецЕсли;
	
	глУстПропись(Гривня, "у");
	
	Ист5  = 0;	
	Ист6  = 0; 	Ист7  = 0;	Ист8  = 0;	Ист9  = 0;
	Ист11 = 0;	Ист12 = 0;	Ист14 = 0;	Ист15 = 0;
	Ист16 = 0;	Ист17 = 0;	Ист18 = 0;	Ист19 = 0;
	Ист20 = 0;	Ист21 = 0;	Ист22 = 0;	Ист23 = 0;
	                          
	Фирма.ИспользоватьДату(Дата2);
	Таб = СоздатьОбъект("Таблица");
	Таб.ИспользоватьФормат("Ч012.2");
	Таб.ВывестиСекцию("Шапка");
	
	Ном = 1;
	ДатаВЗапросе = Дата1;
	Пока ДатаВЗапросе <= Дата2 Цикл
		
		
		Запр.вНачалоВыборки();
		Если Запр.Получить(ДатаВЗапросе,)=1 Тогда                                         
			Пока Запр.Группировка("НН") = 1 Цикл
				
				Ст1 = Ном;
				
				Ст3 = Запр.НН.ДатаДок;
				Ст4 = Запр.Контрагент.ПолнНаименование;
				Ст5 = Запр.Контрагент.ИНН;
				Ст6  = 0;	Ст7  = 0;	Ст8  = 0;	Ст9  = 0;	Ст10 = 0;
				Ст11 = 0;	Ст12 = 0;	Ст13 = 0;	Ст14 = 0;	Ст15 = 0;
				Ст16 = 0;	Ст17 = 0;	Ст18 = 0;	Ст19 = 0;	Ст20 = 0;
				Ст21 = 0;	Ст22 = 0;	Ст23 = 0;
				
				Если Запр.НН.Вид()="НалоговаяНакладная" Тогда
					
					Ст2 = глНомерБезПрефикса(Запр.НН.НомерДок);
					
					СуммаСНДС_грн 		= Окр(глПересчет(Запр.СуммаСНДС,	  Запр.НН.Валюта,Гривня,Запр.НН.Курс,Запр.НН.ДатаДок),2);
					СуммаБезНДС_грн 	= Окр(глПересчет(Запр.СуммаБезНДС,	  Запр.НН.Валюта,Гривня,Запр.НН.Курс,Запр.НН.ДатаДок),2); 
					НДС_грн 			= Окр(глПересчет(Запр.НДС,			  Запр.НН.Валюта,Гривня,Запр.НН.Курс,Запр.НН.ДатаДок),2); 
					
					Ст6 = СуммаСНДС_грн;
					Ист6 = Ист6+СуммаСНДС_грн;
					Если Запр.ВидНДС = НДС_БезНДС Тогда
						Если Сокрлп(Запр.Контрагент.ИНН) = "" Тогда
							// не плательщик
							Ст12 = СуммаБезНДС_грн;
							Ист12 = Ист12+СуммаБезНДС_грн;
						Иначе
							Ст9 = СуммаБезНДС_грн;
							Ист9 = Ист9+СуммаБезНДС_грн;
						КонецЕсли;
						Ст7 = СуммаБезНДС_грн;
						Ист7 = Ист7+СуммаБезНДС_грн;
					ИначеЕсли Запр.ВидНДС = НДС_0 Тогда
						Если Сокрлп(Запр.Контрагент.ИНН) <> "" Тогда
							Ст8 = СуммаБезНДС_грн;
							Ист8 = Ист8+СуммаБезНДС_грн;
						Иначе
							Если Запр.НН.Валюта = Гривня Тогда
								Ст11 = СуммаБезНДС_грн;
								Ист11 = Ист11+СуммаБезНДС_грн;
							Иначе
								Ст14 = СуммаБезНДС_грн;
								Ист14 = Ист14+СуммаБезНДС_грн;
							КонецЕсли;
						КонецЕсли;
						Ст7 = СуммаБезНДС_грн;
						Ист7 = Ист7+СуммаБезНДС_грн;
					Иначе
						// с НДС
						Если Сокрлп(Запр.Контрагент.ИНН) = "" Тогда
							// не плательщик
							Ст18 = СуммаБезНДС_грн;
							Ст19 = НДС_грн;
							Ст15 = СуммаБезНДС_грн+НДС_грн;
							Ист18 = Ист18+Окр(СуммаБезНДС_грн,2);
							Ист19 = Ист19+Окр(НДС_грн,2);
							Ист15 = Ист15+Окр(СуммаБезНДС_грн+НДС_грн,2);
						Иначе
							Ст16 = СуммаБезНДС_грн;
							Ст17 = НДС_грн;
							Ст15 = СуммаБезНДС_грн+НДС_грн;  
							Ист16 = Ист16+Окр(СуммаБезНДС_грн,2);
							Ист17 = Ист17+Окр(НДС_грн,2);
							Ист15 = Ист15+Окр(СуммаБезНДС_грн+НДС_грн,2);
						КонецЕсли;
					КонецЕсли;
					Ст24 = Запр.НН.ФормаРасчетов;
					ДокОснование = Запр.НН.ДокументОснование;    
				ИначеЕсли Запр.НН.Вид()="Приложение2" Тогда
					// расчет корректировки Приложение 2
					Ст2 = глНомерБезПрефикса(Запр.НН.НалоговаяНакладная.НомерДок)+"\ "+глНомерБезПрефикса(Запр.НН.НомерДок);
					
					ИзмСуммыБезНДС_грн  = Окр(глПересчет(Запр.ИзмСуммыБезНДС, Запр.НН.Валюта,Гривня,Запр.НН.Курс,Запр.НН.ДатаДок),2);
					ИзмНДС_грн			= Окр(глПересчет(Запр.ИзмНДС,		  Запр.НН.Валюта,Гривня,Запр.НН.Курс,Запр.НН.ДатаДок),2);
					Если Запр.ВидНДС = НДС_20 Тогда
						// с НДС
						Ст20  = ИзмСуммыБезНДС_грн;
						Ст21  = ИзмНДС_грн;
						Ист20 = Ист20 + ИзмСуммыБезНДС_грн;
						Ист21 = Ист21 + Окр(ИзмНДС_грн,2);
					ИначеЕсли Запр.ВидНДС = НДС_0 Тогда
						Ст22  = ИзмСуммыБезНДС_грн;
						Ист22 = Ист22 + ИзмСуммыБезНДС_грн;
					Иначе
						Ст23  = ИзмСуммыБезНДС_грн;
						Ист23 = Ист23 + ИзмСуммыБезНДС_грн;
					КонецЕсли;
					ДокОснование = Запр.НН.НалоговаяНакладная.ДокументОснование;
				Иначе
					// Приложение 1
					Ст2 = глНомерБезПрефикса(Запр.НН.НалоговаяНакладная.НомерДок)+"\ "+глНомерБезПрефикса(Запр.НН.НомерДок);
					
					Если Запр.ВидНДС = НДС_20 Тогда
						// с НДС
						Ст20 = Запр.СуммаБезНДСОтгр;
						Ст21 = Запр.НДСОтгр;
						Ист20 = Ист20+Запр.СуммаБезНДСОтгр;
						Ист21 = Ист21+Окр(Запр.НДСОтгр,2);
					ИначеЕсли Запр.ВидНДС = НДС_0 Тогда
						Ст22 = Запр.СуммаБезНДСОтгр;
						Ист22 = Ист22+Запр.СуммаБезНДСОтгр;
					Иначе
						Ст23 = Запр.СуммаБезНДСОтгр;
						Ист23 = Ист23+Запр.СуммаБезНДСОтгр;
					КонецЕсли;
					ДокОснование = Запр.НН.НалоговаяНакладная.ДокументОснование;
				КонецЕсли;
				
				// определим дату оплаты
				Ст25 = "";  
				Если ДокОснование.Выбран()=1 Тогда 
					Если (Запр.НН.Вид() = "НалоговаяНакладная") Тогда
						Если глЕстьРеквизитШапки("ВидТорговли",ДокОснование.Вид())=Да Тогда
							Если (ДокОснование.ВидТорговли = Перечисление.ВидыТорговли.Нал) и
								(Запр.НН.РодительскийДокумент.Выбран() = 1) Тогда
									Ст25 = Строка(Запр.НН.РодительскийДокумент.ДатаДок);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Попытка
					    ОснованиеДокОснования = ДокОснование.ДокументОснование;
					Исключение 
					КонецПопытки;
					
					Если ПустоеЗначение(ОснованиеДокОснования)=0 Тогда
					    Если ОснованиеДокОснования.Вид() = "Счет" Тогда
							// основание - счет-фактура
					        ДокОснование = ОснованиеДокОснования;
					    КонецЕсли;
					КонецЕсли;
					
					СписокДат = СоздатьОбъект("СписокЗначений");
					СтрокаДат = "";
					Док = СоздатьОбъект("Документ");            
					Док.ОбратныйПорядок(1); 
					Док.ВыбратьПодчиненныеДокументы(,Дата2,ДокОснование);
					Пока (Док.ПолучитьДокумент() = 1) и (СписокДат.РазмерСписка() < 3) Цикл
						Если (Док.Вид() = "БанковскаяВыписка") или (Док.Вид() = "ПриходныйКассовый") Тогда
							СписокДат.ДобавитьЗначение(Док.ДатаДок);
						КонецЕсли;
					КонецЦикла;         
					СписокДат.Сортировать();
					Если СписокДат.РазмерСписка() = 3 Тогда
						СписокДат.УдалитьЗначение(3);
						СтрокаДат = СписокДат.ВСтрокуСРазделителями()+" ...";
					Иначе
						СтрокаДат = СписокДат.ВСтрокуСРазделителями();
					КонецЕсли;
					Ст25 = СтрЗаменить(СтрокаДат,"""","");
				КонецЕсли;
				Таб.ВывестиСекцию("Строка");
				Ном = Ном+1;
			КонецЦикла;
		КонецЕсли;
		
		НДС 			= 0; 
		База20 			= 0; 
		База0 			= 0; 
		БазаБезНДС	 	= 0;          
		База20Всего 	= 0;
		База0Всего		= 0;
		БазаБезНДСВсего	= 0;
		НДСВсего	 	= 0;
		РознТорг.вНачалоВыборки();
		Если РознТорг.Получить(ДатаВЗапросе,)=1 Тогда
            Пока РознТорг.Группировка("Док") = 1 Цикл
				База20 		= База20 		+ РознТорг.БазаНДС_20;
				База0 		= База0 		+ РознТорг.БазаНДС_0;
				БазаБезНДС 	= БазаБезНДС 	+ РознТорг.БазаНДС_БезНДС;

				НДС = НДС 	+ РознТорг.НДС_20;
				НДС = НДС 	+ РознТорг.НДС_0;
				НДС = НДС 	+ РознТорг.НДС_БезНДС;
				
				База20Всего 	= База20Всего 		+ РознТорг.БазаНДС_20Всего;
				База0Всего		= База0Всего 		+ РознТорг.БазаНДС_0Всего;
				БазаБезНДСВсего	= БазаБезНДСВсего	+ РознТорг.БазаНДС_БезНДСВсего;

				НДСВсего 	= НДСВсего	+ РознТорг.НДС_20Всего;
				НДСВсего	= НДСВсего	+ РознТорг.НДС_0Всего;
				НДСВсего	= НДСВсего	+ РознТорг.НДС_БезНДСВсего;

				Если ПустоеЗначение(РознТорг.Скидка) = 0 Тогда
					КоэфНДС		  = 0;
					текКоэф0 	  = 0;
					текКоэфБезНДС = 0;
					текКоэф20	  = 0;
					// откорректируем базы НДС с учетом скидки
					текБазаНДС = РознТорг.БазаНДС_20+РознТорг.БазаНДС_0+РознТорг.БазаНДС_БезНДС;
					// расчитаем коэф. НДС для текущего документа
					Если ПустоеЗначение(НДС) = 0 Тогда
						КоэфНДС = ?(ПустоеЗначение(текБазаНДС)=1,0,НДС/текБазаНДС);
					КонецЕсли;
					// изменим сумму НДС с учетом скидки
					НДС	= НДС - РознТорг.Скидка*КоэфНДС;
					// изменим базы НДС с учетом скидки                                                                       
					Если ПустоеЗначение(РознТорг.БазаНДС_0) = 0 Тогда
						текКоэф0 = ?(ПустоеЗначение(текБазаНДС)=1,0,РознТорг.БазаНДС_0/текБазаНДС);
					КонецЕсли;
					База0 = База0 - РознТорг.Скидка*текКоэф0;                                                             
					Если ПустоеЗначение(РознТорг.БазаНДС_БезНДС) = 0 Тогда
						текКоэфБезНДС = ?(ПустоеЗначение(текБазаНДС)=1,0,РознТорг.БазаНДС_БезНДС/текБазаНДС);
					КонецЕсли;
					БазаБезНДС = БазаБезНДС - РознТорг.Скидка*текКоэфБезНДС;
					Если ПустоеЗначение(РознТорг.БазаНДС_20) = 0 Тогда
						текКоэф20 = ?(ПустоеЗначение(текБазаНДС)=1,0,РознТорг.БазаНДС_20/текБазаНДС);
					КонецЕсли;
					База20 = База20 - РознТорг.Скидка*текКоэф20;
					// откорректируем базы НДС всего с учетом скидки
					КоэфНДС		  = 0;
					текКоэф0 	  = 0;
					текКоэфБезНДС = 0;
					текКоэф20	  = 0;
					// откорректируем базы НДС с учетом скидки
					текБазаНДСВсего = РознТорг.БазаНДС_20Всего+РознТорг.БазаНДС_0Всего+РознТорг.БазаНДС_БезНДСВсего;
					// расчитаем коэф. НДС для текущего документа
					Если ПустоеЗначение(НДСВсего) = 0 Тогда
						КоэфНДС = ?(ПустоеЗначение(текБазаНДСВсего)=1,0,НДСВсего/текБазаНДСВсего);
					КонецЕсли;
					// изменим сумму НДС с учетом скидки
					НДСВсего = НДСВсего - РознТорг.Скидка*КоэфНДС;
					// изменим базы НДС с учетом скидки                                                                       
					Если ПустоеЗначение(РознТорг.БазаНДС_0Всего) = 0 Тогда
						текКоэф0 = ?(ПустоеЗначение(текБазаНДСВсего)=1,0,РознТорг.БазаНДС_0Всего/текБазаНДСВсего);
					КонецЕсли;
					База0Всего = База0Всего - РознТорг.Скидка*текКоэф0;                                                             
					Если ПустоеЗначение(РознТорг.БазаНДС_БезНДСВсего) = 0 Тогда
						текКоэфБезНДС = ?(ПустоеЗначение(текБазаНДСВсего)=1,0,РознТорг.БазаНДС_БезНДС/текБазаНДСВсего);
					КонецЕсли;
					БазаБезНДСВсего = БазаБезНДСВсего - РознТорг.Скидка*текКоэфБезНДС;
					Если ПустоеЗначение(РознТорг.БазаНДС_20Всего) = 0 Тогда
						текКоэф20 = ?(ПустоеЗначение(текБазаНДСВсего)=1,0,РознТорг.БазаНДС_20Всего/текБазаНДСВсего);
					КонецЕсли;
					База20Всего = База20Всего - РознТорг.Скидка*текКоэф20;
				КонецЕсли;				
			КонецЦикла;
			База20		= База20	  - НДС;
			База20Всего = База20Всего - НДСВсего;
		КонецЕсли;
		
		Если База0 <> 0 Тогда
			Таб.ВывестиСекцию("Розн0");
			Ист5 	= Ист5	+ База0Всего;
			Ист6 	= Ист6	+ База0;
			Ист7 	= Ист7	+ База0;
			Ист11 	= Ист11	+ База0;
		КонецЕсли;                     
		Если База20 <> 0 Тогда
			Таб.ВывестиСекцию("Розн20");
			Ист5 	= Ист5	+ База20Всего 	+ НДСВсего;
			Ист6 	= Ист6	+ База20 		+ НДС;
			Ист15 	= Ист15	+ База20 		+ НДС;
			Ист18 	= Ист18	+ База20;
			Ист19 	= Ист19	+ НДС;           
		КонецЕсли;                      
		Если БазаБезНДС <> 0 Тогда
			Таб.ВывестиСекцию("РознБезНДС");
			Ист5 	= Ист5	+ БазаБезНДСВсего;
			Ист6 	= Ист6	+ БазаБезНДС;
			Ист7 	= Ист7	+ БазаБезНДС;
			Ист12 	= Ист12	+ БазаБезНДС;    
		КонецЕсли;                          
		
		ДатаВЗапросе = ДатаВЗапросе + 1;
	КонецЦикла;
	
	Таб.ВывестиСекцию("Дно");
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,,,"ПарамПечатиУпрКнигаПродаж","РазмОкнаУпрКнигаПродаж");
	Таб.ПараметрыСтраницы(2);
	Таб.Показать("ПЕЧАТЬ: Книга продаж ("+ПериодСтр(Дата1,Дата2)+", "+Фирма+")","");
КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Фирма = глВосстановитьЗначение(,"БазФирма");
	Если Метаданные.РазделительУчета.Выбран() = 0 Тогда
		Форма.Фирма.Видимость(0);
		Форма.тФирма.Видимость(0);
	КонецЕсли;
	Дата1 = РабочаяДата();
	Дата2 = РабочаяДата();
КонецПроцедуры
