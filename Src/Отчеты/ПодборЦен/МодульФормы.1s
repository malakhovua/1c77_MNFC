
//******************************************************************************
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ

Перем СписокПодбора,СправочникПодбора;
// СписокПодбора - список, в который отбираются элементы
//							(используется для множественных фильтров)
// СправочникПодбора - строка - идентификатор справочника, из которого осуществляется отбор
//							(используется для множественных фильтров)             

Перем Индекс; // используется для организации цикла

Перем СписокТоваров;		// список отфильтрованных позиций номенклатуры
Перем СписокПозицийПрайсов;	// список отфильтрованных позиций прайс-листов
Перем СписокЦенПрайсов;		// список отфильтрованных цен прайс-листов

Перем Т;		
Перем Обновить;
Перем Расшифровка; 
// используются для стандартного механизма кнопок "Обновить" и "Настройка"
                                
Перем НаценкаИзСправочника;

//******************************************************************************
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ


//******************************************************************************
// Название: МножественныйФильтрЗадан
// Параметры:
// НЕТ
// Возвращаемое значение:
//  0, если множественный фильтр задан
//  1, иначе
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
Функция МножественныйФильтрЗадан()      
	Если МФНоменклатура.РазмерСписка()>0 Тогда
		Возврат 1;
	КонецЕсли; 
	Если МФКатНоменклатуры.РазмерСписка()>0 Тогда
		Возврат 1;
	КонецЕсли;
	Если МФЕдИзм.РазмерСписка()>0 Тогда
		Возврат 1;
	КонецЕсли; 
	Если МФКатЦены.РазмерСписка()>0 Тогда
		Возврат 1;
	КонецЕсли;
	Если МФВалютыЦен.РазмерСписка()>0 Тогда
		Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции // МножественныйФильтрЗадан	
         

//******************************************************************************
// Название: ПерерисовкаНазванийЗакладок
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
Процедура ПерерисовкаНазванийЗакладок()      
	Форма.Закладки.УстановитьЗначение(2,?(МножественныйФильтрЗадан()=1,"(!) ","")+"Множественный фильтр");
КонецПроцедуры // ПерерисовкаНазванийЗакладок	


//******************************************************************************
// Название: ДоступностьЭлементов
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Устанавливает доступность элементов диалога
Процедура ДоступностьЭлементов() 
	Если ВалютаЦены.Выбран()>0 Тогда
		Форма.ВклЦенаОт.Доступность(1);
		Форма.ВклЦенаДо.Доступность(1);
	Иначе
		Форма.ВклЦенаОт.Доступность(0);
		Форма.ВклЦенаДо.Доступность(0);
		ВклЦенаОт=0;
		ВклЦенаДо=0;
	КонецЕсли;	
	Форма.ЦенаОт.Доступность(ВклЦенаОт);
	Форма.ЦенаДо.Доступность(ВклЦенаДо);
	Форма.НацСебестОт.Доступность(ВклНацСебестОт);
	Форма.НацСебестДо.Доступность(ВклНацСебестДо);
	Форма.НацУчЦенаОт.Доступность(ВклНацУчЦенаОт);
	Форма.НацУчЦенаДо.Доступность(ВклНацУчЦенаДо);
КонецПроцедуры // ДоступностьЭлементов	

//******************************************************************************
// Название: ВыполняетсяФильтрПоЦене
// Параметры:
//  Цена - цена товара в валюте учета
//  Себестоимость - себестоимость товара в валюте учета
//  ОписаниеНаценок - в этот параметр помещается строка с описанием наценок
//  ЕдиницаТовара, за которую - наценка в справочнике
// Возвращаемое значение:                   
//	0 - если цена, себестоимость не удовлетворяют фильтру по наценке
//  1 - если цена, себестоимость удовлетворяют фильтру по наценке, и обе наценки положительные
//  2 - если цена, себестоимость удовлетворяют фильтру по наценке, и 
//		наценки от себестоимости положительная, а от учетной чены - неположительная
//  3 - если цена, себестоимость удовлетворяют фильтру по наценке, и 
//		наценки от себестоимости неположительная, а от учетной чены - положительная
//  4 - если цена, себестоимость удовлетворяют фильтру по наценке, и обе наценки неположительные

// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Проверка фильтра по наценкам и выявление неположительных наценок                              
Функция ВыполняетсяФильтрПоЦене(ЦенаВСпр,Себестоимость,ОписаниеНаценок,ЕдиницаТовара)
	Перем ТекСтрока, Знак, База, СрНаценка;                                                       
	Перем ЦенаВалютеУчета,УчетнаяЦена,НаценкаВСпр; 
	
	// простой фильтр по типу цены
	// фильтр по типу единицы измерения	
	Если ЕдИзм.Выбран()>0 Тогда
		Если ЦенаВСпр.Единица.Единица.ТекущийЭлемент()<>ЕдИзм.ТекущийЭлемент() Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;                                          
		
	Если МФЕдИзм.РазмерСписка()>0 Тогда
		Если МФЕдИзм.НайтиЗначение(ЦенаВСпр.Единица.Единица.ТекущийЭлемент())<=0 Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;        
    
    Если ВалютаЦены.Выбран()>0 Тогда
		Если ЦенаВСпр.Валюта.ТекущийЭлемент()<>ВалютаЦены.ТекущийЭлемент() Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;
	
	Если МФВалютыЦен.РазмерСписка()<>0 Тогда
		Если МФВалютыЦен.НайтиЗначение(ЦенаВСпр.Валюта.ТекущийЭлемент())<=0 Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;
	
	Если (ВклЦенаОт<>0) Тогда
		Если ЦенаВСпр.Цена<ЦенаОт Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;          
	
	Если (ВклЦенаДо<>0) Тогда
		Если ЦенаВСпр.Цена>ЦенаДо Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;
        
	// цена в валюте учета 
	ЦенаВалютеУчета = 0;
	Если ПустоеЗначение(ЦенаВСпр) = 0 Тогда
		ЦенаВалютеУчета = ЕдиницаТовара.Коэффициент/ЦенаВСпр.Единица.Коэффициент*
							ЦенаВСпр.Цена;
	КонецЕсли;						
								     
	УчетнаяЦена = ЦенаВСпр.Владелец.УчетнаяЦена;
	УчетнаяЦена = УчетнаяЦена * ЕдиницаТовара.Коэффициент; 
	
	НаценкаВСпр = ЦенаВСпр.Наценка;
						
	НаценкаОтСебестоимости = Окр(?(Себестоимость<=0,0,(ЦенаВалютеУчета-Себестоимость)/Себестоимость*100),2,1);								 
	НаценкаОтУчетнойЦены = Окр(?(УчетнаяЦена<=0,0,(ЦенаВалютеУчета-УчетнаяЦена)/УчетнаяЦена*100),2,1);								
	
	Если (Себестоимость>0)ИЛИ(УчетнаяЦена>0) Тогда
		ОписаниеНаценок = ОписаниеНаценок + "наценки: ";
		Если (Себестоимость>0) Тогда
			ОписаниеНаценок = ОписаниеНаценок + РазделительСтрок + "на себ.= "+Строка(Окр(НаценкаОтСебестоимости,2,1))+"%, ";
		КонецЕсли;	 
		Если (УчетнаяЦена>0) Тогда                          
			ОписаниеНаценок = ОписаниеНаценок + РазделительСтрок + "на уч.цену = "+Строка(Окр(НаценкаОтУчетнойЦены,2,1))+"%, ";
		КонецЕсли;	
		ОписаниеНаценок = СокрП(ОписаниеНаценок);
	КонецЕсли;	
						                         
	Если (ВклНацСебестОт<>0) Тогда
		Если (Себестоимость>0)И(НаценкаОтСебестоимости<НацСебестОт) Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;   
	
	Если (ВклНацСебестДо<>0) Тогда
		Если (Себестоимость<=0)ИЛИ(НаценкаОтСебестоимости>НацСебестДо) Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;   
		
	Если (ВклНацУчЦенаОт<>0) Тогда
		Если (УчетнаяЦена>0)И(НаценкаОтУчетнойЦены<НацУчЦенаОт) Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;   
	
	Если (ВклНацУчЦенаДо<>0) Тогда
		Если (УчетнаяЦена<=0)ИЛИ(НаценкаОтУчетнойЦены>НацУчЦенаДо) Тогда
			Возврат 0;
		КонецЕсли;	
	КонецЕсли;                
	
	Если (ВклФильтрСравненияНаценок<>0) Тогда
		Если ВидНаценки.ТекущаяСтрока()=1 Тогда	// на себестоимость
			База = Себестоимость;
			СрНаценка = НаценкаОтСебестоимости;
		Иначе                                  
			База = УчетнаяЦена;
			СрНаценка = НаценкаОтУчетнойЦены;
		КонецЕсли;	
		
		ТекСтрока=ЗнакСравнения.ТекущаяСтрока();
		Если ТекСтрока>0 Тогда
			Знак = ЗнакСравнения.ПолучитьЗначение(ТекСтрока);	
			
			Если Знак=">" Тогда
				Если (База>0)И(СрНаценка<=НаценкаВСпр) Тогда
					Возврат 0;
				КонецЕсли;	
				
			ИначеЕсли Знак=">=" Тогда
				Если (База>0)И(СрНаценка<НаценкаВСпр) Тогда
					Возврат 0;
				КонецЕсли;	
				
			ИначеЕсли Знак="<" Тогда
				Если (База<=0)ИЛИ(СрНаценка>=НаценкаВСпр) Тогда
					Возврат 0;
				КонецЕсли;	
				
			ИначеЕсли Знак="<=" Тогда
				Если (База<=0)ИЛИ(СрНаценка>НаценкаВСпр) Тогда
					Возврат 0;
				КонецЕсли;	
				
			ИначеЕсли Знак="=" Тогда        
				Если (База<=0)ИЛИ(СрНаценка<>НаценкаВСпр) Тогда
					Возврат 0;
				КонецЕсли;	
				
			ИначеЕсли Знак="<>" Тогда
				Если (База>0)И(СрНаценка=НаценкаВСпр) Тогда
					Возврат 0;
				КонецЕсли;	
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;	
	
	Если (НаценкаОтСебестоимости>0)ИЛИ((Себестоимость<=0)И(ЦенаВалютеУчета>0)) Тогда
		// наценка от себестоимости > 0
		Если (НаценкаОтУчетнойЦены>0)ИЛИ((УчетнаяЦена<=0)И(ЦенаВалютеУчета>0)) Тогда
			// наценка от учетной цены > 0
			Возврат 1;
		Иначе                              
			// наценка от учетной цены <= 0
			Возврат 2;
		КонецЕсли;	
	Иначе           
		// наценка от себестоимости <= 0
		Если (НаценкаОтУчетнойЦены>0)ИЛИ((УчетнаяЦена<=0)И(ЦенаВалютеУчета>0)) Тогда
			// наценка от учетной цены > 0
			Возврат 3;
		Иначе                              
			// наценка от учетной цены <= 0
			Возврат 4;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат 1;
		
КонецФункции	// ВыполняетсяФильтрПоЦене	
		

//******************************************************************************
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА

//******************************************************************************
// Название: ПриВключенииФильтраСравненияНаценок
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   элементы диалога, отвечающие за фильтр сравнения
// Наименование,.
// Описание:
//   управляет доступностью элементов диалога, отвечающих за фильтр сравнения
Процедура ПриВключенииФильтраСравненияНаценок() 
	Перем ТекСтрока,Знак;
	
	Форма.ВидНаценки.Доступность(ВклФильтрСравненияНаценок);
	Форма.ЗнакСравнения.Доступность(ВклФильтрСравненияНаценок);
	
	ТекСтрока=ЗнакСравнения.ТекущаяСтрока();
	Если ТекСтрока>0 Тогда
		Знак = ЗнакСравнения.ПолучитьЗначение(ТекСтрока);	
	
		Если (Знак="=")ИЛИ(Знак=">=")ИЛИ(Знак="<=") Тогда 
			НаценкаИзСправочника = "наценке из справочника";
		Иначе
			НаценкаИзСправочника = "наценки из справочника";
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры // ПриВключенииФильтраСравненияНаценок

//******************************************************************************
// Название: РаботаСоСписком
// Параметры:
// 	Режим - строка, принимающая 4 значения:
//		"Добавить"
//		"ДобавитьНесколько"
// 		"Удалить"
// 		"УдалитьВсе"
//	Список - список значений, в котором задается множественный фильтр
//	ТипСправочника - строка, содержащая идентификатор справочнника, по
//						которому осуществляется мноджественный фильтр
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   кнопок работы с множественными фильтрами ("...",".....","X","XX")
// Наименование,.
// Описание:
// процедура предназначена для добавления и удаления элементов
// из множественных фильтров
Процедура РаботаСоСписком(Режим,Список,ТипСправочника)
	Перем ТекПоз;
	Перем ТекЭлемент;
	Перем Фрм;
	
	Если ТипСправочника = "Категории" Тогда
		ТипСправочника = "ВидыКатегорий";
	КонецЕсли;	
	
	ТекПоз = Список.ТекущаяСтрока();
	Если ТекПоз>0 Тогда
		ТекЭлемент=Список.ПолучитьЗначение(ТекПоз);
	КонецЕсли;

	Если Режим="Добавить" Тогда		// добавляем в список один элемент
		СписокПодбора = Список;
		СправочникПодбора = ВРег(ТипСправочника);
		// открываем окно подбора
		ОткрытьПодбор("Справочник."+ТипСправочника,,Фрм,0,ТекЭлемент);
		Если ВРег(ТипСправочника)<>"ВИДЫКАТЕГОРИЙ" Тогда
			Фрм.ВыборГруппы(1);
		Иначе
			Фрм.ВыборГруппы(0);
		КонецЕсли;	

	ИначеЕсли Режим="ДобавитьНесколько" Тогда  // добавляем в список несколько элементов
		СписокПодбора = Список;
		СправочникПодбора = ВРег(ТипСправочника);
		// открываем окно подбора
		ОткрытьПодбор("Справочник."+ТипСправочника,,Фрм,1,ТекЭлемент);
		Если ВРег(ТипСправочника)<>"ВИДЫКАТЕГОРИЙ" Тогда
			Фрм.ВыборГруппы(1);
		Иначе
			Фрм.ВыборГруппы(0);
		КонецЕсли;	


 	ИначеЕсли Режим="УдалитьВсе" Тогда	// удаляем все элементы из списка
		Список.УдалитьВсе();

	ИначеЕсли Режим="Удалить" Тогда	// удаляем из списка один элемент
		Если ТекПоз>0 Тогда
			Список.УдалитьЗначение(ТекПоз);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры		// работа со списком

//******************************************************************************
// Название: Выполнить
// Параметры:                                            
//   ЗакрытьЭкран - флаг того, что после формирования отчета надо закрыть экран	
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопки "ОК","Выполнить", и "Просмотр".
// Описание:
//   отбирает позиции номенклатуры, подлежащие групповой обработке и запускает 
//	 групповую обработку справочника номенклатуры
//
Процедура Выполнить(Режим = "Формирование",ЗакрытьЭкран=0)
    Перем Запрос;		// запрос
	Перем ТекстЗапроса;	// текст запроса
	                                 
	Перем ЧислоКатегорий, ЧислоКатегорийВСписке;
	
	Перем НаложенФильтрПоТоварам,СпрНоменклатуры;
	                
	Перем УстановленФильтрПоЦене,УстановленФильтрПоНоменклатуре,УстановленФильтрПоКатЦен;
	                                                                                    
	УстановленФильтрПоЦене=0;
	УстановленФильтрПоНоменклатуре=0;
	УстановленФильтрПоКатЦен=0;
	
	// если установлен фильтр по номенклатуре
	Если (ВидНоменклатуры.ТекущаяСтрока()>1)ИЛИ
		(Номенклатура.Выбран()>0)ИЛИ
		(КатНоменклатуры.Выбран()>0)ИЛИ
		(ТолькоПрисутствующие>0)ИЛИ
		(ТолькоОтсутствующие>0)ИЛИ
		(МФНоменклатура.РазмерСписка()>0)ИЛИ
		(МФКатНоменклатуры.РазмерСписка()>0)  
	Тогда	
		УстановленФильтрПоНоменклатуре=1;
	КонецЕсли;
	
	// если установлен фильтр по категории цен
	Если (КатЦены.Выбран()>0)ИЛИ
		(МФКатЦены.РазмерСписка()>0)
	Тогда	
		УстановленФильтрПоКатЦен = 1;
	КонецЕсли;
	
	// если установлен фильтр по цене
	Если (ВалютаЦены.Выбран()>0)ИЛИ 
		(ЕдИзм.Выбран()>0)ИЛИ
		(МФВалютыЦен.РазмерСписка()>0)ИЛИ
		(МФЕдИзм.РазмерСписка()>0)ИЛИ
		(ВклЦенаОт<>0)ИЛИ
		(ВклЦенаДо<>0)ИЛИ
		(ВклНацСебестОт<>0)ИЛИ
		(ВклНацСебестДо<>0)ИЛИ
		(ВклНацУчЦенаОт<>0)ИЛИ
		(ВклНацУчЦенаДо<>0)ИЛИ
		(ВклФильтрСравненияНаценок<>0)
	Тогда	
		УстановленФильтрПоЦене = 1;
	КонецЕсли;
	
	
	НаложенФильтрПоТоварам = 0;
	
	СписокТоваров= СоздатьОбъект("СписокЗначений");
	СписокЦен = СоздатьОбъект("СписокЗначений");
	                                                              
	
	// накладываем простой фильтр по товарам
	Если Номенклатура.Выбран()>0 Тогда
		Если Номенклатура.ЭтоГруппа()>0 Тогда
			СпрНоменклатуры = СоздатьОбъект("Справочник.ТМЦ");
			СпрНоменклатуры.ИспользоватьРодителя(Номенклатура.ТекущийЭлемент());
			СпрНоменклатуры.ВыбратьЭлементы(1);
			Пока СпрНоменклатуры.ПолучитьЭлемент()>0 Цикл
				Если СпрНоменклатуры.ЭтоГруппа()=0 Тогда     
					СписокТоваров.ДобавитьЗначение(СпрНоменклатуры.ТекущийЭлемент());
				КонецЕсли;
			КонецЦикла;	
		Иначе
			СписокТоваров.ДобавитьЗначение(Номенклатура.ТекущийЭлемент());
		КонецЕсли;	
		
		Если СписокТоваров.РазмерСписка()=0 Тогда
			Предупреждение("Список позиций пуст");
			Возврат;
		КонецЕсли;	
	    
		НаложенФильтрПоТоварам=1;
	КонецЕсли;	
	
	
	// накладываем множественный фильтр по товарам
	Если МФНоменклатура.РазмерСписка()<>0 Тогда
		    
		СписокТоваров2 = СоздатьОбъект("СписокЗначений");
		
		Для Индекс=1 По МФНоменклатура.РазмерСписка() Цикл
			Номенк = МФНоменклатура.ПолучитьЗначение(Индекс);
			
			НоменкВложенаВДругую=0;
			Для Индекс2=1 По МФНоменклатура.РазмерСписка() Цикл
			    Если Индекс2<>Индекс Тогда       
					Номенк2 = МФНоменклатура.ПолучитьЗначение(Индекс2);
					Если Номенк2.ЭтоГруппа()>0 Тогда
						Если Номенк.ПринадлежитГруппе(Номенк2)>0 Тогда
							НоменкВложенаВДругую=1;
							Прервать;
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;	
			
			Если НоменкВложенаВДругую=1 Тогда
				Продолжить;
			КонецЕсли;	    
			
			Если Номенк.ЭтоГруппа()>0 Тогда
				СпрНоменклатуры = СоздатьОбъект("Справочник.ТМЦ");
				СпрНоменклатуры.ИспользоватьРодителя(Номенк.ТекущийЭлемент());
				СпрНоменклатуры.ВыбратьЭлементы(1);
				Пока СпрНоменклатуры.ПолучитьЭлемент()>0 Цикл
					Если СпрНоменклатуры.ЭтоГруппа()=0 Тогда     
						СписокТоваров2.ДобавитьЗначение(СпрНоменклатуры.ТекущийЭлемент());
					КонецЕсли;
				КонецЦикла;	
			Иначе
				СписокТоваров2.ДобавитьЗначение(Номенк.ТекущийЭлемент());
			КонецЕсли;	                                                      
			
		КонецЦикла;	
		
		Если НаложенФильтрПоТоварам=1 Тогда
			ЧислоТов = СписокТоваров.РазмерСписка();
			Для Индекс2 = 1 По ЧислоТов Цикл
				Если СписокТоваров2.НайтиЗначение(СписокТоваров.ПолучитьЗначение(ЧислоТов+1-Индекс2))<=0 Тогда     
					СписокТоваров.УдалитьЗначение(ЧислоТов+1-Индекс2);
				КонецЕсли;
			КонецЦикла;
		Иначе
			СписокТоваров2.Выгрузить(СписокТоваров);
		КонецЕсли;	
	
		Если СписокТоваров.РазмерСписка()=0 Тогда
			Предупреждение("Список позиций пуст");
			Возврат;
		КонецЕсли;	
	    
		НаложенФильтрПоТоварам=1;
	КонецЕсли;	
	    
	
	// накладываем фильтр по виду 
	Если (ВидНоменклатуры.ТекущаяСтрока()>1) Тогда
		
		// накладываем фильтр по виду 
		Если ВидНоменклатуры.ТекущаяСтрока()=2 Тогда
			ВидНоменк = Перечисление.ВидыТМЦ.Товар;
		ИначеЕсли ВидНоменклатуры.ТекущаяСтрока()=3 Тогда
			ВидНоменк = Перечисление.ВидыТМЦ.Услуга;
		ИначеЕсли ВидНоменклатуры.ТекущаяСтрока()=4 Тогда
			ВидНоменк = Перечисление.ВидыТМЦ.Набор;
		КонецЕсли;
	        
		
		Если НаложенФильтрПоТоварам=1 Тогда
			
			ЧислоТов = СписокТоваров.РазмерСписка();
			Для Индекс = 1 По ЧислоТов Цикл
				
				НеУдовлетвФильтру=0;
				Номенк = СписокТоваров.ПолучитьЗначение(ЧислоТов+1-Индекс);
				
				// накладываем фильтр по виду 
				Если Номенк.ВидТМЦ<>ВидНоменк Тогда
					НеУдовлетвФильтру=1;
				КонецЕсли;	           
	            
				Если НеУдовлетвФильтру=1 Тогда                     
					СписокТоваров.УдалитьЗначение(ЧислоТов+1-Индекс);
				КонецЕсли;
			КонецЦикла;
			
		Иначе   
			
			СпрНоменклатуры = СоздатьОбъект("Справочник.ТМЦ");
			СпрНоменклатуры.ВыбратьЭлементы();
			Пока СпрНоменклатуры.ПолучитьЭлемент()>0 Цикл
				Если СпрНоменклатуры.ЭтоГруппа()=0 Тогда     
					Если СпрНоменклатуры.ВидТМЦ=ВидНоменк Тогда
						СписокТоваров.ДобавитьЗначение(СпрНоменклатуры.ТекущийЭлемент());
					КонецЕсли;	
				КонецЕсли;
			КонецЦикла;	
			
		КонецЕсли;
		
		Если СписокТоваров.РазмерСписка()=0 Тогда
			Предупреждение("Список позиций пуст");
			Возврат;
		КонецЕсли;	
	    
		НаложенФильтрПоТоварам=1;
	КонецЕсли;
	
	// накладываем фильтр по категориям товаров
	//	    если он необходим
	Если ((КатНоменклатуры.Выбран()>0)ИЛИ(МФКатНоменклатуры.РазмерСписка()>0)) Тогда
	
		ТекстЗапроса = "      
			|Обрабатывать НеПомеченныеНаУдаление;
			|КатегорияТовара = Справочник.КатегорииТоваров.ТекущийЭлемент;
			|Товар = Справочник.КатегорииТоваров.Владелец; 
			|Категория = Справочник.КатегорииТоваров.Категория;                 
			|Группировка Товар Без Групп;
			|";                          
		
		Если НаложенФильтрПоТоварам=1 Тогда
			ТекстЗапроса = ТекстЗапроса + "Условие (Товар В СписокТоваров);";
		КонецЕсли;	
			
		Если (МФКатНоменклатуры.РазмерСписка()>0)И(ТипМФКатНоменклатуры.ТекущаяСтрока()=2) Тогда
			// одновременно все категории должны быть в списке МФКатНоменклатуры
			ТекстЗапроса = ТекстЗапроса + "Группировка Категория Без Групп;";
		КонецЕсли;	
	    
		Если КатНоменклатуры.Выбран()>0 Тогда
   			ТекстЗапроса = ТекстЗапроса + "Условие (Категория = КатНоменклатуры);";	
		КонецЕсли;	
		
		Если МФКатНоменклатуры.РазмерСписка()>0 Тогда
			ТекстЗапроса = ТекстЗапроса + "Условие (Категория в МФКатНоменклатуры);";	
		КонецЕсли;	
		
		// выполняем запрос
		Запрос = СоздатьОбъект("Запрос");
		Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
			Возврат;
		КонецЕсли;
    	
		ЧислоКатегорийВСписке = МФКатНоменклатуры.РазмерСписка();
		
		// выгружаем все отобранные товары в список
		СписокТоваров.УдалитьВсе();
		Запрос.ВНачалоВыборки();
		Пока Запрос.Группировка("Товар")>0 Цикл
			Если Запрос.Товар.Выбран()=1 Тогда                                   
				Если (ЧислоКатегорийВСписке>0)И(ТипМФКатНоменклатуры.ТекущаяСтрока()=2) Тогда
					// одновременно все категории должны быть в списке МФКатНоменклатуры
					
					Товар = Запрос.Товар.ТекущийЭлемент();
					
					// ячитаем число категорий 
					ЧислоКатегорий = 0;
					Пока Запрос.Группировка("Категория")>0 Цикл
						Если Запрос.Категория.Выбран()=1 Тогда                                   
							ЧислоКатегорий = ЧислоКатегорий + 1;
						КонецЕсли;
					КонецЦикла;
					
					// все категории будут тогда и только тогда, когда число отобранных 
					// категорий будет не меньше размера списка МФКатНоменклатуры
					Если ЧислоКатегорий>=ЧислоКатегорийВСписке Тогда
						СписокТоваров.ДобавитьЗначение(Товар);
					КонецЕсли;	
				Иначе
					СписокТоваров.ДобавитьЗначение(Запрос.Товар.ТекущийЭлемент());
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
		Если СписокТоваров.РазмерСписка()=0 Тогда
			Предупреждение("Список позиций пуст");
			Возврат;
		КонецЕсли;	
	                             
		НаложенФильтрПоТоварам=1;
		
	КонецЕсли;	
	    
	
	// накладываем фильтр по присутствию/отсутствию на складе
	Если (ТолькоОтсутствующие=1)ИЛИ(ТолькоПрисутствующие=1) Тогда
	
		ВремРегистры=СоздатьОбъект("Регистры");
		Рег=ВремРегистры.Остатки;
		Если РабочаяДата()<ПолучитьДатуТА() Тогда
			Рег.ВременныйРасчет();                             
			Если НаложенФильтрПоТоварам=1 Тогда
				Рег.УстановитьЗначениеФильтра("ТМЦ",СписокТоваров,2);
			КонецЕсли;	
			ВремРегистры.РассчитатьРегистрыПО(РабочаяДата());
		КонецЕсли;
	            
		
		Если НаложенФильтрПоТоварам=1 Тогда
			
			ЧислоТов = СписокТоваров.РазмерСписка();
			Для Индекс = 1 По ЧислоТов Цикл
				
				НеУдовлетвФильтру=0;
				Номенк = СписокТоваров.ПолучитьЗначение(ЧислоТов+1-Индекс);
				
				// накладываем фильтр по присутствию/отсутствию 
				Если Номенк.ВидТМЦ<>Перечисление.ВидыТМЦ.Товар Тогда
					НеУдовлетвФильтру=1;
				Иначе
					Кол = ВремРегистры.Остатки.СводныйОстаток(,Номенк,,"ОстатокТовара");
					Если ((Кол>0)И(ТолькоОтсутствующие=1))ИЛИ((Кол<=0)И(ТолькоПрисутствующие=1)) Тогда
						НеУдовлетвФильтру=1;
					КонецЕсли;
				КонецЕсли;	           
	            
				Если НеУдовлетвФильтру=1 Тогда                     
					СписокТоваров.УдалитьЗначение(ЧислоТов+1-Индекс);
				КонецЕсли;
			КонецЦикла;
			
		Иначе   
			
			СпрНоменклатуры = СоздатьОбъект("Справочник.ТМЦ");
			СпрНоменклатуры.ВыбратьЭлементы();
			Пока СпрНоменклатуры.ПолучитьЭлемент()>0 Цикл
				Если СпрНоменклатуры.ЭтоГруппа()=0 Тогда     
					Если СпрНоменклатуры.ВидТМЦ=Перечисление.ВидыТМЦ.Товар Тогда
						Кол = ВремРегистры.Остатки.СводныйОстаток(,СпрНоменклатуры.ТекущийЭлемент(),,"ОстатокТовара");
						Если НЕ(((Кол>0)И(ТолькоОтсутствующие=1))ИЛИ((Кол<=0)И(ТолькоПрисутствующие=1))) Тогда
					        СписокТоваров.ДобавитьЗначение(СпрНоменклатуры.ТекущийЭлемент());
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
			КонецЦикла;	
			
		КонецЕсли;
		
		Если СписокТоваров.РазмерСписка()=0 Тогда
			Предупреждение("Список позиций пуст");
			Возврат;
		КонецЕсли;	
	    
		НаложенФильтрПоТоварам=1;
	КонецЕсли;
	
	
	ВремРегистры=СоздатьОбъект("Регистры");
	Рег=ВремРегистры.Партии;
	Если РабочаяДата()<ПолучитьДатуТА() Тогда
		Рег.ВременныйРасчет();                             
		Если НаложенФильтрПоТоварам=1 Тогда
			Рег.УстановитьЗначениеФильтра("ТМЦ",СписокТоваров,2);
		КонецЕсли;	
		ВремРегистры.РассчитатьРегистрыПО(РабочаяДата());
	КонецЕсли;
    	     
	              
	// формируем список всех отбираемых цен
	СписокОтбираемыхЦен = СоздатьОбъект("СписокЗначений");
	ИспользованиеОтбираемыхЦен = СоздатьОбъект("СписокЗначений");
	
	Если КатЦены.Выбран()>0 Тогда
	    Если МФКатЦены.РазмерСписка()<>0 Тогда
			Если МФКатЦены.НайтиЗначение(КатЦены.ТекущийЭлемент())>0 Тогда
				СписокОтбираемыхЦен.ДобавитьЗначение(КатЦены.ТекущийЭлемент());
			КонецЕсли;	
		Иначе
			СписокОтбираемыхЦен.ДобавитьЗначение(КатЦены.ТекущийЭлемент());
		КонецЕсли;
    Иначе	
		Если МФКатЦены.РазмерСписка()<>0 Тогда
			Для Индекс = 1 По МФКатЦены.РазмерСписка() Цикл
				СписокОтбираемыхЦен.ДобавитьЗначение(МФКатЦены.ПолучитьЗначение(Индекс).ТекущийЭлемент());
			КонецЦикла;
		Иначе
	        // отбираем все цены
			СпрКатегЦен = СоздатьОбъект("Справочник.КатегорииЦен");
			СпрКатегЦен.ВыбратьЭлементы();
			Пока СпрКатегЦен.ПолучитьЭлемент()>0 Цикл
				СписокОтбираемыхЦен.ДобавитьЗначение(СпрКатегЦен.ТекущийЭлемент());
			КонецЦикла;	
		КонецЕсли;
        СписокОтбираемыхЦен.СортироватьПоПредставлению();
	КонецЕсли;
	    
	Для Индекс = 1 По СписокОтбираемыхЦен.РазмерСписка() Цикл
		ИспользованиеОтбираемыхЦен.ДобавитьЗначение(0);
	КонецЦикла;	
	                                       
	
	Цена = СоздатьОбъект("Справочник.Цены");
	Цена.ИспользоватьДату(РабочаяДата());
	
	Если НаложенФильтрПоТоварам=1 Тогда
		СписокТоваров.СортироватьПоПредставлению();
		КолТов = СписокТоваров.РазмерСписка();
		Индекс = 0;
	Иначе	
		СправочникТоваров = СоздатьОбъект("Справочник.ТМЦ");
		СправочникТоваров.ПорядокНаименований();
		СправочникТоваров.ВыбратьЭлементы(0);
	КонецЕсли;	
		                            
	Обработано = 0;
	
	// цикл по всем товарам
	// для каждого товара проверяем, отберется ли для него хотя бы одна цена,
	// а для каждой цены - отберется ли хотя бы один товар
	Пока 1=1 Цикл
		
		Если НаложенФильтрПоТоварам=1 Тогда
			Индекс = Индекс+1;
			Если Индекс>КолТов Тогда
				Прервать;
			КонецЕсли;	
			Товар = СписокТоваров.ПолучитьЗначение(КолТов+1-Индекс);
	    Иначе
			Если СправочникТоваров.ПолучитьЭлемент()<=0 Тогда
		    	Прервать;
			КонецЕсли;	
			Товар = СправочникТоваров.ТекущийЭлемент();
		КонецЕсли;	
		    
		ТоварНеУдовлФильтру = 0;
		
		Если Товар.Выбран()=0 Тогда                     
			ТоварНеУдовлФильтру = 1;
		ИначеЕсли Товар.ЭтоГруппа()=1 Тогда
			ТоварНеУдовлФильтру = 1;
		КонецЕсли;	                
		
		Если ТоварНеУдовлФильтру = 1 Тогда
			Если НаложенФильтрПоТоварам=1 Тогда
				СписокТоваров.УдалитьЗначение(КолТов+1-Индекс);
		    КонецЕсли;
			Продолжить;
		КонецЕсли;	
		                         
		ТоварНеУдовлФильтру = 1;
		
		Единица = ?(ВидЕдИзм.ТекущаяСтрока()=1,Товар.ЕдиницаПоУмолчанию,глВернутьБазовуюЕдиницуТовара(Товар));
		
		Количество = ВремРегистры.Партии.СводныйОстаток(,,,Товар,,,,"ОстатокТовара");
		Себестоимость = 0;
		Если Количество>0 Тогда
			Себестоимость = ВремРегистры.Партии.СводныйОстаток(,,,Товар,,,,"Стоимость");
			Себестоимость = Себестоимость/Количество;
		КонецЕсли;	

		Если (Единица.Выбран()=1)И(Единица.Коэффициент<>1) Тогда
			Себестоимость = Себестоимость * Единица.Коэффициент;
		КонецЕсли;                                           
		                             
		Цена.ИспользоватьВладельца(Товар.ТекущийЭлемент());
		
		Для Индекс2 = 1 По СписокОтбираемыхЦен.РазмерСписка() Цикл
			
			КатегорияЦены = СписокОтбираемыхЦен.ПолучитьЗначение(Индекс2).ТекущийЭлемент();
			
			Если Цена.НайтиПоРеквизиту("КатегорияЦены",КатегорияЦены,0)>0 Тогда
				ОписаниеНаценок="";
				Если ВыполняетсяФильтрПоЦене(Цена,Себестоимость,ОписаниеНаценок,Единица)>0 Тогда
					ТоварНеУдовлФильтру = 0;	// товар содержит цену
					ИспользованиеОтбираемыхЦен.УстановитьЗначение(Индекс2,1);	// цена используется
			
					Обработано = Обработано + 1;
					Если (Обработано%10=0) Тогда
						Состояние("Анализ цен: "+Строка(Обработано));
					КонецЕсли;
	            
				Иначе 
					Если Режим = "Формирование" Тогда
					    ТоварНеУдовлФильтру = 1;
						Прервать;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;
		
		Если (ТоварНеУдовлФильтру=1)И((Режим = "Просмотр")ИЛИ(УстановленФильтрПоЦене=1)) Тогда
			Если НаложенФильтрПоТоварам=1 Тогда
				СписокТоваров.УдалитьЗначение(КолТов+1-Индекс);
			КонецЕсли;
		Иначе	      
			Если НаложенФильтрПоТоварам=0 Тогда
				СписокТоваров.ДобавитьЗначение(Товар.ТекущийЭлемент());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	// удаляем все неиспользуемые категории цен 
	КолЦен = СписокОтбираемыхЦен.РазмерСписка();
	Для Индекс = 1 По СписокОтбираемыхЦен.РазмерСписка() Цикл
		Если ИспользованиеОтбираемыхЦен.ПолучитьЗначение(КолЦен+1-Индекс)=0 Тогда
			СписокОтбираемыхЦен.УдалитьЗначение(КолЦен+1-Индекс);
		КонецЕсли;	
	КонецЦикла;	
				
	// список цен, передаваемый групповой обработке
	СписокЦен.УдалитьВсе();
	
	Если Режим = "Формирование" Тогда
		// сначала выгружаем все категории цен
		Для Индекс = 1 По СписокОтбираемыхЦен.РазмерСписка() Цикл
			СписокЦен.ДобавитьЗначение(СписокОтбираемыхЦен.ПолучитьЗначение(Индекс).ТекущийЭлемент());
		КонецЦикла; 
	
	ИначеЕсли Режим = "Просмотр" Тогда
		
		Если (ТипЗначенияСтр(Т) <> "Таблица") ИЛИ (Обновить = 0) Тогда
		   	Т = СоздатьОбъект("Таблица");
		Иначе
		 	Т.Очистить();
		КонецЕсли;
	    Т.ИсходнаяТаблица( "Таблица" );
	    
	   	Расшифровка = СоздатьОбъект("СписокЗначений");
	    Расшифровка.Установить("Объект", "ПодборЦен");
	    
		// все настройки помещаем в список
		
		Расшифровка.Установить("ВидНоменклатуры", ВидНоменклатуры.ТекущаяСтрока());
		Расшифровка.Установить("Номенклатура", Номенклатура);
		Расшифровка.Установить("КатНоменклатуры", КатНоменклатуры);
		Расшифровка.Установить("ТолькоПрисутствующие", ТолькоПрисутствующие);
		Расшифровка.Установить("ТолькоОтсутствующие", ТолькоОтсутствующие);
		Расшифровка.Установить("КатЦены", КатЦены);
		Расшифровка.Установить("ВалютаЦены", ВалютаЦены); 
		Расшифровка.Установить("ЕдИзм", ЕдИзм);
		
		Расшифровка.Установить("ВклЦенаОт", ВклЦенаОт);
		Расшифровка.Установить("ЦенаОт", ЦенаОт);
		Расшифровка.Установить("ВклЦенаДо", ВклЦенаДо);
		Расшифровка.Установить("ЦенаДо", ЦенаДо);
		
		Расшифровка.Установить("ВклНацСебестОт", ВклНацСебестОт);
		Расшифровка.Установить("НацСебестОт", НацСебестОт);
		Расшифровка.Установить("ВклНацСебестДо", ВклНацСебестДо);
		Расшифровка.Установить("НацСебестДо", НацСебестДо);
		
		Расшифровка.Установить("ВклНацУчЦенаОт", ВклНацУчЦенаОт);
		Расшифровка.Установить("НацУчЦенаОт", НацУчЦенаОт);
		Расшифровка.Установить("ВклНацУчЦенаДо", ВклНацУчЦенаДо);
		Расшифровка.Установить("НацУчЦенаДо", НацУчЦенаДо);
		                                                       
		Расшифровка.Установить("ВидЕдИзм", ВидЕдИзм.ТекущаяСтрока());
		
		Расшифровка.Установить("МФНоменклатура", МФНоменклатура); 
		Расшифровка.Установить("МФКатНоменклатуры", МФКатНоменклатуры);
		Расшифровка.Установить("ТипМФКатНоменклатуры", ТипМФКатНоменклатуры.ТекущаяСтрока());
		Расшифровка.Установить("МФКатЦены", МФКатЦены);
		Расшифровка.Установить("МФВалютыЦен", МФВалютыЦен);
		Расшифровка.Установить("МФЕдИзм", МФЕдИзм);
			                            
		Расшифровка.Установить("ВклФильтрСравненияНаценок", ВклФильтрСравненияНаценок);
		Расшифровка.Установить("ВидНаценки", ВидНаценки.ТекущаяСтрока());
		Расшифровка.Установить("ЗнакСравнения", ЗнакСравнения.ТекущаяСтрока());
		Расшифровка.Установить("НаценкаИзСправочника", НаценкаИзСправочника);
		                   
		
		Т.ВывестиСекцию("Отчет|Наименование");		
			  
		Т.ВывестиСекцию("Шапка|Наименование");
		    
		ЧислоЦен = 0;
		
		Для Индекс = 1 По СписокОтбираемыхЦен.РазмерСписка() Цикл
			КатегорияЦены = СписокОтбираемыхЦен.ПолучитьЗначение(Индекс).ТекущийЭлемент();
			Т.ПрисоединитьСекцию("Шапка|Цена");
			ЧислоЦен = ЧислоЦен + 1;
		КонецЦикла; 
		
	КонецЕсли;	
	
	
	Обработано = 0;
	
	// цикл по всем товарам
	Для Индекс = 1 По СписокТоваров.РазмерСписка() Цикл
		
		Товар = СписокТоваров.ПолучитьЗначение(Индекс);
	         
		Единица = ?(ВидЕдИзм.ТекущаяСтрока()=1,Товар.ЕдиницаПоУмолчанию,глВернутьБазовуюЕдиницуТовара(Товар));

		Количество = ВремРегистры.Партии.СводныйОстаток(,,,Товар,,,,"ОстатокТовара");
		Себестоимость = 0;
		Если Количество>0 Тогда
			Себестоимость = ВремРегистры.Партии.СводныйОстаток(,,,Товар,,,,"Стоимость");
			Себестоимость = Себестоимость/Количество;
		КонецЕсли;	

		Если (Единица.Выбран()=1)И(Единица.Коэффициент<>1) Тогда
			Себестоимость = Себестоимость * Единица.Коэффициент;
		КонецЕсли;                                           
		                             
		Если Режим = "Формирование" Тогда
		
			СписокЦен.ДобавитьЗначение(Товар.ТекущийЭлемент()); 
			СписокЦен.ДобавитьЗначение(Единица.ТекущийЭлемент());
			СписокЦен.ДобавитьЗначение(Себестоимость); 
		
		ИначеЕсли Режим = "Просмотр" Тогда	
			    
			ПечКол = СокрЛП(Окр(Количество,3,1));
			ПечСебест = СокрЛП(Окр(Себестоимость,2,1)); 
			
			ПечУчЦена = СокрЛП(Окр(Товар.УчетнаяЦена.Получить(РабочаяДата())/Единица.Коэффициент,2,1));
				
			Т.ВывестиСекцию("Товар|Наименование");
		
		КонецЕсли;	
		
		Цена.ИспользоватьВладельца(Товар.ТекущийЭлемент());
		
		Для Индекс2 = 1 По СписокОтбираемыхЦен.РазмерСписка() Цикл
			
			КатегорияЦены = СписокОтбираемыхЦен.ПолучитьЗначение(Индекс2).ТекущийЭлемент();
			
			ПроверкаЦены=0;
			ОписаниеЦены = "";
			
			Если Цена.НайтиПоРеквизиту("КатегорияЦены",КатегорияЦены,0)>0 Тогда
				
				ОписаниеНаценок = "";								
				ПроверкаЦены = ВыполняетсяФильтрПоЦене(Цена,Себестоимость,ОписаниеНаценок,Единица);
				
				Если ПроверкаЦены >0 Тогда
				    
					Если Режим = "Формирование" Тогда
		    			
						СписокЦен.ДобавитьЗначение(Цена.ТекущийЭлемент());
						                    
					ИначеЕсли Режим = "Просмотр" Тогда	
						
						ОписаниеЦены = ОписаниеЦены + Строка(Цена.Цена)+" ";
						ОписаниеЦены = ОписаниеЦены + СокрЛП(Цена.Валюта.Кратко);
						ОписаниеЦены = ОписаниеЦены + " за "+СокрЛП(Цена.Единица.Наименование);
						Если (Цена.Наценка<>0)ИЛИ(Цена.ПометкаУдаления()=1) Тогда
							ОписаниеЦены = ОписаниеЦены + РазделительСтрок + " (";
							ОписаниеЦены = ОписаниеЦены + ?(Цена.ПометкаУдаления()=0,"","уд.");
							Если (Цена.Наценка<>0)И(Цена.ПометкаУдаления()=1) Тогда
								ОписаниеЦены = ОписаниеЦены + ", ";
							КонецЕсли;	                           
							ОписаниеЦены = ОписаниеЦены + ?(Цена.Наценка=0,"","нац. = "+Строка(Цена.Наценка)+"%");
							ОписаниеЦены = ОписаниеЦены + ")";
						КонецЕсли;                            
						Если ПустаяСтрока(ОписаниеНаценок)=0 Тогда
							ОписаниеЦены = ОписаниеЦены + РазделительСтрок + ОписаниеНаценок;
						КонецЕсли;	
						
					КонецЕсли;	
					
					Обработано = Обработано + 1;
					Если (Обработано%10=0) Тогда
						Состояние("Отобрано цен: "+Строка(Обработано));
					КонецЕсли;
				     
					ЦенаДляПоиска = Цена.ТекущийЭлемент();
				Иначе
					ЦенаДляПоиска = "Цена не удовлетворяет фильтру";	
            	КонецЕсли;	 
			
			Иначе
				ЦенаДляПоиска = "Цена не задана";
			КонецЕсли;
			
			Если Режим = "Просмотр" Тогда	
				Если ПроверкаЦены=0 Тогда
					Т.ПрисоединитьСекцию("Товар|Цена");
				ИначеЕсли ПроверкаЦены=1 Тогда	//  наценки от себестоимости и от учетной цены > 0
					Т.ПрисоединитьСекцию("Товар|Цена");
				ИначеЕсли ПроверкаЦены=2 Тогда	//  наценка от себестоимости > 0, а от учетной цены <= 0
					Т.ПрисоединитьСекцию("Товар|Цена2");
				ИначеЕсли ПроверкаЦены=3 Тогда	//  наценка от себестоимости <= 0, а от учетной цены > 0
					Т.ПрисоединитьСекцию("Товар|Цена3");
				ИначеЕсли ПроверкаЦены=4 Тогда	//  наценки от себестоимости и от учетной цены <= 0
					Т.ПрисоединитьСекцию("Товар|Цена3");
				Иначе                               
					Т.ПрисоединитьСекцию("Товар|Цена");
				КонецЕсли;	
			КонецЕсли;	 
			
		КонецЦикла;	                                          
		
	КонецЦикла;	      
	
	Если Режим = "Формирование" Тогда
		
		ОткрытьФормуМодально("Обработка.ГрупповаяОбработкаЦен",СписокЦен);
		
	ИначеЕсли Режим = "Просмотр" Тогда

		Т.Защита(Константа.ФлагЗащитыТаблиц);
		Т.ТолькоПросмотр(1);
		Т.Опции(0,0,3,3);   
		Т.ОбластьПечати(2);
		Т.Показать("Прайс-лист");
    	
		Если (Обновить = 2)ИЛИ(ЗакрытьЭкран=1) Тогда
		    СтрокаДействийФормы = "#Закрыть";
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры


//******************************************************************************
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

//******************************************************************************
Процедура ПриВыбореЗакладки(Номер,Значение)	// Предопределенная процедура
	// закладки
    Если Номер=1 Тогда
    	Форма.ИспользоватьСлой("Основной,Общий",2);
    ИначеЕсли Номер=2 Тогда
    	Форма.ИспользоватьСлой("МножественныйФильтр,Общий",2);
	КонецЕсли;                    
	
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки

//******************************************************************************
Процедура ОбработкаПодбора(Значение)  // Предопределенная процедура
	Если (ВРег(Значение.Вид())=СправочникПодбора)И(СписокПодбора.НайтиЗначение(Значение)=0) Тогда
		СписокПодбора.ДобавитьЗначение(Значение);
		СписокПодбора.ТекущаяСтрока(СписокПодбора.РазмерСписка());
	КонецЕсли;
КонецПроцедуры  // ОбработкаПодбора

//******************************************************************************
Процедура ПриОткрытии()	// Предопределенная процедура

	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		              
		ВидНоменклатуры.ТекущаяСтрока(глРасшифровка.Получить("ВидНоменклатуры"));
		Номенклатура = глРасшифровка.Получить("Номенклатура");
		КатНоменклатуры = глРасшифровка.Получить("КатНоменклатуры"); 
		ТолькоПрисутствующие = глРасшифровка.Получить("ТолькоПрисутствующие"); 
		ТолькоОтсутствующие = глРасшифровка.Получить("ТолькоОтсутствующие"); 
		КатЦены = глРасшифровка.Получить("КатЦены");
		ВалютаЦены = глРасшифровка.Получить("ВалютаЦены"); 
		ЕдИзм = глРасшифровка.Получить("ЕдИзм");  
		
		ВклЦенаОт = глРасшифровка.Получить("ВклЦенаОт"); 
		ЦенаОт = глРасшифровка.Получить("ЦенаОт");
		ВклЦенаДо = глРасшифровка.Получить("ВклЦенаДо"); 
		ЦенаДо = глРасшифровка.Получить("ЦенаДо"); 
		
		ВклНацСебестОт = глРасшифровка.Получить("ВклНацСебестОт"); 
		НацСебестОт = глРасшифровка.Получить("НацСебестОт");
		ВклНацСебестДо = глРасшифровка.Получить("ВклНацСебестДо"); 
		НацСебестДо = глРасшифровка.Получить("НацСебестДо"); 
		
		ВклНацУчЦенаОт = глРасшифровка.Получить("ВклНацУчЦенаОт"); 
		НацУчЦенаОт = глРасшифровка.Получить("НацУчЦенаОт");
		ВклНацУчЦенаДо = глРасшифровка.Получить("ВклНацУчЦенаДо"); 
		НацУчЦенаДо = глРасшифровка.Получить("НацУчЦенаДо"); 
		
		ВидЕдИзм.ТекущаяСтрока(глРасшифровка.Получить("ВидЕдИзм"));
    	
		МФНоменклатура.УдалитьВсе();
		глРасшифровка.Получить("МФНоменклатура").Выгрузить(МФНоменклатура); 
	
		ТипМФКатНоменклатуры.ТекущаяСтрока(глРасшифровка.Получить("ТипМФКатНоменклатуры"));
    
		МФКатНоменклатуры.УдалитьВсе();
	  	глРасшифровка.Получить("МФКатНоменклатуры").Выгрузить(МФКатНоменклатуры); 
	
		МФКатЦены.УдалитьВсе();
		глРасшифровка.Получить("МФКатЦены").Выгрузить(МФКатЦены); 
	
		МФВалютыЦен.УдалитьВсе();
		глРасшифровка.Получить("МФВалютыЦен").Выгрузить(МФВалютыЦен); 
	
		МФЕдИзм.УдалитьВсе();
		глРасшифровка.Получить("МФЕдИзм").Выгрузить(МФЕдИзм); 
		
		ВклФильтрСравненияНаценок = глРасшифровка.Получить("ВклФильтрСравненияНаценок"); 
		ВидНаценки.ТекущаяСтрока(глРасшифровка.Получить("ВидНаценки"));
    	ЗнакСравнения.ТекущаяСтрока(глРасшифровка.Получить("ЗнакСравнения"));
        НаценкаИзСправочника = глРасшифровка.Получить("НаценкаИзСправочника"); 
		
		
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Выполнить("Просмотр");
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
	                                         
	ПерерисовкаНазванийЗакладок();      
	ДоступностьЭлементов();
	ПриВключенииФильтраСравненияНаценок();
	
КонецПроцедуры	// ПриОткрытии


//******************************************************************************
Процедура ВводНового()	// Предопределенная процедура
	                              
	ПерерисовкаНазванийЗакладок();
	ДоступностьЭлементов();
	ПриВключенииФильтраСравненияНаценок();
	
КонецПроцедуры	// ВводНового


//******************************************************************************
Процедура ПриЗакрытии()	// Предопределенная процедура
	
КонецПроцедуры	// ПриЗакрытии


//******************************************************************************
// ТЕЛО МОДУЛЯ

// инициализация списков
ВидНоменклатуры.УдалитьВсе();
ВидНоменклатуры.ДобавитьЗначение("вся номенклатура");
ВидНоменклатуры.ДобавитьЗначение("только товары");
ВидНоменклатуры.ДобавитьЗначение("только услуги");  
ВидНоменклатуры.ДобавитьЗначение("только наборы"); 
ВидНоменклатуры.ТекущаяСтрока(1);

ТипМФКатНоменклатуры.УдалитьВсе();
ТипМФКатНоменклатуры.ДобавитьЗначение("одна из");
ТипМФКатНоменклатуры.ДобавитьЗначение("одновременно все");
ТипМФКатНоменклатуры.ТекущаяСтрока(1);

ВидЕдИзм.УдалитьВсе();
ВидЕдИзм.ДобавитьЗначение("единицу по умолчанию");
ВидЕдИзм.ДобавитьЗначение("базовую единицу");
ВидЕдИзм.ТекущаяСтрока(1);

ЗнакСравнения.УдалитьВсе();
ЗнакСравнения.ДобавитьЗначение(">"); 
ЗнакСравнения.ДобавитьЗначение(">="); 
ЗнакСравнения.ДобавитьЗначение("<"); 
ЗнакСравнения.ДобавитьЗначение("<="); 
ЗнакСравнения.ДобавитьЗначение("=");
ЗнакСравнения.ДобавитьЗначение("<>");
ЗнакСравнения.ТекущаяСтрока(1);

ВидНаценки.УдалитьВсе();
ВидНаценки.ДобавитьЗначение("наценка на себестоимость");
ВидНаценки.ДобавитьЗначение("наценка на учетную цену");
ВидНаценки.ТекущаяСтрока(1);

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.ИспользоватьСлой("Основной,Общий",2);

Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
	    
НаценкаИзСправочника = "";

КатНоменклатуры.ВыборГруппы(0);