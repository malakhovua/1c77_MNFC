//*********************************************
//Расчет затрат производства
Функция РасчетПрямыхЗатратНаОплатуТруда(Продукция, Док)
	ПрямаяЗарплата = 0;
	Если ПустоеЗначение(Продукция.СхемаРасчетаЗП) = 0 Тогда
		СхемаЗП = Продукция.СхемаРасчетаЗП.Получить(Док.ДатаДок);
		СхемаЗП.ВыбратьСтроки();
		Пока СхемаЗП.ПолучитьСтроку() = 1 Цикл
			ВидРабот = СхемаЗП.ВидРабот;
			ВидБазы = СхемаЗП.ВидРабот.База;
			База = ?((СхемаЗП.ВидРабот.База = Перечисление.ТипБазыВР.КвоФарша) ИЛИ (СхемаЗП.ВидРабот.База = Перечисление.ТипБазыВР.Кутера), Док.КвоНорма, Док.КвоГПприемки);
			Тариф = ВидРабот.Тариф.Получить(Док.ДатаДок);
			СтоимостьЗП = База*Тариф;
			ПрямаяЗарплата = ПрямаяЗарплата + СтоимостьЗП;
		КонецЦикла;
	КонецЕсли;
	Возврат Окр(ПрямаяЗарплата,2);
КонецФункции

Функция ВычислитьЗатраты(Продукция, Док)
	
	Возврат глПроизводственныеЗатратыПоДокументу(Продукция, Док) + //Мат. затраты
	        Продукция.ОПР.Получить(Док.ДатаДок)*Док.КвоГПприемки + // ОПР
	        глПолучитьСтУпак(Продукция,, Док.ДатаДок)*Док.КвоГПприемки + // С/с Упак.
	        РасчетПрямыхЗатратНаОплатуТруда(Продукция, Док); //З/п 
			
КонецФункции

Функция ЦенаЕдПродукцит(Продукция, Док)
	
	ЦенаЦены = 0;
	ЦенаТовара = глВернутьЦену(Продукция, ВидЦены);
	
	Если ПустоеЗначение(ЦенаТовара) = 0 Тогда
		
		ЦенаТовара.ИспользоватьДату(Док.ДатаДок);
		ЦенаЦены = ЦенаТовара.Цена;
		ВалютаЦены = ЦенаТовара.Валюта;
		ЕдЦены = ЦенаТовара.Единица;
		Попытка		              
			Если ПустоеЗначение(Док.Коэффициент) = 1 Тогда
				ЦенаЦены = ЦенаЦены * 1 / ЕдЦены.Коэффициент;
			Иначе     
				ЦенаЦены = ЦенаЦены * Док.Коэффициент / ЕдЦены.Коэффициент;			
			КонецЕсли;	
		Исключение       
			// если на дату документа у единицы еще не было коэффициента
			ЦенаЦены = 0;
			глКомментарий(Шаблон("[Док.ТекущийДокумент()] .[Док.Продукция] - У единицы [ЕдЦены] на дату [Док.ДатаДок] не установлен коэффициент."),1);
		КонецПопытки;
		
	КонецЕсли;
	
	
	Возврат ЦенаЦены;
	

КонецФункции

//*******************************************
// Процедура генерации запроса Сформировать.
//
Процедура Сформировать()
	Перем Запрос, ТекстЗапроса, Таб;
	
	Если ПустоеЗначение(ВидЦены) = 1 Тогда
		Сообщить("Вид цен не заполнен", "!");
		Возврат;
	КонецЕсли;
	
	
	Если (ПустоеЗначение(ВыбНачПериода) = 1) ИЛИ (ПустоеЗначение(ВыбКонПериода) = 1)  Тогда
		Сообщить("Период не заполнен","!");
		Возврат;
	КонецЕсли;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ВыбНачПериода по ВыбКонПериода;
	|Обрабатывать НеПомеченныеНаУдаление;
	|ТекущийДокумент = Документ.ВыпускПродукции.ТекущийДокумент;
	|Продукция = Документ.ВыпускПродукции.Продукция;
	|Группировка Продукция;
	|Группировка ТекущийДокумент;
	|Условие(Продукция в списТовар);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИтогПланКол = 0;
	ИтогФактКол = 0;
	ИтогОтклонение = 0;
	ИтогСумма = 0;
	ИтогЗатраты = 0;
	

	// Подготовка к заполнению выходных форм данными запроса
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Отчет");
	// Заполнение полей "Заголовок"
	Таб.ВывестиСекцию("Заголовок");
	Состояние("Заполнение выходной таблицы...");
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0);
	Пока Запрос.Группировка(1) = 1 Цикл
		// Заполнение полей Продукция
		Если Запрос.Продукция.ЭтоГруппа() = 1 Тогда
			Таб.ВывестиСекцию("ПродукцияГруппа");
			Продолжить;
		КонецЕсли;
		
		Если Запрос.Продукция.ВидТМЦ <> Перечисление.ВидыТМЦ.Продукция Тогда
			Продолжить;
		КонецЕсли;
		
		
		Продукция = Запрос.Продукция;
		
		//ТЗ для итогов
		ТЗ = СоздатьОбъект("ТаблицаЗначений");
		ТЗ.НоваяКолонка("ПланКол");
		ТЗ.НоваяКолонка("ФактКол");
		ТЗ.НоваяКолонка("Отклонение");
		ТЗ.НоваяКолонка("ОтнОтклонение");
		ТЗ.НоваяКолонка("Цена");
		ТЗ.НоваяКолонка("Сумма");
		ТЗ.НоваяКолонка("Затраты");
		ТЗ.НоваяКолонка("ТекущийДокумент");
		
		Пока Запрос.Группировка(2) = 1 Цикл
			// В контексте группировки
			Док = Запрос.ТекущийДокумент;
			Док.ВыбратьСтроки();
			
			Пока  Док.ПолучитьСтроку() = 1 Цикл
				
				Если Запрос.Продукция <> Док.Продукция Тогда
					Продолжить;
				КонецЕсли;
				
				ТЗ.НоваяСтрока();
				ТЗ.ТекущийДокумент = Док;
				ТЗ.ПланКол = Док.Кво;
				ТЗ.ФактКол = Док.КвоГППриемки;
				ТЗ.Отклонение = Док.КвоГППриемки-Док.Кво;
				ТЗ.ОтнОтклонение = Окр(?(Док.Кво = 0,0,(Док.КвоГППриемки-Док.Кво)/Док.Кво)*100,2);
				ТЗ.Цена = ЦенаЕдПродукцит(Продукция, Док);
				ТЗ.Сумма = Окр(ТЗ.Цена*ТЗ.ФактКол,2);
				ТЗ.Затраты = Окр(ВычислитьЗатраты(Продукция, Док),2);
				
				//Итоги
				ИтогПланКол = ИтогПланКол + ТЗ.ПланКол;
				ИтогФактКол = ИтогФактКол + ТЗ.ФактКол;
				
				ИтогСумма = ИтогСумма + ТЗ.Сумма;
				ИтогЗатраты = ИтогЗатраты + ТЗ.Затраты;
			
			КонецЦикла;
			
		КонецЦикла;
	
		//Вывод итогов
		Таб.ВывестиСекцию("Продукция");
		
		ТЗ.ВыбратьСтроки();
		//Вывод детальн. записей
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			Таб.ВывестиСекцию("ТекущийДокумент");
		КонецЦикла;
	
	КонецЦикла;
	
	//Итоги по отчету
	ИтогОтнОтклонение = Окр(?(ИтогПланКол = 0,0, (ИтогФактКол - ИтогПланКол)/ИтогПланКол)*100,2);
	ИтогОтклонение = ИтогФактКол - ИтогПланКол;
	ИтогРентабельность = Окр(?(ИтогЗатраты=0,0,ИтогСумма/ИтогЗатраты)*100,2);
	
	Таб.ВывестиСекцию("Итоги");
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчет", "");
	
КонецПроцедуры

//*******************************************
//Работа со списком

Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры


