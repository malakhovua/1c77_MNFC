Перем ИмяСписка;

//======================================================================
Процедура ОбработитьЗаписьСправочника(Спр, ТЗ, ТЗКол)
	Если Спр.ПометкаУдаления() = 0 Тогда			
		ТМЦ = ?(Спр.Вид() = "УМК_НормыСписанияМатериаловУпаковок", Спр.Владелец.Владелец, Спр.Владелец);
		Если ТМЦ.Вид() = "ТМЦ" Тогда
			Если списПродукция.РазмерСписка() > 0 Тогда
				Если списПродукция.Принадлежит(ТМЦ) = 0 Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		
		Если (фНормаНаУп = 1) И (Спр.Вид() = "УМК_НормыСписанияМатериаловУпаковок") Тогда
			Кво = Спр.НормаСписанияУп.Получить(НаДату);
		Иначе
			Кво = Спр.НормаСписания.Получить(НаДату);
		КонецЕсли;
		Если Кво = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Мат = Спр.Материал.Получить(НаДату);
		Если списТовар.РазмерСписка() > 0 Тогда
			Если списТовар.Принадлежит(Мат) = 0 Тогда
				Возврат;;
			КонецЕсли;
		КонецЕсли;
		Стр = 0;
		Если ТЗ.НайтиЗначение(Мат, Стр, "Материал") = 0 Тогда
			ТЗ.НоваяСтрока();
		Иначе
			ТЗ.ПолучитьСтрокуПоНомеру(Стр);
		КонецЕсли;
		ТЗ.Материал = Мат;
		Если Спр.Вид() = "УМК_НормыСписанияМатериаловУпаковок" Тогда
			Ключ = ЗначениеВСтрокуВнутр(Спр.Владелец.Владелец) + "/**/" + ЗначениеВСтрокуВнутр(Спр.Владелец.ВидУпаковки);
		Иначе
			Ключ = ЗначениеВСтрокуВнутр(Спр.Владелец) + "/**/";
		КонецЕсли;
		Стр = 0;
		Если ТЗКол.НайтиЗначение(Ключ, Стр, "Ключ") = 0 Тогда
			ТЗКол.НоваяСтрока();
			ТЗКол.Признак = ?(Спр.Вид() = "УМК_НормыСписанияМатериаловУпаковок", 1, 0);
			ТЗКол.Ключ = Ключ;
			ТЗКол.ТМЦ = ТМЦ;
			ТЗКол.Группа = ТМЦ.Родитель;
			ТЗКол.ВидУпаковки = ?(Спр.Вид() = "УМК_НормыСписанияМатериаловУпаковок", Спр.Владелец.ВидУпаковки, "");
			ТЗКол.ИмяКол = "К" + ТЗКол.КоличествоСтрок();
			ТЗ.НоваяКолонка(ТЗКол.ИмяКол, "Число");
			ТЗ.НоваяКолонка(ТЗКол.ИмяКол + "Кр", "Число");
		Иначе
			ТЗКол.ПолучитьСтрокуПоНомеру(Стр);
		КонецЕсли;
		
		ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, ТЗКол.ИмяКол, Кво);
		Если Спр.Вид() = "УМК_НормыСписанияМатериаловУпаковок" Тогда
			Если Спр.Владелец.Стандартный.Получить(НаДату) = 1 Тогда
				ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, ТЗКол.ИмяКол + "Кр", 1);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // гл

//*******************************************
Процедура Сформировать()
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Материал");
	ТЗКол = СоздатьОбъект("ТаблицаЗначений");
	ТЗКол.НоваяКолонка("Ключ", "Строка");
	ТЗКол.НоваяКолонка("Признак", "Число");
	ТЗКол.НоваяКолонка("ТМЦ", "Справочник");
	ТЗКол.НоваяКолонка("Группа", "Справочник");
	ТЗКол.НоваяКолонка("ВидУпаковки", "Справочник.ВидыУпаковки");
	ТЗКол.НоваяКолонка("ИмяКол", "Строка");
	ТЗКол.НоваяКолонка("ИмяКолКраска", "Число", 1);
	
	Состояние("Отбор справочника");
	Если (ргВыводить = 1) ИЛИ (ргВыводить = 3) Тогда
		Спр = СоздатьОбъект("Справочник.УМК_НормыСписанияМатериаловУпаковок");
		Спр.ВыбратьЭлементы(0);
		Пока Спр.ПолучитьЭлемент() = 1 Цикл
			ОбработитьЗаписьСправочника(Спр.ТекущийЭлемент(), ТЗ, ТЗКол);
		КонецЦикла;		
	КонецЕсли;
	
	Если (ргВыводить = 2) ИЛИ (ргВыводить = 3) Тогда
		Спр = СоздатьОбъект("Справочник.УМК_НормыСписанияМатериаловУпаковокФорм");
		Спр.ВыбратьЭлементы(0);
		Пока Спр.ПолучитьЭлемент() = 1 Цикл
			ОбработитьЗаписьСправочника(Спр.ТекущийЭлемент(), ТЗ, ТЗКол);
		КонецЦикла;		
	КонецЕсли;	
	
	ТЗКол.Сортировать("Признак,Группа,ТМЦ,ВидУпаковки");
	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка|Основная");
	
	Состояние("Вывод шапки");
	СтарТМЦ = ""; НКол = Таб.ШиринаТаблицы(); НСтр = 4; СтарПризнак = -1; СтарГруппа = ""; НСтрГ = 3; НКолГруппы = Таб.ШиринаТаблицы();
	НачКол = НКол - 1;
	Таб.ПовторятьПриПечатиСтолбцы(1, 1);
	
	ТЗКол.ВыбратьСтроки();
	Пока ТЗКол.ПолучитьСтроку() = 1 Цикл
		Если СтарПризнак <> ТЗКол.Признак Тогда
			Если СтарПризнак <> -1 Тогда
				Таб.ПрисоединитьСекцию("Шапка|РазделительПризнаков");
			КонецЕсли;
			СтарПризнак = ТЗКол.Признак;
			СтарТМЦ = "";			
		КонецЕсли;
		
		Если СтарТМЦ <> ТЗКол.ТМЦ Тогда
			Если СтарТМЦ <> "" Тогда
				Таб.Область(НСтр, НКол + 1, НСтр , Таб.ШиринаТаблицы()).Объединить();
				Таб.Область(НСтр, Таб.ШиринаТаблицы(), НСтр + 1, Таб.ШиринаТаблицы()).РамкаСправа(4);
			КонецЕсли;
			
			НКол = Таб.ШиринаТаблицы();
			СтарТМЦ = ТЗКол.ТМЦ;
		КонецЕсли;
		Если СтарГруппа <> ТЗКол.Группа Тогда
			Если СтарГруппа <> "" Тогда
				Таб.Область(НСтрГ, НКолГруппы + 1, НСтрГ, Таб.ШиринаТаблицы()).Объединить();
			КонецЕсли;
			НКолГруппы = Таб.ШиринаТаблицы(); 
			СтарГруппа = ТЗКол.Группа;
		КонецЕсли;
		
		Таб.ПрисоединитьСекцию("Шапка|ПродукцияВУ");
	КонецЦикла;
	
	Если СтарТМЦ <> "" Тогда
		Таб.Область(НСтр, НКол + 1, НСтр , Таб.ШиринаТаблицы()).Объединить();				
	КонецЕсли;
	Если СтарГруппа <> "" Тогда
		Таб.Область(НСтрГ, НКолГруппы + 1, НСтрГ, Таб.ШиринаТаблицы()).Объединить();
	КонецЕсли;	
	
	Таб.Опции(0,0, Таб.ВысотаТаблицы(), 1);
	
	ТЗ.Сортировать("Материал");
	Состояние("Вывод таблицы");
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		НКол = НачКол;
		Таб.ВывестиСекцию("Строка|Основная");
		НСтр = Таб.ВысотаТаблицы();
		СтарПризнак = - 1; СтарТМЦ = "";
		ТЗКол.ВыбратьСтроки();
		Пока ТЗКол.ПолучитьСтроку() = 1 Цикл
			Если СтарПризнак <> ТЗКол.Признак Тогда
				Если СтарПризнак <> -1 Тогда
					Таб.ПрисоединитьСекцию("Строка|РазделительПризнаков");
				КонецЕсли;
				СтарПризнак = ТЗКол.Признак;
				НКол = НКол + 1;
			КонецЕсли;

			Если ТЗ.ПолучитьЗначение(ТЗ.НомерСтроки, ТЗКол.ИмяКол + "Кр") = 1 Тогда
				Таб.ПрисоединитьСекцию("СтрокаКр|ПродукцияВУ");
			Иначе
				Таб.ПрисоединитьСекцию("Строка|ПродукцияВУ");
			КонецЕсли;			
			
			
			Если СтарТМЦ <> ТЗКол.ТМЦ Тогда
				Если СтарТМЦ <> "" Тогда
					Таб.Область(НСтр, ТЗКол.НомерСтроки - 1 + НКол).РамкаСправа(4);
				КонецЕсли;
				СтарТМЦ = ТЗКол.ТМЦ;
			КонецЕсли;
			
		КонецЦикла;		
	КонецЦикла;
	
	Таб.ПараметрыСтраницы(2);	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Материалы упаковок");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если (Элт.Вид() = "ТМЦ") И (ИмяСписка = "") Тогда
		ДобавитьвСписок(Элт, списТовар);
	ИначеЕсли (Элт.Вид() = "ТМЦ") И (ИмяСписка = "списПродукция") Тогда
		ДобавитьвСписок(Элт, списПродукция);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмяСписка = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

ргВыводить = 1;
НаДату = ТекущаяДата();