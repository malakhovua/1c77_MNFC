//*******************************************
Процедура Сформировать()
	Таб = СоздатьОбъект("Таблица");
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("ПН", "Документ.ПриходнаяНакладнаяЗапасы");
	ТЗ.НоваяКолонка("Кво", "Число");
	
	ПН = СоздатьОбъект("Документ.ПриходнаяНакладнаяЗапасы");
	ПН.УстановитьФильтр(1,0);
	ПН.ВыбратьДокументы(НачДата, КонДата);
	Пока ПН.ПолучитьДокумент() = 1 Цикл
		Если (ПустоеЗначение(ПН.ДокументОснование) = 0) И (ПН.ДокументОснование.Вид() = "ЗаказПоставщику") Тогда
			Продолжить;
		КонецЕсли;
		Если списКонтр.РазмерСписка() > 0 Тогда
			Если списКонтр.Принадлежит(ПН.Контрагент) = 0 Тогда
				Продолжить;
			КонецЕсли;		    
		КонецЕсли;
		
		ПН.ВыбратьСтроки();
		Пока ПН.ПолучитьСтроку() = 1 Цикл
			Если ПустоеЗначение(ПН.Заказ) = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если списТовар.РазмерСписка() > 0 Тогда
			    Если списТовар.Принадлежит(ПН.ТМЦ) = 0 Тогда
					Продолжить;
			    КонецЕсли;
			КонецЕсли;
			
			ТЗ.НоваяСтрока();
			ТЗ.ТМЦ = ПН.ТМЦ;
			ТЗ.ПН = ПН.ТекущийДокумент();
			ТЗ.Кво = ПН.Кво;
		КонецЦикла;		
	КонецЦикла;	
	
	ТЗ.Сортировать(?(ргСорт = 1,"ТМЦ,ПН","ПН,ТМЦ"), 1);
	Таб.ВывестиСекцию("Шапка");
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Приходы без заказов");	
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "Контрагенты" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списКонтр);
	ИначеЕсли Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

ргСорт = 1;