//*******************************************
Процедура Сформировать()
	Таб = СоздатьОбъект("Таблица");
	СтрФ = "Включены " + ?(фСчФ = 1, "СФ", "РН") + ": ";
	СтрФ = СтрФ + ?(фПров = 1, "проведенные","");
	СтрФ = СтрФ + ?(фНеПров = 1, ?(Прав(СтрФ, 1) = "е",", ","") + "не проведенные","");
	СтрФ = СтрФ + ?(фНеПомУд= 1, ?(Прав(СтрФ, 1) = "е",", ","") + "не помеченные на удаление","");
	СтрФ = СтрФ + ?(фПомУд = 1, ?(Прав(СтрФ, 1) = "е",", ","") + "помеченные на удаление","");
	
	глФильтры(СтрФ, списПокупатель, "Контрагенты");
	Таб.ВывестиСекцию("Шапка");
	ДокРН = СоздатьОбъект("Документ." + ?(фСчФ = 1, "СчетФактура","РасходнаяНакладная"));
	ДокРН.УстановитьФильтр(фПров, фНеПров);
	ДокРН.ВыбратьДокументы(НачДата, КонДата);
	ИтСуммаСНДС = 0; ИтВес = 0; ИтПеревес = 0; ИтРазница = 0;
	
	Пока ДокРН.ПолучитьДокумент() = 1 Цикл
		Если ((фПомУд = 0) И (ДокРН.ПометкаУдаления() = 1)) ИЛИ 
		     ((фНеПомУд = 0) И (ДокРН.ПометкаУдаления() = 0)) Тогда
			Продолжить;
		КонецЕсли;
		Если списПокупатель.РазмерСписка() > 0 Тогда
			Если списПокупатель.Принадлежит(ДокРН.Контрагент) = 0 Тогда
				Продолжить;
			КонецЕсли;		    
		КонецЕсли;
		
		АдресК = глАдресСтрокой(?(ПустоеЗначение(ДокРН.Договор) = 0, ДокРН.Договор.Адрес, ДокРН.Контрагент.ФизическийАдрес));
		Примечание = ДокРН.Примечание;
		СуммаСНДС = ДокРН.Итог("СуммаСНДС"); 
		Вес = ДокРН.ИтогоВес; 
		Перевес = ДокРН.ИтогоВесК; 
		Разница = Перевес - Вес;
		ИтСуммаСНДС = ИтСуммаСНДС + СуммаСНДС;
		ИтВес = ИтВес + Вес;
		ИтПеревес = ИтПеревес + Перевес;
		ИтРазница = ИтРазница + Разница;
		
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	Таб.ВывестиСекцию("Итого");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Реестр РН");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "Контрагенты" Тогда
		ДобавитьвСписок(Элт, списПокупатель);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
		НачДата = Форма.Параметр.Получить("НачДата");
		КонДата = Форма.Параметр.Получить("КонДата");
		фСчФ = Форма.Параметр.Получить("СФ");
		Сформировать();
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

фПров = 1;
фНеПров = 1;
фПомУд = 0;
фНеПомУд = 1;