// ===============================
Функция ТекущийКаталог()
	Если ГруппыОтчетов.ТекущаяСтрока() > 0 Тогда
		Возврат ГруппыОтчетов.ПолучитьЗначение(ГруппыОтчетов.ТекущаяСтрока());
	Иначе	
		Возврат "";
	КонецЕсли;	
КонецФункции

// ===============================
Функция ТекущийОтчет()
	Если Отчеты.ТекущаяСтрока() > 0 Тогда
		Возврат Отчеты.ПолучитьЗначение(Отчеты.ТекущаяСтрока());
	Иначе	
		Возврат "";
	КонецЕсли;	
КонецФункции

// ===============================
Процедура СохранитьТекОтчет()
	Если Отчеты.ТекущаяСтрока() > 0 Тогда
		СохранитьЗначение("РеглОтчОтчет",ТекущийОтчет());
	КонецЕсли;	
КонецПроцедуры	
                                  
// ===============================
Процедура ВосстановитьТекОтчет()
	Если Отчеты.РазмерСписка() <> 0 Тогда
		Отчеты.ТекущаяСтрока(Макс(1,Отчеты.НайтиЗначение(ВосстановитьЗначение("РеглОтчОтчет"))));
	КонецЕсли;	
КонецПроцедуры	

// ===============================
Процедура ЗаполнитьГруппу(Каталог,Вариант)
	Файл = Каталог+"\RpList.txt";
	Если ФС.СуществуетФайл(Файл)=0 Тогда
		Возврат;		    
	КонецЕсли;
	Текст = СоздатьОбъект("Текст");
	Текст.Открыть(Файл);
	Стр = Текст.ПолучитьСтроку(1);
	Если ПустаяСтрока(Стр)=1 Тогда
		Возврат;		    
	КонецЕсли;
	Если Вариант = 1 Тогда
	    ГруппыОтчетов.ДобавитьЗначение(Каталог,Стр);
	    Возврат;
	КонецЕсли;
	Отчеты.УдалитьВсе();
	Для Инд = 2 По Текст.КоличествоСтрок()  Цикл
		Стр = Текст.ПолучитьСтроку(Инд);
		К = Найти(Стр,";");
		Если К > 0 Тогда
			ИмяФайла = Лев(Стр,К-1);
			ИмяОтчета = Сред(Стр,К+1);
		    Отчеты.ДобавитьЗначение(ИмяФайла,ИмяОтчета);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
                        
// ===============================
Процедура ВыборГруппы()    
	СохранитьТекОтчет();
	ЗаполнитьГруппу(ТекущийКаталог(),2);
	ВосстановитьТекОтчет();
КонецПроцедуры

// ===============================
Процедура Заполнить()
	Каталог = КаталогИБ()+"ExtForms\";
	ФГ = СоздатьОбъект("ФС");
	Файл = ФГ.НайтиПервыйФайл(Каталог+"rp*.grp");
	ГруппыОтчетов.УдалитьВсе();
	Пока ПустаяСтрока(Файл) = 0 Цикл
	    ЗаполнитьГруппу(Каталог+Файл,1);
		Файл = ФГ.НайтиСледующийФайл();
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	Заполнить();
	Если ГруппыОтчетов.РазмерСписка() <> 0 Тогда
		ГруппыОтчетов.ТекущаяСтрока(Макс(1,ГруппыОтчетов.НайтиЗначение(ВосстановитьЗначение("РеглОтчГруппа"))));
	КонецЕсли;	
	
	ЗаполнитьГруппу(ТекущийКаталог(),2);
	ВосстановитьТекОтчет();
КонецПроцедуры
                                  
// ===============================
Процедура ПриЗакрытии()
	СохранитьЗначение("РеглОтчГруппа",ТекущийКаталог());
	СохранитьТекОтчет();
КонецПроцедуры

// ===============================
Процедура Выбрать()
	Перем Заголовок;
	Если Отчеты.ТекущаяСтрока()=0 Тогда
		Возврат;
	КонецЕсли;
	Расширение = ВРег(Прав(ТекущийОтчет(), 3));     
	Отчеты.ПолучитьЗначение(Отчеты.ТекущаяСтрока(), Заголовок);
	Если Расширение = "ERT" Тогда     
		ОткрытьФорму("Отчет",,ТекущийКаталог()+"\"+ТекущийОтчет());
	ИначеЕсли Расширение = "MXL" Тогда
		Таб = СоздатьОбъект("Таблица");
		Таб.Открыть(ТекущийКаталог()+"\"+ТекущийОтчет());
     	Таб.ТолькоПросмотр(1);
     	Таб.Опции(0, 0, 0, 0);
     	Таб.Показать(Заголовок);
	ИначеЕсли Расширение = "TXT" Тогда
		Текст = СоздатьОбъект("Текст");
		Текст.Открыть(ТекущийКаталог()+"\"+ТекущийОтчет());
     	Текст.Показать(Заголовок, "");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура Добавить()
	Перем ИсхКаталог;
	Перем Каталог;
	Перем ИмяФайла; 
	Если ФС.ВыбратьФайл(0, ИмяФайла, ИсхКаталог, "Выберите файл обновления отчетов", "Программы (*.exe) |*.exe|Все файлы (*.*) |*.*") = 1 Тогда
		Номер = 0;
		Если Лев(КаталогИБ(), 2)="\\" Тогда
			ВремКаталог = КаталогВременныхФайлов()+"temp.grp\";
		Иначе
			ВремКаталог = КаталогИБ()+"ExtForms\temp.grp\";
		КонецЕсли;
        Каталог = КаталогИБ()+"ExtForms\"+Лев(ИмяФайла, СтрДлина(ИмяФайла)-4)+".grp";

        НовыйКаталог = 0;
        Если ФС.СуществуетФайл(Каталог) = 1 Тогда
        	А = Вопрос("Данная группа отчетов уже существует."+РазделительСтрок+
        	           "Заменить на новые?", 3);
            Если А = 2 Тогда
            	Возврат;    
            КонецЕсли;  
            Если А = 7 Тогда
            	Если СтрДлина(ИмяФайла) = 11 Тогда
            		Каталог = Лев(Каталог, СтрДлина(Каталог)-5);
            	Иначе
            		Каталог = Лев(Каталог, СтрДлина(Каталог)-4);
	            КонецЕсли;         
            	Номер = 1;
            	Пока ФС.СуществуетФайл(Каталог+Номер+".grp") = 1 Цикл
            	    Номер = Номер+1;
            	КонецЦикла;
            	Каталог = Каталог+Номер+".grp";
            	НовыйКаталог = 1;
			КонецЕсли;   
		Иначе
			НовыйКаталог = 1;
        КонецЕсли;
        Каталог = Каталог+"\";
		ФС.СоздатьКаталог(ВремКаталог);
		ФС.КопироватьФайл(ИсхКаталог+ИмяФайла, ВремКаталог+ИмяФайла, 0); 
		ТекКаталог = ФС.ТекКаталог();
		ФС.УстТекКаталог(ВремКаталог);
		Если КомандаСистемы(""""+ВремКаталог+ИмяФайла+"""") <> 0 Тогда
			ФС.УдалитьФайл(ВремКаталог+ИмяФайла);
			ФС.УдалитьКаталог(ВремКаталог);
			ФС.УстТекКаталог(ТекКаталог);
			Возврат;
		КонецЕсли;  
		Если НовыйКаталог = 1 Тогда
			ФС.СоздатьКаталог(Каталог);
		КонецЕсли;
		ФС.УдалитьФайл(ВремКаталог+ИмяФайла);
		Файл = ФС.НайтиПервыйФайл(ВремКаталог+"*.*");
		Пока ПустаяСтрока(Файл) = 0 Цикл
			Если (Файл <> "..") И (Файл <> ".") Тогда
		    	ФС.КопироватьФайл(ВремКаталог+Файл, Каталог+Файл, 0);
		    	ФС.УдалитьФайл(ВремКаталог+Файл);  
			КонецЕсли;
			Файл = ФС.НайтиСледующийФайл();
		КонецЦикла;
				
		Если Номер <> 0 Тогда
			Файл=Каталог+"\RpList.txt";
			Если ФС.СуществуетФайл(Файл) = 1 Тогда
				Текст=СоздатьОбъект("Текст");
				Текст.Открыть(Файл);
				Текст.ЗаменитьСтроку(1, Текст.ПолучитьСтроку(1)+" ("+Номер+")");
				Текст.Записать(Файл);
			КонецЕсли;
		КонецЕсли;
		ФС.УдалитьКаталог(ВремКаталог);
		ФС.УстТекКаталог(ТекКаталог);
		ПриОткрытии();
		ГруппыОтчетов.ТекущаяСтрока(ГруппыОтчетов.НайтиЗначение(Лев(Каталог, СтрДлина(Каталог)-1)));
	КонецЕсли;
КонецПроцедуры
