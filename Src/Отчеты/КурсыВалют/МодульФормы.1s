// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Таб;
Перем Обновить;
Перем Расшифровка;


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Процедура Сформировать()
	Если Валюта.Выбран()=0 Тогда
	    Предупреждение("Не выбрана валюта");
	    Возврат;
	КонецЕсли;
	СохранитьЗначение("ИсторияКурсовВалюта",Валюта);
	
	Если глПроверкаИнтервалаОтчета(ДатаНач,ДатаКон) = 0 Тогда
	    Возврат;
	КонецЕсли;

	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;

	глУстПропись(Гривня);
	Таб.ИсходнаяТаблица("Таблица");
	Заголовок = "История курсов валюты " + Валюта; 
	Если Константа.ФормыНаУкраинском = Да Тогда
		Заголовок = "Історія курсів валюти " + Валюта; 
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "КурсыВалют");
    Расшифровка.Установить("ДатаНач", ДатаНач);
    Расшифровка.Установить("ДатаКон", ДатаКон);
	Расшифровка.Установить("Валюта", Валюта);

	ИстНБУ = СоздатьОбъект("Периодический");
	ИстНБУ.ИспользоватьОбъект("Курс",Валюта);
	ИстНБУ.ВыбратьЗначения(ДатаНач,ДатаКон);

	Таб.ВывестиСекцию("Шапка");
	
	Пока ИстНБУ.ПолучитьЗначение()=1 Цикл
		ДатаЗнач = ИстНБУ.ДатаЗнач;
		ЗначНБУ = ИстНБУ.Значение;
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	Таб.ВывестиСекцию("Дно");
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,5,0,"ПарамПечатиУпрКурсыВалют","РазмОкнаУпрКурсыВалют");
	Таб.ОбластьПечати(3);
	Таб.Показать("ПЕЧАТЬ: Курс валюты "+Валюта+" ("+ПериодСтр(ДатаНач,ДатаКон)+")","");

	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()
	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ДатаНач = глРасшифровка.Получить("ДатаНач");
		ДатаКон = глРасшифровка.Получить("ДатаКон");
		Валюта = глРасшифровка.Получить("Валюта");

		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ДатаНач = НачалоПериодаБИ();
		ДатаКон = КонецПериодаБИ();
		Если Валюта.Выбран()=0 Тогда
			глВосстановитьЗначение("ИсторияКурсовВалют","Валюта",Валюта);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры