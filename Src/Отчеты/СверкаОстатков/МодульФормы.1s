//*******************************************
Процедура Сформировать()
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("Остаток", "Число");
	ТЗ.НоваяКолонка("ИтогоБух", "Число");
	ТЗ.НоваяКолонка("ИтогоПартии", "ЧИсло");
	ТЗ.НоваяКолонка("ИтогоБухС", "Число");
	ТЗ.НоваяКолонка("ИтогоПартииС", "ЧИсло");
	
	
	РегОст = СоздатьОбъект("Регистр.Остатки");
	РегП = СоздатьОбъект("Регистр.Партии");
	РегОст.УстановитьЗначениеФильтра("Фирма", Константа.БазФирма, 1);
	РегП.УстановитьЗначениеФильтра("Фирма", Константа.БазФирма, 1);
	
	Если списТовар.РазмерСписка() > 0 Тогда
		РегОст.УстановитьЗначениеФильтра("ТМЦ", списТовар, 2);
		РегП.УстановитьЗначениеФильтра("ТМЦ", списТовар, 2);
	КонецЕсли;	
	Если НаДату < ПолучитьДатуТА() Тогда
		РегОст.ВременныйРасчет(1);
		РегП.ВременныйРасчет(1);
		РассчитатьРегистрыПо(НаДату);
	КонецЕсли;
	
	РегОст.ВыбратьИтоги();
	Пока РегОст.ПолучитьИтог() = 1 Цикл
		ТЗ.НоваяСтрока();
		ТЗ.ТМЦ = РегОст.ТМЦ;
		ТЗ.Остаток = РегОст.ОстатокТовара;
	КонецЦикла;

	СписСчетов = СоздатьОбъект("СписокЗначений");
	СтрСв = "";
	
	РегП.ВыбратьИтоги();
	Пока РегП.ПолучитьИтог() = 1 Цикл
		Если (РегП.МестоХранения.Вид() = "МестаХранения") ИЛИ (ПустоеЗначение(РегП.МестоХранения) = 1) Тогда
			ТЗ.НоваяСтрока();
			ТЗ.ТМЦ = РегП.ТМЦ;
			Поз = СписСчетов.НайтиЗначение(РегП.Счет);
		
			Если Поз = 0 Тогда
				СписСчетов.ДобавитьЗначение(РегП.Счет);
				Поз = СписСчетов.РазмерСписка();
				ТЗ.НоваяКолонка("П" + Поз, "Число");
				ТЗ.НоваяКолонка("Б" + Поз, "Число");
				ТЗ.НоваяКолонка("ПС" + Поз, "Число");
				ТЗ.НоваяКолонка("БС" + Поз, "Число");
				
				СтрСв = СтрСв + ",П" + Поз + "," + "Б" + Поз + ",ПС" + Поз + "," + "БС" + Поз;
			КонецЕсли;
			
			ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, "П" + Поз, РегП.ОстатокТовара);
			ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, "ПС" + Поз, РегП.Стоимость);
			ТЗ.ИтогоПартии = ТЗ.ИтогоПартии + РегП.ОстатокТовара;
			ТЗ.ИтогоПартииС = ТЗ.ИтогоПартииС + РегП.Стоимость;
		КонецЕсли;
	КонецЦикла;	
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если списТовар.РазмерСписка() > 0 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ, списТовар, 1, 0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ, , 1, 0);		
	КонецЕсли;

	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,,1, 0);
	Ит.ВключатьСубсчета(-1);
	Ит.Опции(1);
	Ит.ВыполнитьЗапрос(,НаДату,,,,,,);
	Ит.ВыбратьСубконто(1);
	Пока Ит.ПолучитьСубконто(1) = 1 Цикл
		ТЗ.НоваяСтрока();
		ТЗ.ТМЦ = Ит.Субконто(1);
		ТЗ.ИтогоБух = ТЗ.ИтогоБух + Ит.СКД("К");
		ТЗ.ИтогоБухС = ТЗ.ИтогоБухС + Ит.СКД();
		
		Ит.ВыбратьСчета();
		Пока Ит.ПолучитьСчет() = 1 Цикл			
			Поз = СписСчетов.НайтиЗначение(Ит.Счет);
			Если Поз = 0 Тогда
				СписСчетов.ДобавитьЗначение(Ит.Счет);
				Поз = СписСчетов.РазмерСписка();
				ТЗ.НоваяКолонка("П" + Поз, "Число");
				ТЗ.НоваяКолонка("Б" + Поз, "Число");
				ТЗ.НоваяКолонка("ПС" + Поз, "Число");
				ТЗ.НоваяКолонка("БС" + Поз, "Число");
				
				СтрСв = СтрСв + ",П" + Поз + "," + "Б" + Поз + ",ПС" + Поз + "," + "БС" + Поз;
			КонецЕсли;
			
			ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, "Б" + Поз, Ит.СКД("К"));
			ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, "БС" + Поз, Ит.СКД());
		КонецЦикла;
	КонецЦикла;
	
	ТЗ.Свернуть("ТМЦ", "Остаток,ИтогоБух,ИтогоПартии,ИтогоБухС,ИтогоПартииС" + СтрСв);
	
	Таб = СоздатьОбъект("Таблица");
	СтрФ = "";
	ХвостСумм = ?(фСуммы = 1, "С", "");
	глФильтры(СтрФ, списТовар, "Товары");
	Таб.ВывестиСекцию("Шапка|ТМЦ");
	Для Инд = 1 По СписСчетов.РазмерСписка() Цикл
		Таб.ПрисоединитьСекцию("Шапка|Партии" + ХвостСумм);
	КонецЦикла;
	Таб.ПрисоединитьСекцию("Шапка|Хвост" + ХвостСумм);
	
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Разница = ТЗ.Остаток-ТЗ.ИтогоПартии; 
		РазницаС = ТЗ.ИтогоПартииС - ТЗ.ИтогоБухС;
		Если (фТолькоРазница = 1) И 
			(((фСуммы = 0) И (Разница = 0))
			ИЛИ ((фСуммы = 1) И (Разница = 0) И (РазницаС = 0))) Тогда
			Продолжить;
		КонецЕсли;
		Таб.ВывестиСекцию("Строка|ТМЦ");
		Для Инд = 1 По СписСчетов.РазмерСписка() Цикл
			Таб.ПрисоединитьСекцию("Строка|Партии" + ХвостСумм);
		КонецЦикла;
		Таб.ПрисоединитьСекцию("Строка|Хвост" + ХвостСумм);		
	КонецЦикла;
	
	Таб.ПараметрыСтраницы(2);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать();
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ПриОткрытии()
	
КонецПроцедуры // ПриОткрытии()

НаДату = ПолучитьДатуТА();