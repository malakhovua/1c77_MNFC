//*******************************************
Функция ПолучитьЗапрос()
	ТекстЗ =
	"//{{ЗАПРОС(Сформировать)
	|Период с НаДату по НаДату;
	|ТМЦ = Справочник.ТМЦ.ТекущийЭлемент;
	|СхемаРасчетаЗП = Справочник.ТМЦ.СхемаРасчетаЗП;
	|ВидТМЦ = Справочник.ТМЦ.ВидТМЦ;
	|Функция Сч = Счётчик();
	|Условие(ТМЦ в списТовар);
	|Условие(СхемаРасчетаЗП " + ?(фНЕ = 1, "<>" , "=") + " Схема);" +
	?((фНЕ = 1) и (ПустоеЗначение(Схема) = 1),"","
	|Условие((ВидТМЦ = Перечисление.ВидыТМЦ.Продукция) или (ВидТМЦ = Перечисление.ВидыТМЦ.Полуфабрикат));") + "
	|Группировка ТМЦ Упорядочить по ТМЦ.Наименование;
	|"//}}ЗАПРОС
	;

	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат Запрос;
КонецФункции

Процедура Сформировать()
	Запрос = ПолучитьЗапрос();	
	Если Запрос = 0 Тогда
		Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	Если Схема.Выбран() = 1 Тогда
		Заг = "Продукция, в которой схема з/п " + ?(фНЕ = 1, " не", "") + " соответсвтует " + Схема;
	Иначе
		Заг = "Продукция, в которой схема з/п " + ?(фНЕ = 1, " не", "") + " пустая";
	КонецЕсли;
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0, Таб.ВысотаТаблицы());

	Пока Запрос.Группировка(1) = 1 Цикл
		Если Запрос.ЭтоГруппа("ТМЦ") = 1 Тогда
		    Таб.ВывестиСекцию("Группа");
		Иначе
			Таб.ВывестиСекцию("Строка");
		КонецЕсли;		
	КонецЦикла;	
	
	Таб.ПовторятьПриПечатиСтроки(4,4);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ПараметрыСтраницы(1);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчет по схеме");
КонецПроцедуры

Процедура ОбновитьРодителя(ТЗВР, Род, КвоР)
	Если ПустоеЗначение(Род) = 1 Тогда
		Возврат;
	КонецЕсли;
	Стр = 0;
	Если ТЗВР.НайтиЗначение(Род, Стр, "Прод") = 1 Тогда
		ТЗВР.УстановитьЗначение(Стр, "КвоВР", ТЗВР.ПолучитьЗначение(Стр, "КвоВР") + КвоР);
		ОбновитьРодителя(ТЗВР, ТЗВР.ПолучитьЗначение(Стр, "Род"), КвоР);
	КонецЕсли;
КонецПроцедуры

Процедура ПоВидамРабот()
	Запрос = ПолучитьЗапрос();	
	Если Запрос = 0 Тогда
		Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	Если Схема.Выбран() = 1 Тогда
		Заг = "Продукция, в которой схема з/п " + ?(фНЕ = 1, " не", "") + " соответсвтует " + Схема;
	Иначе
		Заг = "Продукция, в которой схема з/п " + ?(фНЕ = 1, " не", "") + " пустая";
	КонецЕсли;
	глФильтры(Заг, списТовар, "Продукция");
	глФильтры(Заг, списВидР, "Виды работ");
	
	ТЗВР = СоздатьОбъект("ТаблицаЗначений");
	СписВР = СоздатьОбъект("СписокЗначений");
	ТЗВР.НоваяКолонка("Прод", "Справочник.ТМЦ");
	ТЗВР.НоваяКолонка("Род", "Справочник.ТМЦ");
	ТЗВР.НоваяКолонка("Схема", "Документ.СхемаРасчетаЗП");
	ТЗВР.НоваяКолонка("КвоВР", "Число");

	Пока Запрос.Группировка(1) = 1 Цикл
		ТЗВР.НоваяСтрока();
		ТЗВР.Прод = Запрос.ТМЦ;
		ТЗВР.Род = ТЗВР.Прод.Родитель;
		// нужно удалить продукцию, по которой нет ни одного ВР, а также её родителей
		
		Если Запрос.ЭтоГруппа("ТМЦ") = 0 Тогда
			ТЗВР.Схема = Запрос.СхемаРасчетаЗП;
			ДокСх = Запрос.СхемаРасчетаЗП;
			Если ПустоеЗначение(ДокСх) = 0 Тогда
			    ДокСх.ВыбратьСтроки();
				Пока ДокСх.ПолучитьСтроку() = 1 Цикл
					Если списВидР.РазмерСписка() > 0 Тогда
						Если списВидР.Принадлежит(ДокСх.ВидРабот) = фНЕВР Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					Поз = СписВР.НайтиЗначение(ДокСх.ВидРабот);
					Если Поз = 0 Тогда
						ИмКол = "К" + Строка(СписВР.РазмерСписка()+1);
					    СписВР.ДобавитьЗначение(ДокСх.ВидРабот, ИмКол);
						ТЗВР.НоваяКолонка(ИмКол, "Число", 13, 5);						
					Иначе
						ИмКол = "";
						СписВР.ПолучитьЗначение(Поз, ИмКол);
					КонецЕсли;
					ТЗВР.УстановитьЗначение(ТЗВр.НомерСтроки, ИмКол, ДокСх.ВидРабот.Тариф.Получить(НаДату));
					ТЗВР.КвоВР=ТЗВР.КвоВР+1;
				КонецЦикла;
			КонецЕсли;
			
		    Если ТЗВР.КвоВР <> 0 Тогда
		        ОбновитьРодителя(ТЗВР, ТЗВР.Род, ТЗВР.КвоВР);
		    КонецЕсли;
		КонецЕсли;		
	КонецЦикла;	
	
	Таб.ИсходнаяТаблица("Кросс");
	СписВР.Сортировать();
	
	Таб.ВывестиСекцию("Шапка|Основная");
	Для Инд = 1 По СписВР.РазмерСписка() Цикл
		ВР = СписВР.ПолучитьЗначение(Инд);
		Если ТипЗначенияСтр(ВР) = "Справочник" Тогда
			ВРСтр = Строка(ВР);
			ВРБаза = Строка(ВР.База);
			ВРСпособ = глСпособ(ВР);
		Иначе
			ВрСтр = ВР;		    
			ВРБаза = "";
			ВРСпособ = "";
		КонецЕсли;
		Таб.ПрисоединитьСекцию("Шапка|ВР");
	КонецЦикла;
	Таб.Опции(0,0, Таб.ВысотаТаблицы(), 2);
	
	ТЗВР.ВыбратьСтроки();
	Пока ТЗВР.ПолучитьСтроку() = 1 Цикл
		Если ТЗВР.КвоВР = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если ТЗВР.Прод.ЭтоГруппа() = 0 Тогда
			Таб.ВывестиСекцию("Строка|Основная");
			Для Инд = 1 По СписВР.РазмерСписка() Цикл
				СписВР.ПолучитьЗначение(Инд, ИмКол);
				Таб.ПрисоединитьСекцию("Строка|ВР");
			КонецЦикла;
		Иначе
			Таб.ВывестиСекцию("Группа|Основная");
		КонецЕсли;
	КонецЦикла;
	
	Таб.ПовторятьПриПечатиСтроки(4,6);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ПараметрыСтраницы(2);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("По видам работ");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, списТовар);
	ИначеЕсли Элт.Вид() = "ВидыРабот" Тогда
		ДобавитьвСписок(Элт, списВидР);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры
