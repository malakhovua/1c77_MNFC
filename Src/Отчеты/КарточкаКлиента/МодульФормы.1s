// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ

Перем ПереченьКодовОпераций;	
Перем ИтПрих, ИтРасх;
                                 
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
// используются для стандартного механизма кнопок "Обновить" и "Настройка"

Перем ВидОтчетаСтр;
Перем Язык, Заг;
Перем Запрос;
Перем СальдоОсн, СальдоОснД, КвоВалют, ТаблВалСальдо;
Перем СпрК;
Перем РазницаПрих, РазницаРасх, ИтСуммаПрих, ИтСуммаРасх;

// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
Функция НазвВалСальдо(Валюта,ВидСальдо)
    Если ВидСальдо = "Н" Тогда
		Возврат СтрЗаменить(Валюта," ","_")+"НачСальдо";    	
    ИначеЕсли ВидСальдо = "НО" Тогда
		Возврат СтрЗаменить(Валюта," ","_")+"НачСальдоОсн";    	
    ИначеЕсли ВидСальдо = "КО" Тогда
		Возврат СтрЗаменить(Валюта," ","_")+"КонСальдоОсн";    	
	Иначе
		Возврат СтрЗаменить(Валюта," ","_")+"КонСальдо";    	
    КонецЕсли;
КонецФункции

// ===============================
Процедура ПечатьШапки(фБланк = 0)
	ЗагОбщий = ?(Язык = "у", "Борг клієнта", "Долг клиента");
	Если фАктСверки = 1 Тогда
		ЗагОбщий = ЗагОбщий + ?(Язык = "у", " за даними ", " по данным ");
		Если фБланк = 0 Тогда
			ЗагОбщий = ЗагОбщий + """" + СокрЛП(ВыбФирма.ПолнНаименование) + """";
		Иначе
			ЗагОбщий = ЗагОбщий + """" + СокрЛП(ВыбКонтрагент.ПолнНаименование) + """";
		КонецЕсли;
	КонецЕсли;
	Если (фАктСверки = 1) И (фДопДанные = 1) И (фБланк = 1) Тогда
		Таб.ПрисоединитьСекцию("Шапка|ДопИнфо");
		Таб.ПрисоединитьСекцию("Шапка|Разделитель");
	КонецЕсли;
	
	ЗагСальдо = ?(Язык = "у","Борг поч." + ?(фБланк = 1, " клієнта", ""),"Нач. долг");
	Таб.ПрисоединитьСекцию("Шапка|НачСальдо");
	Для Инд = 1 По КвоВалют Цикл
		Валюта = ТаблВалСальдо.ПолучитьЗначение(Инд,"Валюта");			
		ЗагСальдо = ?(Язык = "у","Борг поч." + ?(фБланк = 1, " клієнта", ""),"Нач. долг")+Валюта.Кратко;
		Таб.ПрисоединитьСекцию("Шапка|КонСальдо");
	КонецЦикла;	

	Таб.ПрисоединитьСекцию("Шапка|Движения<");
	
	Если фТранспорт = 1 Тогда 	//--- УМК Сандомирский В.Ю, транспортные расходы (10.10.14)
		Таб.ПрисоединитьСекцию("Шапка|Транспорт");
	КонецЕсли;

	ЗагСальдо = ?(Язык = "у","Борг кiн." + ?(фБланк = 1, " клієнта", ""),"Кон. долг");
	Таб.ПрисоединитьСекцию("Шапка|КонСальдо");
	Для Инд = 1 По КвоВалют Цикл
		Валюта = ТаблВалСальдо.ПолучитьЗначение(Инд,"Валюта");			
		ЗагСальдо = ?(Язык = "у","Борг кiн." + ?(фБланк = 1, " клієнта", ""),"Кон. долг")+Валюта.Кратко;
		Таб.ПрисоединитьСекцию("Шапка|КонСальдо");
	КонецЦикла;	
	Если (фАктСверки = 1) И (фБланк = 0) Тогда		
		Таб.ПрисоединитьСекцию("Шапка|Разделитель");
	Иначе
		Таб.ПрисоединитьСекцию("Шапка|Рамка");
	КонецЕсли;

КонецПроцедуры

//======================================================================
Процедура ПолучитьСуммаПоДаннымПродавца(Документ, Приход, Расход, ДопИнфо)
	Если глЕстьРеквизитШапки("ПоДаннымПродавца", Документ.Вид()) = Да Тогда
		Если Найти("РасходнаяНакладная,ВозвратнаяНакладная,ПереоценкаРозницы,", Документ.Вид() + ",") <> 0 Тогда
			Приход = Документ.ПоДаннымПродавца * ?(Документ.Вид() = "ВозвратнаяНакладная", -1, 1);
		Иначе
			Расход = Документ.ПоДаннымПродавца;
		КонецЕсли;
		Если Документ.Вид() = "РасходнаяНакладная" Тогда
			Если ПустоеЗначение(Документ.РодительскийДокумент) = 0 Тогда
				Если Документ.РодительскийДокумент.Вид() = "ПриходнаяНакладнаяЗапасы" Тогда
					ДопИнфо = Документ.РодительскийДокумент.Контрагент.ПолнНаименование;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если Документ.Вид() = "ВозвратнаяНакладная" Тогда
			ДокВН = СоздатьОбъект("Документ");
			ДокВН.УстановитьФильтр(1, 0);
			ДокВН.ВыбратьПодчиненныеДокументы(,,Документ);
			Пока ДокВН.ПолучитьДокумент() = 1 Цикл
				Если ДокВН.Вид() = "ВозвратПоставщику" Тогда
					ДопИнфо = ДокВН.Контрагент.ПолнНаименование;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли глЕстьРеквизитМнЧ("ПоДаннымПродавца", Документ.Вид()) = Да Тогда
		СписК = СоздатьОбъект("СписокЗначений");
		Документ.ВыбратьСтроки();
		Пока Документ.ПолучитьСтроку() = 1 Цикл
			Если Документ.Вид() = "УМК_ПерезачетСписком" Тогда
				К = "";
				Если (ПустоеЗначение(Документ.КонтрагентОткуда) = 0) И ((Документ.КонтрагентОткуда <> ВыбКонтрагент) ИЛИ ((ПустоеЗначение(ВыбДоговор) = 0) И (ВыбДоговор <> Документ.ДоговорКонтрагентаОткуда))) Тогда
					К = Документ.КонтрагентОткуда; 	
					Д = Строка(Документ.ДоговорКонтрагентаОткуда) + ?(ПустоеЗначение(Документ.ДоговорКонтрагентаОткуда) = 0, " / " + Документ.ДоговорКонтрагентаОткуда.Продавец, "");	
				ИначеЕсли (ПустоеЗначение(Документ.КонтрагентКуда) = 0) И ((Документ.КонтрагентКуда <> ВыбКонтрагент) ИЛИ ((ПустоеЗначение(ВыбДоговор) = 0) И (ВыбДоговор <> Документ.ДоговорКонтрагентаКуда))) Тогда
					К = Документ.КонтрагентКуда;
					Д = Строка(Документ.ДоговорКонтрагентаКуда) + ?(ПустоеЗначение(Документ.ДоговорКонтрагентаКуда) = 0, " / " + Документ.ДоговорКонтрагентаКуда.Продавец, "");	
				КонецЕсли;
				Если К <> "" Тогда
					Контр = СокрЛП(К.ПолнНаименование) + " / " + Строка(Д);
					Если СписК.НайтиЗначение(Контр) = 0 Тогда
						СписК.ДобавитьЗначение(Контр);
					КонецЕсли;
				КонецЕсли;
				Если ((Документ.КонтрагентОткуда = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДоговорКонтрагентаОткуда))) Тогда
					Если Документ.ВидКонтрагентаОткуда = Перечисление.ВидыКлиентов.Покупатель Тогда
						Расход = Расход + Документ.ПоДаннымПродавца;						
					Иначе
						Приход = Приход - Документ.ПоДаннымПродавца;
					КонецЕсли;					
				ИначеЕсли ((Документ.КонтрагентКуда = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДоговорКонтрагентаКуда))) Тогда
					Если Документ.ВидКонтрагентаКуда = Перечисление.ВидыКлиентов.Покупатель Тогда
						Расход = Расход - Документ.ПоДаннымПродавца;
					Иначе
						Приход = Приход + Документ.ПоДаннымПродавца;						
					КонецЕсли;										
				КонецЕсли;
			ИначеЕсли (Документ.Вид() = "УМК_КассоваяВедомость") Тогда
				Если (Документ.Контрагент = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДокументДоговор)) Тогда
					Расход = Расход + Документ.ПоДаннымПродавца;
				КонецЕсли;
			ИначеЕсли (Документ.Вид() = "УМК_КассоваяВедомостьРасходная") Тогда
				Если (Документ.Контрагент = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДокументДоговор)) Тогда
					Приход = Приход + Документ.ПоДаннымПродавца;					
				КонецЕсли;				
			КонецЕсли;
		КонецЦикла;
		
		Для Инд = 1 По СписК.РазмерСписка() Цикл
			ДопИнфо = ДопИнфо + ?(ДопИнфо = "", "", ", ") + Строка(СписК.ПолучитьЗначение(Инд));
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // гл

//======================================================================
Процедура ПрисоединитьДопИнфо(ПечП, ПечР, фДвижения = 0)
	ДопИнфо = "";
	
	ПечПрих = "";
	ПечРасх = "";
	ДопИнфо = "";
	ПечП = 0;
	ПечР = 0;
	Если фДвижения = 1 Тогда
		ПечПрих = Запрос.ПрихДолгОсн;
		ПечРасх = Запрос.РасхДолгОсн;
		
		ПолучитьСуммаПоДаннымПродавца(Запрос.Документ, ПечП, ПечР, ДопИнфо);

		Если ПечП <> 0 Тогда
			ПечПрих = ПечПрих - ПечП + ПечР;
		Иначе
			ПечРасх = ПечРасх - ПечР + ПечП;
		КонецЕсли;	
		Если ПечПрих <> 0 Тогда
			РазницаПрих = РазницаПрих + ПечПрих;
		Иначе
			РазницаРасх = РазницаРасх + ПечРасх;
		КонецЕсли;	
	
		ПечРазница = глФРМ(-ПечПрих + ПечРасх, Гривня, 1); 
		ПечПрих = глФРМ(ПечПрих, Гривня, 1);
		ПечРасх = глФРМ(ПечРасх, Гривня, 1);		
	КонецЕсли;
	
	Таб.ПрисоединитьСекцию("Документ|ДопИнфо");
	Таб.ПрисоединитьСекцию("Документ|Разделитель");		
КонецПроцедуры // ПрисоединитьДопИнфо()

// ===============================
Процедура ПечатьВзаиморасчетов(фБланк = 0, фДвижения = 1, Дно = 0)
	Если (фБланк = 1) Тогда
		ПечТекущееСальдоВалНач = ?(фДопДанные = 1, глФРМ(СальдоОснД,Гривня,1), "");
	Иначе
		ПечТекущееСальдоВалНач = глФРМ(СальдоОсн,Гривня,1);
	КонецЕсли;

	ПрихОснД = 0; РасхОснД = 0;	
	Если (фАктСверки = 1) И (фБланк = 1) И (фДопДанные = 1) Тогда		
		ПрисоединитьДопИнфо(ПрихОснД, РасхОснД, фДвижения);
	КонецЕсли;
	
	Таб.ПрисоединитьСекцию("Документ|НачСальдо");
	Для Инд = 1 По КвоВалют Цикл
		ТаблВалСальдо.ПолучитьСтрокуПоНомеру(Инд);
		Валюта = ТаблВалСальдо.Валюта;			
		Если фБланк = 1 Тогда
			ПечТекущееСальдоВалНач = "";
			Таб.ПрисоединитьСекцию("Документ|НачСальдо");
			Продолжить;
		КонецЕсли;

		ПечТекущееСальдоВалНач = глФРМ(ТаблВалСальдо.СальдоОсн,Гривня,1);
		Если Валюта <> Гривня Тогда
			ПечТекущееСальдоВалНач = глФРМ(ТаблВалСальдо.Сальдо,Валюта,1) + РазделительСтрок + ПечТекущееСальдоВалНач;
		КонецЕсли;

		ТекСекция = Таб.ПолучитьСекцию("Документ|НачСальдо");
		ТекОбласть = ТекСекция.Область("R1C1");
		Если (Валюта = Запрос.Валюта) И (КвоВалют>1) Тогда
			ТекОбласть.Полужирный(1);
		КонецЕсли;
		Таб.ПрисоединитьСекцию(ТекСекция);
	КонецЦикла;	                     
	
	Если (фБланк = 1) Или ((фДвижения = 0) И (Дно = 0)) Тогда
		ПечПрих	= "";
		ПечРасх	= "";
		Если фДопДанные = 1 Тогда
//			ПолучитьСуммаПоДаннымПродавца(Запрос.Документ, ПрихОснД, РасхОснД);
			Если ПрихОснД <> 0 Тогда
				ПрихОснД = ПрихОснД - РасхОснД;
				РасхОснД = 0;
			Иначе
				РасхОснД = РасхОснД - ПрихОснД;
				ПрихОснД = 0;
			КонецЕсли;
			
			ИтСуммаПрих = ИтСуммаПрих + ПрихОснД;
			ИтСуммаРасх = ИтСуммаРасх + РасхОснД;
			ПечПрих = глФРМ(ПрихОснД, Гривня, 1);
			ПечРасх = глФРМ(РасхОснД, Гривня, 1);
		КонецЕсли;
	Иначе
		ПрихОсн = Запрос.ПрихДолгОсн;
		РасхОсн = Запрос.РасхДолгОсн;
		Если ВидОтчетаСтр = "ОбщиеВзаиморасчеты" Тогда
			ПрихОсн = ПрихОсн + Запрос.ПрихДолгПостОсн;
			РасхОсн = РасхОсн + Запрос.РасхДолгПостОсн;
		КонецЕсли;
		ПечПрих	= глФРМ(ПрихОсн,Гривня,1);
		ПечРасх	= глФРМ(РасхОсн,Гривня,1);
		ИтПрих = ИтПрих + ПрихОсн;
		ИтРасх = ИтРасх + РасхОсн;		
	
		Если фПоВалютам = 1 Тогда
			Прих = Запрос.ПрихДолг;
			Расх = Запрос.РасхДолг;
			Если ВидОтчетаСтр = "ОбщиеВзаиморасчеты" Тогда
				Прих = Прих + Запрос.ПрихДолгПост;
				Расх = Расх + Запрос.РасхДолгПост;
			КонецЕсли;
			Если Запрос.Валюта <> Гривня Тогда
				ПечПрих = глФРМ(Прих,Запрос.Валюта,1) + РазделительСтрок + ПечПрих;
				ПечРасх = глФРМ(Расх,Запрос.Валюта,1) + РазделительСтрок + ПечРасх;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Таб.ПрисоединитьСекцию("Документ|Движения<");
	
	Если фТранспорт = 1 Тогда		//--- УМК Сандомирский В.Ю, транспортные расходы (09.06.10)
		Попытка
			Если Запрос.Документ.ТранспортныеРасходы > 0 Тогда
				ПечТранспорт = глФРМ(Запрос.Документ.ТранспортныеРасходы,Гривня,1);
			Иначе
				ПечТранспорт = "";
			КонецЕсли;
		Исключение
			ПечТранспорт = "";
		КонецПопытки;
		Таб.ПрисоединитьСекцию("Документ|Транспорт");
	КонецЕсли;						//... УМК Сандомирский В.Ю, транспортные расходы (09.06.10)
			
	Если (фБланк = 1) Или (фДвижения = 0) Тогда
		ПечТекущееСальдоВалКон = "";
		Если (фДопДанные = 1) И (фДвижения = 1) Тогда
			СальдоОснД = СальдоОснД + ПрихОснД - РасхОснД;
			ПечТекущееСальдоВалКон = глФРМ(СальдоОснД, Гривня, 1);
		КонецЕсли;
	Иначе
		СальдоОсн = СальдоОсн + ПрихОсн - РасхОсн;
		ПечТекущееСальдоВалКон = глФРМ(СальдоОсн,Гривня,1);
	КонецЕсли;
	
	Таб.ПрисоединитьСекцию("Документ|КонСальдо");
	Для Инд = 1 По КвоВалют Цикл
		ТаблВалСальдо.ПолучитьСтрокуПоНомеру(Инд);
		Валюта = ТаблВалСальдо.Валюта;			
		Если (фБланк = 1) Или (фДвижения = 0) Тогда
			ПечТекущееСальдоВалКон = "";
			Таб.ПрисоединитьСекцию("Документ|КонСальдо");
			Продолжить;
		КонецЕсли;

		Если Валюта = Запрос.Валюта Тогда
			ТаблВалСальдо.Сальдо 	= ТаблВалСальдо.Сальдо 		+ Прих 		- Расх;
			ТаблВалСальдо.СальдоОсн = ТаблВалСальдо.СальдоОсн	+ ПрихОсн 	- РасхОсн;
		КонецЕсли;

		ПечТекущееСальдоВалКон = глФРМ(ТаблВалСальдо.СальдоОсн,Гривня,1);
		Если Валюта <> Гривня Тогда
			ПечТекущееСальдоВалКон = глФРМ(ТаблВалСальдо.Сальдо,Валюта,1) + РазделительСтрок + ПечТекущееСальдоВалКон;
		КонецЕсли;

		ТекСекция = Таб.ПолучитьСекцию("Документ|КонСальдо");
		ТекОбласть = ТекСекция.Область("R1C1");
		Если (Валюта = Запрос.Валюта) И (КвоВалют>1) Тогда
			ТекОбласть.Полужирный(1);
		КонецЕсли;
		Таб.ПрисоединитьСекцию(ТекСекция);
	КонецЦикла;	                     
	Если (фАктСверки = 1) И (фБланк = 0) Тогда
		Таб.ПрисоединитьСекцию("Документ|Разделитель");
	Иначе
		Таб.ПрисоединитьСекцию("Документ|Рамка");
	КонецЕсли;
		
	// --- Сандомирский В.Ю. (11.12.14) (расшифровка кассовой ведомости если несколько строк по одному контрагенту)
	Если фКассовыеВедомости = 1 Тогда
	
		Если (Запрос.Документ.Вид() = "УМК_КассоваяВедомость") ИЛИ (Запрос.Документ.Вид() = "УМК_КассоваяВедомостьРасходная") Тогда
			ТекВедомость = Запрос.Документ;
			//--- Первый цикл считает количество строк с текущим контрагентом
			КвоВхождений = 0;
			ТекВедомость.ВыбратьСтроки();
			Пока ТекВедомость.ПолучитьСтроку() = 1 Цикл
				Если ТекВедомость.Контрагент = ВыбКонтрагент Тогда		
					КвоВхождений = КвоВхождений + 1;				
				КонецЕсли;		
			КонецЦикла;	
			//--- Второй цикл выводит расшифровку если количество вхождений более 1	
			Если КвоВхождений > 1 Тогда
				ТекВедомость.ВыбратьСтроки();
				Пока ТекВедомость.ПолучитьСтроку() = 1 Цикл
					Если ТекВедомость.Контрагент = ВыбКонтрагент Тогда		
						ТекСтрока = ТекВедомость.НомерСТроки;
						
						Если (Запрос.Документ.Вид() = "УМК_КассоваяВедомость") Тогда
							ПечПрих   = "";
							ПечРасх   = глФРМ(ТекВедомость.Сумма,Гривня,1);
						ИначеЕсли (Запрос.Документ.Вид() = "УМК_КассоваяВедомостьРасходная") Тогда
							ПечПрих   = глФРМ(ТекВедомость.Сумма,Гривня,1);
							ПечРасх   = "";							
						КонецЕсли;
						
						Таб.ВывестиСекцию("СтрокаВедомость|Название");
						Таб.ПрисоединитьСекцию("СтрокаВедомость|НачСальдо");
						Таб.ПрисоединитьСекцию("СтрокаВедомость|Движения");
						Таб.ПрисоединитьСекцию("СтрокаВедомость|КонСальдо");		
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;	
		КонецЕсли;
	
	КонецЕсли;
	
	// ... Сандомирский В.Ю. (11.12.14) (расшифровка кассовой ведомости если несколько строк по одному контрагенту)
	
КонецПроцедуры

// ===============================
Процедура Печать(ЗаписатьДанные = 0)
	Если ЗаписатьДанные = 0 Тогда
		Таб.ВывестиСекцию(?(фАктСверки = 1,"Акт","Отчет") + "|Название");		
		глОживить(Таб.ВысотаСекции(?(фАктСверки = 1,"Акт","Отчет")));
	
		Таб.ВывестиСекцию("Шапка|Название");
		ПечатьШапки();
		Если фАктСверки = 1 Тогда
			ПечатьШапки(1);
		КонецЕсли;
		глОживить(Таб.ВысотаСекции("Шапка"));
		
		ДатаИтогов = Дата1;
		РазницаПрих = 0;
		РазницаРасх = 0;
		ИтСуммаПрих = 0;
		ИтСуммаРасх = 0;
	
		ПечПрих = глФРМ(ИтПрих, Гривня, 1);
		ПечРасх = глФРМ(ИтРасх, Гривня, 1);	
		Таб.ВывестиСекцию("Итоги|Название");
		ПечатьВзаиморасчетов(,0,1);
		Если фАктСверки = 1 Тогда		
			ПечатьВзаиморасчетов(1,0);
		КонецЕсли;
		ВСтрока = Таб.ВысотаТаблицы();
		глОживить(1);
	Иначе
		НачатьТранзакцию();
	КонецЕсли;
	
	Пока Запрос.Группировка("Документ")=1 Цикл
		
		Если ЗаписатьДанные = 0 Тогда
			Док	= Запрос.Документ;
			Если Док.Вид() = "УМК_ДооценкаПартии" Тогда //--- УМК Сандомирский В.Ю. (16.09.14)
				НазваниеДокумента = глДокументВОтчете(Док,,,Язык) + " " + глДокументВОтчете(Док.ПриходнаяНакладная,1,1,) ;
			Иначе
				НазваниеДокумента = глДокументВОтчете(Док,,,Язык);			
			КонецЕсли;
			Если фКомментарий = 1 Тогда
				Если СокрЛП(Док.Примечание) <> "" Тогда
					НазваниеДокумента = НазваниеДокумента + "
					|" + СокрЛП(Док.Примечание);
				КонецЕсли;				
			КонецЕсли;
				
			Таб.ВывестиСекцию("Документ|Название");
			ПечатьВзаиморасчетов();
			Если фАктСверки = 1 Тогда
				ПечатьВзаиморасчетов(1);
			КонецЕсли;
			глОживить(1);
		Иначе
			Документ = СоздатьОбъект("Документ");
			Документ.НайтиДокумент(Запрос.Документ);
			Если глЕстьРеквизитШапки("ПоДаннымПродавца", Документ.Вид()) = Да Тогда
				Если Документ.Вид() = "ПереоценкаРозницы" Тогда
					Документ.ПоДаннымПродавца = Документ.Итог("НоваяСумма") - Документ.Итог("СуммаСНДС");
				ИначеЕсли глЕстьРеквизитМнЧ("СуммаСНДС", Документ.Вид()) = Да Тогда
					Документ.ПоДаннымПродавца = Документ.Итог("СуммаСНДС");
				Иначе
					Документ.ПоДаннымПродавца = Документ.СуммаВал;
				КонецЕсли;				
			ИначеЕсли глЕстьРеквизитМнЧ("ПоДаннымПродавца", Документ.Вид()) = Да Тогда
				Документ.ВыбратьСтроки();
				Пока Документ.ПолучитьСтроку() = 1 Цикл
					Если Документ.Вид() = "УМК_ПерезачетСписком" Тогда
						Если ((Документ.КонтрагентОткуда = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДоговорКонтрагентаОткуда))) Тогда
							Документ.ПоДаннымПродавца = Документ.СуммаВал;
						ИначеЕсли ((Документ.КонтрагентКуда = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДоговорКонтрагентаКуда))) Тогда
							Документ.ПоДаннымПродавца = Документ.СуммаВал;
						КонецЕсли;
					ИначеЕсли (Документ.Вид() = "УМК_КассоваяВедомость") Тогда
						Если (Документ.Контрагент = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДокументДоговор)) Тогда
							Документ.ПоДаннымПродавца = Документ.Сумма;
						КонецЕсли;
					ИначеЕсли (Документ.Вид() = "УМК_КассоваяВедомостьРасходная") Тогда
						Если (Документ.Контрагент = ВыбКонтрагент) И ((ПустоеЗначение(ВыбДоговор) = 1) ИЛИ (ВыбДоговор = Документ.ДокументДоговор)) Тогда
							Документ.ПоДаннымПродавца = Документ.Сумма;
						КонецЕсли;				
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Документ.Записать();
			Сообщить(Документ);
		КонецЕсли;		
	КонецЦикла;
	
	Если ЗаписатьДанные = 0 Тогда
		ДатаИтогов = Дата2;
	
		ПечПрих = "";
		ПечРасх = "";
		Таб.ВывестиСекцию("Итоги|Название");
		ПечатьВзаиморасчетов(,0,0);
		Если фАктСверки = 1 Тогда
			ПечатьВзаиморасчетов(1,0);
		КонецЕсли;
		глОживить(1);
	
		Если фДопДанные = 1 Тогда
			Таб.Область(ВСТрока, 10).Текст = глФРМ(РазницаПрих, Гривня, 1);
			Таб.Область(ВСТрока, 11).Текст = глФРМ(РазницаРасх, Гривня, 1);
			Таб.Область(ВСТрока, 12).Текст = глФРМ(-РазницаПрих + РазницаРасх, Гривня, 1);
			Таб.Область(ВСТрока, 15).Текст = глФРМ(ИтСуммаПрих, Гривня, 1);
			Таб.Область(ВСТрока, 16).Текст = глФРМ(ИтСуммаРасх, Гривня, 1);
		КонецЕсли;
		
		Если фАктСверки=1 Тогда
			Таб.ВывестиСекцию("Подписи|Название");
			глОживить(Таб.ВысотаСекции("Подписи"));
		КонецЕсли;
		Таб.Область(3, 1, 3, Таб.ШиринаТаблицы()).ГоризонтальноеПоложение(1+4);
	Иначе
		Если Вопрос("Подтвердить изменения", "Да+Нет") = "Да" Тогда
			ЗафиксироватьТранзакцию();
		Иначе
			ОтменитьТранзакцию();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
                                         
// ===============================
// Название: Сформировать
// Параметры:
//   ЗакрытьЭкран - флаг того, что после формирования отчета надо закрыть экран	
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
// Наименование,.
// Описание:
//    запускает отчет
//
Процедура Сформировать(ЗакрытьЭкран=0, ЗаписатьДанные = 0)
Перем НомерСтроки;
	
	Если ВыбФирма.Выбран() = 0 Тогда
		Предупреждение("Не выбрана фирма! Отчет не сформирован.");
		Возврат;
	КонецЕсли;
    
    Если ВыбКонтрагент.Выбран()=0 Тогда
		Предупреждение ("Не выбран клиент! Отчет не сформирован.");
		Возврат;
	КонецЕсли;

	глПроверкаДаты(Дата1,Дата2);
	ИтПрих = 0; ИтРасх = 0;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;
	 
	Если ЗаписатьДанные = 1 Тогда
		Если Вопрос("Записать сумма в поле ""По данным продавца""?", "Да+Нет") = "Нет" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПечФорма = "Таблица";
	Язык = глЯзык(ПечФорма); 
	глУстПропись(Гривня,Язык);
    Таб.ИсходнаяТаблица(ПечФорма);
	
    Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", 			"КарточкаКлиента");
	Расшифровка.Установить("Дата1", 			Дата1);
    Расшифровка.Установить("Дата2", 			Дата2);
    Расшифровка.Установить("ВыбФирма", 			ВыбФирма);
    Расшифровка.Установить("ВыбКонтрагент", 	ВыбКонтрагент);
    Расшифровка.Установить("ВидВзаим", 			ВидОтчета.ТекущаяСтрока());
    Расшифровка.Установить("фАктСверки", 		фАктСверки);
    Расшифровка.Установить("фПоВалютам", 		фПоВалютам);
	Расшифровка.Установить("ВыбДоговор", 		ВыбДоговор);
	Расшифровка.Установить("фДопДанные", 		фДопДанные);
	Расшифровка.Установить("фКомментарий", 		фКомментарий);
	Расшифровка.Установить("фПустой", 			фПустой);
	
	Расшифровка.Установить("фТранспорт", 		фТранспорт);			//--- УМК Сандомирский В.Ю, транспортные расходы (09.06.14)	
	
	Расшифровка.Установить("фКассовыеВедомости",фКассовыеВедомости);	//--- УМК Сандомирский В.Ю, транспортные расходы (11.12.14)	

	ВыбКонтрагент.ИспользоватьДату(Дата2);
	ВидОтчетаСтр = ВидОтчета.ПолучитьЗначение(ВидОтчета.ТекущаяСтрока());
	ТаблВалСальдо = СоздатьОбъект("ТаблицаЗначений");
	ТаблВалСальдо.НоваяКолонка("Валюта");
	ТаблВалСальдо.НоваяКолонка("Сальдо");
	ТаблВалСальдо.НоваяКолонка("СальдоОсн");
	ТаблВалСальдо.НоваяКолонка("СальдоД");
	ТаблВалСальдо.НоваяКолонка("СальдоОснД");	

	Заг="";                    
	глЗаголовокФирма(ВыбФирма,Заг,Язык);
	Если ВыбДоговор.Выбран() = 1 Тогда
		Заг = Заг + ?(Заг = "", "", " ") + "По договору №" + СокрЛП(ВыбДоговор.НомерДоговора) + " от " + ВыбДоговор.ДатаНачала + ". Продавец: " + ВыбДоговор.Продавец;
	ИначеЕсли фПустой = 1 Тогда
		Заг = Заг + ?(Заг = "", "", " ") + "По пустому договору";
	КонецЕсли;
	
	Если Дата2 >= ПолучитьДатуТА() Тогда
		ТекстЗапроса = "
			|Период с Дата1;";
	Иначе 
		ТекстЗапроса = "
			|Период с Дата1 по Дата2;";
	КонецЕсли;
	
	Если ВидОтчетаСтр = "ОбщиеВзаиморасчеты" Тогда

		ТекстЗапроса = ТекстЗапроса + "
			|Фирма		= Регистр.ВзаиморасчетыПокупателей.Фирма,			Регистр.ВзаиморасчетыПоставщиков.Фирма;
			|Контрагент	= Регистр.ВзаиморасчетыПокупателей.Контрагент,		Регистр.ВзаиморасчетыПоставщиков.Контрагент;
			|Договор = Регистр.ВзаиморасчетыПокупателей.Договор,		Регистр.ВзаиморасчетыПоставщиков.Договор;
			|ДолгОсн		= Регистр.ВзаиморасчетыПокупателей.ДолгОсн;
			|ДолгПостОсн	= Регистр.ВзаиморасчетыПоставщиков.ДолгОсн;
			|Условие (Фирма 		= ВыбФирма);
			|Условие (Контрагент	= ВыбКонтрагент);
			|Условие (Договор В ВыбДоговор);";
	
		Если фПоВалютам = 1 Тогда
			ТекстЗапросаВал = ТекстЗапроса + "
				|Валюта		= Регистр.ВзаиморасчетыПокупателей.Валюта,	Регистр.ВзаиморасчетыПоставщиков.Валюта;
				|Долг		= Регистр.ВзаиморасчетыПокупателей.Долг;
				|ДолгПост	= Регистр.ВзаиморасчетыПоставщиков.Долг;
				|Функция НачДолг 			= НачОст(Долг);
				|Функция НачДолгПост 		= НачОст(ДолгПост);
				|Функция НачДолгОсн 		= НачОст(ДолгОсн);
				|Функция НачДолгПостОсн 	= НачОст(ДолгПостОсн);
				|Функция ПрихДолг			= Приход(Долг);
				|Функция ПрихДолгОсн		= Приход(ДолгОсн);
				|Функция ПрихДолгПост		= Приход(ДолгПост);
				|Функция ПрихДолгПостОсн	= Приход(ДолгПостОсн);
				|Группировка Валюта;";
			ТекстЗапроса = ТекстЗапроса + "
				|Валюта		= Регистр.ВзаиморасчетыПокупателей.Валюта,	Регистр.ВзаиморасчетыПоставщиков.Валюта;
				|Долг		= Регистр.ВзаиморасчетыПокупателей.Долг;
				|ДолгПост	= Регистр.ВзаиморасчетыПоставщиков.Долг;
				|Функция НачДолг 		= НачОст(Долг);
				|Функция ПрихДолг		= Приход(Долг);
				|Функция РасхДолг 		= Расход(Долг);
				|Функция НачДолгПост 	= НачОст(ДолгПост);
				|Функция ПрихДолгПост	= Приход(ДолгПост);
				|Функция РасхДолгПост 	= Расход(ДолгПост);";
		КонецЕсли;

		ТекстЗапроса = ТекстЗапроса + "
			|Функция НачДолгОсн 		= НачОст(ДолгОсн);
			|Функция ПрихДолгОсн		= Приход(ДолгОсн);
			|Функция РасхДолгОсн 		= Расход(ДолгОсн);
			|Функция НачДолгПостОсн 	= НачОст(ДолгПостОсн);
			|Функция ПрихДолгПостОсн	= Приход(ДолгПостОсн);
			|Функция РасхДолгПостОсн 	= Расход(ДолгПостОсн);
			|Группировка Документ;";

	Иначе // по покупателям или поставщикам

		Если Лев(ВидОтчетаСтр, 10) = "Поставщики" Тогда
			Р = ?(Прав(ВидОтчетаСтр, 1) = "Р", "Р", "");
			ИмяРег="ВзаиморасчетыПоставщиков" + Р;
			Заг = Заг + ?(Язык="у","По постачальнику ","По поставщику ") + ?(Р = "Р", " без б/н ","") + ВыбКонтрагент.Наименование;
		Иначе
			ИмяРег="ВзаиморасчетыПокупателей";
			Заг = Заг + ?(Язык="у","По покупцю ","По покупателю ") + ВыбКонтрагент.Наименование;
		КонецЕсли;

		ТекстЗапроса = ТекстЗапроса + "
			|Фирма		= Регистр."+ИмяРег+".Фирма;
			|Контрагент	= Регистр."+ИмяРег+".Контрагент;
			|Договор 	= Регистр."+ИмяРег+".Договор;
			|ДолгОсн	= Регистр."+ИмяРег+".ДолгОсн;
			|Условие (Фирма 		= ВыбФирма);
			|Условие (Контрагент	= ВыбКонтрагент);
			|Условие (Договор В ВыбДоговор);";

		Если фПоВалютам = 1 Тогда
			ТекстЗапросаВал = ТекстЗапроса + "
				|Валюта		= Регистр."+ИмяРег+".Валюта;
				|Долг		= Регистр."+ИмяРег+".Долг;
				|Функция НачДолг 		= НачОст(Долг);
				|Функция НачДолгОсн 	= НачОст(ДолгОсн);
				|Функция ПрихДолг 			= Приход(Долг);
				|Функция ПрихДолгОсн 		= Приход(ДолгОсн);
				|Группировка Валюта;";
			ТекстЗапроса = ТекстЗапроса + "
				|Валюта		= Регистр."+ИмяРег+".Валюта;
				|Долг		= Регистр."+ИмяРег+".Долг;
				|Функция НачДолг 		= НачОст(Долг);
				|Функция ПрихДолг		= Приход(Долг);
				|Функция РасхДолг 		= Расход(Долг);";
		КонецЕсли;

		ТекстЗапроса = ТекстЗапроса + "
			|Функция НачДолгОсн 	= НачОст(ДолгОсн);
			|Функция ПрихДолгОсн	= Приход(ДолгОсн);
			|Функция РасхДолгОсн 	= Расход(ДолгОсн);
			|Группировка Документ;";

	КонецЕсли;
	Если НазваниеНабораПрав() = "УдаленныйДоступ" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие (Контрагент В (глГруппыДоступаКонтрагенты));";
	КонецЕсли;
		

	Если фПустой = 1 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Условие (Договор В ВыбДоговор);", "Условие (ПустоеЗначение(Договор) = 1);")
	КонецЕсли;
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
		Предупреждение("Запрос по кредитам не выполнился!");
		Возврат;
	КонецЕсли;

	СальдоОсн = Запрос.НачДолгОсн;
	СальдоОснД = Запрос.НачДолгОсн;
	Если ВидОтчетаСтр = "ОбщиеВзаиморасчеты" Тогда
		СальдоОсн = СальдоОсн + Запрос.НачДолгПостОсн;
		СальдоОснД = СальдоОснД + Запрос.НачДолгПостОсн;
	КонецЕсли;

	Если фПоВалютам = 1 Тогда
		ЗапросВал = СоздатьОбъект("Запрос");
		Если ЗапросВал.Выполнить(ТекстЗапросаВал)=0 Тогда
			Предупреждение("Запрос по кредитам в валюте не выполнился!");
			Возврат;
		КонецЕсли;

		Пока ЗапросВал.Группировка("Валюта")=1 Цикл					
			ТаблВалСальдо.НоваяСтрока();					
			ТаблВалСальдо.Валюта 		= ЗапросВал.Валюта;
			ТаблВалСальдо.Сальдо 		= ЗапросВал.НачДолг;
			ТаблВалСальдо.СальдоОсн		= ЗапросВал.НачДолгОсн;
			ТаблВалСальдо.СальдоД 		= ЗапросВал.НачДолг;
			ТаблВалСальдо.СальдоОснД	= ЗапросВал.НачДолгОсн;
			
			Если ВидОтчетаСтр = "ОбщиеВзаиморасчеты" Тогда
				ТаблВалСальдо.Сальдо 	= ТаблВалСальдо.Сальдо 		+ ЗапросВал.НачДолгПост;
				ТаблВалСальдо.СальдоОсн	= ТаблВалСальдо.СальдоОсн	+ ЗапросВал.НачДолгПостОсн;
				ТаблВалСальдо.СальдоД 	= ТаблВалСальдо.Сальдо 		+ ЗапросВал.НачДолгПост;
				ТаблВалСальдо.СальдоОснД= ТаблВалСальдо.СальдоОсн	+ ЗапросВал.НачДолгПостОсн;				
			КонецЕсли;
		КонецЦикла;		
	КонецЕсли;
	КвоВалют = ТаблВалСальдо.КоличествоСтрок();

	//  Создание Таблицы для выходного отчета
	глЧислоСтрок = 0;
	Таб.ВывестиСекцию("Кнопки|Название");
	Печать(ЗаписатьДанные);
	
	ФиксКвоСтрок = Таб.ВысотаСекции("Кнопки")+Таб.ВысотаСекции("Шапка")+Таб.ВысотаСекции(?(фАктСверки = 1,"Акт","Отчет"));
	ФиксКвоКолон = Таб.ШиринаСекции("Название");
	Таб.Опции(0,0,ФиксКвоСтрок,ФиксКвоКолон, "ПарамПечатиУпрКарточкаКлиента", "РазмОкнаУпрКарточкаКлиента");
	Таб.ОбластьПечати(Таб.ВысотаСекции("Кнопки")+1);

	// Вывод заполненной формы
	Таб.ПараметрыСтраницы(1+фАктСверки);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("ПЕЧАТЬ: Карточка клиента "+ВыбКонтрагент.Наименование+" ("+ПериодСтр(Дата1,Дата2)+?(ТипЗначения(ВыбФирма)=0, "", ", "+ВыбФирма)+")","");
	
	
	Если (Обновить = 2) Или (ЗакрытьЭкран=1) Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры		// Сформировать


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

Процедура ИзмАктСверки()
	Если фАктСверки = 0 Тогда
		фДопДанные = 0;
	КонецЕсли;	
	Форма.фДопДанные.Доступность(фАктСверки);
КонецПроцедуры // ИзмАктСверки

// ===============================
Процедура ПриОткрытии() //предопределенная
	Если (НазваниеНабораПрав() = "Администратор") ИЛИ 
		(НазваниеНабораПрав() = "ПроизводствоКасса") Тогда
		Форма.кЗаписатьДанные.Видимость(1);
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		Дата1 = глРасшифровка.Получить("Дата1");
		Дата2 = глРасшифровка.Получить("Дата2");

		ВыбФирма	 	= глРасшифровка.Получить("ВыбФирма");
		ВыбКонтрагент 	= глРасшифровка.Получить("ВыбКонтрагент");
		фАктСверки 		= глРасшифровка.Получить("фАктСверки");
		фПоВалютам		= глРасшифровка.Получить("фПоВалютам");
		ВыбДоговор		= глРасшифровка.Получить("ВыбДоговор");
		ВидОтчета.ТекущаяСтрока(глРасшифровка.Получить("ВидВзаим"));
		фДопДанные 		= глРасшифровка.Получить("фДопДанные");
		фКомментарий 	= глРасшифровка.Получить("фКомментарий");
		фТранспорт 		= глРасшифровка.Получить("фТранспорт");	//--- УМК Сандомирский В.Ю, транспортные расходы (09.06.14)
		фПустой			= глРасшифровка.Получить("фПустой");
		
		фКассовыеВедомости 		= глРасшифровка.Получить("фКассовыеВедомости");	//--- УМК Сандомирский В.Ю, транспортные расходы (11.12.14)
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
		ИзмАктСверки();
	КонецЕсли;	
КонецПроцедуры //ПриОткрытии 

//Процедура ПриНачалеВыбораЗначения(Идент, Флаг)
//	Если Идент = "ВыбКонтрагент" Тогда
//		Если НазваниеНабораПрав() = "Заказ" Тогда
//			Флаг = 0;
//			ОткрытьПодбор("Справочник.Контрагенты", "ДляВыбора", СпрК, 0, ВыбКонтрагент);
//		КонецЕсли;
//	КонецЕсли;
//КонецПроцедуры

Процедура ИзмКонтрагент()
	Если НазваниеНабораПрав() = "Заказ" Тогда
		Если ВыбКонтрагент.ПринадлежитГруппе(СпрК.ТекущийЭлемент()) = 0 Тогда
			Предупреждение("Разрешено выбирать контрагентов только из группы ""Сотрудники""");
			ВыбКонтрагент = "";
			Возврат;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Элемент,Флаг)	// Предопределенная процедура
	Если Элемент="ВыбДоговор" Тогда	// выбираем договор
		
		Флаг = 0;
		Если НазваниеНабораПрав() <> "УдаленныйДоступ" Тогда
			Контр = СоздатьОбъект("Справочник.Контрагенты");
			Контр.НайтиЭлемент(ВыбКонтрагент);			
			Фрм = СоздатьОбъект("СписокЗначений");
			Фрм.ДобавитьЗначение("ДоговораКонтрагентов","ГрафаОтбора");
			Фрм.ДобавитьЗначение(Контр.ТекущийЭлемент(),"Контрагент");
			ОткрытьПодбор("Журнал.ПолныйЖурнал","ДляВыбораДоговоровИСчетов",Фрм,0,ВыбДоговор);
		Иначе
			Если ПустоеЗначение(ВыбКонтрагент) = 0 Тогда
				СписД = СоздатьОбъект("СписокЗначений");
				ДокОт = СоздатьОбъект("Документ");
				ДокОт.ВыбратьПоЗначению(,, "ДоговораКонтрагентов", ВыбКонтрагент);
				Пока ДокОт.ПолучитьДокумент() = 1 Цикл
					Если ДокОт.ПометкаУдаления() = 0 Тогда
						СписД.ДобавитьЗначение(ДокОт.ТекущийДокумент());
					КонецЕсли;
				КонецЦикла;
				СписД.ВыбратьЗначение(ВыбДоговор, ,,,2);
			Иначе
				Предупреждение("Нужно выбрать контрагента");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	           
КонецПроцедуры	// ПриНачалеВыбораЗначения

//======================================================================
Процедура ЗаписатьДанные()
	
КонецПроцедуры // ЗаписатьДанные()

//======================================================================
//Процедура ОбработкаПодбора(Элт, КФорма)
//	Если ТипЗначенияСтр(Элт) = "Справочник" Тогда
//		ВыбКонтрагент = Элт;
//	КонецЕсли;
//КонецПроцедуры

// ===============================
// ТЕЛО МОДУЛЯ

Дата2=ПолучитьДатуТА();
Дата1=глВосстановитьЗначение(,"ОсновнаяДатаНачалаОтчета");
Если Число(Дата1)=0 Тогда
	Дата1='01.01.1998';
Конецесли;         

Форма.ВыбКонтрагент.ВыборГруппы(0);

ВидОтчета.УдалитьВсе();
ВидОтчета.ДобавитьЗначение("ОбщиеВзаиморасчеты", "Общие взаиморасчеты"); 
ВидОтчета.ДобавитьЗначение("Поставщики", "По поставщикам");
ВидОтчета.ДобавитьЗначение("Покупатели", "По покупателям");
ВидОтчета.ДобавитьЗначение("ПоставщикиР", "По поставщикам нал");
ВидОтчета.ТекущаяСтрока(1);
ВыбФирма = Константа.БазФирма;
СпрК = СоздатьОбъект("Справочник.Контрагенты");
спрК.НайтиПоНаименованию("Сотрудники");
