Перем Таб;
Перем Обновить;
Перем Расшифровка;

//======================================================================
Функция ПолучитьТаблицуМФ()
	ТаблицаМФ = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаМФ.НоваяКолонка("Тип");
	ТаблицаМФ.НоваяКолонка("Вид");
	ТаблицаМФ.НоваяКолонка("ИмяПеременной");
	ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
	ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
	ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
	ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
	ТаблицаМФ.НоваяКолонка("ВидДляПустого",,,,"");	
	//                  			тип			вид					переменная  		название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ТМЦ",				"Номенклатура",		"По позициям номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыУпаковки",		"ВидУпаковки",		"По видам упаковки");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ФормыУпаковки",	"ФормаУпаковки",	"По формам упаковки");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"ПланСчетов","Основной",			"Счет",	        	"По счетам учета");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты",		"Поставщик",		"По поставщикам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты",		"Покупатель",		"По покупателям");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Регионы",			"РегионПоставщика",	"По регионам поставщиков");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Регионы",			"РегионПокупателя",	"По регионам покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","КатегорииЦен",		"КатегорияЦен",		"По категориям цен");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",	"Номенклатура",		"По категориям номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",	"Поставщик",		"По категориям поставщиков");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",	"Покупатель",		"По категориям покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Экспедитор",		"ЧерезКого",		"По экспедиторам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Документ"  ,"Договор",			"Договор",			"По договорам", "");	
	
	Возврат ТаблицаМФ;
КонецФункции // 

//======================================================================
Процедура ЗаполнитьРасшифровку()	
	Группировки = СоздатьОбъект("СписокЗначений");
	Группировки.ДобавитьЗначение("Покупатель", 		"Покупатель");
	Группировки.Пометка(1, 1);
	Группировки.ДобавитьЗначение("Номенклатура",   	"Номенклатура");
	Группировки.Пометка(2, 1);
	Группировки.ДобавитьЗначение("ВидУпаковки",   	"Вид упаковки");
	Группировки.ДобавитьЗначение("ФормаУпаковки",   "Форма упаковки");
	Группировки.ДобавитьЗначение("КатегорияЦен",   	"Категория цены");
	Группировки.ДобавитьЗначение("ЧерезКого", 	    "Через кого");
	Группировки.ДобавитьЗначение("Договор", 	    "Договор");
	Группировки.ДобавитьЗначение("Поставщик", 	    "Поставщик");           
	Группировки.ДобавитьЗначение("Счет", 	        "Счет учета");
	Группировки.ДобавитьЗначение("РегионПоставщика","Регион поставщика");
	Группировки.ДобавитьЗначение("РегионПокупателя","Регион покупателя");
	Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");
	Группировки.ДобавитьЗначение("Партия",			"Партия");
	Группировки.ДобавитьЗначение("Документ", 	    "Документ движения");
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "Обороты");
		
	Расшифровка.Установить("ВыбФирма",			Константа.БазФирма);
	Расшифровка.Установить("фТолькоКво",		1);
	Расшифровка.Установить("ргВозвр",			3);
	Расшифровка.Установить("фВозвратОтдельно",	0);
	Расшифровка.Установить("ргВП",				1);
	Расшифровка.Установить("фТолькоФ1",			0);
	Расшифровка.Установить("фБезГрупп",		2);
	Расшифровка.Установить("СписокПериодов", 3);
	Расшифровка.Установить("фЗакупка",	0);
	Расшифровка.Установить("фПродажа",	1);
	Расшифровка.Установить("фДоход",	0);
	Расшифровка.Установить("фСуммаУп",0);
	Расшифровка.Установить("фССУпаковки",0);
	Расшифровка.Установить("фСуммаВЦенах",0);
	Расшифровка.Установить("Группировки",	Группировки);
	Расшифровка.Установить("Обновить", 2);

КонецПроцедуры // ЗаполнитьРасшифровку()

//======================================================================
Функция ПолучитьТЗПолучателей(ДокУС)
	ТЗПолучателей = СоздатьОбъект("ТаблицаЗначений");
	ТЗПолучателей.НоваяКолонка("Контрагент", "Справочник.Контрагенты");
	ТЗПолучателей.НоваяКолонка("ПолучательБонуса", "Справочник");
	ТЗПолучателей.НоваяКолонка("РН", "Строка", 2);
	ТЗПолучателей.НоваяКолонка("Документ", "Документ.ПолучателиСкидки");

	ДокП = СоздатьОбъект("Документ");
	ДокП.УстановитьФильтр(1, 0);
	ДокП.ВыбратьПодчиненныеДокументы(,,ДокУС.ТекущийДокумент());
	Пока ДокП.ПолучитьДокумент() = 1 Цикл
		Если ДокП.Вид() = "ПолучателиСкидки" Тогда
			ДокП.ВыбратьСтроки();
			Пока ДокП.ПолучитьСтроку() = 1 Цикл
				ТЗПолучателей.НоваяСтрока();
				ТЗПолучателей.Документ = ДокП.ТекущийДокумент();
				ТЗПолучателей.Контрагент = ДокП.Контрагент;
				ТЗПолучателей.ПолучательБонуса = ДокП.ПолучательБонуса;
				
				Если (ДокП.ПолучательБонуса.Вид() = "Контрагенты") И (ПустоеЗначение(ДокП.ПолучательБонуса) = 0) Тогда
					Если ДокП.НеЗакрыватьВРН = Да Тогда
						ТЗПолучателей.РН = "ПО";
					Иначе
						ТЗПолучателей.РН = "РН";
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ТЗПолучателей;
КонецФункции // ПолучитьТЗПолучателей(ДокУС)

//*******************************************
Процедура Сформировать()
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;	
	Таб.ИсходнаяТаблица("Таблица");
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "УстановленныеСкидки");
		
	Расшифровка.Установить("НаДату", 		НаДату);
    Расшифровка.Установить("фПолучатели", 	фПолучатели);
	Таб.ВывестиСекцию("Кнопки");
	
	ЗаполнитьРасшифровку();
	
	ДокУС = СоздатьОбъект("Документ.УстановкаСкидокТМЦ");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0, 0, Таб.ВысотаТаблицы());
	ДокУС.УстановитьФильтр(1, 0);
	ДокУС.ВыбратьДокументы(, НаДату);
	Пока ДокУС.ПолучитьДокумент() = 1 Цикл
		Если (ДокУС.ДатаПо > НаДату) ИЛИ (ПустоеЗначение(ДокУС.ДатаПо) = 1) Тогда
			Расшифровка.Установить("Дата1", 		ДокУС.ДатаДок);
		    Расшифровка.Установить("Дата2", 		?(ПустоеЗначение(ДокУС.ДатаПо) = 1, ТекущаяДата(), ДокУС.ДатаПо));
			Расшифровка.Установить("ВыбПокупатель", "");
			Расшифровка.Установить("ВыбНоменклатура", "");
			
			ТаблицаМФ = ПолучитьТаблицуМФ();
			ДокУС.ВыгрузитьТабличнуюЧасть(ТаблицаМФ.ПолучитьЗначение(1, "СписокЭлементов"), "ТМЦ");
			ТаблицаМФ.УстановитьЗначение(1, "ФлВкл", 2);
			ТЗПолучателей = ПолучитьТЗПолучателей(ДокУС.ТекущийДокумент());			
			
			ТЗПолучателей.ВыбратьСтроки();
			Пока ТЗПолучателей.ПолучитьСтроку() = 1 Цикл
				Если ТаблицаМФ.ПолучитьЗначение(6, "СписокЭлементов").НайтиЗначение(ТЗПолучателей.Контрагент) = 0 Тогда
					ТаблицаМФ.ПолучитьЗначение(6, "СписокЭлементов").ДобавитьЗначение(ТЗПолучателей.Контрагент);
				КонецЕсли;
			КонецЦикла;
			
			ТаблицаМФ.УстановитьЗначение(6, "ФлВкл", 2);			
			Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ);
			
			Таб.ВывестиСекцию("Документ");			
			ВыведенПолучатель = 0;
			
			Если фПолучатели = 1 Тогда				
				ТЗПолучателей.ВыбратьСтроки();
				Пока ТЗПолучателей.ПолучитьСтроку() = 1 Цикл
					Расшифровка.Установить("ВыбПокупатель", ТЗПолучателей.Контрагент);
					Таб.ВывестиСекцию("Контрагент");
					ВыведенПолучатель = 1;
				КонецЦикла;
			КонецЕсли;
			
			Если ВыведенПолучатель = 1 Тогда
				Таб.ВывестиСекцию("ДноПолучатели");
			КонецЕсли;
			Расшифровка.Установить("ВыбПокупатель", "");
			ДокУС.ВыбратьСтроки();
			Пока ДокУС.ПолучитьСтроку() = 1 Цикл
				Расшифровка.Установить("ВыбНоменклатура", ДокУС.ТМЦ);
				Таб.ВывестиСекцию("ТМЦ");
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	Таб.ОбластьПечати(2);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Установленные скидки");
	
	Если (Обновить = 2) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;	
КонецПроцедуры  

Процедура НаДату()

КонецПроцедуры

//======================================================================
Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
		ЗаполнитьРасшифровку();
		Док = Форма.Параметр.Получить("Документ");
		Если ПустоеЗначение(Док) = 0 Тогда
			Расшифровка.Установить("ТЗПолучателей", ПолучитьТЗПолучателей(Форма.Параметр.Получить("Документ")));
		КонецЕсли;
		Расшифровка.Установить("ТаблицаМФ", ПолучитьТаблицуМФ());
		
		Форма.Параметр = Расшифровка;
		Форма.Закрыть();
	ИначеЕсли глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		глФлагРасшифровки = 0;
		
		// восстанавливаем настройки из списка
		НаДату = глРасшифровка.Получить("НаДату");
		фПолучатели = глРасшифровка.Получить("фПолучатели");

		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;		
	КонецЕсли;
КонецПроцедуры // ПриОткрытии

Функция ПолучитьСпособПредоставленияСкидкиСтрокой(СпособПредоставления, ЗначениеСкидки, КатегорияЦен)
	Если СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.Процент Тогда
		Возврат Строка(ЗначениеСкидки) +" %";
	ИначеЕсли СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.Сумма Тогда
		Возврат Строка(ЗначениеСкидки) +" грн.";
	ИначеЕсли СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.ФиксЦена Тогда
		Возврат "Фикс. " + Строка(ЗначениеСкидки) +" грн.";
	ИначеЕсли СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.ЦенаПоКатегории Тогда
		Возврат "Кат.: " + Строка(КатегорияЦен);
	КонецЕсли;
	
	Возврат "";
КонецФункции // гл
НаДату = ТекущаяДата();