// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем ТекСтрокаВТаблицеМФ; // текущая строка в таблице значений  МФ 

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;
Перем Обновить;
Перем Расшифровка;
                        
Перем Запрос;
Перем Язык;

Перем ОстКвоПоДок, ОстСумПоДок;

// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
Процедура ВыбратьПоФильтру()
Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

// ===============================
Функция ПерерисовкаНазванийЗакладок()      

	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");

КонецФункции // ПерерисовкаНазванийЗакладок	

// ===============================
Процедура ПечатьСтроки(НазваниеСекции,ПечТекстСтроки,ТекРасшифровка = "")
	
	Наим 			= ПечТекстСтроки;
	Значение 		= ТекРасшифровка;
	Номенклатура 	= Запрос.Номенклатура;

	ПечПрихЗначение = глФРМКоличествоВОтчете(Запрос.ПрихОстаток,Номенклатура);
	ПечРасхЗначение = глФРМКоличествоВОтчете(Запрос.РасхОстаток,Номенклатура);
	Если НазваниеСекции = "Документ" Тогда
		НачКвоПоДок = ОстКвоПоДок;
		ОстКвоПоДок = НачКвоПоДок + Запрос.ПрихОстаток - Запрос.РасхОстаток;
		ПечНачЗначение 	= глФРМКоличествоВОтчете(НачКвоПоДок,Номенклатура); 
		ПечКонЗначение 	= глФРМКоличествоВОтчете(ОстКвоПоДок,Номенклатура);
	Иначе
		ПечНачЗначение 	= глФРМКоличествоВОтчете(Запрос.НачОстаток,	Номенклатура); 
		ПечКонЗначение 	= глФРМКоличествоВОтчете(Запрос.КонОстаток,	Номенклатура);
	КонецЕсли;
	
	ПечПрихЗначение = ПечПрихЗначение + РазделительСтрок + глФРМ(Запрос.ПрихСтоимость,Гривня,1);
	ПечРасхЗначение = ПечРасхЗначение + РазделительСтрок + глФРМ(Запрос.РасхСтоимость,Гривня,1);
	Если НазваниеСекции = "Документ" Тогда
		НачСумПоДок = ОстСумПоДок;
		ОстСумПоДок = НачСумПоДок + Запрос.ПрихСтоимость - Запрос.РасхСтоимость;
		ПечНачЗначение = ПечНачЗначение + РазделительСтрок + глФРМ(НачСумПоДок, Гривня,1); 
		ПечКонЗначение = ПечКонЗначение + РазделительСтрок + глФРМ(ОстСумПоДок, Гривня,1);
	Иначе
		ПечНачЗначение = ПечНачЗначение + РазделительСтрок + глФРМ(Запрос.НачСтоимость, Гривня,1); 
		ПечКонЗначение = ПечКонЗначение + РазделительСтрок + глФРМ(Запрос.КонСтоимость, Гривня,1);
	КонецЕсли;

	Таб.ВывестиСекцию(НазваниеСекции);
	
КонецПроцедуры //ПечатьСтроки()

// ===============================
Процедура ВывестиДокументы()

	Пока Запрос.Группировка("Документ") = 1 Цикл
		Значение = Запрос.Документ;
		Если ПустоеЗначение(Значение) = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ПечТекстСтроки = ""+глДокументВОтчете(Значение,"с номером","с датой",Язык);
		ПечТекстСтроки = ?(ПустоеЗначение(ПечТекстСтроки)=1,глПредставлениеПустогоЗначения("Документ",Язык),ПечТекстСтроки);

		ПечатьСтроки("Документ",ПечТекстСтроки,Значение);
		глОживить(1);
	КонецЦикла;

КонецПроцедуры


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Процедура ДоступностьЭлементов(ВсеЭлементы=0)           
	
	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбФирма") Или (ЭлементДиалога="кХВыбФирма") Тогда
		Форма.кХВыбФирма.Доступность(ВыбФирма.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбМестоХранения") Или (ЭлементДиалога="кХВыбМестоХранения") Тогда
		Форма.кХВыбМестоХранения.Доступность(ВыбМестоХранения.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбНоменклатура") Или (ЭлементДиалога="кХВыбНоменклатура") Тогда
		Форма.кХВыбНоменклатура.Доступность(ВыбНоменклатура.Выбран()); 
	КонецЕсли;

КонецПроцедуры  	// ДоступностьЭлементов

// ===============================
Процедура Сформировать(ЗакрытьЭкран=0)    
	
	глПроверкаДаты(Дата1,Дата2);
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	глПроверкаАктуальностиОтчета(Дата1,Дата2,ВыбФирма,0);
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;

	ПечФорма = "Таблица";
	Язык = глЯзык(ПечФорма); 
	глУстПропись(Гривня,Язык);
	Таб.ИсходнаяТаблица(ПечФорма);
    
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", "КарточкаТМЦ");
    
	// все настройки помещаем в список
	Расшифровка.Установить("Дата1", 			Дата1);
    Расшифровка.Установить("Дата2", 			Дата2); 
	
	Расшифровка.Установить("ВыбФирма", 			ВыбФирма);
	Расшифровка.Установить("ВыбМестоХранения", 	ВыбМестоХранения);
	Расшифровка.Установить("ВыбНоменклатура", 	ВыбНоменклатура);
	// запомним МФ только если он задан
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ);
	КонецЕсли;

	Запрос=СоздатьОбъект("Запрос");

	Если Дата2 >= ПолучитьДатуТА() Тогда
		ТекстЗапроса = "
			|Период с Дата1;";
	Иначе 
		ТекстЗапроса = "
			|Период с Дата1 по Дата2;";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
		"Фирма			= Регистр.Партии.Фирма;
		|МестоХранения	= Регистр.Партии.МестоХранения;
		|Номенклатура	= Регистр.Партии.ТМЦ;
		|Остаток		= Регистр.Партии.ОстатокТовара;
		|Стоимость		= Регистр.Партии.Стоимость;

		|Функция НачОстаток			= НачОст(Остаток);
		|Функция КонОстаток			= КонОст(Остаток);
		|Функция ПрихОстаток		= Приход(Остаток);
		|Функция РасхОстаток		= Расход(Остаток);

		|Функция НачСтоимость		= НачОст(Стоимость);
		|Функция КонСтоимость		= КонОст(Стоимость);
		|Функция ПрихСтоимость		= Приход(Стоимость);
		|Функция РасхСтоимость		= Расход(Стоимость);

		|Группировка Номенклатура;
		|Группировка Документ;";
   	
	Загол="";
               
	Если глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Фирма", ВыбФирма, "ВыбФирма", ТекстЗапроса, Загол, Язык)=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по фирмам. Отчет не сформирован.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"МестоХранения", ВыбМестоХранения, "ВыбМестоХранения", ТекстЗапроса, Загол, Язык)=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по местам хранения. Отчет не сформирован.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Номенклатура", ВыбНоменклатура, "ВыбНоменклатура", ТекстЗапроса, "", Язык, "КатегорииТоваров")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по номенклатурным позициям. Отчет не сформирован.");
		Возврат;
	КонецЕсли;

	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
		Предупреждение("Запрос по партиям не выполнился!!!");
		Возврат;
	КонецЕсли;

	// обнуляем счетчик числа строк, выведенных в таблицу отчета
	глЧислоСтрок=0;           
	Таб.ВывестиСекцию("Кнопки");

	Пока Запрос.Группировка("Номенклатура") = 1 Цикл
		Номенклатура = Запрос.Номенклатура;
		Если Запрос.ЭтоГруппа("Номенклатура")=1 Тогда
			Продолжить;
		КонецЕсли;

		НазвНоменклатуры = """" + СокрЛП(Номенклатура.Наименование) + """";
		Если ПустаяСтрока(Номенклатура.Артикул) = 0 Тогда
			НазвНоменклатуры = НазвНоменклатуры + РазделительСтрок + ?(Язык="у","Артикул ","Артикул ") + СокрЛП(Номенклатура.Артикул);
		КонецЕсли;
		Если ПустоеЗначение(Номенклатура.ЕдиницаПоУмолчанию) = 0 Тогда
			НазвНоменклатуры = НазвНоменклатуры + РазделительСтрок + ?(Язык="у","Облік. в ","Учит. в ") + СокрЛП(Номенклатура.ЕдиницаПоУмолчанию.Наименование);
		КонецЕсли;
		Таб.ВывестиСекцию("Заголовок");
		
		ПечЗаголовокСтолбца = "Документ";
		Таб.ВывестиСекцию("Шапка");
		глОживить(Таб.ВысотаСекции("Заголовок")+Таб.ВысотаСекции("Шапка"));
        
		ОстКвоПоДок = Запрос.НачОстаток;
		ОстСумПоДок = Запрос.НачСтоимость;
		ВывестиДокументы();
		ПечатьСтроки("Итого",?(Язык="у","Разом","Итого"));
	КонецЦикла;

	//Вызов выходного отчета в окно просмотра и редактирования.
	ФиксКвоСтрок = Таб.ВысотаСекции("Кнопки");
	ФиксКвоКолон = 8;
	Таб.Опции(0, 0, ФиксКвоСтрок, ФиксКвоКолон, "ПарамПечатиУпрКарточкаТМЦ", "РазмОкнаУпрКарточкаТМЦ");
	Таб.ОбластьПечати(Таб.ВысотаСекции("Кнопки")+1);
	
	// Вывод заполненной формы
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);

	Таб.Показать("ПЕЧАТЬ: Карточка номенклатуры: ("+ПериодСтр(Дата1, Дата2)+?(ВыбФирма.Выбран()=0, "", ", "+ВыбФирма)+")","");

	Если (Обновить = 2) Или (ЗакрытьЭкран=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры     // Сформировать                   
                                         

// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение="")	
	// закладки
    Если Номер=1 Тогда
		Форма.ИспользоватьСлой("Шапка,Основной,Подвал");
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;      
	ПерерисовкаНазванийЗакладок();
	ДоступностьЭлементов(1);
КонецПроцедуры	// ПриВыбореЗакладки

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		Дата1 		= глРасшифровка.Получить("Дата1");
		Дата2		= глРасшифровка.Получить("Дата2");

		ВыбФирма		= глРасшифровка.Получить("ВыбФирма");
		ВыбМестоХранения= глРасшифровка.Получить("ВыбМестоХранения");
		ВыбНоменклатура	= глРасшифровка.Получить("ВыбНоменклатура");
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
	                                                                           
	Доступность = ?(глПартионныйУчетПоСкладам = Да,1,0);
	Форма.тМестоХранения. 	 Доступность(Доступность);
	Форма.ВыбМестоХранения.  Доступность(Доступность);
	                  
	ТаблицаМФ.ВидимостьКолонки("Тип",				0);
	ТаблицаМФ.ВидимостьКолонки("Вид",				0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",	0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",				0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",		0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	                                                                                
	ПриВыбореЗакладки(1);

КонецПроцедуры		// ПриОткрытии

// ===============================
Процедура ВводНового()
	// эта предопределенная процедура выполняется при восстановлении настройки
	ТекСтрокаВТаблицеМФ="";
	ПриВыбореЗакладки(1);
КонецПроцедуры // ВводНового()   

// ===============================
Процедура ОбработкаПодбора(Значение)  

	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

Дата2=ПолучитьДатуТА();
Дата1=глВосстановитьЗначение(,"ОсновнаяДатаНачалаОтчета");
Если ПустоеЗначение(Дата1)=0 Тогда
	Дата1=НачМесяца(Дата1);
КонецЕсли;         

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");

//                  			тип			вид						переменная  			название
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","МестаХранения",		"МестоХранения",   		"По местам хранения");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ТМЦ",					"Номенклатура",    		"По позициям номенклатуры");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",		"Номенклатура",			"По категориям номенклатуры");

ТекСтрокаВТаблицеМФ="";
