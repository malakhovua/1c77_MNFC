Перем Таб;
Перем Обновить, списВЗ, ИмСпис;
Перем Расшифровка;
Перем СтрФ, ИтПриход, ИтЗаказ, ИтПриходПрч, ИтПриходПН, ТЗ, ПредТМЦ, ИтРасходПр, ИтРасходНО, ИтПриходОИ;
Перем СсылкаИнвентаризация;

Процедура СоздатьРасшифровку()
	// все настройки помещаем в список
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", "ЗаказыПоставщиковС");
	Расшифровка.Установить("Дата1", НачДата);
	Расшифровка.Установить("Дата2", КонДата);
	Расшифровка.Установить("списТовар", списТовар);
	Расшифровка.Установить("списТовар1", списТовар1);
	Расшифровка.Установить("списПост", списПост);
	Расшифровка.Установить("списПостНЕ", списПостНЕ);	
	Расшифровка.Установить("фКонОст", фКонОст);
	Расшифровка.Установить("фПриходП", фПриходП);
	Расшифровка.Установить("фРасходП", фРасходП);
	Расшифровка.Установить("фЗаказ", фЗаказ);
	Расшифровка.Установить("фПустоеКво", фПустоеКво);
	Расшифровка.Установить("фБлок", фБлок);
	Расшифровка.Установить("фКонтроль", фКонтроль);
	Расшифровка.Установить("ргПорядок", ргПорядок);    
	Расшифровка.Установить("фПодробныйПриход", фПодробныйПриход);
	Расшифровка.Установить("фПодробныйРасход", фПодробныйРасход);	
	Расшифровка.Установить("фБезУ3", фБезУ3);
	Расшифровка.Установить("ВыбСклад", ВыбСклад);	
КонецПроцедуры

Процедура ВывестиСекции(ИмСекц)
	Таб.ВывестиСекцию(ИмСекц + "|Основная");
	
	Если фПустоеКво = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|ПустоеКво");
	КонецЕсли;
	Если фБлок = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|Движения");
	КонецЕсли;	
	Если фКонОст = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|КонОст");
	КонецЕсли;

	Если фРасходП = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|РасходП");
		Если фПодробныйРасход = 1 Тогда
		    Таб.ПрисоединитьСекцию(ИмСекц + "|ПодрРасход");
			Для Инд = 1 По списВЗ.РазмерСписка() Цикл
				Кол = "";
				списВЗ.ПолучитьЗначение(Инд, Кол);
				Таб.ПрисоединитьСекцию(ИмСекц + "|ВЗ");
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если фПриходП = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|ПриходП");
		Если фПодробныйПриход = 1 Тогда
		    Таб.ПрисоединитьСекцию(ИмСекц + "|ПодрПриход");
		КонецЕсли;
	КонецЕсли;
	Если фЗаказ = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|Заказ");
	КонецЕсли;
	Если фКонтроль = 1 Тогда
		Таб.ПрисоединитьСекцию(ИмСекц + "|Контроль");
	КонецЕсли;
КонецПроцедуры

//*******************************************
Процедура Сформировать()
	СоздатьРасшифровку();
	Если (ТипЗначенияСтр(Таб) = "Таблица") и (Обновить <> 0) Тогда
	    Таб.Очистить();
	Иначе
		Таб = СоздатьОбъект("Таблица");
	КонецЕсли;
	Таб.ИсходнаяТаблица("Таблица");

	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("Ключ", "Строка");
	ТЗ.НоваяКолонка("Поставщик", "Справочник.Контрагенты");
	ТЗ.НоваяКолонка("КонОст", "Число" ,15, 3);
	ТЗ.НоваяКолонка("Расход", "Число" ,15, 3);
	ТЗ.НоваяКолонка("ОбнулятьОстаток", "Число" ,1);
	Если фПодробныйРасход = 1 Тогда
		ТЗ.НоваяКолонка("РасходПр", "Число" ,15, 3);
		ТЗ.НоваяКолонка("РасходНО", "Число" ,15, 3);
	КонецЕсли;
	ТЗ.НоваяКолонка("Приход", "Число" ,15, 3);
	Если фПодробныйПриход = 1 Тогда
	    ТЗ.НоваяКолонка("ПриходПН", "Число" ,15, 3);
		ТЗ.НоваяКолонка("ПриходОИ", "Число" ,15, 3);
		ТЗ.НоваяКолонка("ПриходПрч", "Число" ,15, 3);
	КонецЕсли;
	ТЗ.НоваяКолонка("Заказ", "Число" ,15, 3);
	списВЗ = СоздатьОбъект("СписокЗначений");
	
	Если фКонОст+фПриходП+фРасходП+фЗаказ = 0 Тогда
		Предупреждение("Нужно выбрать хоть что-нибудь");
		Возврат;
	КонецЕсли;
	
	КДата = Мин(КонДата, ПолучитьДатуТА());
	НДата = Мин(НачДата, КонДата);
	СтрФ = "";
	СтрСв = "";
	глФильтры(СтрФ, списТовар, "ТМЦ:");
	глФильтры(СтрФ, списТовар1, "ТМЦ:", 1);
	
	Если фКонОст+фПриходП+фРасходП <> 0 Тогда
		ТекстЗ = 
		"//{{ЗАПРОС(Сформировать)
		|Период с НДата по КДата;
		|ТМЦ = Регистр.Остатки.ТМЦ;
		|МестоХранения = Регистр.Остатки.МестоХранения;
		|Ост = Регистр.Остатки.ОстатокТовара;
		|ФлагУчета = Регистр.Остатки.ФлагУчета;
		|ФлагСписания = Регистр.Остатки.ФлагСписания;
		|Пост = Регистр.Остатки.ТекущийДокумент.ПриходнаяНакладнаяЗапасы.Контрагент;"+
		?(фКонОст = 1, "
		|Функция ОстКонОст = КонОст(Ост) когда (МестоХранения В ВыбСклад);", "") + 
		?(фПриходП = 1, "
		|Функция ОстПриход = Приход(Ост) когда ((ФлагУчета  = 2) И (МестоХранения В ВыбСклад));", "") +
		?(фРасходП = 1, "
		|Функция ОстРасход = Расход(Ост) когда ((ФлагУчета  = 2) И (МестоХранения В ВыбСклад));", "") + "
		|Группировка ТМЦ Упорядочить по ТМЦ.Наименование без групп;
		//|Группировка ТМЦ без упорядочивания без групп;
		|Группировка Пост;"
		+?(фПодробныйРасход + фПодробныйПриход <> 0,"
		|Группировка Документ;","") + "
		|Условие (ТМЦ в списТовар);
		|Условие (МестоХранения в ВыбСклад);" + 
		?(списТовар1.РазмерСписка() > 0, "
		|Условие (НЕ (ТМЦ в списТовар1));", "") + "
		|"//}}ЗАПРОС
		;
		
		Запрос = СоздатьОбъект("Запрос");
		Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Док = СоздатьОбъект("Документ");
		ТЗТМЦ = СоздатьОбъект("ТаблицаЗначений");
		ТЗТМЦ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
		ТЗТМЦ.НоваяКолонка("КонОст", "Число");
		ТЗТМЦ.НоваяКолонка("Расход", "Число");
		ТЗТМЦ.НоваяКолонка("РасходПр", "Число");
		ТЗТМЦ.НоваяКолонка("РасходНО", "Число");
		ТЗТМЦ.НоваяКолонка("ТЗВЗ", "ТаблицаЗначений");
		
		Пока Запрос.Группировка(1) = 1 Цикл
			Если ПустоеЗначение(ВыбСклад) = 0 Тогда
				Если (Запрос.МестоХранения <> ВыбСклад) ИЛИ ((ВыбСклад.ЭтоГруппа() = 1) И (Запрос.МестоХранения.ПринадлежитГруппе(ВыбСклад) = 0)) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ТЗТМЦ.НоваяСтрока();
			ТЗТМЦ.ТМЦ = Запрос.ТМЦ;
			ТЗТМЦ.ТЗВЗ = СоздатьОбъект("ТаблицаЗначений");
			ТЗТМЦ.ТЗВЗ.НоваяКолонка("ВидЗатрат", "Справочник.ВидыЗатрат");
			ТЗТМЦ.ТЗВЗ.НоваяКолонка("Сумма", "Число", 12, 2);			
			
			Если фРасходП = 1 Тогда
				ТЗТМЦ.Расход = Запрос.ОстРасход;
			КонецЕсли;
			Если фКонОст = 1 Тогда
				ТЗТМЦ.КонОст = Запрос.ОстКонОст;
			КонецЕсли;

			Если фПодробныйРасход = 1 Тогда
				Пока Запрос.Группировка(2) = 1 Цикл
					Если (ПустоеЗначение(Запрос.Пост) = 1) Тогда
						Пока Запрос.Группировка(3) = 1 Цикл
							Если (Запрос.Документ.Вид() = "СписаниеТМЦВПроизводство") Тогда
								Если (фБезУ3 = 1) И (Запрос.ФлагСписания = 3) Тогда
									ТЗТМЦ.РасходНО = ТЗТМЦ.РасходНО + Запрос.ОстРасход;
								Иначе
									ТЗТМЦ.РасходПр = ТЗТМЦ.РасходПр + Запрос.ОстРасход;
								КонецЕсли;								
							ИначеЕсли Запрос.Документ.Вид() = "СписаниеТМЦ" Тогда
								Если Запрос.ФлагСписания = 1 Тогда
									ТЗТМЦ.РасходПр = ТЗТМЦ.РасходПр + Запрос.ОстРасход;
								Иначе
									ТЗТМЦ.ТЗВЗ.НоваяСтрока();
									ТЗТМЦ.ТЗВЗ.ВидЗатрат = Запрос.Документ.СубконтоСписания3;
									ТЗТМЦ.ТЗВЗ.Сумма = Запрос.ОстРасход;
									Если списВЗ.НайтиЗначение(ТЗТМЦ.ТЗВЗ.ВидЗатрат) = 0 Тогда							    
										Кол = "ВЗ" + списВЗ.РазмерСписка() + 1;
										ТЗ.НоваяКолонка(Кол);									
										списВЗ.ДобавитьЗначение(ТЗТМЦ.ТЗВЗ.ВидЗатрат, Кол);
										СтрСв = СтрСв + ","+Кол;
									КонецЕсли;
								КонецЕсли;
							ИначеЕсли фРасходП = 1 Тогда 							    
								Если Запрос.ФлагСписания = 1 Тогда
									ТЗТМЦ.РасходПр = ТЗТМЦ.РасходПр + Запрос.ОстРасход;
								Иначе
									ТЗТМЦ.РасходНО = ТЗТМЦ.РасходНО + Запрос.ОстРасход;
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;				
				ТЗТМЦ.ТЗВЗ.Свернуть("ВидЗатрат", "Сумма");
			КонецЕсли;			
			
			ПоследТМЦ = "";
			Если фПриходП = 1 Тогда
				Пока Запрос.Группировка(2) = 1 Цикл
					Если (ПустоеЗначение(Запрос.Пост) = 0) или (фПодробныйПриход = 1) Тогда
						ТЗ.НоваяСтрока();
						ТЗ.ТМЦ = Запрос.ТМЦ;
						ТЗ.Поставщик = Запрос.Пост;
						ТЗ.Приход = Запрос.ОстПриход;
						ПоследТМЦ = Запрос.ТМЦ;
						ПервыйПроход = 1;
						Если фПодробныйПриход + фПодробныйРасход <> 0 Тогда
							Пока Запрос.Группировка(3) = 1 Цикл
								Если Запрос.Документ.Вид() = "ПриходнаяНакладнаяЗапасы" Тогда
								    ТЗ.ПриходПН = ТЗ.ПриходПН + Запрос.ОстПриход;
								ИначеЕсли Запрос.Документ.Вид() = "ОприходованиеИзлишков" Тогда
								    ТЗ.ПриходОИ = ТЗ.ПриходОИ + Запрос.ОстПриход;
								Иначе
									ТЗ.ПриходПрч = ТЗ.ПриходПрч + Запрос.ОстПриход;
								КонецЕсли;
						    КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;				
			КонецЕсли;
			
			Если ПоследТМЦ = "" Тогда
				ТЗ.НоваяСтрока();
				ТЗ.ТМЦ = Запрос.ТМЦ;
			КонецЕсли;
		КонецЦикла;
		
		Если (ВыбСклад.Выбран() = 1) И (фПриходП <> 0) Тогда
			// выполняем доп. запрос по поставщикам без учёта склада
			ТекстЗ = 
			"//{{ЗАПРОС(Сформировать)
			|Период с НДата по КДата;
			|ТМЦ = Регистр.Остатки.ТМЦ;
			|Ост = Регистр.Остатки.ОстатокТовара;
			|ФлагУчета = Регистр.Остатки.ФлагУчета;
			|ФлагСписания = Регистр.Остатки.ФлагСписания;
			|Пост = Регистр.Остатки.ТекущийДокумент.ПриходнаяНакладнаяЗапасы.Контрагент;
			|Функция ОстПриход = Приход(Ост) когда (ФлагУчета  = 2);
			|Группировка ТМЦ Упорядочить по ТМЦ.Наименование без групп;
			|Группировка Пост;
			|Условие (ТМЦ в списТовар);" + 
			?(списТовар1.РазмерСписка() > 0, "
			|Условие (НЕ (ТМЦ в списТовар1));", "") + "
			|"//}}ЗАПРОС
			;
			
			ЗапросПост = СоздатьОбъект("Запрос");
			Если ЗапросПост.Выполнить(ТекстЗ) = 0 Тогда
				Возврат;
			КонецЕсли;
			
			ТЗ.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСтроку() = 1 Цикл
				ЗапросПост.ВНачалоВыборки();
				Если ЗапросПост.Получить(ТЗ.ТМЦ,) = 1 Тогда
					Пока ЗапросПост.Группировка(2) = 1 Цикл
						Если ПустоеЗначение(ЗапросПост.Пост) = 0 Тогда
							ТЗ.Поставщик = ЗапросПост.Пост;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// теперь добавим поставщиков там, где они пустые
		ПредТМЦ = ""; ПредПост = ""; ПредОст = 0;
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			Если ПустоеЗначение(ТЗ.Поставщик) = 1 Тогда
				Если ТЗ.ТМЦ = ПредТМЦ Тогда
					ТЗ.Поставщик = ПредПост;
					ПредОст = ТЗ.КонОст;
					Продолжить;
				КонецЕсли;
				
				Док.ОбратныйПорядок(1);
				Док.УстановитьФильтр(1,0);
				Док.ВыбратьПоЗначению(,НачДата-1,"ТМЦ", ТЗ.ТМЦ);
				Пока Док.ПолучитьДокумент() = 1 Цикл
					ТЗ.Поставщик = Док.Контрагент;
					ПредПост = ТЗ.Поставщик;
					Прервать;
				КонецЦикла;
				ПредТМЦ = ТЗ.ТМЦ;
			КонецЕсли;			
		КонецЦикла;
	КонецЕсли;
	
	Если фЗаказ = 1 Тогда
		ТекстЗ = 
		"//{{ЗАПРОС(Заказ)
		|Период с НачДата по КонДата;
		|Поставщик = Документ.ЗаказПоставщику.Поставщик;
		|ТМЦ = Документ.ЗаказПоставщику.ТМЦ;
		|Кво = Документ.ЗаказПоставщику.Кво;
		|Коэффициент = Документ.ЗаказПоставщику.Коэффициент;
		|Функция КвоСумма = Сумма(Кво);
		|Группировка ТМЦ без упорядочивания;
		|Группировка Поставщик без упорядочивания без групп;
		|Условие (ТМЦ в списТовар);" + 
		?(списТовар1.РазмерСписка() > 0, "
		|Условие (НЕ (ТМЦ в списТовар1));", "") + "
		|"//}}ЗАПРОС
		;
		
		Запрос = СоздатьОбъект("Запрос");
		Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Пока Запрос.Группировка(1) = 1 Цикл
			Пока Запрос.Группировка(2) = 1 Цикл
				ТЗ.НоваяСтрока();
				ТЗ.ТМЦ = Запрос.ТМЦ;
				ТЗ.Поставщик = Запрос.Поставщик;
				ТЗ.Заказ = Запрос.КвоСумма;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ТЗ.Свернуть("Поставщик,ТМЦ", "Заказ,Приход,Расход,КонОст,ПриходПН,ПриходОИ,ПриходПрч,РасходПр,РасходНО"+СтрСв);
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Стр = 0;
		Если ТЗТМЦ.НайтиЗначение(ТЗ.ТМЦ, Стр, "ТМЦ") = 1 Тогда
			ТЗТМЦ.ПолучитьСтрокуПоНомеру(Стр);
			ТЗ.КонОст = ТЗТМЦ.КонОст;
			ТЗ.Расход = ТЗТМЦ.Расход;
			Если фПодробныйРасход = 1 Тогда
				ТЗ.РасходНО = ТЗТМЦ.РасходНО;
				ТЗ.РасходПр = ТЗТМЦ.РасходПр;
				ТЗТМЦ.ТЗВЗ.ВыбратьСтроки();
				Пока ТЗТМЦ.ТЗВЗ.ПолучитьСтроку() = 1 Цикл
					Кол = "";
					Поз = списВЗ.НайтиЗначение(ТЗТМЦ.ТЗВЗ.ВидЗатрат);
					списВЗ.ПолучитьЗначение(Поз, Кол);
					ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, Кол, ТЗТМЦ.ТЗВЗ.Сумма);
				КонецЦикла;
			КонецЕсли;				

		КонецЕсли;
	КонецЦикла;
	ТЗ.Сортировать(?(ргПорядок = 1,"ТМЦ,Поставщик","Поставщик,ТМЦ"));
	СписФормирование = СоздатьОбъект("СписокЗначений");
	СписФормирование.Установить("Формирование Инвентаризация", 1);
	СписФормирование.Установить("СсылкаИнвентаризация", СсылкаИнвентаризация);
	Таб.ВывестиСекцию("Заголовок|Основная");

	// все настройки помещаем в список
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", "ОстаткиТМЦ");
    
	Расшифровка.Установить("Дата1", НачДата);
    Расшифровка.Установить("Дата2", КонДата);
	
	Расшифровка.Установить("ВыбФирма", Константа.БазФирма);
	
	Расшифровка.Установить("ВидОтчета", 		1);
	Расшифровка.Установить("ПоказатьОстатки", 	1);
	Расшифровка.Установить("ЦенаТовара", 		1); 
	Расшифровка.Установить("ЦенаИзСправочника", "");

	Расшифровка.Установить("ДеталПоСкладам", 	1); 
	Расшифровка.Установить("ДеталПоДокументам", 1);
	Расшифровка.Установить("фБезГрупп", 1);
	Расшифровка.Установить("фДопКво", 1);	
	
	ПредТМЦ = ""; 
	ИтПриход = 0; ИтЗаказ = 0; КвоТМЦ = 0; ИтПриходПрч = 0; ИтПриходПН = 0; ИтПриходОИ = 0; ИтРасходПр = 0; ИтРасходНО = 0;
	списВЗ.Сортировать();	
    ВывестиСекции("Шапка");
	Таб.Опции(0,0, Таб.ВысотаТаблицы());

	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ТЗ.Поставщик) = 1 Тогда
			ТЗ.Поставщик = ТЗ.ТМЦ.ПоследнийПоставщик.Получить(КонДата);
		КонецЕсли;
	КонецЦикла;	
	
	Если (списПост.РазмерСписка() > 0) ИЛИ (списПостНЕ.РазмерСписка() > 0) Тогда
		Инд = 1;
		Пока Инд <= ТЗ.КоличествоСтрок() Цикл
			ТЗ.ПолучитьСтрокуПоНомеру(Инд);
			Если ((списПост.РазмерСписка() > 0) И (списПост.Принадлежит(ТЗ.Поставщик) = 0)) ИЛИ 
				((списПостНЕ.РазмерСписка() > 0) И (списПостНЕ.Принадлежит(ТЗ.Поставщик) = 1)) Тогда
				ТЗ.УдалитьСтроку(Инд);
			Иначе
				Инд = Инд + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Если (ПредТМЦ <> ТЗ.ТМЦ) Тогда
			Если (ПредТМЦ <> "") и (ргПорядок = 1) и (КвоТМЦ > 1) Тогда
				ВывестиСекции("ИтогоТМЦ");
			КонецЕсли;
			ИтПриход = 0; ИтЗаказ = 0; КвоТМЦ = 0;
			ИтПриходПрч = 0; ИтПриходПН = 0; ИтПриходОИ = 0; ИтРасходПр = 0; ИтРасходНО = 0;
		КонецЕсли;

		Расшифровка.Установить("ВыбНоменклатура", ТЗ.ТМЦ);
		ВывестиСекции("Строка");		
		ПредТМЦ = ТЗ.ТМЦ;
		ИтПриход = ИтПриход + ТЗ.Приход;
		ИтЗаказ = ИтЗаказ + ТЗ.Заказ;
		Если фПодробныйПриход = 1 Тогда
			ИтПриходПН = ИтПриходПН + ТЗ.ПриходПН;
			ИтПриходОИ = ИтПриходОИ + ТЗ.ПриходОИ;
			ИтПриходПрч = ИтПриходПрч + ТЗ.ПриходПрч;
		КонецЕсли;
		Если фПодробныйРасход = 1 Тогда
			ИтРасходПр = ИтРасходПр + ТЗ.РасходПр;
			ИтРасходНО = ИтРасходНО + ТЗ.РасходНО;		    
		КонецЕсли;
		КвоТМЦ=КвоТМЦ+1;
	КонецЦикла;
	
	Таб.ПовторятьПриПечатиСтроки(4, 4);
	Таб.ОбластьПечати(2);
	Таб.ПараметрыСтраницы(2,,,,,,,,,1);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);	
	Таб.Показать("Заказы пост.");
	
	Расшифровка.Установить("Обновить",0);	
	Если (Обновить = 2) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;		
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), ?(ИмСпис = "списТовар1", списТовар1, списТовар));
	ИначеЕсли Элт.Вид() = "Контрагенты" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), ?(ИмСпис = "списПостНЕ", списПостНЕ, списПост));		
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ИзмКол()
	Если (фПриходП = 0) и (фЗаказ = 0) Тогда
		фКонтроль = 0;
		Форма.фКонтроль.Доступность(0);
	Иначе
		Форма.фКонтроль.Доступность(1);
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		НачДата = глРасшифровка.Получить("Дата1");
		КонДата = глРасшифровка.Получить("Дата2");
		
		глРасшифровка.Получить("списТовар").Выгрузить(списТовар);
		глРасшифровка.Получить("списТовар1").Выгрузить(списТовар1);
		глРасшифровка.Получить("списПост").Выгрузить(списПост);
		глРасшифровка.Получить("списПостНЕ").Выгрузить(списПостНЕ);
		
		фКонОст = глРасшифровка.Получить("фКонОст");
		фПриходП = глРасшифровка.Получить("фПриходП");
		фРасходП = глРасшифровка.Получить("фРасходП");
		фЗаказ = глРасшифровка.Получить("фЗаказ");
		фПустоеКво = глРасшифровка.Получить("фПустоеКво");
		фБлок = глРасшифровка.Получить("фБлок");
		фКонтроль = глРасшифровка.Получить("фКонтроль");
		ргПорядок = глРасшифровка.Получить("ргПорядок"); 
		фБезУ3 = глРасшифровка.Получить("фБезУЗ"); 
		ВыбСклад = глРасшифровка.Получить("ВыбСклад");
		фПодробныйПриход = глРасшифровка.Получить("фПодробныйПриход"); 
		фПодробныйРасход = глРасшифровка.Получить("фПодробныйРасход");
		СсылкаИнвентаризация = глРасшифровка.Получить("СсылкаИнвентаризация");
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
		НаДату = ТекущаяДата();
	КонецЕсли; 
КонецПроцедуры

фПустоеКво = 1;
фКонОст = 1;
фПриходП = 1;
фРасходП = 1;
фКонтроль = 1;
фЗаказ = 1;
ргПорядок = 1;
НачДата = НачМесяца(РабочаяДата());
КонДата = КонМесяца(РабочаяДата());
ИмСпис = "";