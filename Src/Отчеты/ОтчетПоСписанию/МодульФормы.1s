Перем ТЗ, Таб, списПП, списВЗ, ИтКвоУтиль, Расшифровка, РасшДок, ИтОтнОИСН;

Функция РассчитатьЦену(Кво, Сумма)
	Возврат ?(Кво = 0, Сумма, Сумма / Кво);	
КонецФункции

Функция ПроверитьНужноВыводить()
	Если фВыпуск = 1 Тогда
		Если (ТЗ.Выпуск <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.ВыпускС <> 0))  Тогда
			Возврат 1;
	    КонецЕсли;
	КонецЕсли;
	Если фОИ = 1 Тогда
	    Если (ТЗ.ОИ <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.ОИС <> 0))  Тогда
	        Возврат 1;
	    КонецЕсли;
	КонецЕсли;
	Если фИтПриход = 1 Тогда
	    Если (ТЗ.ОИ + ТЗ.Выпуск <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.ОИС + ТЗ.ВыпускС <> 0))  Тогда
	        Возврат 1;
	    КонецЕсли;
	КонецЕсли;
	
	Если (фДетПП = 1) или (фИтПП = 1) Тогда
	    Если (ТЗ.ИтПП <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.ИтППС <> 0))  Тогда
	        Возврат 1;
	    КонецЕсли;
	КонецЕсли;
	
	Если (фРеализ = 1) Тогда
	    Если (ТЗ.Реализация <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.РеализацияС <> 0))  Тогда
	        Возврат 1;
	    КонецЕсли;
	КонецЕсли;
	Если (фДетВЗ = 1) или (фИтСпис = 1) Тогда
	    Если (ТЗ.Списано <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.СписаноС <> 0))  Тогда
	        Возврат 1;
	    КонецЕсли;
	КонецЕсли;

	Если фИтРасход = 1 Тогда
	    Если (ТЗ.ИтПП + ТЗ.Списано + ТЗ.Реализация <> 0) ИЛИ ((фСуммы = 1) и (ТЗ.ИтППС + ТЗ.СписаноС + ТЗ.РеализацияС <> 0))  Тогда
	        Возврат 1;
	    КонецЕсли;
	КонецЕсли;
	
	Возврат 0;
КонецФункции

//*******************************************
Процедура ВывестиСекции(Имя)	
	// проверяем, нужно ли выводить строку
	Если Имя = "Строка" Тогда
		Если ПроверитьНужноВыводить() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Таб.ВывестиСекцию(Имя+"|Основная");

	Если фВыпуск = 1 Тогда
   		РасшДок.Установить("Признак", "Выпуск");
		РасшДок.Установить("ПризнакСтр", "Выпуск");
		Таб.ПрисоединитьСекцию(Имя+"|Выпуск");
	КонецЕсли;
	Если фОИ = 1 Тогда
    	РасшДок.Установить("Признак", "ОП");
		РасшДок.Установить("ПризнакСтр", "Опр. изл.");
		Таб.ПрисоединитьСекцию(Имя+"|ОИ");
	КонецЕсли;
	Если фИтПриход = 1 Тогда
		РасшДок.Установить("Признак", "ИП");
		РасшДок.Установить("ПризнакСтр", "Итого приход");		
		Таб.ПрисоединитьСекцию(Имя+"|ИП");
	КонецЕсли;	
	Если фДетПП = 1 Тогда
		Для Инд = 1 По списПП.РазмерСписка() Цикл
			РасшДок.Установить("Признак", списПП.ПолучитьЗначение(Инд));
			РасшДок.Установить("ПризнакСтр", списПП.ПолучитьЗначение(Инд));
			Таб.ПрисоединитьСекцию(Имя+"|ПП");
		КонецЦикла;
	КонецЕсли;
	Если фИтПП = 1 Тогда
		РасшДок.Установить("Признак", "ИтПП");
		РасшДок.Установить("ПризнакСтр", "Итого П/П");
		Таб.ПрисоединитьСекцию(Имя+"|ИтПП");
	КонецЕсли;
	Если фРеализ = 1 Тогда
		РасшДок.Установить("Признак", "Реализация");
		РасшДок.Установить("ПризнакСтр", "Реализация");
		Таб.ПрисоединитьСекцию(Имя+"|Реализация");
	КонецЕсли;
	Если фДетВЗ = 1 Тогда
		Для Инд = 1 По списВЗ.РазмерСписка() Цикл
			РасшДок.Установить("Признак", списВЗ.ПолучитьЗначение(Инд));
			РасшДок.Установить("ПризнакСтр", списВЗ.ПолучитьЗначение(Инд));
			Таб.ПрисоединитьСекцию(Имя+"|ВЗ");
		КонецЦикла;	    
	КонецЕсли;
	Если фИтСпис = 1 Тогда
		РасшДок.Установить("Признак", "ИтСпис");
		РасшДок.Установить("ПризнакСтр", "Итого списано");
	    Таб.ПрисоединитьСекцию(Имя+"|ИтСпис");
	КонецЕсли;
	Если (фОИСН = 1) И (фДетВЗ = 1) И (фОИ = 1) Тогда
	    Таб.ПрисоединитьСекцию(Имя+"|ОИСН");
	КонецЕсли;
	
	Если фИтРасход = 1 Тогда
		РасшДок.Установить("Признак", "ИтРасход");
		РасшДок.Установить("ПризнакСтр", "Итого расход");
		Таб.ПрисоединитьСекцию(Имя+"|ИтРасход");
	КонецЕсли;
	
	Если (Имя = "Дно") и (фИтОтн = 1) Тогда
   		ИтОтнППВ = ?(ТЗ.Итог("Выпуск") = 0, 0, ТЗ.Итог("ИтПП") / ТЗ.Итог("Выпуск")) * 100;
   		Поз = списВЗ.НайтиЗначение(ВЗУтиль);
   		Если Поз <> 0 Тогда
   		    ИтОтнУтиль = ?(ТЗ.Итог("Реализация") = 0, 0, ТЗ.Итог("С"+Поз) / ТЗ.Итог("Реализация")) * 100;
   		Иначе
   			ИтОтнУтиль = 0;
   		КонецЕсли;		
   		ИтОтнОИСН = ?(ТЗ.Итог("Реализация") = 0, 0, ТЗ.Итог("ОИСН") / ТЗ.Итог("Реализация")) * 100;
	КонецЕсли;
   	
	Если (фИтОтн = 1) Тогда
	    Таб.ПрисоединитьСекцию(Имя+"|Отношения");
	КонецЕсли;	
КонецПроцедуры

Процедура ДобавитьДокументы(Ит, СписДок, Признак)
	Ит.ВыбратьПериоды();
	Пока Ит.ПолучитьПериод() = 1 Цикл
		СписДок.НоваяСтрока();
		СписДок.Документ = Ит.Операция.Документ;
		Если Признак = 1 Тогда
			СписДок.Кво = Ит.КорДО("К");
			СписДок.Сумма = Ит.КорДО();
		Иначе
			СписДок.Кво = Ит.КорКО("К");
			СписДок.Сумма = Ит.КорКО();			    
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

Процедура ВернутьКолонкуПоВидуЗатрат(ИмКол, ИмКолС, Ит, Признак, СписДок, Кво, Сумма, ВидЗатрат);
	Поз = списВЗ.НайтиЗначение(ВидЗатрат);
	Если Поз = 0 Тогда
		списВЗ.ДобавитьЗначение(ВидЗатрат);
		Поз = списВЗ.РазмерСписка();
		ИмКол = "С"+ Поз;
		ИмКолС = "СС"+ Поз;
		ТЗ.НоваяКолонка(ИмКол, "Число", 15, 3);
		ТЗ.НоваяКолонка(ИмКолС, "Число", 12, 2);									
	Иначе
	    ИмКол = "С"+Поз;
		ИмКолС = "СС"+ Поз;
	КонецЕсли;
	Если (Признак = списВЗ.ПолучитьЗначение(Поз)) ИЛИ (Признак = "ИтСпис") ИЛИ (Признак = "ИтРасход") Тогда
	    ДобавитьДокументы(Ит, СписДок, 2);
	КонецЕсли;								
	ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, ИмКол, ТЗ.ПолучитьЗначение(ТЗ.НомерСтроки, ИмКол) + Кво);
	ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, ИмКолС, ТЗ.ПолучитьЗначение(ТЗ.НомерСтроки, ИмКолС) + Сумма);
КонецПроцедуры

Процедура Сформировать(Признак = "", Товар = "", ПризнакСтр = "")
	Расшифровка = СоздатьОбъект("СписокЗначений");
	глРасшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", "ОстаткиТМЦ");
	Расшифровка.Установить("Дата1", НачДата);
    Расшифровка.Установить("Дата2", КонДата);
	
	Расшифровка.Установить("ВыбФирма", ВыбФирма);
	
	Расшифровка.Установить("ВидОтчета", 		1);
	Расшифровка.Установить("ПоказатьОстатки", 	1);
	Расшифровка.Установить("ЦенаТовара", 		1); 
	Расшифровка.Установить("ЦенаИзСправочника", "");

	Расшифровка.Установить("ДеталПоСкладам", 	0); 
	Расшифровка.Установить("ДеталПоДокументам", 1);
	Расшифровка.Установить("фБезГрупп", 1);
	Расшифровка.Установить("фДопКво", 0);
	
	РасшДок = СоздатьОбъект("СписокЗначений");
	РасшДок.Установить("Дата1", НачДата);
	РасшДок.Установить("Дата2", КонДата);
	РасшДок.Установить("СписаниеПоДок", Да);
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Родитель", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("Выпуск", "Число", 15, 3);
	ТЗ.НоваяКолонка("ОИ", "Число", 15, 3);
	ТЗ.НоваяКолонка("ИтПП", "Число", 15, 3);
	ТЗ.НоваяКолонка("Реализация", "Число", 15, 3);
	ТЗ.НоваяКолонка("Списано", "Число", 15, 3);
	ТЗ.НоваяКолонка("ОтнППВ", "Число", 12, 2);
	ТЗ.НоваяКолонка("ОтнУтиль", "Число", 12, 2);
	ТЗ.НоваяКолонка("ВыпускС", "Число", 12, 2);	
	ТЗ.НоваяКолонка("ОИС", "Число", 12, 2);
	ТЗ.НоваяКолонка("ИтППС", "Число", 12, 2);
	ТЗ.НоваяКолонка("РеализацияС", "Число", 12, 2);
	ТЗ.НоваяКолонка("СписаноС", "Число", 12, 2);
	ТЗ.НоваяКолонка("ОИСН", "Число", 15, 3);
	ТЗ.НоваяКолонка("ЦенаОИСН", "Число", 12, 2);
	ТЗ.НоваяКолонка("ОИСНС", "Число", 12, 2);
	ТЗ.НоваяКолонка("ОтнОИСН", "Число", 12, 2);
	
	списПП = СоздатьОбъект("СписокЗначений");
	списВЗ = СоздатьОбъект("СписокЗначений");
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ПустоеЗначение(Товар) = 0 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,Товар,2,0);
	Иначе
		Если списТовар.РазмерСписка() = 0 Тогда
		    Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,,1,0);
		Иначе
			Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,списТовар,1,0);
		КонецЕсли;	    
	КонецЕсли;
	
	Ит.ИспользоватьКорСубконто();
	Ит.ИспользоватьКорСубконто();
	Ит.ИспользоватьКорСубконто();
	Ит.ВключатьСубсчета(-1,-1);
	Если ВыбФирма.Выбран() = 1 Тогда
		Ит.ИспользоватьРазделительУчета(ВыбФирма);
	КонецЕсли;
	
	Если Признак = "" Тогда
		Ит.ВыполнитьЗапрос(НачДата, КонДата, ?(фСырьё = 1,"201,202,203,204,205,206,207,208,2091,22,24,25","26,281"),,,2);
	Иначе		
	    Ит.ВыполнитьЗапрос(НачДата, КонДата, ?(фСырьё = 1,"201,202,203,204,205,206,207,208,2091,22,24,25","26,281"),,,2, "Операция");
	КонецЕсли;
	
	СписДок = СоздатьОбъект("ТаблицаЗначений");
	СписДок.НоваяКолонка("Документ", "Документ");
	СписДок.НоваяКолонка("Кво", "Число");
	СписДок.НоваяКолонка("Сумма", "Число");
	
	Ит.ВыбратьСубконто(1);
	Пока Ит.ПолучитьСубконто(1) = 1 Цикл
		ТЗ.НоваяСтрока();
		ТЗ.ТМЦ = Ит.Субконто(1);
		ТЗ.Родитель = Ит.Субконто(1).Родитель;
		КвоУтиль = 0;
		СписаноНедостач = 0; СписаноНедостачСумма = 0;
		Ит.ВыбратьКорСчета();
		Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Если (Ит.КорСчет = СчетПоКоду("231")) ИЛИ (Ит.КорСчет = СчетПоКоду("631")) Тогда
			    ТЗ.Выпуск = Ит.КорДО("К");
				ТЗ.ВыпускС = Ит.КорДО();
				Если (Признак = "Выпуск") ИЛИ (Признак = "ИП") Тогда
				    ДобавитьДокументы(Ит, СписДок, 1);
				КонецЕсли;
			ИначеЕсли Ит.КорСчет = СчетПоКоду("901") Тогда
			    ТЗ.Реализация = Ит.КорКО("К");
				ТЗ.РеализацияС = Ит.КорКО();
				Если (Признак = "Реализация") ИЛИ (Признак = "ИтРасход") Тогда
				    ДобавитьДокументы(Ит, СписДок, 2);
				КонецЕсли;
			ИначеЕсли Ит.КорСчет.ПринадлежитГруппе(СчетПоКоду("71")) = 1 Тогда
			    ТЗ.ОИ = Ит.КорДО("К");
				ТЗ.ОИС = Ит.КорДО();
				Если (Признак = "ОИ") ИЛИ (Признак = "ИП") Тогда
				    ДобавитьДокументы(Ит, СписДок, 1);
				КонецЕсли;
			ИначеЕсли Ит.КорСчет.ПринадлежитГруппе(СчетПоКоду("74")) = 1 Тогда
				ТЗ.Списано = ТЗ.Списано + Ит.КорКО("К");
				ТЗ.СписаноС = ТЗ.СписаноС + Ит.КорКО();
				ИмКол = ""; ИмКолС = "";
				ВернутьКолонкуПоВидуЗатрат(ИмКол, ИмКолС, Ит, Признак, СписДок, Ит.КорКО("К"), Ит.КорКО(), ВЗСписание);
			ИначеЕсли Ит.КорСчет.ПринадлежитГруппе(СчетПоКоду("20")) = 1 Тогда
			    ТЗ.ИтПП = Ит.КорКО("К");
				ТЗ.ИтППС = Ит.КорКО();
				Ит.ВыбратьКорСубконто(1);
				Пока Ит.ПолучитьКорСубконто(1) = 1 Цикл
					Ит.ВыбратьКорСубконто(2);
					Пока Ит.ПолучитьКорСубконто(2) = 1 Цикл
						Если Ит.КорСубконто(2).ЭтоГруппа() = 0 Тогда
							Поз = списПП.НайтиЗначение(Ит.КорСубконто(2));
							Если Поз = 0 Тогда
								списПП.ДобавитьЗначение(Ит.КорСубконто(2));
								Поз = списПП.РазмерСписка();
								ИмКол = "П"+ Поз;
								ИмКолС = "ПС"+ Поз;
								ТЗ.НоваяКолонка(ИмКол, "Число", 15, 3);
								ТЗ.НоваяКолонка(ИмКолС, "Число", 12, 2);
							Иначе
							    ИмКол = "П"+Поз;
								ИмКолС = "ПС"+Поз;
							КонецЕсли;
							Если (Признак = списПП.ПолучитьЗначение(Поз)) ИЛИ (Признак = "ИтПП") ИЛИ (Признак = "ИтРасход") Тогда
							    ДобавитьДокументы(Ит, СписДок, 2);
							КонецЕсли;							
							ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, ИмКол, ТЗ.ПолучитьЗначение(ТЗ.НомерСтроки, ИмКол) + Ит.КорКО("К"));
							ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, ИмКолС, ТЗ.ПолучитьЗначение(ТЗ.НомерСтроки, ИмКолС) + Ит.КорКО());
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			ИначеЕсли Ит.КорСчет.ПринадлежитГруппе(СчетПоКоду("94")) = 1 Тогда
			    ТЗ.Списано = ТЗ.Списано + Ит.КорКО("К");
				ТЗ.СписаноС = ТЗ.СписаноС + Ит.КорКО();
				Ит.ВыбратьКорСубконто(1);
				Пока Ит.ПолучитьКорСубконто(1) = 1 Цикл
					Ит.ВыбратьКорСубконто(2);
					Пока Ит.ПолучитьКорСубконто(2) = 1 Цикл
						Ит.ВыбратьКорСубконто(3);
						Пока Ит.ПолучитьКорСубконто(3) = 1 Цикл
							Если Ит.КорСубконто(3).ЭтоГруппа() = 0 Тогда
								ИмКол = ""; ИмКолС = "";
								ВернутьКолонкуПоВидуЗатрат(ИмКол, ИмКолС, Ит, Признак, СписДок, Ит.КорКО("К"), Ит.КорКО(), Ит.КорСубконто(3));
								
								Если Ит.КорСубконто(3) = ВЗУтиль Тогда
									КвоУтиль = Ит.КорКО("К");
								ИначеЕсли Ит.КорСубконто(3) = ВЗСписание Тогда
									СписаноНедостач = СписаноНедостач + Ит.КорКО("К");
									СписаноНедостачСумма = СписаноНедостачСумма + Ит.КорКО();									
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;				
			КонецЕсли;
		КонецЦикла;
		
		ТЗ.ОтнППВ = ?(ТЗ.Выпуск = 0, 0, ТЗ.ИтПП / ТЗ.Выпуск) * 100;
		ТЗ.ОтнУтиль = ?(ТЗ.Реализация = 0, 0, КвоУтиль / ТЗ.Реализация) * 100;
		ЦенаОИ = РассчитатьЦену(ТЗ.ОИ, ТЗ.ОИС);
		ЦенаСН = РассчитатьЦену(СписаноНедостач, СписаноНедостачСумма);
		ТЗ.ЦенаОИСН = ?(ЦенаСН = 0, ЦенаОИ, ЦенаСН);
		ТЗ.ОИСН = СписаноНедостач - ТЗ.ОИ;
		ТЗ.ОИСНС = ТЗ.ОИСН * ТЗ.ЦенаОИСН;
		ТЗ.ОтнОИСН = ?(ТЗ.Реализация = 0, 0, ТЗ.ОИСН / ТЗ.Реализация) * 100;
	КонецЦикла;

	Если Признак <> "" Тогда
		Т = СоздатьОбъект("Таблица");
		Т.ИсходнаяТаблица("Документы");
		Т.ВывестиСекцию("Шапка");
		СписДок.ВыбратьСтроки();
		Пока СписДок.ПолучитьСтроку() = 1 Цикл
			Т.ВывестиСекцию("Строка");
		КонецЦикла;
		Т.ВывестиСекцию("Итого");
		Т.Защита(Константа.ФлагЗащитыТаблиц);
		Т.ТолькоПросмотр(1);
		Т.Показать("Движения");
	Иначе
		ТЗ.Сортировать("Родитель,ТМЦ");
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("Таблица"+?(фСуммы = 1, "С", ""));
		//ТЗ.ВыбратьСтроку();	
		ВывестиСекции("Шапка");
		Таб.Опции(0,0, Таб.ВысотаТаблицы(), 2);
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			Расшифровка.Установить("ВыбНоменклатура", ТЗ.ТМЦ);
			РасшДок.Установить("Товар", ТЗ.ТМЦ);
			ВывестиСекции("Строка");
		КонецЦикла;
		Таб.Показать();
		ВывестиСекции("Дно");
		Таб.ПараметрыСтраницы(2);
		Таб.Защита(Константа.ФлагЗащитыТаблиц);
		Таб.ТолькоПросмотр(1);
		Таб.Показать("Отчет по продукции");		
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗакрытии()
	СохранитьЗначение("ВЗУтиль", ВЗУтиль);
КонецПроцедуры

Процедура ПриОткрытии()
	Если (НазваниеНабораПрав() = "ПрочиеЗаказ") ИЛИ (НазваниеНабораПрав() = "МастерЭкспедиции") Тогда
		Форма.фСырьё.Доступность(0);
	КонецЕсли;
	ВЗУтиль = ВосстановитьЗначение("ВЗУтиль");
	Если глФлагРасшифровки = 1 Тогда
		НачДата = глРасшифровка.Получить("Дата1");
		КонДата = глРасшифровка.Получить("Дата2");
		Сформировать(глРасшифровка.Получить("Признак"), глРасшифровка.Получить("Товар"), глРасшифровка.Получить("ПризнакСтр"));
		глФлагРасшифровки = 0;
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

Процедура ВыделитьВсе(П)
	Если П = -1 Тогда
		фВыпуск = 1-фВыпуск;
		фОИ = 1-фОИ;
		фИтПриход = 1-фИтПриход;
		фДетПП = 1-фДетПП;
		фИтПП = 1-фИтПП;
		фРеализ = 1-фРеализ;
		фДетВЗ = 1-фДетВЗ;
		фИтСпис = 1-фИтСпис;
		фОИСН = 1 - фОИСН;
		фИтРасход = 1-фИтРасход;
		фИтОтн = 1-фИтОтн;	
	Иначе
		фВыпуск = П;
		фОИ = П;
		фИтПриход = П;
		фДетПП = П;
		фИтПП = П;
		фРеализ = П;
		фДетВЗ = П;
		фИтСпис = П;
		фОИСН = П;
		фИтРасход = П;
		фИтОтн = П;    
	КонецЕсли;
КонецПроцедуры

НачДата = ТекущаяДата();
КонДата = ТекущаяДата();
ВыделитьВсе(1);
ВыбФирма = Константа.БазФирма;