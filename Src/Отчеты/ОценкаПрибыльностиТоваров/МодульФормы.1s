// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Таб;		
Перем Обновить;
Перем Расшифровка; // используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем ПредставлениеРУ;

Перем ТекСтрокаВТаблицеМФ; // текущая строка в таблице значений  МФ 

// переменные предназначены для механизма универсальных группировок отчета
Перем СписокГруппировок;
Перем КоличествоГруппировок;
Перем НомерГруппировкиПоТМЦ;

// переменные предназначены для запроса по периодам
Перем Запрос;
Перем Язык;


// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
// Название: ПерерисовкаНазванийЗакладок
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.           
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
Функция ПерерисовкаНазванийЗакладок()      
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
КонецФункции // ПерерисовкаНазванийЗакладок	

// ===============================
Функция КоэффициентОплаты(Док)	
	Рег = СоздатьОбъект("Регистр.ВзаиморасчетыПокупателей");
    	
	ОбщийДолг = 0;
	Рег.ВыбратьДвиженияДокумента(Док.ТекущийДокумент());
	Пока Рег.ПолучитьДвижение()>0 Цикл             
		Если Рег.Фирма <> ВыбФирма Тогда
			Продолжить;
		КонецЕсли;
			
		ОбщийДолг = ОбщийДолг + (Рег.Приход - Рег.Расход) * Рег.ДолгОсн;
	КонецЦикла;
	
	ОстатокДолга = Рег.СводныйОстаток(ВыбФирма,,,,,Док.ТекущийДокумент(),,"ДолгОсн");
	Если (ОстатокДолга<=0) Или (ОбщийДолг<=0) Тогда
		КоэфОпл = 1;	// все оплачено 
	Иначе
		КоэфОпл = ?(ОбщийДолг<ОстатокДолга, 0, 1-ОстатокДолга/ОбщийДолг);
	КонецЕсли;                                            

	Возврат КоэфОпл;
	
КонецФункции	


// ===============================
Функция ДенежнаяИлиКоличественная()
	Возврат ВидДиаграммы.ПолучитьЗначение(ВидДиаграммы.ТекущаяСтрока());
КонецФункции

// ===============================
Функция ОтборПоДенежнойИлиКоличественной()
	Если Форма.ДеньгиКоличество.Доступность()=1 Тогда
		Возврат ДеньгиКоличество.ПолучитьЗначение(ДеньгиКоличество.ТекущаяСтрока());
	Иначе
		Возврат ДенежнаяИлиКоличественная();
	КонецЕсли;
КонецФункции


// ===============================
// Название: ПоказатьДиаграммуПрибылейВПроцентах
// Параметры:
//	Диаграмма - диаграмма
//	ОтобранныеДанные - данные, сформированные отчетом 	
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   
// Наименование,.
// Описание:
//    формирует диаграмму прибылей, выраженных в процентах
Процедура ПоказатьДиаграммуПрибылейВПроцентах(Диаграмма,ОтобранныеДанные) 
	Перем КолТоваров;
    
	Диаграмма.Обновление(0);
	
	Диаграмма.Заголовок = ?(Язык = "у", "Прибутки по товарам", "Прибыли по товарам");
	                                           
	Серия1 = ?(Язык = "у", "грошовий", "денежная");
	Серия2 = ?(Язык = "у", "кількісний", "количественная");
    
	Если Найти(ДенежнаяИлиКоличественная(), "Деньги")>0 Тогда
		Диаграмма.УстановитьИмяСерии(1,Серия1); 
	КонецЕсли;
	Если ДенежнаяИлиКоличественная()="Количество" Тогда
		Диаграмма.УстановитьИмяСерии(1,Серия2);
		Диаграмма.ЦветСерии(1,0,255,0); // зеленый
	ИначеЕсли Найти(ДенежнаяИлиКоличественная(), "Количество")>0 Тогда
		Диаграмма.УстановитьИмяСерии(2,Серия2);
	КонецЕсли;
	
	ТаблицаЗапроса=СоздатьОбъект("ТаблицаЗначений");
	ТаблицаЗапроса.НоваяКолонка("Наименование");
	ТаблицаЗапроса.НоваяКолонка("ДенежнаяПрибыль");
	ТаблицаЗапроса.НоваяКолонка("КолПрибыль");
	НомерСтроки = 0;
	ОтобранныеДанные.ВыбратьСтроки();
	Пока ОтобранныеДанные.ПолучитьСтроку() > 0 Цикл
		Если ОтобранныеДанные.Товар.ЭтоГруппа()=1 Тогда
			Продолжить;
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
		ТаблицаЗапроса.НоваяСтрока();
		ТаблицаЗапроса.ТекущаяСтрока(НомерСтроки);
		ТаблицаЗапроса.Наименование		= " "+ОтобранныеДанные.Товар;
		ТаблицаЗапроса.ДенежнаяПрибыль 	= ОтобранныеДанные.ДенежнаяПрибыль;
		ТаблицаЗапроса.КолПрибыль 	 	= ОтобранныеДанные.КолПрибыль;
	КонецЦикла;

	КвоТочекВДиаграмме = ТаблицаЗапроса.КоличествоСтрок();
	Если фОграничитьЧислоТочек=1 Тогда
		Если ОтборПоДенежнойИлиКоличественной()="Деньги" Тогда
			ТаблицаЗапроса.Сортировать("ДенежнаяПрибыль-");
		Иначе
			ТаблицаЗапроса.Сортировать("КолПрибыль-");
		КонецЕсли;
		КвоСтрокТабЗн = ТаблицаЗапроса.КоличествоСтрок();
		Если МаксКоличествоТочек < КвоСтрокТабЗн Тогда
			КвоТочекВДиаграмме = МаксКоличествоТочек+1;
			Для Инд=МаксКоличествоТочек+1 По КвоСтрокТабЗн Цикл
			    ТаблицаЗапроса.ПолучитьСтрокуПоНомеру(Инд);
		        ТаблицаЗапроса.Наименование = "<ПРОЧИЕ>";
			КонецЦикла;
			ТаблицаЗапроса.Свернуть("Наименование","ДенежнаяПрибыль,КолПрибыль");
			НомерСтроки = 0;
			Если ТаблицаЗапроса.НайтиЗначение("<ПРОЧИЕ>",НомерСтроки,"Наименование")=1 Тогда
			    ТаблицаЗапроса.ПолучитьСтрокуПоНомеру(НомерСтроки);
				ТаблицаЗапроса.ДенежнаяПрибыль 	= ТаблицаЗапроса.ДенежнаяПрибыль/ (КвоСтрокТабЗн - МаксКоличествоТочек);
				ТаблицаЗапроса.КолПрибыль 		= ТаблицаЗапроса.КолПрибыль 	/ (КвоСтрокТабЗн - МаксКоличествоТочек);
			КонецЕсли;
		Конецесли;
	КонецЕсли;
	ТаблицаЗапроса.Сортировать("Наименование");

	Для Инд=1 По КвоТочекВДиаграмме Цикл
	    ТаблицаЗапроса.ПолучитьСтрокуПоНомеру(КвоТочекВДиаграмме-Инд+1);
		Диаграмма.УстановитьИмяТочки(Инд,ТаблицаЗапроса.Наименование);
		ДенежнаяПрибыль = Окр(ТаблицаЗапроса.ДенежнаяПрибыль,2,1); 
		КолПрибыль      = Окр(ТаблицаЗапроса.КолПрибыль,     1,1);
		Если Найти(ДенежнаяИлиКоличественная(), "Деньги")>0 Тогда
			Диаграмма.УстановитьЗначение(Инд,1,ДенежнаяПрибыль,ТаблицаЗапроса.Наименование+" "+ДенежнаяПрибыль	+"%"); 
		КонецЕсли;
		Если ДенежнаяИлиКоличественная()="Количество" Тогда
			Диаграмма.УстановитьЗначение(Инд,1,КолПрибыль,     ТаблицаЗапроса.Наименование+" "+КолПрибыль		+"%");
		ИначеЕсли Найти(ДенежнаяИлиКоличественная(), "Количество")>0 Тогда
			Диаграмма.УстановитьЗначение(Инд,2,КолПрибыль,     ТаблицаЗапроса.Наименование+" "+КолПрибыль		+"%");
		КонецЕсли;
		Состояние("Для диаграммы сформировано "+Инд+" значений.");
	КонецЦикла;

	Диаграмма.Обновление(1);
	
КонецПроцедуры		// ПоказатьДиаграммуПрибылейВПроцентах

// ===============================
// Название: ПоказатьДиаграммуАбсолютныхПрибылей
// Параметры:
//	Диаграмма - диаграмма
//	ОтобранныеДанные - данные, сформированные отчетом 	
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   
// Наименование,.
// Описание:
//    формирует диаграмму абсолютных прибылей
Процедура ПоказатьДиаграммуАбсолютныхПрибылей(Диаграмма,ОтобранныеДанные) 
	Перем КолТоваров;
    
	Диаграмма.Обновление(0);
	
	Диаграмма.Заголовок = ?(Язык = "у", "Прибутки по товарам", "Прибыли по товарам");
	        
	Серия1 = ?(Язык = "у", "грошовий", "денежная");
	Серия2 = ?(Язык = "у", "кількісний", "количественная");
    
	Если Найти(ДенежнаяИлиКоличественная(), "Деньги")>0 Тогда
		Диаграмма.УстановитьИмяСерии(1,Серия1);
	КонецЕсли;
	Если ДенежнаяИлиКоличественная()="Количество" Тогда
		Диаграмма.УстановитьИмяСерии(1,Серия2);
		Диаграмма.ЦветСерии(1,0,255,0); // зеленый
	ИначеЕсли Найти(ДенежнаяИлиКоличественная(), "Количество")>0 Тогда
		Диаграмма.УстановитьИмяСерии(2,Серия2);
	КонецЕсли;
	
	ТаблицаЗапроса=СоздатьОбъект("ТаблицаЗначений");
	ТаблицаЗапроса.НоваяКолонка("Наименование");
	ТаблицаЗапроса.НоваяКолонка("ДенАбсПриб");
	ТаблицаЗапроса.НоваяКолонка("КолАбсПриб"); 
	НомерСтроки = 0;
	ОтобранныеДанные.ВыбратьСтроки();
	Пока ОтобранныеДанные.ПолучитьСтроку() > 0 Цикл
		Если ОтобранныеДанные.Товар.ЭтоГруппа()=1 Тогда
			Продолжить;
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
		ТаблицаЗапроса.НоваяСтрока();
		ТаблицаЗапроса.ТекущаяСтрока(НомерСтроки);
		ТаблицаЗапроса.Наименование = " "+ОтобранныеДанные.Товар;
		ТаблицаЗапроса.ДенАбсПриб 	= ОтобранныеДанные.Выручка - ОтобранныеДанные.Себ; 
		ТаблицаЗапроса.КолАбсПриб 	= (ОтобранныеДанные.КоличественнаяВыручка - ОтобранныеДанные.Кол)*ОтобранныеДанные.ЗакупЦена;
	КонецЦикла;

	КвоТочекВДиаграмме = ТаблицаЗапроса.КоличествоСтрок();
	Если фОграничитьЧислоТочек=1 Тогда
		Если ОтборПоДенежнойИлиКоличественной()="Деньги" Тогда
			ТаблицаЗапроса.Сортировать("ДенАбсПриб-");
		Иначе
			ТаблицаЗапроса.Сортировать("КолАбсПриб-");
		КонецЕсли;
		КвоСтрокТабЗн = ТаблицаЗапроса.КоличествоСтрок();
		Если МаксКоличествоТочек < КвоСтрокТабЗн Тогда
			КвоТочекВДиаграмме = МаксКоличествоТочек+1;
			Для Инд=МаксКоличествоТочек+1 По КвоСтрокТабЗн Цикл
			    ТаблицаЗапроса.ПолучитьСтрокуПоНомеру(Инд);
		        ТаблицаЗапроса.Наименование = "<ПРОЧИЕ>";
			КонецЦикла;
			ТаблицаЗапроса.Свернуть("Наименование","ДенАбсПриб,КолАбсПриб");
		Конецесли;
	КонецЕсли;
	ТаблицаЗапроса.Сортировать("Наименование");

	Для Инд=1 По КвоТочекВДиаграмме Цикл
	    ТаблицаЗапроса.ПолучитьСтрокуПоНомеру(КвоТочекВДиаграмме-Инд+1);
		Диаграмма.УстановитьИмяТочки(Инд,ТаблицаЗапроса.Наименование);
		ДенАбсПриб = Окр(ТаблицаЗапроса.ДенАбсПриб,2,1); 
		КолАбсПриб = Окр(ТаблицаЗапроса.КолАбсПриб,2,1);
		Если Найти(ДенежнаяИлиКоличественная(), "Деньги")>0 Тогда
			Диаграмма.УстановитьЗначение(Инд,1,ДенАбсПриб,ТаблицаЗапроса.Наименование+" "+ДенАбсПриб+Гривня); 
		КонецЕсли;
		Если ДенежнаяИлиКоличественная()="Количество" Тогда
			Диаграмма.УстановитьЗначение(Инд,1,КолАбсПриб,ТаблицаЗапроса.Наименование+" "+КолАбсПриб+Гривня);
		ИначеЕсли Найти(ДенежнаяИлиКоличественная(), "Количество")>0 Тогда
			Диаграмма.УстановитьЗначение(Инд,2,КолАбсПриб,ТаблицаЗапроса.Наименование+" "+КолАбсПриб+Гривня);
		КонецЕсли;
		Состояние("Для диаграммы сформировано "+Инд+" значений.");
	КонецЦикла;

	Диаграмма.Обновление(1);
	
КонецПроцедуры		// ПоказатьДиаграммуАбсолютныхПрибылей


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

// ===============================
// Название: ДоступностьЭлементов
// Параметры:
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//    	управляет доступностью элементов
Процедура ДоступностьЭлементов(ВсеЭлементы=0)           

	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбФирма") Или (ЭлементДиалога="кХВыбФирма") Тогда
		Форма.кХВыбФирма.Доступность(ВыбФирма.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбТовар") Или (ЭлементДиалога="кХВыбТовар") Тогда
		Форма.кХВыбТовар.Доступность(ВыбТовар.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ЦенаЗакупки") Тогда
		Если ЦенаЗакупки.ТекущаяСтрока()=0 Тогда
			ЦенаИзСправочника=0;
			Форма.ЦенаИзСправочника.Доступность(0);
		ИначеЕсли ЦенаЗакупки.ПолучитьЗначение(ЦенаЗакупки.ТекущаяСтрока())="изсправочника" Тогда
			Форма.ЦенаИзСправочника.Доступность(1);
		Иначе
			ЦенаИзСправочника=0;
			Форма.ЦенаИзСправочника.Доступность(0);
		КонецЕсли;	                 
	КонецЕсли;
	
	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклВО") Или (ЭлементДиалога="ВалютаВО") Тогда
		Форма.ОборотВыручка.Доступность(ВклВО); 
		Форма.ВалютаВО.Доступность(ВклВО);
		Форма.ВклВООт.Доступность(ВклВО); 
		Форма.ВклВОДо.Доступность(ВклВО);
		ВклВООт=Мин(ВклВО,ВклВООт);
		ВклВОДо=Мин(ВклВО,ВклВОДо);;
		Форма.ВООт.Доступность(ВклВООт); 
		Форма.ВОДо.Доступность(ВклВОДо);
	КонецЕсли;	                 

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклВООт") Тогда
		Форма.ВООт.Доступность(ВклВООт); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклВОДо") Тогда
		Форма.ВОДо.Доступность(ВклВОДо);
	КонецЕсли;
	
	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклПриб") Тогда
	    Форма.КолСуммаПриб.Доступность(ВклПриб); 
		Форма.ВклПрибОт.Доступность(ВклПриб); 
		Форма.ВклПрибДо.Доступность(ВклПриб);
		ВклПрибОт=Мин(ВклПриб,ВклПрибОт);
		ВклПрибДо=Мин(ВклПриб,ВклПрибДо);;
		Форма.ПрибОт.Доступность(ВклПрибОт); 
		Форма.ПрибДо.Доступность(ВклПрибДо);
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклПрибОт") Тогда
		Форма.ПрибОт.Доступность(ВклПрибОт); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклПрибДо") Тогда
		Форма.ПрибДо.Доступность(ВклПрибДо);
	КонецЕсли;
	
	Если (ВсеЭлементы=1) Или (ЭлементДиалога="фДиаграмма")
	Или (ЭлементДиалога="фОграничитьЧислоТочек")
	Или (ЭлементДиалога="ВидДиаграммы") Тогда
		Форма.фОграничитьЧислоТочек.Доступность(фДиаграмма);
		Форма.ВидДиаграммы.Доступность(фДиаграмма);
		фОграничитьЧислоТочек = Мин(фДиаграмма,фОграничитьЧислоТочек);
		Форма.МаксКоличествоТочек.Доступность(фОграничитьЧислоТочек);
		Попытка
			фСмешанная = ?(ВидДиаграммы.ПолучитьЗначение(ВидДиаграммы.ТекущаяСтрока())="ДеньгиКоличество", 1, 0);
		Исключение
			фСмешанная = 0;
		КонецПопытки;
		Форма.ДеньгиКоличество.Доступность(Мин(фСмешанная, фОграничитьЧислоТочек));
		Форма.тДеньгиКоличество.Доступность(Мин(фСмешанная, фОграничитьЧислоТочек));
	КонецЕсли;
	
КонецПроцедуры  	// ДоступностьЭлементов
	                     
// ===============================
// Название: Сформировать
// Параметры:
//   ЗакрытьЭкран - флаг того, что после формирования отчета надо закрыть экран	
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
// Наименование,.
// Описание:
//    запускает отчет
Процедура Сформировать(ЗакрытьЭкран=0)    
	Перем Запрос;					// запрос
	        
	СпособВычисленияЦенЗакупки = ЦенаЗакупки.ПолучитьЗначение(ЦенаЗакупки.ТекущаяСтрока());
		
	Если (СпособВычисленияЦенЗакупки="изсправочника") И (ЦенаИзСправочника.Выбран()=0) Тогда
		Предупреждение("Не выбран тип цен закупки");
		Возврат;
	КонецЕсли;	
	
	глПроверкаДаты(Дата1,Дата2);
	
	// Здесь формируется отчет, который использует регистры, критичные к
    // последовательности проведения документов
    // поэтому сравним установленные даты периода формируемого отчета с ГП
    глПроверкаАктуальностиОтчета(Дата1,Дата2,ВыбФирма,0);
	
	//  Создание Таблицы для выходного отчета
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;

	ПечФорма = "Таблица";
	Язык = глЯзык(ПечФорма); 
	глУстПропись(Гривня,Язык);
	Таб.ИсходнаяТаблица(ПечФорма);
    
	// все настройки помещаем в список
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", "ОценкаПрибыльностиТоваров");
    
	Расшифровка.Установить("Дата1", 	Дата1);
    Расшифровка.Установить("Дата2", 	Дата2);
	
	Расшифровка.Установить("ВыбФирма", 	ВыбФирма);
	Расшифровка.Установить("ВыбТовар", 	ВыбТовар);
	// запомним МФ только если он задан
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ);
	КонецЕсли;

	Расшифровка.Установить("фДиаграмма", 			фДиаграмма);
	Расшифровка.Установить("фОграничитьЧислоТочек", фОграничитьЧислоТочек);
	Расшифровка.Установить("МаксКоличествоТочек", 	МаксКоличествоТочек);
	Расшифровка.Установить("ДеньгиКоличество",		ДеньгиКоличество.ТекущаяСтрока()); 

	Расшифровка.Установить("ОплатаОтгрузка", ОплатаОтгрузка.ТекущаяСтрока()); 
	Расшифровка.Установить("ЦенаЗакупки", ЦенаЗакупки.ТекущаяСтрока()); 
	Расшифровка.Установить("ЦенаИзСправочника", ЦенаИзСправочника);
	                    
	Расшифровка.Установить("ВклВО", 		ВклВО);
	Расшифровка.Установить("ВалютаВО", 		ВалютаВО); 
	Расшифровка.Установить("ВклВООт", 		ВклВООт); 
	Расшифровка.Установить("ВклВОДо", 		ВклВОДо);
	Расшифровка.Установить("ВООт", 			ВООт);
	Расшифровка.Установить("ВОДо", 			ВОДо);
	Расшифровка.Установить("ОборотВыручка", ОборотВыручка.ТекущаяСтрока()); 

	Расшифровка.Установить("ВклПриб", 		ВклВО);
	Расшифровка.Установить("ВклПрибОт", 	ВклПрибОт); 
	Расшифровка.Установить("ВклПрибДо", 	ВклПрибДо);
	Расшифровка.Установить("ПрибОт", 		ПрибОт);
	Расшифровка.Установить("ПрибДо", 		ПрибДо); 
	Расшифровка.Установить("КолСуммаПриб",	КолСуммаПриб.ТекущаяСтрока()); 

	Если Дата2 >= ПолучитьДатуТА() Тогда
		ТекстЗапроса = "
			|Период с Дата1;";
	Иначе 
		ТекстЗапроса = "
			|Период с Дата1 по Дата2;";
	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса + "
		|Обрабатывать НеПомеченныеНаУдаление;
		|Товар = Регистр.Обороты.ТМЦ;
		|Фирма = Регистр.Обороты.Фирма;
		|РасходСум = Регистр.Обороты.РасходСум;
		|РасходКво = Регистр.Обороты.РасходКво;
		|Доход = Регистр.Обороты.Доход;    
		|ТекДок = Регистр.Обороты.ТекущийДокумент;    	
		|Функция РасходСумСумма = Сумма(РасходСум);
		|Функция РасходКвоСумма = Сумма(РасходКво);
		|Функция ДоходСумма = Сумма(Доход);
		|Группировка Товар;
		|Группировка ТекДок;  
		|"
		;
	
	Загол="";
    
	Если глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Фирма", ВыбФирма, "ВыбФирма", ТекстЗапроса, Загол, Язык)=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по фирмам. Отчет не сформирован.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Товар", ВыбТовар, "ВыбТовар", ТекстЗапроса, Загол, Язык, "КатегорииТоваров")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по товарам. Отчет не сформирован.");
		Возврат;
	КонецЕсли;

	// выполняем запрос
	Запрос=СоздатьОбъект("Запрос");
    Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
		Предупреждение("Запрос по движениям регистров не выполнился!");
		Возврат;
	КонецЕсли;          
	             
	Таб.ВывестиСекцию("Кнопки");              

	ПечЦенаЗакупки = "";
	Если СпособВычисленияЦенЗакупки="изсправочника" Тогда
		ПечЦенаЗакупки = ?(Язык = "у","Ціна "+глУкр(СокрЛП(ЦенаИзСправочника)),"Цена "+СокрЛП(ЦенаИзСправочника));
	ИначеЕсли СпособВычисленияЦенЗакупки="учетная" Тогда	
		ПечЦенаЗакупки = ?(Язык = "у","Облікова ціна","Учетная цена");
	Иначе	// цена последнего приобретения
		ПечЦенаЗакупки = ?(Язык = "у","Ціна останнього придбання","Цена последнего приобретения");
	КонецЕсли;	
	
	// выводим шапку отчета
	Таб.ВывестиСекцию("Отчет");  
	Таб.ВывестиСекцию("Шапка"); 
	глОживить(Таб.ВысотаСекции("Отчет")+Таб.ВысотаСекции("Шапка"));
    
	ОтобранныеДанные = СоздатьОбъект("ТаблицаЗначений");
	ОтобранныеДанные.НоваяКолонка("Товар");		// товар или группа 
	ОтобранныеДанные.НоваяКолонка("КредДок");   // документ отгрузки
	ОтобранныеДанные.НоваяКолонка("Себ"); 		// себестоимость отгр. товара	
	ОтобранныеДанные.НоваяКолонка("Оборот");	// оборот отгр. товара
	ОтобранныеДанные.НоваяКолонка("Кол");		// количество отгр. товара
	ОтобранныеДанные.НоваяКолонка("Оплата");
	ОтобранныеДанные.НоваяКолонка("Выручка");
	ОтобранныеДанные.НоваяКолонка("ЗакупЦена"); 
	ОтобранныеДанные.НоваяКолонка("КоличественнаяВыручка"); 
	ОтобранныеДанные.НоваяКолонка("ДенежнаяПрибыль"); 
	ОтобранныеДанные.НоваяКолонка("КолПрибыль"); 
	ОтобранныеДанные.НоваяКолонка("ДопВложения");

	СпособОпределенияВыручки = ОплатаОтгрузка.ПолучитьЗначение(ОплатаОтгрузка.ТекущаяСтрока()); 
	Пока Запрос.Группировка("Товар") = 1 Цикл
		               
		СуммаОпл=0;
		Пока Запрос.Группировка("ТекДок") = 1 Цикл
			// используем Макс чтобы не учитывать возвраты
			СуммаОпл = СуммаОпл + Макс(Запрос.РасходСум,0)*КоэффициентОплаты(Запрос.ТекДок);
		КонецЦикла;                              		
		
		ОтобранныеДанные.НоваяСтрока();
		ОтобранныеДанные.Товар 		= Запрос.Товар;		
		ОтобранныеДанные.Себ 		= Запрос.РасходСумСумма - Запрос.ДоходСумма; 		
		ОтобранныеДанные.Оборот 	= Запрос.РасходСумСумма;	
		ОтобранныеДанные.Кол 		= Запрос.РасходКвоСумма;		
		ОтобранныеДанные.Оплата 	= СуммаОпл;
		ОтобранныеДанные.Выручка 	= ?(СпособОпределенияВыручки="оплата",ОтобранныеДанные.Оплата,Запрос.РасходСумСумма);
		ОтобранныеДанные.КолПрибыль = Запрос.ДоходСумма; 
		
	КонецЦикла;                              

	СпособФильтраПоОборотуВыручке = ОборотВыручка.ПолучитьЗначение(ОборотВыручка.ТекущаяСтрока());
	Если (ВклВО<>0) И ( (ВклВООт<>0) Или (ВклВОДо<>0) ) Тогда
		// проверяем интервальный фильтр по обороту или выручке
		НомСтроки = ОтобранныеДанные.КоличествоСтрок()+1;
		Пока НомСтроки>1 Цикл
			НомСтроки = НомСтроки-1;
			СуммаФильтра = ОтобранныеДанные.ПолучитьЗначение(НомСтроки,СпособФильтраПоОборотуВыручке);
			СуммаФильтра = глПересчет(СуммаФильтра,Гривня,ВалютаВО,Дата2);

			Если ( (ВклВООт<>0) И (ВООт>СуммаФильтра) ) Или ( (ВклВОДо<>0) И (ВОДо<СуммаФильтра) ) Тогда
				ОтобранныеДанные.УдалитьСтроку(НомСтроки);
			КонецЕсли;	
		КонецЦикла;	                                          
	КонецЕсли;	
	
	Рег = СоздатьОбъект("Регистр.Партии");
	                      
	// получим цены закупки товаров
	Товары = СоздатьОбъект("СписокЗначений"); 
	ЦеныЗакупкиТоваров = СоздатьОбъект("СписокЗначений");
	Цена = СоздатьОбъект("Справочник.Цены");
	Цена.ИспользоватьДату(Дата2);
	
	ОтобранныеДанные.Выгрузить(Товары,,,"Товар");
	КолТоваров = Товары.РазмерСписка();
	Для Индекс = 1 По КолТоваров Цикл
		Товар = Товары.ПолучитьЗначение(Индекс);        
		
		Если Товар.ЭтоГруппа()>0 Тогда
			ЦеныЗакупкиТоваров.ДобавитьЗначение(0);
			Продолжить;
		КонецЕсли;	
			
		Если СпособВычисленияЦенЗакупки="изсправочника" Тогда
			Цена.ИспользоватьВладельца(Товар.ТекущийЭлемент());
			Если Цена.НайтиПоРеквизиту("КатегорияЦены",ЦенаИзСправочника,0)=0 Тогда // не нашли
				ЦеныЗакупкиТоваров.ДобавитьЗначение(0);
			Иначе
				ЦеныЗакупкиТоваров.ДобавитьЗначение(глПересчет(Цена.Цена,Цена.Валюта,Гривня,Дата2)/Цена.Единица.Коэффициент);
			КонецЕсли;                                              			
		ИначеЕсли СпособВычисленияЦенЗакупки="учетная" Тогда	
			ЦеныЗакупкиТоваров.ДобавитьЗначение(Товар.УчетнаяЦена.Получить(Дата2));
		Иначе	// цена последнего приобретения
			Рег.УстановитьЗначениеФильтра("ТМЦ",Товар); 
			Рег.УстановитьЗначениеФильтра("Фирма",ВыбФирма);
			Рег.ОбратныйПорядок(1);   
			                                  
			НайденнаяЦена = 0;
			
			Рег.ВыбратьДвижения(,Дата2,);
			Пока Рег.ПолучитьДвижение()>0 Цикл             
				Если Рег.Приход=0 Тогда
					Продолжить;
				КонецЕсли;
				Док = Рег.ТекущийДокумент();
				ВидДок = "="+ВРег(Док.Вид())+"=";


				Если Найти("=ПРИХОДНАЯНАКЛАДНАЯГТД=ПРИХОДНАЯНАКЛАДНАЯЗАПАСЫ=",ВидДок)>0 Тогда
					ВалютаЦены = Док.Валюта;
					КурсВалюты = Док.Курс;
					// нашли последнее поступление
				ИначеЕсли Найти("=ВВОДОСТАТКОВТМЦ=",ВидДок)>0 Тогда	
					// нашли последнее поступление
					ВалютаЦены = Гривня;
					КурсВалюты = Дата2;
				Иначе
					Продолжить;
				КонецЕсли;	
				
				// нашли последнее поступление
				// ВалютаЦены - валюта цены
				Док.ВыбратьСтроки();
				Пока Док.ПолучитьСтроку()>0 Цикл
					Если Док.ТМЦ<>Товар Тогда
						Продолжить;
					КонецЕсли;
					НайденнаяЦена = Док.ЦенаСНДС/Док.Единица.Коэффициент;
					НайденнаяЦена = глПересчет(НайденнаяЦена,ВалютаЦены,КурсВалюты,Гривня,Дата2,);
					Прервать;
				КонецЦикла;	
				
				Предупреждение("В документе "+Док+" партии товара "+ Товар+" этот товар отсутствует! 
					|Возможно произошло нарушение ссылочной целосности базы данных!!!");
				Прервать;
				
			КонецЦикла;          
			
			ЦеныЗакупкиТоваров.ДобавитьЗначение(НайденнаяЦена);
			
		КонецЕсли;	
	КонецЦикла;
	       
	СписокГруппТоваров = СоздатьОбъект("СписокЗначений");
	              
	ОтобранныеДанные.НоваяКолонка("ЗакупЦена"); 
	ОтобранныеДанные.НоваяКолонка("КоличественнаяВыручка"); 
	ОтобранныеДанные.НоваяКолонка("ДенежнаяПрибыль"); 
	ОтобранныеДанные.НоваяКолонка("КолПрибыль"); 
	ОтобранныеДанные.НоваяКолонка("ДопВложения");
	
	СпособФильтрацииПоПроцПрибыли = КолСуммаПриб.ПолучитьЗначение(КолСуммаПриб.ТекущаяСтрока());
	          
	// теперь удаляем все промежуточные строки
	ОтобранныеДанные.ВыбратьСтроки();
	Пока ОтобранныеДанные.ПолучитьСтроку()>0 Цикл
		
		Товар = ОтобранныеДанные.Товар;
		
		Если Товар.ЭтоГруппа()>0 Тогда  // это группа
			// помещаем в список встретившихся групп
			СписокГруппТоваров.ДобавитьЗначение(Товар.ТекущийЭлемент());
			Продолжить;
		КонецЕсли;              
		
		ЗакупЦена 	= ЦеныЗакупкиТоваров.ПолучитьЗначение(Товары.НайтиЗначение(Товар.ТекущийЭлемент()));
		Выручка 	= ОтобранныеДанные.Выручка;
		Себ 		= ОтобранныеДанные.Себ;
		Кво 		= ОтобранныеДанные.Кол;
		ДопВложения = Макс(0, ЗакупЦена*Кво - Выручка);
		КолВыручка	= ?(ЗакупЦена=0, 0, Выручка/ЗакупЦена);
		ОтобранныеДанные.ЗакупЦена 				= ЗакупЦена;
		ОтобранныеДанные.КоличественнаяВыручка 	= КолВыручка; 
		ОтобранныеДанные.ДопВложения 			= ДопВложения;
		ОтобранныеДанные.КолПрибыль 			= Окр(?(Кво=0,0,(КолВыручка	- Кво)*100/Кво),2,1);
		ОтобранныеДанные.ДенежнаяПрибыль 		= Окр(?(Себ=0,0,(Выручка	- Себ)*100/Себ),2,1);
		
		Если (ВклПриб<>0) И ( (ВклПрибОт<>0) Или (ВклПрибДо<>0) ) Тогда	
			// проверяем интервальный фильтр по проценту прибыли
			ПроцПриб = ?(СпособФильтрацииПоПроцПрибыли="количественная",ОтобранныеДанные.КолПрибыль,ОтобранныеДанные.ДенежнаяПрибыль);
			
			Если ( (ВклПрибОт<>0) И (ПрибОт>ПроцПриб) ) Или ( (ВклПрибДо<>0) И (ПрибДо<ПроцПриб) ) Тогда
				Продолжить;
			КонецЕсли;	
		КонецЕсли;
		
		// выводим группы товара
		Пока СписокГруппТоваров.РазмерСписка()>0 Цикл
			ГруппаТоваров = СписокГруппТоваров.ПолучитьЗначение(1);
			Если Товар.ПринадлежитГруппе(ГруппаТоваров)=1 Тогда
				// формируем смещение группы в соответствии с иерархией
				Наим = глСмещениеГруппы(ГруппаТоваров,0) + ГруппаТоваров;
				// выводим строку в таблицу
				Таб.ВывестиСекцию("ГруппаТоваров"); 
				глОживить(1);
			КонецЕсли;
			СписокГруппТоваров.УдалитьЗначение(1);
		КонецЦикла;
		
		// формируем смещение товара в соответствии с иерархией
		Наим = глСмещениеГруппы(Товар,0) + глТоварВОтчете(Товар);
		                                           
		ПечКолПродано 			= глФРМКоличествоВОтчете(ОтобранныеДанные.Кол,					Товар); 
		ПечКолВыручкаПродано 	= глФРМКоличествоВОтчете(ОтобранныеДанные.КоличественнаяВыручка,Товар); 
		ПечСебПродано 	= глФРМ(ОтобранныеДанные.Себ,		 Гривня,1); 
		ПечОборот 		= глФРМ(ОтобранныеДанные.Оборот,	 Гривня,1); 
		ПечВыручка 		= глФРМ(ОтобранныеДанные.Выручка,	 Гривня,1);
		ПечДопВложения	= глФРМ(ОтобранныеДанные.ДопВложения,Гривня,1);
		
		Если (ПустоеЗначение(Товар.ЕдиницаПоУмолчанию.Коэффициент) = 0) И (Товар.ЕдиницаПоУмолчанию.Коэффициент <> 0) Тогда
			ПечЗакупЦена = глФРМ(ОтобранныеДанные.ЗакупЦена/Товар.ЕдиницаПоУмолчанию.Коэффициент,Гривня,1); 
		КонецЕсли;
		
		ПечДенПрибыль 	= ?(ОтобранныеДанные.ДенежнаяПрибыль=0,	"",Шаблон("[ОтобранныеДанные.ДенежнаяПрибыль#Ч7.2] %"));         
		ПечКолПрибыль 	= ?(ОтобранныеДанные.КолПрибыль=0,		"",Шаблон("[ОтобранныеДанные.КолПрибыль#Ч7.2] %"));         
		
		// выводим строку по товару
		Таб.ВывестиСекцию("Товар"); 
		глОживить(1);
		
	КонецЦикла;                                        
	
	Если фДиаграмма = 1  Тогда
		Таб.ВывестиСекцию("Диаграмма"); 
		Таб.ВывестиСекцию("ДиаграммаАбсПрибылей");
	КонецЕсли;	
	
	ФиксКвоСтрок = Таб.ВысотаСекции("Кнопки")+Таб.ВысотаСекции("Отчет")+Таб.ВысотаСекции("Шапка");
	ФиксКвоКолон = 1;
	Таб.Опции(0, 0, ФиксКвоСтрок, ФиксКвоКолон, "ПарамПечатиУпрОценкаПрибыльности", "РазмОкнаУпрОценкаПрибыльности");
	Таб.ОбластьПечати(Таб.ВысотаСекции("Кнопки")+1);

	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Таб.Показать("ПЕЧАТЬ: Оценка прибыльности товаров ("+ПериодСтр(Дата1, Дата2)+?(ТипЗначения(ВыбФирма)=0, "", ", "+ВыбФирма)+")","");

	Если (Обновить = 2) Или (ЗакрытьЭкран=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры    	// Сформировать


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение="")	// Предопределенная процедура
	// закладки
    Если Номер=1 Тогда
		Форма.ИспользоватьСлой("Шапка,Основной,Подвал");
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
    ИначеЕсли Номер=2 Тогда
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;      
	ПерерисовкаНазванийЗакладок();
	ДоступностьЭлементов(1);
КонецПроцедуры	// ПриВыбореЗакладки

// ===============================
Процедура ОбработкаПодбора(Значение)  // Предопределенная процедура
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
КонецПроцедуры  // ОбработкаПодбора

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	                           
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки отчета из списка
		Дата1 	= глРасшифровка.Получить("Дата1");
		Дата2 	= глРасшифровка.Получить("Дата2");

		ВыбФирма = глРасшифровка.Получить("ВыбФирма");
		ВыбТовар = глРасшифровка.Получить("ВыбТовар");
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;

		фДиаграмма 				= глРасшифровка.Получить("фДиаграмма");
		фОграничитьЧислоТочек 	= глРасшифровка.Получить("фОграничитьЧислоТочек");
		МаксКоличествоТочек 	= глРасшифровка.Получить("МаксКоличествоТочек");
		ДеньгиКоличество.ТекущаяСтрока(глРасшифровка.Получить("ДеньгиКоличество")); 

		ОплатаОтгрузка.ТекущаяСтрока(глРасшифровка.Получить("ОплатаОтгрузка"));  
		ЦенаЗакупки.ТекущаяСтрока(глРасшифровка.Получить("ЦенаЗакупки")); 
		ЦенаИзСправочника = глРасшифровка.Получить("ЦенаИзСправочника");
		                                                                
		ВклВО 		= глРасшифровка.Получить("ВклВО");                          
        ВалютаВО	= глРасшифровка.Получить("ВалютаВО");                          
		ВклВООт 	= глРасшифровка.Получить("ВклВООт");                          
		ВклВОДо 	= глРасшифровка.Получить("ВклВОДо");                          
		ВООт 		= глРасшифровка.Получить("ВООт");                          
		ВОДо 		= глРасшифровка.Получить("ВОДо");                          
		ОборотВыручка.ТекущаяСтрока(глРасшифровка.Получить("ОборотВыручка")); 

		ВклПриб 	= глРасшифровка.Получить("ВклПриб");                          
        ВклПрибОт 	= глРасшифровка.Получить("ВклПрибОт");                          
		ВклПрибДо 	= глРасшифровка.Получить("ВклПрибДо");                          
		ПрибОт 		= глРасшифровка.Получить("ПрибОт");                          
		ПрибДо 		= глРасшифровка.Получить("ПрибДо");       
		КолСуммаПриб.ТекущаяСтрока(глРасшифровка.Получить("КолСуммаПриб")); 

		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
	
	ТаблицаМФ.ВидимостьКолонки("Тип",				0);
	ТаблицаМФ.ВидимостьКолонки("Вид",				0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",	0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",				0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",		0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	                                                                                
	ПриВыбореЗакладки(1);
	
КонецПроцедуры		// ПриОткрытии

// ===============================
Процедура ВводНового()	// Предопределенная процедура
	// эта предопределенная процедура выполняется при восстановлении настройки
	ТекСтрокаВТаблицеМФ="";
	ПриВыбореЗакладки(1);
КонецПроцедуры		// ВводНового


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================
                                     
// инициализация границ периодов
Дата2=ПолучитьДатуТА(); 
Дата1=глВосстановитьЗначение(,"ОсновнаяДатаНачалаОтчета");
Если ПустоеЗначение(Дата1)=0 Тогда
	Дата1=НачМесяца(Дата1);
КонецЕсли;         

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");

//                  			тип			вид					переменная  	название
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ТМЦ",				"Товар",	"По номенклатуре");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",	"Товар",	"По категориям номенклатуры");

ТекСтрокаВТаблицеМФ="";

// инициализация списков	                       
ОплатаОтгрузка.УдалитьВсе();
ОплатаОтгрузка.ДобавитьЗначение("оплата","оплате");
ОплатаОтгрузка.ДобавитьЗначение("отгрузка","отгрузке"); 
ОплатаОтгрузка.ТекущаяСтрока(1);

ЦенаЗакупки.УдалитьВсе();
ЦенаЗакупки.ДобавитьЗначение("изсправочника","из справочника");
ЦенаЗакупки.ДобавитьЗначение("учетная","учетной");
ЦенаЗакупки.ДобавитьЗначение("последнегоприобретения","последнего приобретения"); 
ЦенаЗакупки.ТекущаяСтрока(1);

ОборотВыручка.УдалитьВсе();
ОборотВыручка.ДобавитьЗначение("оборот","оборота");
ОборотВыручка.ДобавитьЗначение("выручка","выручки");
ОборотВыручка.ТекущаяСтрока(1);

КолСуммаПриб.УдалитьВсе();
КолСуммаПриб.ДобавитьЗначение("количественная","количественной");
КолСуммаПриб.ДобавитьЗначение("денежная","денежной");
КолСуммаПриб.ТекущаяСтрока(1);

ДеньгиКоличество.УдалитьВсе();
ДеньгиКоличество.ДобавитьЗначение("Деньги","денежная");
ДеньгиКоличество.ДобавитьЗначение("Количество","количественная");
ДеньгиКоличество.ТекущаяСтрока(1);

ВидДиаграммы.УдалитьВсе();
ВидДиаграммы.ДобавитьЗначение("Деньги",				"Денежная");
ВидДиаграммы.ДобавитьЗначение("Количество",			"Количественная");
ВидДиаграммы.ДобавитьЗначение("ДеньгиКоличество",	"Смешанная");

ВалютаВО = Гривня;
