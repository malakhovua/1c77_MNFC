// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ======================================
Процедура ИзмПоВсемРС()
	Если ПоВсемРС = 1 Тогда
		РСчет = 0;
		Форма.РСчет.Доступность(0);
	Иначе
		РСчет = Фирма.РС;
		Форма.РСчет.Доступность(1);
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура Сформировать()
	Если Фирма.Выбран() = 0 Тогда
		глКомментарий("Не выбрана фирма",0,,"!");
		Возврат;
	КонецЕсли;
	                    
	Если глПроверкаИнтервалаОтчета(Дата1,Дата2,1) = 0 Тогда
	    Возврат;
	КонецЕсли;

	Фирма.ИспользоватьДату(Дата2);
	
	// РС - расчетный счет, который печатается в форме,
	// если установлено "Пл всем РС", Тогда выведем основной счет
	Если ПоВсемРС = 1 Тогда
		РС = Фирма.РС;
	Иначе
		Если РСчет.Выбран() = 0 Тогда
			глКомментарий("Не выбран расчетный счет",0,,"!");
			Возврат;
		КонецЕсли;
		РС = РСчет;
	КонецЕсли;

	ПК = Фирма.ЕДРПОУ;
	
	ПКД1 = Сред(ПК, 1, 1); 
	ПКД2 = Сред(ПК, 2, 1); 
	ПКД3 = Сред(ПК, 3, 1); 
	ПКД4 = Сред(ПК, 4, 1); 
	ПКД5 = Сред(ПК, 5, 1); 
	ПКД6 = Сред(ПК, 6, 1); 
	ПКД7 = Сред(ПК, 7, 1); 
	ПКД8 = Сред(ПК, 8, 1); 
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ПовторятьПриПечатиСтроки(27,27);
	Таб.ВывестиСекцию("Шапка");
	
	тбДок = СоздатьОбъект("ТаблицаЗначений");
	тбДок.НоваяКолонка("ДатаДок","Дата");
	тбДок.НоваяКолонка("Док");
	тбДок.НоваяКолонка("НомерДокумента","Число",10,0);
	тбДок.НоваяКолонка("НомерДокументаСтр","Строка");
	тбДок.НоваяКолонка("Ст3","Число",12,2);
	тбДок.НоваяКолонка("Ст4","Число",12,2);
	тбДок.НоваяКолонка("Ст5","Число",12,2);
	тбДок.НоваяКолонка("Ст6","Число",12,2);
	тбДок.НоваяКолонка("Ст7","Число",12,2);
	
	ИСт3 = 0;
	ИСт4 = 0;
	ИСт5 = 0;
	ИСт6 = 0;
	ИСт7 = 0;
	
	// банковские выписки за интервал	
	БВ = СоздатьОбъект("Документ.БанковскаяВыписка");
	БВ.ВыбратьДокументы(Дата1,Дата2);
	Пока БВ.ПолучитьДокумент() = 1 Цикл
		Если БВ.Фирма = Фирма Тогда
			Если (БВ.Проведен() = 0) или (БВ.ПометкаУдаления() = 1) Тогда
			    Продолжить;
			КонецЕсли;
			Если (ПоВсемРС = 0) и (БВ.РСчет <> РСчет) Тогда
			    Продолжить;
			КонецЕсли;
			
			БВ.ВыбратьСтроки();
			Пока БВ.ПолучитьСтроку() = 1 Цикл
				тбДок.НоваяСтрока();
				
				Если БВ.ВидПриходаРасхода = Перечисление.ВидыПриходаДенег.ВыручкаОтРеализации Тогда
				    тбДок.Ст3 = глПересчет(БВ.СуммаСНДС, БВ.Рсчет.Валюта, Гривня, БВ.ДатаДок);
					ИСт3 = ИСт3 + тбДок.Ст3;
				ИначеЕсли БВ.ВидПриходаРасхода = Перечисление.ВидыПриходаДенег.ВыручкаОтРеализацииОС Тогда
				    тбДок.Ст4 = глПересчет(БВ.СуммаСНДС, БВ.Рсчет.Валюта, Гривня, БВ.ДатаДок);
					ИСт4 = ИСт4 + тбДок.Ст4;
				ИначеЕсли БВ.ВидПриходаРасхода = Перечисление.ВидыПриходаДенег.ВыручкаОтПрочейРеализации Тогда
				    тбДок.Ст5 = глПересчет(БВ.СуммаСНДС, БВ.Рсчет.Валюта, Гривня, БВ.ДатаДок);
					ИСт5 = ИСт5 + тбДок.Ст5;
				ИначеЕсли БВ.ВидПриходаРасхода = Перечисление.ВидыРасходаДенег.НаВедениеХозДеятельности Тогда
				    тбДок.Ст7 = глПересчет(БВ.СуммаСНДС, БВ.Рсчет.Валюта, Гривня, БВ.ДатаДок);
					ИСт7 = ИСт7 + тбДок.Ст7;
				Иначе
					тбДок.УдалитьСтроку();
					Продолжить;
				КонецЕсли;
				тбДок.Ст6 = тбДок.Ст3 + тбДок.Ст4 + тбДок.Ст5;
				ИСт6 = ИСт6 + тбДок.Ст6;
				                      
				тбДок.Док = БВ.ТекущийДокумент();
				тбДок.ДатаДок = БВ.ДатаДок;
				тбДок.НомерДокумента = Число(БВ.НомерДокумента);
				тбДок.НомерДокументаСтр = "ПД "+Формат(БВ.ДатаДок,"ДДММГГГГ")+", № "+БВ.НомерДокумента;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла; // банковская выписка
	БВ = 0;

	// приходные ордера за интервал	
	ПКО = СоздатьОбъект("Документ.ПриходныйКассовый");
	ПКО.ВыбратьДокументы(Дата1,Дата2);
	Пока ПКО.ПолучитьДокумент() = 1 Цикл
		Если ПКО.Фирма = Фирма Тогда
			Если (ПКО.Проведен() = 0) или (ПКО.ПометкаУдаления() = 1) Тогда
			    Продолжить;
			КонецЕсли;
			Если (ПоВсемРС = 0) и (ПКО.РСчет <> РСчет) Тогда
			    Продолжить;
			КонецЕсли;
			
			тбДок.НоваяСтрока();
			
			Если ПКО.ВидПриходаДенег = Перечисление.ВидыПриходаДенег.ВыручкаОтРеализации Тогда
			    тбДок.Ст3 = глПересчет(ПКО.СуммаВал, ПКО.РСчет.Валюта, Гривня, ПКО.ДатаДок);
				ИСт3 = ИСт3 + тбДок.Ст3;
			ИначеЕсли ПКО.ВидПриходаДенег = Перечисление.ВидыПриходаДенег.ВыручкаОтРеализацииОС Тогда
			    тбДок.Ст4 = глПересчет(ПКО.СуммаВал, ПКО.РСчет.Валюта, Гривня, ПКО.ДатаДок);
				ИСт4 = ИСт4 + тбДок.Ст4;
			ИначеЕсли ПКО.ВидПриходаДенег = Перечисление.ВидыПриходаДенег.ВыручкаОтПрочейРеализации Тогда
			    тбДок.Ст5 = глПересчет(ПКО.СуммаВал, ПКО.РСчет.Валюта, Гривня, ПКО.ДатаДок);
				ИСт5 = ИСт5 + тбДок.Ст5;
			Иначе
				тбДок.УдалитьСтроку();
				Продолжить;
			КонецЕсли;
			тбДок.Ст6 = тбДок.Ст3 + тбДок.Ст4 + тбДок.Ст5;
			ИСт6 = ИСт6 + тбДок.Ст6;
			                                                    
			тбДок.Док = ПКО.ТекущийДокумент();
			тбДок.ДатаДок = ПКО.ДатаДок;
			тбДок.НомерДокумента = ПКО.НомерПО;
			тбДок.НомерДокументаСтр = "ПКО "+Формат(ПКО.ДатаДок,"ДДММГГГГ")+", № "+ПКО.НомерПО;
		КонецЕсли;
	КонецЦикла; // ПКО
	ПКО = 0;
	
	// расходные ордера за интервал	
	РКО = СоздатьОбъект("Документ.РасходныйКассовый");
	РКО.ВыбратьДокументы(Дата1,Дата2);
	Пока РКО.ПолучитьДокумент() = 1 Цикл
		Если РКО.Фирма = Фирма Тогда
			Если (РКО.Проведен() = 0) или (РКО.ПометкаУдаления() = 1) Тогда
			    Продолжить;
			КонецЕсли;
			Если (ПоВсемРС = 0) и (РКО.РСчет <> РСчет) Тогда
			    Продолжить;
			КонецЕсли;
			
			Если РКО.ВидРасходаДенег = Перечисление.ВидыРасходаДенег.НаВедениеХозДеятельности Тогда
				тбДок.НоваяСтрока();
			    тбДок.Ст7 = глПересчет(РКО.СуммаВал, РКО.РСчет.Валюта, Гривня, РКО.ДатаДок);
				ИСт7 = ИСт7 + тбДок.Ст7;
			Иначе
				Продолжить;
			КонецЕсли;
			                        
			тбДок.Док = РКО.ТекущийДокумент();
			тбДок.ДатаДок = РКО.ДатаДок;
			тбДок.НомерДокумента = РКО.НомерРО;
			тбДок.НомерДокументаСтр = "РКО "+Формат(РКО.ДатаДок,"ДДММГГГГ")+", № "+РКО.НомерРО;
		КонецЕсли;
	КонецЦикла; // РКО
	РКО = 0;
	
	// теперь выведем накопленную информацию, отсортировав ее по дате и номеру документа
	Ном = ПервыйНом;
	
	тбДок.Сортировать("+ДатаДок,НомерДокумента");
	тбДок.ВыбратьСтроки();                       
	Пока тбДок.ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");
		Ном = Ном+1;
	КонецЦикла;
	тбДок = 0;
	
	Таб.ВывестиСекцию("Дно");
	Таб.Опции(0,0,,,"ПарамПечатиУпрКнигаДоходовРасходов","РазмОкнаУпрКнигаДоходовРасходов");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("ПЕЧАТЬ: Книга доходов и расходов ("+ПериодСтр(Дата1,Дата2)+", "+Фирма+")", "");
	
	глСохранитьЗначение("КнигаДоходовРасходов","ПервыйНом",Ном);
КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Фирма = глВосстановитьЗначение(,"БазФирма");
	Если Метаданные.РазделительУчета.Выбран() = 0 Тогда
		Форма.Фирма.Видимость(0);
		Форма.тФирма.Видимость(0);
	КонецЕсли;
	РСчет = Фирма.РС;
	ПоВсемРС = 0;
	
	Дата1 = РабочаяДата();
	Дата2 = РабочаяДата();
	ПервыйНом = Макс(Число(глВосстановитьЗначение("КнигаДоходовРасходов","ПервыйНом")),1);
КонецПроцедуры

