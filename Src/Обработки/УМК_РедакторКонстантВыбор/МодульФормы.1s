Перем Парам, Комментарий;
Перем Маска;
Перем ИмяФайла;
//*******************************************
Функция ВыборФайла(Что)
	Перем СтрФайл, ИмяПути;
	Если ПустаяСтрока(ИмяФайла)=0 Тогда
		СтрФайл=ИмяФайла;
		ИмяПути="";
		Пока Найти(СтрФайл,"\")>0 Цикл
			ИмяПути=ИмяПути+Лев(СтрФайл,Найти(СтрФайл,"\"));
			СтрФайл=Прав(СтрФайл,СтрДлина(СтрФайл)-Найти(СтрФайл,"\"));
		КонецЦикла;
	Иначе
		РасположениеФайла(ИмяПути,СтрФайл);
		СтрФайл="_Data";
	КонецЕсли;
	Если ФС.ВыбратьФайл(Что,СтрФайл,ИмяПути,"Выберите файл:","Все файлы (*.*) |*.*",,)=1 Тогда 
		ИмяФайла=СокрЛП(ИмяПути+СтрФайл);
		Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции 
//*******************************************
Процедура ВФайл()
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	Если (ТипЗнач="Строка")	и
		 (Многострочные=1)	тогда
		Что=ДлСтр;
	Иначе
		Что=Данные;
	КонецЕсли;
 	Если ВыборФайла(1)=1 Тогда
 		Если ТипФорматаФайла=3 тогда
 			Текст=СоздатьОбъект("Текст");
 			Текст.ДобавитьСтроку(Что);
 			Текст.Записать(ИмяФайла);
 		Иначе
			ЗначениеВФайл(ИмяФайла,Что,ТипФорматаФайла);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура Конвертор()
	Если Тип=2 тогда
		ДКод=_StrToID(СокрЛП(ДКод));
	Иначе
		ДКод=_IdToStr(Число(ДКод));
	КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура СформироватьID()
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	Если (ТипЗнач="Справочник") или (ТипЗнач="Документ") тогда
		Код=СокрЛП(ВнутрКод);
		Код=Прав(Код,15);
		Код=Лев(Код,10);
		ВНКод=_IdToStr(Число(Код));
		Код=СокрЛП(ВнутрКод);
		Код=Прав(Код,5);
		ВНКод=СокрЛП(ВНКод)+Лев(Код,3);
	Иначе
		ВНКод="";
		Возврат;
	КонецЕсли;
	//ВидЗнач=СоздатьОбъект("СписокЗначений");
	//Код=СокрЛП(ВнутрКод);
	//Код=Прав(Код,СтрДлина(Код)-1);
	//Код=Лев (Код,СтрДлина(Код)-1);
	//ВидЗнач.ИзСтрокиСРазделителями(Код);
	//Код=ВидЗнач.ПолучитьЗначение(4);
	//Если Код=0 тогда
	//	Форма.ТД.Доступность(0);
	//	Форма.Поле.Доступность(0);
	//	Форма.ВНКод.Доступность(0);
	//	Возврат;
	//КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура Сформировать()

//	ТипЗнач=ТипЗначенияСтр(Данные);
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	Если (ТипЗнач="Строка")	и
		 (Многострочные=1)	тогда
		Парам.Установить("Элемент",ДлСтр);
		Парам.Установить("Точность",Многострочные);
	Иначе
		Парам.Установить("Элемент",Данные);
		Парам.Установить("Точность",ТочностьЧисел);
	КонецЕсли;
	Если (ПустаяСтрока(ТипЗнач)=0)	и
		 (ТипЗнач<>"Число")			и
		 (ТипЗнач<>"Дата")			и
		 (ТипЗнач<>"Строка")		и
		 (ТипЗнач<>"ВидРасчета")	и
		 (ТипЗнач<>"Календарь")		и
		 (ТипЗнач<>"ВидСубконто")	и
		 (ТипЗнач<>"ПланСчетов")	тогда
//		ВидЗнач=Данные.Вид();
		ВидЗнач=ВидДанных.ПолучитьЗначение(ВидДанных.ТекущаяСтрока());
	Иначе
		ВидЗнач="";
	КонецЕсли;
	Парам.Установить("Тип",ТипЗнач);
	Парам.Установить("Вид",ВидЗнач);
	Парам.Установить("Длина",ДлинаЧисел);
	Парам.Установить("Многострочные",Многострочные);
	Парам.Установить("Результат","OK");
//	Форма.Закрыть(0);
	СтрокаДействийФормы="Закрыть";
КонецПроцедуры
//*******************************************
Процедура Видимость()
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	Если ТипЗнач="Строка" тогда
		Форма.флБазовый.Доступность(1);
		Что=1;
		Если Многострочные=1 тогда
			 Вид=0;
		Иначе
			 Вид=1;
		КонецЕсли;
	 	Форма.тРодитель.Видимость(0);
	 	Форма.оРодителе.Видимость(0);
	 	Форма.тВладелец.Видимость(0);
	 	Форма.оВладельце.Видимость(0);
	Иначе
		Форма.флБазовый.Доступность(0);
		Если ТипЗнач="Справочник" тогда
		 	Что=1;
		Иначе
		 	Что=0;
		КонецЕсли;
	 	Форма.тРодитель.Видимость(Что);
	 	Форма.тВладелец.Видимость(Что);
	 	Форма.оВладельце.Видимость(Что);
	 	Форма.оРодителе.Видимость(Что);
		Что=0;
		Вид=1;
	КонецЕсли;
 	Форма.Данные.Видимость(Вид);
 	Форма.ДлСтр.Видимость(?(Вид=0,1,0));
	Форма.ФсФайл.Видимость(Что*Вид);
 	Форма.ФсФайл_1.Видимость(Что*Вид);
	Форма.ФсФайл_2.Видимость(Что*Вид);
	Форма.Маска.Видимость(Что*Вид);
	Форма.ЗагМаска.Видимость(Что*Вид);
	Форма.Обновить(0);
КонецПроцедуры
//*******************************************
Процедура ТипВид(ТипЗнач, ВидЗнач="")
	Если ТипЗнач="ПланСчетов" тогда
		//не понятно, почему .НазначитьТип() не работет
		Форма.Данные.УстановитьТип(ЗначениеИзСтроки("{"+"""ПланСчетов"""+"}"""));
		// а вот не может функция 'ПолучитьПустоеЗначение("ПланСчетов")' вернуть пустой план счетов!(?)
		//релиз 7.70.017 для SQL
	ИначеЕсли ТипЗнач="Счет" тогда
		Форма.Данные.НазначитьТип(ТипЗнач+?(ПустаяСтрока(ВидЗнач)=1,"","."+ВидЗнач),,);
		Данные.ИспользоватьПланСчетов(ВидЗнач);
	ИначеЕсли (ТипЗнач="ВидСубконто") и (ПустоеЗначение(ВидЗнач)=0) тогда
		Форма.Данные.НазначитьТип(ВидЗнач.ТипСубконто());
	ИначеЕсли ТипЗнач="Число" Тогда
		Форма.Данные.НазначитьТип(ТипЗнач,ДлинаЧисел,ТочностьЧисел);
	ИначеЕсли ТипЗнач="Строка" Тогда
		Форма.Данные.НазначитьТип(ТипЗнач,ДлинаЧисел,Многострочные);
	Иначе
		Форма.Данные.НазначитьТип(ТипЗнач+?(ПустаяСтрока(ВидЗнач)=1,"","."+ВидЗнач),,);
	КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура СписокВидов()
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());

	Форма.Данные.Доступность(1);
	Форма.ВидДанных.Доступность(0);

	Форма.КнВыбор.Доступность(0);
	Форма.КнРед.Доступность(0);
 	Форма.КнХ.Доступность(0);
 	Форма.КнПросм.Доступность(0);

	ВидДанных.УдалитьВсе();
	ВидДанных.ДобавитьЗначение("");
	Если ТипЗнач="Перечисление" Тогда
		Форма.ВидДанных.Доступность(1);
		Для i=1 по Метаданные.Перечисление() цикл
			ВидДанных.ДобавитьЗначение(Метаданные.Перечисление(i).Идентификатор);
		КонецЦикла;
	ИначеЕсли ТипЗнач="Справочник" Тогда
		Форма.КнВыбор.Доступность(1);
		Форма.КнРед.Доступность(1);
	 	Форма.КнХ.Доступность(1);
	 	Форма.КнПросм.Доступность(1);
		Форма.ВидДанных.Доступность(1);
		Для i=1 по Метаданные.Справочник() цикл
			ВидДанных.ДобавитьЗначение(Метаданные.Справочник(i).Идентификатор);
		КонецЦикла;
	ИначеЕсли ТипЗнач="Документ" Тогда
		Форма.КнВыбор.Доступность(1);
		Форма.КнРед.Доступность(1);
	 	Форма.КнХ.Доступность(1);
	 	Форма.КнПросм.Доступность(1);
		Форма.ВидДанных.Доступность(1);
		Для i=1 по Метаданные.Документ() цикл
			ВидДанных.ДобавитьЗначение(Метаданные.Документ(i).Идентификатор);
		КонецЦикла;
	ИначеЕсли ТипЗнач="Операция" тогда
		Форма.ВидДанных.Доступность(0);
	ИначеЕсли ТипЗнач="Счет" тогда
		//Попытка
		// вырубает "1С" напрочь
		// пусть уж просто ругается...
		Для i=1 по ПланыСчетов.КоличествоЗначений() цикл
			Мета=ПланыСчетов.ЗначениеПоНомеру(i);
			Значен=Метаданные.ПланСчетов(Мета.Идентификатор());
			ВидДанных.ДобавитьЗначение(Мета,Значен.Идентификатор);
		КонецЦикла;
		Форма.ВидДанных.Доступность(1);
		//Исключение
		// если нет ниодного плана счетов
		//КонецПопытки;
	ИначеЕсли ТипЗнач="ПланСчетов" тогда
		Форма.ВидДанных.Доступность(0);
	ИначеЕсли ТипЗнач="ВидРасчета" тогда
		Форма.ВидДанных.Доступность(0);
	ИначеЕсли ТипЗнач="ВидСубконто" тогда
		Попытка
			Для i=1 по ВидыСубконто.КоличествоЗначений() цикл
				Мета=ВидыСубконто.ЗначениеПоНомеру(i);
				Значен=Метаданные.ВидСубконто(Мета.Идентификатор());
				ВидДанных.ДобавитьЗначение(Мета,Значен.Идентификатор);
			КонецЦикла;
			Форма.ВидДанных.Доступность(1);
		Исключение
		// если нет ниодного вида субконто
			Форма.ВидДанных.Доступность(0);
			Форма.Данные.Доступность(0);
			Возврат;
		КонецПопытки;
	ИначеЕсли ТипЗнач="Календарь" тогда

//	ИначеЕсли Тип="Регистр" тогда
//	ИначеЕсли Тип="ЖурналРасчетов" тогда
//	ИначеЕсли Тип="Операция" тогда
//	ИначеЕсли Тип="КорректныеПроводки" тогда
//	ИначеЕсли Тип="БухгалтерскиеИтоги" тогда
//	ИначеЕсли Тип="Таблица" тогда
//	ИначеЕсли Тип="Текст" тогда
//	ИначеЕсли Тип="Запрос" тогда
//	ИначеЕсли Тип="СписокЗначений" тогда
//	ИначеЕсли Тип="ТаблицаЗначений" тогда
//	ИначеЕсли Тип="Периодический" тогда
//	ИначеЕсли Тип="Картинка" тогда
//	ИначеЕсли Тип="ГрупповойКонтекст" тогда
//	ИначеЕсли Тип="OLE" тогда
//	ИначеЕсли Тип="НеизвестныйОбьект" тогда
	Иначе //Если ТипЗнач="" тогда
	КонецЕсли;
	ВидДанных.ТекущаяСтрока(1);
	Видимость();
КонецПроцедуры
//*******************************************
Процедура ТекВид(ТипЗнач, ВидЗнач)
	Если ПустаяСтрока(ТипЗнач)=0 тогда
		Поз=ТипДанных.НайтиЗначение(ТипЗначенияСтр(Данные));
		Если Поз>0 тогда
			ТипДанных.ТекущаяСтрока(Поз);
		Иначе
			ТипДанных.ТекущаяСтрока(1);
		КонецЕсли;

		СписокВидов();
		Поз=ВидДанных.НайтиЗначение(ВидЗнач);
		Если Поз>0 тогда
			ВидДанных.ТекущаяСтрока(Поз);
		Иначе
			ВидДанных.ТекущаяСтрока(1);
		КонецЕсли;
	Иначе
		ТипДанных.ТекущаяСтрока(1);
		ВидДанных.ТекущаяСтрока(1);
	КонецЕсли;
	Видимость();
КонецПроцедуры
//*******************************************
Процедура СформироватьЗначение(Что)
	ТипЗнач=ТипЗначенияСтр(Что);
	Если (ПустаяСтрока(ТипЗнач)=0)	и
		 (ТипЗнач<>"Дата")			и
		 (ТипЗнач<>"Число")			и
		 (ТипЗнач<>"Строка")		и
		 (ТипЗнач<>"ВидРасчета")	и
		 (ТипЗнач<>"Календарь")		и
		 (ТипЗнач<>"ВидСубконто")	и
		 (ТипЗнач<>"ПланСчетов")	и
		 (ТипЗнач<>"СписокЗначений")	и
		 (ТипЗнач<>"ТаблицаЗначений")	тогда
		ВидЗнач=Что.Вид();
	Иначе
		ВидЗнач="";
	КонецЕсли;
	ТипВид(ТипЗнач, ВидЗнач);
	ТекВид(ТипЗнач, ВидЗнач);
	Данные=Что;
КонецПроцедуры
//*******************************************
Процедура СформироватьКоды()
	ВнутрКод=ЗначениеВСтрокуВнутр(Данные);
	Значение=ЗначениеВСтроку(Данные);
	СформироватьID();
	Попытка
		Что="Родитель: "+Данные.Родитель;
	Исключение
		Что="";
	КонецПопытки;
 	Форма.тРодитель.Заголовок(Что);
	Попытка
		Что="Владелец: "+Данные.Владелец;
	Исключение
		Что="";
	КонецПопытки;
 	Форма.тВладелец.Заголовок(Что);
КонецПроцедуры
//*******************************************
Процедура ПриОткрытии()
	Перем ТипЗнач, ВидЗнач;
	ТипДанных.ДобавитьЗначение("");
	ТипДанных.ДобавитьЗначение("Число");
	ТипДанных.ДобавитьЗначение("Строка");
	ТипДанных.ДобавитьЗначение("Дата");
	Если Метаданные.Перечисление()>0 тогда
		ТипДанных.ДобавитьЗначение("Перечисление");
	КонецЕсли;
	Если Метаданные.Справочник()>0 тогда
		ТипДанных.ДобавитьЗначение("Справочник");
	КонецЕсли;
	Если Метаданные.Документ()>0 тогда
		ТипДанных.ДобавитьЗначение("Документ");
	КонецЕсли;
	Если Метаданные.ПланСчетов()>0 тогда
		ТипДанных.ДобавитьЗначение("Счет");
//		ТипДанных.ДобавитьЗначение("Операция");
		ТипДанных.ДобавитьЗначение("ПланСчетов");
	КонецЕсли;
	Если Метаданные.ЖурналРасчетов()>0 тогда
		ТипДанных.ДобавитьЗначение("ЖурналРасчетов");
	КонецЕсли;
	Если Метаданные.ВидРасчета()>0 тогда
		ТипДанных.ДобавитьЗначение("ВидРасчета");
	КонецЕсли;
	Если Метаданные.Календарь()>0 тогда
		ТипДанных.ДобавитьЗначение("Календарь");
	КонецЕсли;
	Если Метаданные.ВидСубконто()>0 тогда
		ТипДанных.ДобавитьЗначение("ВидСубконто");
	КонецЕсли;
	ТипДанных.ТекущаяСтрока(1);

	Парам=Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="СписокЗначений" тогда
		Парам.Установить("Результат","NO");
		ДлинаЧисел=Парам.Получить("Длина");
		ТочностьЧисел=Парам.Получить("Точность");
		Многострочные=Парам.Получить("Многострочные");
		Комментарий=Парам.Получить("Комментарий");
		ТипЗнач=Парам.Получить("Тип");
		ВидЗнач=Парам.Получить("Вид");
		Что=Парам.Получить("Элемент");
		Если ПустоеЗначение(ТипЗнач)=0 тогда
			Если ТипЗнач="Неопределенный" тогда
				ТипЗнач="";
			Иначе
				Форма.ТипДанных.Доступность(0);
			КонецЕсли;
			Если ТипЗнач="Число" тогда
				Поз=СтрДлина(Что);
				Если ДлинаЧисел<Поз тогда
				 	ДлинаЧисел=СтрДлина(Что);
				КонецЕсли;
				Поз=Найти(Строка(Что),".");
				Если ТочностьЧисел>Поз тогда
				 	ТочностьЧисел=Поз;
				КонецЕсли;
				 
			ИначеЕсли ТипЗнач="Строка" тогда
				Поз=СтрДлина(Что);
				Если ДлинаЧисел<Поз тогда
				 	ДлинаЧисел=Поз;
				КонецЕсли;
				Поз=СтрКоличествоСтрок(Что);
				Если Поз>1 тогда
				 	Многострочные=1;
				 Иначе
				 	Многострочные=0;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТипЗнач="";
		КонецЕсли;
		Если ПустоеЗначение(ВидЗнач)=0 тогда
			Форма.ВидДанных.Доступность(0);
		КонецЕсли;
		Если ПустоеЗначение(Что)=1 тогда
			Что=ПолучитьПустоеЗначение(ТипЗнач+?(ПустаяСтрока(ВидЗнач)=1,"","."+ВидЗнач));
		КонецЕсли;
		Если (ТипЗнач="Строка") и (Многострочные=1) тогда
			ДлСтр=Что;
		Иначе
			Данные=Что;
		КонецЕсли;
		СформироватьЗначение(Что);
		СформироватьКоды();
		Маска=Парам.Получить("Маска");
		ФсФайл_1=Парам.Получить("ТипФайла");
		Активизировать("Данные",);
	Иначе
		Комментарий="Форма ввода любых значений";
		Если Форма.МодальныйРежим()=1 тогда
			Предупреждение("Выбор осуществить невозможно!",3);
			СтатусВозврата(0);
		КонецЕсли;
	КонецЕсли;
	Тип=2;
	ТипФорматаФайла=1;
	Если ФсФайл_1=0 тогда
		ФсФайл_1=1;
	КонецЕсли;
	Если Форма.МодальныйРежим()=0 тогда
		Форма.КнOK.Видимость(0);
	КонецЕсли;

КонецПроцедуры
//*******************************************
Процедура ХитрыйВыбор()
	Перем Что, Имя;
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	Если ПустаяСтрока(ТипЗнач)=0 тогда
		Если (ТипЗнач<>"Справочник")	и
			 (ТипЗнач<>"Документ")		и
			 (ТипЗнач<>"ЖурналРасчетов")тогда
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	ВидЗнач=ВидДанных.ПолучитьЗначение(ВидДанных.ТекущаяСтрока());
	Список = СоздатьОбъект("СписокЗначений");
	Список.ДобавитьЗначение(Данные, "Элемент");
	Список.ДобавитьЗначение(ТипЗнач, "Тип");
	Список.ДобавитьЗначение(ВидЗнач, "Вид");
	Список.ДобавитьЗначение(Комментарий, "Комментарий");
	Что = Список;
	РасположениеФайла(Имя,);
	Имя=Имя+"UnSele.ert";
	Если ФС.СуществуетФайл(Имя)=1 тогда
		Если Форма.МодальныйРежим()=1 тогда
			ОткрытьФорму("Отчет",Что,Имя);
		Иначе
			ОткрытьФормуМодально("Отчет",Что,Имя);
		КонецЕсли;
	Иначе
		Предупреждение("Форма выбора не найдена!");
		Возврат;
	КонецЕсли;
	Что=Список.Получить("Результат");
	Если Что = "OK" Тогда // NO
		Что=Список.Получить("Элемент");
		ТипЗнач=Список.Получить("Тип");
		ВидЗнач=Список.Получить("Вид");
		ДлинаЧисел=Список.Получить("Длина");
		ТочностьЧисел=Список.Получить("Точность");
		Многострочные=Список.Получить("Многострочные");
		СформироватьЗначение(Что);
		Данные=Что;
		СформироватьКоды();
	КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура ПриЗакрытии()
	Если (Форма.АктивныйЭлемент()<>"Exit") и
		 (Форма.АктивныйЭлемент()<>"КнOK") тогда
		//выход только по кнопке "Выход" во избежание ошибочного закрытия
		Сообщить("Закрытие формы - кнопка"+"""Выход"""); 
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
КонецПроцедуры
//*******************************************
//*******************************************
Процедура СтрокаПредставления(ТекстМодуля, Знач Что)
	i=Найти(Что,Симв(34));
	Пока i>0 Цикл
//		ТекстМодуля=ТекстМодуля+""""+Лев(Что,i)+"+"+""""+""""+""""+""""+"+";
		ТекстМодуля=ТекстМодуля+""""+Лев(Что,i);
		Что=Строка(Прав(Что,СтрДлина(Что)-i));
		i=Найти(Что,Симв(34));
	КонецЦикла;
	Сообщить(ТекстМодуля+""""+Что+""")");
КонецПроцедуры
//*******************************************
Процедура ПриНачалеВыбораЗначения(Имя,Флаг)
	Перем СтрФайл, ИмяПути;
	Если (Имя="ВнутрКод") или (Имя="Значение") тогда
		СтрокаПредставления("ЗначениеИзСтрокиВнутр(", ВнутрКод);
		СтрокаПредставления("ЗначениеИзСтроки(", ЗначениеВСтроку(Данные));

		ТипЗнач=ТипЗначенияСтр(Данные);
		Если ТипЗнач="Перечисление" Тогда
			Сообщить("Перечисление."+Данные.Вид()+"."+Данные.Идентификатор());
		ИначеЕсли ТипЗнач="Счет" тогда
			Сообщить("СчетПоКоду("""+Данные.Код+""","""+Данные.Вид()+""")");
		ИначеЕсли ТипЗнач="ПланСчетов" тогда
			Сообщить("ПланыСчетов."+Данные.Идентификатор());
		ИначеЕсли ТипЗнач="ВидРасчета" тогда
			Сообщить("ВидРасчета.ПолучитьАтрибут("""+Данные.Код+""")");
		ИначеЕсли ТипЗнач="Календарь" тогда
			Сообщить("Календари.ПолучитьАтрибут("""+Данные.Вид()+""")");
		КонецЕсли;
	ИначеЕсли Имя="Данные" тогда
		Если ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока())="Строка" тогда
			Если ПустаяСтрока(Данные)=0 Тогда
				СтрФайл=Данные;
				ИмяПути="";
				Пока Найти(СтрФайл,"\")>0 Цикл
					ИмяПути=ИмяПути+Лев(СтрФайл,Найти(СтрФайл,"\"));
					СтрФайл=Прав(СтрФайл,СтрДлина(СтрФайл)-Найти(СтрФайл,"\"));
				КонецЦикла;
			КонецЕсли;
			Если ФсФайл_1=2 тогда
				Если ФС.ВыбратьКаталог(ИмяПути,"Выберите каталог:")=1 Тогда 
					Данные=СокрЛП(ИмяПути);
				КонецЕсли;
			ИначеЕсли ФсФайл_1=1 тогда
				Если ПустоеЗначение(Маска)=1 тогда
					Маска="Все файлы (*.*) |*.*";
				КонецЕсли;	
				Если ФС.ВыбратьФайл(0,СтрФайл,ИмяПути,"Выберите файл:",Маска,,)=1 Тогда 
					Данные=СокрЛП(ИмяПути+СтрФайл);
				КонецЕсли;
			КонецЕсли;
		Иначе
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 
//*******************************************
Процедура ИзФайла()
	Перем Что, Ном, Кол;
	Если ВыборФайла(0)=1 Тогда 
 		Если ТипФорматаФайла=3 тогда
 			Что="";
 			Текст=СоздатьОбъект("Текст");
 			Текст.Открыть(ИмяФайла);
 			Кол=Текст.КоличествоСтрок();
 			Для Ном=1 по Кол-1 цикл
 				Что=Что+Текст.ПолучитьСтроку(Ном)+РазделительСтрок;
 			КонецЦикла;
			Что=Что+Текст.ПолучитьСтроку(Ном);
 		Иначе
			ЗначениеИзФайла(ИмяФайла,Что,ТипФорматаФайла);
		КонецЕсли;
	КонецЕсли;
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	СформироватьЗначение(Что);
	Если (ТипЗнач="Строка")	и
		 (Многострочные=1)	тогда
		ДлСтр=Что;
	Иначе
		Данные=Что;
	КонецЕсли;
	СформироватьКоды();
КонецПроцедуры
//*******************************************
Процедура Ореквизите(Что)
	Перем Имя;
	РасположениеФайла(Имя,);
	Имя=Имя+"Info.ert";
	Если ФС.СуществуетФайл(Имя)=1 тогда
		ОткрытьФорму("Отчет",Что,Имя);
	Иначе
		Предупреждение("Форма выбора не найдена!");
	КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура РедДанные()
	Перем Имя;
	Список = СоздатьОбъект("СписокЗначений");
	Список.Установить("Результат","NO");
	ТипЗнач=ТипДанных.ПолучитьЗначение(ТипДанных.ТекущаяСтрока());
	Список.Установить("Тип",ТипЗнач);
	ВидЗнач=ВидДанных.ПолучитьЗначение(ВидДанных.ТекущаяСтрока());
	Список.Установить("Вид",ВидЗнач);
	Список.Установить("Длина",ДлинаЧисел);
	Список.Установить("Точность",ТочностьЧисел);
	Список.Установить("Многострочные",0);
	Список.Установить("Элемент",Данные);
	Список.Установить("Комментарий",Комментарий);
	РасположениеФайла(Имя,);
	Если ТипЗнач="Справочник" тогда
		Имя=Имя+"AttrSpr.ert";
	ИначеЕсли ТипЗнач="Документ" тогда
		Имя=Имя+"AttrDoc.ert";
	КонецЕсли;
	Если ФС.СуществуетФайл(Имя)=1 тогда
		ОткрытьФорму("Отчет",Список,Имя);//Модально
	КонецЕсли;
 КонецПроцедуры
