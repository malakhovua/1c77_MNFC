// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

// переменные для управления работой помощника
Перем Шаг, ПервыйШагПокупка, ПоследнийШагПокупка, ПервыйШагПродажа, ПоследнийШагПродажа;
Перем ПоследнийШаг, Док, стрСовет, стрОписание;


// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ======================================
// Рисует слои помощника в зависимости от текущего шага
Процедура НарисоватьСлои()
	Форма.ИспользоватьСлой("Общий, Слой"+Строка(Шаг),2);
КонецПроцедуры

// ===============================
// Выполняет проверку корректности заполнения реквизитов после каждого шага
Функция ПроверитьШаг()
    Если Шаг=2 Тогда
		Если ПустоеЗначение(Фирма)=1 Тогда
		    стрСовет = "Не выбрана фирма";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=3 Тогда
		Если ПустоеЗначение(РСчет)=1 Тогда
		    стрСовет = "Не задан валютный расчетный счет!";
	    	РСчет = 0;
			Возврат 0;
		ИначеЕсли ПустоеЗначение(Валюта)=1 Тогда
		    стрСовет = "Не задана валюта! Возможно, не заполнен реквизит Валюта в справочнике ""Счета нашей фирмы""";
			Возврат 0;
		ИначеЕсли РСчетГрн.Валюта <> Гривня Тогда
		    стрСовет = "В качестве расчетного счета должен быть выбран счет с валютой Гривня!";
	    	РСчетГрн = 0;
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=4 Тогда
		Если ПустоеЗначение(Банк) = 1 Тогда
		    стрСовет = "Не выбран банк!";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=5 Тогда
		Если ПустоеЗначение(СчетДоходовКР)=1 Тогда
		    стрСовет = "Счет доходов по курсовой разнице!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СчетЗатратКР)=1 Тогда
		    стрСовет = "Счет затрат по курсовой разнице!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоЗатратКР)=1 Тогда
		    стрСовет = "Не выбран вид затрат по курсовой разнице!";
			Возврат 0;
		КонецЕсли;
	ИначеЕсли Шаг=6 Тогда
		Если ПустоеЗначение(СчетЗатратПенс)=1 Тогда
		    стрСовет = "Не выбран счет затрат по Пенсионному фонду!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоЗатратПенс)=1 Тогда
		    стрСовет = "Не выбран вид затрат по Пенсионному фонду!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(ВалРасхПенсионный)=1 Тогда
		    стрСовет = "Не выбран вид валовых расходов по Пенсионному фонду!";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=7 Тогда
		Если ПустоеЗначение(СчетЗатратПенс)=1 Тогда
		    стрСовет = "Не выбран счет затрат по комиссионным!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоЗатратКом)=1 Тогда
		    стрСовет = "Не выбран вид затрат по комиссионным!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоВалРасх)=1 Тогда
		    стрСовет = "Не выбран вид валовых расходов!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(ВидДеятельности)=1 Тогда
		    стрСовет = "Не выбран вид деятельности!";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=8 Тогда
		Если РСчет1.Валюта <> Гривня Тогда
		    стрСовет = "В качестве расчетного счета должен быть выбран счет с валютой Гривня!";
	    	РСчет1 = 0;
			Возврат 0;
		ИначеЕсли ПустоеЗначение(РСчетВал1)=1 Тогда
		    стрСовет = "Не задан валютный расчетный счет!";
	    	РСчетВал1 = 0;
			Возврат 0;
		ИначеЕсли ПустоеЗначение(Валюта1)=1 Тогда
		    стрСовет = "Не задана валюта! Возможно, не заполнен реквизит Валюта в справочнике ""Счета нашей фирмы""";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=10 Тогда
		Если ПустоеЗначение(Банк1) = 1 Тогда
		    стрСовет = "Не выбран банк!";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=11 Тогда
		Если ПустоеЗначение(СчетЗатратКом1)=1 Тогда
		    стрСовет = "Не выбран счет затрат по комиссионным!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(ВидДеятельности1)=1 Тогда
		    стрСовет = "Не выбран вид деятельности!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоЗатратКом1)=1 Тогда
		    стрСовет = "Не выбран вид затрат по комиссионным!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоВалРасхКом1)=1 Тогда
		    стрСовет = "Не выбран вид валовых расходов по комиссионным!";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг=12 Тогда
		Если ПустоеЗначение(СубконтоВалДох1)=1 Тогда
		    стрСовет = "Не выбран вид валовых доходов!";
			Возврат 0;
		ИначеЕсли ПустоеЗначение(СубконтоВалРасхБал1)=1 Тогда
		    стрСовет = "Не выбран вид валовых расходов по балансовой стоимости!";
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	Возврат 1;
КонецФункции

// ======================================
Процедура СоздатьДокументПокупкаВалюты()
	Док.Фирма 				= Фирма;
	Док.НомерДок 			= НомерДок;
	Док.ДатаДок 			= ДатаДок;
	Док.Автор 				= глПользователь;

	Док.РСчет 				= РСчет;
	Док.РСчетГрн 			= РСчетГрн;
	Док.Валюта 				= Валюта;
	Док.КурсНБУ 			= КурсНБУ;
	Док.СуммаВал 			= СуммаВал;
	Док.СуммаГрнУМВБ		= СуммаГрнУМВБ;
	Док.Банк				= Банк;
	Док.КомБанкаГрн			= КомБанкаГрн;
	Док.СуммаГрн			= СуммаГрн;
	Док.Примечание			= Примечание;
	Док.СчетЗатратКР		= СчетЗатратКР;
	Док.СчетДоходовКР		= СчетДоходовКР;
	Док.СубконтоЗатратКР	= СубконтоЗатратКР;
	Док.СчетЗатратКом		= СчетЗатратКом;
	Док.СубконтоЗатратКом	= СубконтоЗатратКом;
	Док.СчетЗатратПенс 		= СчетЗатратПенс;
	Док.СубконтоЗатратПенс 	= СубконтоЗатратПенс;
	Док.СубконтоВалРасх 	= СубконтоВалРасх;
	Док.ВалРасхПенсионный 	= ВалРасхПенсионный;
	Док.ВидДеятельности 	= ВидДеятельности;

	глСохранитьЗначение("ПокупкаВалюты","СубконтоВалРасх",		СубконтоВалРасх); 
	глСохранитьЗначение("ПокупкаВалюты","ВалРасхПенсионный",	ВалРасхПенсионный);
	глСохранитьЗначение("ПокупкаВалюты","СчетЗатратКР",			СчетЗатратКР);
	глСохранитьЗначение("ПокупкаВалюты","СчетДоходовКР",		СчетДоходовКР);
	глСохранитьЗначение("ПокупкаВалюты","СчетЗатратКом",		СчетЗатратКом);
	глСохранитьЗначение("ПокупкаВалюты","СчетЗатратПенс",		СчетЗатратПенс);
	глСохранитьЗначение("ПокупкаВалюты","СубконтоЗатратКР",		СубконтоЗатратКР);
	глСохранитьЗначение("ПокупкаВалюты","СубконтоЗатратКом",	СубконтоЗатратКом);
	глСохранитьЗначение("ПокупкаВалюты","СубконтоЗатратПенс",	СубконтоЗатратПенс);
	глСохранитьЗначение("ПокупкаВалюты","Банк",					Банк);

	глПроверкаДатыДок(Док,"Запись");
	Док.Записать();
КонецПроцедуры 

// ======================================
Процедура СоздатьДокументПродажаВалюты()
	Док.Фирма 				= Фирма;
	Док.НомерДок 			= НомерДок;
	Док.ДатаДок 			= ДатаДок;
	Док.Автор 				= глПользователь;

	Док.РСчет 				= РСчет1;
	Док.РСчетВал 			= РСчетВал1;
	Док.Валюта 				= Валюта1;
	Док.КурсНБУ 			= КурсНБУ1;
	Док.СуммаВал 			= СуммаВал1;
	Док.СуммаГрнУМВБ 		= СуммаГрнУМВБ1;
	Док.Банк 				= Банк1;
	Док.СуммаГрн 			= СуммаГрн1;
	Док.СуммаКомГрн 		= СуммаКомГрн1;
	Док.Примечание 			= Примечание1;
	Док.СчетЗатратКом 		= СчетЗатратКом1;
	Док.СубконтоЗатратКом 	= СубконтоЗатратКом1;
	Док.СубконтоВалРасхКом 	= СубконтоВалРасхКом1;
	Док.СубконтоВалРасхБал 	= СубконтоВалРасхБал1;
	Док.СубконтоВалДох 		= СубконтоВалДох1;
	Док.ВидДеятельности 	= ВидДеятельности1;

	глСохранитьЗначение("ПродажаВалюты","СубконтоВалДох",		СубконтоВалДох1);
	глСохранитьЗначение("ПродажаВалюты","СубконтоВалРасхКом",	СубконтоВалРасхКом1);
	глСохранитьЗначение("ПродажаВалюты","СубконтоВалРасхБал",	СубконтоВалРасхБал1);
	глСохранитьЗначение("ПродажаВалюты","СчетЗатратКом",		СчетЗатратКом1);
	глСохранитьЗначение("ПродажаВалюты","СубконтоЗатратКом",	СубконтоЗатратКом1);

	глПроверкаДатыДок(Док,"Запись");
	Док.Записать();
КонецПроцедуры 

// ======================================
Процедура ПоказатьСовет()
	Если Шаг = 1 Тогда
	    стрСовет = "Здесь будут показываться советы и комментарии по заполнению реквизитов, находящихся в окне.";

	ИначеЕсли Шаг = 2 Тогда
	    стрСовет = "Внимание! Здесь нужно выбрать тип создаваемого документа и фирму. Эти реквизиты являются обязательными.";

	ИначеЕсли Шаг = 3 Тогда
	    стрСовет = "При необходимости Вы можете откорректировать курс НБУ."+РазделительСтрок+
					   "Счет, с которого берутся деньги для покупки, должен быть гривневым.";
	ИначеЕсли Шаг = 4 Тогда
	    стрСовет = "Итоговая сумма определяется как сумма приобретаемой валюты и комиссионных банка (в грн).";

	ИначеЕсли Шаг = 5 Тогда
	    стрСовет = "Введите счета и аналитику для отражения в учете курсовой разницы.";

	ИначеЕсли Шаг = 6 Тогда
	    стрСовет = "Введите счета и аналитику для отражения в учете сбора в Пенсионный фонд.";

	ИначеЕсли Шаг = 7 Тогда
	    стрСовет = "Вид деятельности используется в проводках по счетам доходов и затрат."+РазделительСтрок+
		               "В поле ""Примечание"" можно ввести комментарий к создаваемому документу.";
	ИначеЕсли Шаг = 8 Тогда
	    стрСовет = "Счет, на который поступают деньги от продажи валюты должен быть гривневым.";

	ИначеЕсли Шаг = 9 Тогда
	    стрСовет = "Банк должен быть выбран!";

	ИначеЕсли Шаг = 10 Тогда
	    стрСовет = "Виды затрат должны быть заполнены!"+РазделительСтрок+
			            "В поле ""Примечание"" можно ввести комментарий к создаваемому документу.";
	Иначе
		стрСовет = "";

	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ПоказатьОписание()

	Если Шаг<=2 Тогда
        стрОписание = "Здесь будут показываться все выбранные параметры в процессе работы помощника.";
		Возврат;
	КонецЕсли;

	стрОписание = ?(ТипДок=1,"Покупка валюты №","Продажа валюты №")+СокрЛП(НомерДок)+
					" от "+СокрЛП(Строка(ДатаДок)) + " по фирме "+Строка(Фирма) + ".";
					
	Если ТипДок=1 Тогда

		Если Шаг > 3 Тогда
			стрОписание = стрОписание + " Приобретаемая валюта: " + Валюта + ". Курс НБУ: "+КурсНБУ + ".";
		КонецЕсли;
	
		Если Шаг > 4 Тогда
			стрОписание = стрОписание + " Сумма валюты: "+СуммаВал+" "+СокрЛП(Валюта.Кратко)+
							". Стоимость приобретенной валюты: "+Строка(СуммаГрнУМВБ)+
							" грн. Комиссионные банка: " + КомБанкаГрн +
							" грн. Всего затрачено на покупку: "+Строка(СуммаГрн)+ " грн.";
		КонецЕсли;
    Иначе
		Если Шаг > 8 Тогда
			стрОписание = стрОписание + " Приобретаемая валюта: " + Валюта1 + 
		        	        ". Курс НБУ: "+КурсНБУ1 + ".";
		КонецЕсли;

		Если Шаг > 9 Тогда
			стрОписание = стрОписание + " Сумма валюты: "+СуммаВал1+" "+СокрЛП(Валюта1.Кратко) + ".";
		КонецЕсли;

		Если Шаг > 10 Тогда
			стрОписание = стрОписание +" Стоимость проданной валюты: "+Строка(СуммаГрнУМВБ1)+
							" грн. Комиссионные банка: " + СуммаКомГрн1 +
							" грн. Всего получено без комиссионных: "+Строка(СуммаГрн1)+ " грн.";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ======================================
Функция УстДоступность()
	Форма.кНазад.Доступность(1);
	Форма.кДальше.Доступность(1);
	Форма.кДальше.Заголовок("&Дальше >");
	Если Шаг = 1 Тогда
		Форма.кНазад.Доступность(0);
	ИначеЕсли Шаг = 2 Тогда
		// если пользователь возвращается на шаг с выбором типа документа,
		// вернем старый заголовок окна
		Форма.Заголовок("Помощник создания документов ""Покупка валюты"" и ""Продажа валюты""",0);
	ИначеЕсли (Шаг = ПервыйШагПокупка) Или (Шаг = ПервыйШагПродажа) Тогда
		// после выбора типа документа изменим соответственно заголовок окна
		Форма.Заголовок("Помощник создания документа """+?(ТипДок=1,"Покупка валюты","Продажа валюты")+"""", 0);
	ИначеЕсли Шаг = ПоследнийШаг Тогда
		Форма.кДальше.Заголовок("&Готово");
	КонецЕсли;
	Возврат "";
КонецФункции

// ======================================
Процедура ИзмДатаДок()
	КурсНБУ  = Валюта. Курс.Получить(ДатаДок);
	КурсНБУ1 = Валюта1.Курс.Получить(ДатаДок);
КонецПроцедуры 

// ======================================
// Основная процедура рассчета документа ПокупкаВалюты
Процедура РассчитатьДокументПокупкаВалюты()
	Валюта = РСчет.Валюта;
	СуммаГРН = СуммаГРНУМВБ + КомБанкаГРН;
КонецПроцедуры

// ======================================
// Основная процедура расчета документа ПокупкаВалюты
Процедура РассчитатьДокументПродажаВалюты()
	Валюта1 = РСчетВал1.Валюта;
	СуммаГРН1 = СуммаГРНУМВБ1 - СуммаКомГРН1;
КонецПроцедуры

// ===============================
Процедура РассчитатьДокумент()
	Если ТипДок=1 Тогда
	    РассчитатьДокументПокупкаВалюты();
	Иначе
	    РассчитатьДокументПродажаВалюты();
	КонецЕсли;
	ИзмДатаДок();
КонецПроцедуры

// ======================================
Процедура РассчитатьБалансовуюСтоимость()
	// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
	//// рассчитаем бал. стоимость валюты по среднему
	//Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	//Ит.ИспользоватьРазделительУчета(Фирма);
	//Ит.ИспользоватьСубконто(ВидыСубконто.НашиДенежныеСчета, РСчетВал1, 2);
	//Ит.ВыполнитьЗапрос(ДатаДок,,"ВЛ",,РСчетВал1.Валюта);
	//СуммаБал1 = ?(Ит.СНД("В") = 0,0,СуммаВал1*Ит.СНД("С")/Ит.СНД("В"));
	//СуммаБал2 = СуммаБал1;
	//Если СуммаБал1 = 0 Тогда
	//	стрСовет = "Балансовая стоимость продаваемой валюты равна 0! Проверьте остаток на забалансовом счете ВЛ";
	//КонецЕсли;
	// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
КонецПроцедуры 

// ======================================
Процедура ИзмРСчет()
	Если РСчет.Владелец <> Фирма Тогда
		стрСовет = "Валютный расчетный счет не принадлежит выбранной фирме";
		РСчет = 0;
	КонецЕсли;
	РассчитатьДокумент();
КонецПроцедуры 

// ======================================
Процедура ИзмРСчетГрн()
	Если РСчетГрн.Владелец <> Фирма Тогда
		стрСовет = "Расчетный счет не принадлежит выбранной фирме";
		РСчетГрн = 0;
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ИзмРСчет1()
	Если РСчет1.Владелец <> Фирма Тогда
		стрСовет = "Расчетный счет не принадлежит выбранной фирме";
		РСчет1 = 0;
	КонецЕсли;
КонецПроцедуры 

// ======================================
Процедура ИзмРСчетВал1()
	Если РСчетВал1.Владелец <> Фирма Тогда
		стрСовет = "Валютный расчетный счет не принадлежит выбранной фирме";
		РСчетВал1 = 0;
	КонецЕсли;
	РассчитатьДокумент();
КонецПроцедуры 

// ======================================
// инициализация переменных для документа ПокупкаВалюты
Процедура НовыйДокументПокупкаВалюты()
	Если ПустоеЗначение(Фирма) = 0 Тогда
		РСчетГрн = Фирма.РС;
		РСчет = Фирма.ВС;
	КонецЕсли;
	Банк 			= глВосстановитьЗначение("ПокупкаВалюты","Банк");
	СубконтоВалРасх = глВосстановитьЗначение("ПокупкаВалюты","СубконтоВалРасх");
	Если СубконтоВалРасх.Выбран() = 0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Если Спр.НайтиПоКоду("2/2/11") = 1 Тогда
	        СубконтоВалРасх = Спр.ТекущийЭлемент();
	    КонецЕсли;
	КонецЕсли;
	ВалРасхПенсионный = глВосстановитьЗначение("ПокупкаВалюты","ВалРасхПенсионный");
	Если ВалРасхПенсионный.Выбран() = 0 Тогда 
		Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Если Спр.НайтиПоКоду("2/1/16") = 1 Тогда
	    	ВалРасхПенсионный = Спр.ТекущийЭлемент();
		КонецЕсли;	
	КонецЕсли;
	СчетЗатратКР 		= глВосстановитьЗначение("ПокупкаВалюты","СчетЗатратКР");
	СчетДоходовКР 		= глВосстановитьЗначение("ПокупкаВалюты","СчетДоходовКР",СчетПоКоду("71.4"));
	СчетЗатратКом 		= глВосстановитьЗначение("ПокупкаВалюты","СчетЗатратКом");
	СчетЗатратПенс 		= глВосстановитьЗначение("ПокупкаВалюты","СчетЗатратПенс");
	СубконтоЗатратКР 	= глВосстановитьЗначение("ПокупкаВалюты","СубконтоЗатратКР");
	СубконтоЗатратКом 	= глВосстановитьЗначение("ПокупкаВалюты","СубконтоЗатратКом");
	СубконтоЗатратПенс 	= глВосстановитьЗначение("ПокупкаВалюты","СубконтоЗатратПенс");
	Если СчетЗатратКР.Выбран()=0 Тогда
	    СчетЗатратКР = СчетПоКоду(?(ИспользоватьСчетаРасходов=Класс8,"84","94.5"));
	КонецЕсли;
	Если СчетЗатратКом.Выбран()=0 Тогда
	    СчетЗатратКом = СчетПоКоду(?(ИспользоватьСчетаРасходов=Класс8,"84","94.9"));
	КонецЕсли;
	Если СчетЗатратПенс.Выбран()=0 Тогда
	    СчетЗатратПенс = СчетПоКоду(?(ИспользоватьСчетаРасходов=Класс8,"84","94.9"));
	КонецЕсли;
	ВидДеятельности = глВосстановитьЗначение(,"БазВидДеятельности");
	РассчитатьДокумент();
КонецПроцедуры 

// ======================================
// инициализация переменных для документа ПродажаВалюты
Процедура НовыйДокументПродажаВалюты()
	Если ПустоеЗначение(Фирма) = 0 Тогда
		РСчет1 = Фирма.РС;
		РСчетВал1 = Фирма.ВС;
	КонецЕсли;
	СчетЗатратКом1 = глВосстановитьЗначение("ПродажаВалюты","СчетЗатратКом");
	Если СчетЗатратКом1.Выбран() = 0 Тогда
		СчетЗатратКом1 = СчетПоКоду(?(ИспользоватьСчетаРасходов=Класс9,"949","84"));
	КонецЕсли;
	СубконтоЗатратКом1 	= глВосстановитьЗначение("ПродажаВалюты","СубконтоЗатратКом");
	СубконтоВалДох1 	= глВосстановитьЗначение("ПродажаВалюты","СубконтоВалДох");
	Если СубконтоВалДох1.Выбран()=0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Спр.НайтиПоКоду("1/2/3");
	    СубконтоВалДох1 = Спр.ТекущийЭлемент();
	КонецЕсли;                                
	СубконтоВалРасхКом1 = глВосстановитьЗначение("ПродажаВалюты","СубконтоВалРасхКом");
	Если СубконтоВалРасхКом1.Выбран()=0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Спр.НайтиПоКоду("2/2/11");
	    СубконтоВалРасхКом1 = Спр.ТекущийЭлемент();
	КонецЕсли;                                
	СубконтоВалРасхБал1 = глВосстановитьЗначение("ПродажаВалюты","СубконтоВалРасхБал");
	Если СубконтоВалРасхБал1.Выбран()=0 Тогда
	    Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	    Спр.НайтиПоКоду("2/2/12");
	    СубконтоВалРасхБал1 = Спр.ТекущийЭлемент();
	КонецЕсли;
	ВидДеятельности1 = глВосстановитьЗначение(,"БазВидДеятельности");
	РассчитатьДокумент();
КонецПроцедуры 

// ===============================
Процедура ИзмТипДок()
	Если ТипДок=1 Тогда
		Док = СоздатьОбъект("Документ.ПокупкаВалюты");
	Иначе
		Док = СоздатьОбъект("Документ.ПродажаВалюты");
	КонецЕсли;
	Док.Новый();
	Док.Фирма = Фирма;
	глУстановитьНомер(Док);
	НомерДок = Док.НомерДок;
	ДатаДок  = Док.ДатаДок;
	ИзмДатаДок();
	// вызовем процедуру для инициализации переменных
	Если ТипДок=1 Тогда
	    НовыйДокументПокупкаВалюты();
	Иначе
	    НовыйДокументПродажаВалюты();
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	ИзмТипДок();
КонецПроцедуры 

// ===============================
Процедура кОтмена()
	Если Вопрос("Прекратить работу помощника?",4)=6 Тогда
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

// ===============================
// Обрабатывает нажатие на кнопки Назад, Дальше
// Дельта - направление движения
Процедура ИзмШаг(Дельта)
	Если Дельта = 1 Тогда
		Если ПроверитьШаг()=0 Тогда
			Возврат;
		КонецЕсли;
		
		Если Шаг = ПоследнийШаг Тогда
		    Если ТипДок=1 Тогда
	    	    СоздатьДокументПокупкаВалюты();
			Иначе
	    	    СоздатьДокументПродажаВалюты();
			КонецЕсли;
			Форма.Закрыть();

			Если ОткрытьДокумент = 1 Тогда
				// откроем форму документа, не проводя его
				ОткрытьФорму(Док.ТекущийДокумент());
			Иначе
				// документ уже записан, проведем его
				Док.Провести();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Шаг = Шаг + Дельта;
	// пропустим те шаги, которые относятся к другому документу
	Если ТипДок=1 Тогда
    	Пока (Шаг > ПоследнийШагПокупка) И (Шаг < ПоследнийШаг) Цикл
			Шаг = Шаг + Дельта;
	    КонецЦикла;
   	Иначе
    	Пока (Шаг >= ПервыйШагПокупка) И (Шаг < ПервыйШагПродажа) Цикл
			Шаг = Шаг + Дельта;
	    КонецЦикла;
	КонецЕсли;
	
	НарисоватьСлои();
	ПоказатьСовет();
	ПоказатьОписание();
КонецПроцедуры

// ======================================
Процедура ВыбратьБанковскуюВыписку()
	ДокБВ = СоздатьОбъект("Документ.БанковскаяВыписка");
	Если ДокБВ.Выбрать("Выбор банковской выписки") = 1 Тогда

		ТекДокБВ = ДокБВ.ТекущийДокумент();
		Если ТекДокБВ.РСчет <> РСчетВал1  Тогда
			стрСовет = "Была выбрана выписка по другому расчетному счету. Сумма не установлена";
			Возврат;
		КонецЕсли;

		СуммаВыписки = 0;
		ТекДокБВ.ВыбратьСтроки();
		Пока ТекДокБВ.ПолучитьСтроку() = 1 Цикл
			Если ТекДокБВ.ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Поступление Тогда
				СуммаВыписки = СуммаВыписки + ТекДокБВ.СуммаСНДС;
			КонецЕсли;
		КонецЦикла;
		СуммаВал1 = СуммаВыписки / 2;
	КонецЕсли;
КонецПроцедуры 


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ======================================
Процедура ПриОткрытии()
	Фирма = глВосстановитьЗначение(,"БазФирма");
	Шаг 				= 1;
	ТипДок 				= 1;
	ПервыйШагПокупка 	= 3;
	ПоследнийШагПокупка = 7;
	ПервыйШагПродажа 	= 8;
	ПоследнийШагПродажа = 12;
	ПоследнийШаг 		= 13;
	ОткрытьДокумент 	= 1;
	ИзмТипДок();
	ПоказатьСовет();
	ПоказатьОписание();
	НарисоватьСлои();
	Форма.ПанельИнструментов(0);
	// выбирать группы элементов справочников и счетов нельзя
	Форма.РСчет.				ВыборГруппы(0);
	Форма.РСчетГрн.				ВыборГруппы(0);
	Форма.Банк.					ВыборГруппы(0);
	Форма.СчетДоходовКР.		ВыборГруппы(0);
	Форма.СчетЗатратКР.			ВыборГруппы(0);
	Форма.СубконтоЗатратКР.		ВыборГруппы(0);
	Форма.СчетЗатратПенс.		ВыборГруппы(0);
	Форма.СубконтоЗатратПенс.	ВыборГруппы(0);
	Форма.ВалРасхПенсионный.	ВыборГруппы(0);
	Форма.СчетЗатратКом.		ВыборГруппы(0);
	Форма.СубконтоЗатратКом.	ВыборГруппы(0);
	Форма.СубконтоВалРасх.		ВыборГруппы(0);
	Форма.РСчет1.				ВыборГруппы(0);
	Форма.РСчетВал1.			ВыборГруппы(0);
	Форма.Банк1.				ВыборГруппы(0);
	Форма.СчетЗатратКом1.		ВыборГруппы(0);
	Форма.СубконтоЗатратКом1.	ВыборГруппы(0);
	Форма.СубконтоВалРасхКом1.	ВыборГруппы(0);
	Форма.СубконтоВалДох1.		ВыборГруппы(0);
	Форма.СубконтоВалРасхБал1.	ВыборГруппы(0);
КонецПроцедуры
