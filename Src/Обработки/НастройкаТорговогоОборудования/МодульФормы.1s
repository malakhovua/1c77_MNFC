Перем НомерСтраницы,Последняя;          

// ===============================
Процедура УстДоступность()
	Форма.рТипСканера.Доступность(РаботаСК);
	Форма.ТипСканера.Доступность(РаботаСК);
	Форма.ТипСканера1.Доступность(РаботаСК);
	Форма.ЕстьПрефикс.Доступность(РаботаСК);
КонецПроцедуры

//==================================
Процедура УстРабМесто()
	СпрЭККАРабМесто = СоздатьОбъект("Справочник.ЭККАРабочегоМеста");	
	СпрЭККАРабМесто.ИспользоватьВладельца(РабМесто); 
	СпрЭККАРабМесто.ВыбратьЭлементы();
	Пока СпрЭККАРабМесто.ПолучитьЭлемент()=1 цикл
		Если СпрЭККАРабМесто.ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Регистрация тогда
			спФискРегЭККА.ДобавитьЗначение(СпрЭККАРабМесто.ЭККА,Строка(СпрЭККАРабМесто.ЭККА));              
		ИначеЕсли СпрЭККАРабМесто.ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Отчет тогда
			спОфЛЭККА.ДобавитьЗначение(СпрЭККАРабМесто.ЭККА,Строка(СпрЭККАРабМесто.ЭККА));            
		ИначеЕсли СпрЭККАРабМесто.ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Автономный тогда
			спАвтЭККА.ДобавитьЗначение(СпрЭККАРабМесто.ЭККА,Строка(СпрЭККАРабМесто.ЭККА));                 
		КонецЕсли;			
	КонецЦикла;	 
	РаботаСК = РабМесто.РаботаСК;
	ТипСканера = РабМесто.ТипСканера;
	ЕстьПрефикс = РабМесто.ЕстьПрефикс;
КонецПроцедуры

//==================================
Процедура УстановитьКнопки()
	// утстанавливает доступность кнопок
	Форма.Назад.Доступность(1);
	Форма.Готово.Доступность(1);
	Форма.Вперед.Доступность(1);
	Если НомерСтраницы=1 Тогда
		Форма.Назад.Доступность(0);
		Форма.Готово.Доступность(0);
	ИначеЕсли НомерСтраницы=2 Тогда
		Если РабМесто.Выбран() = 0 тогда
			ТоргОбор = СоздатьОбъект("Справочник.ТорговоеОборудование");
			Если ТоргОбор.НайтиПоКоду(ИмяКомпьютера())=1 Тогда
				 РабМесто = ТоргОбор.ТекущийЭлемент();
				 УстРабМесто();
			Иначе
				Форма.Вперед.Доступность(0);
				Форма.Готово.Доступность(0);
			КонецЕсли;
		Иначе
			Форма.Вперед.Доступность(1);
			Форма.Готово.Доступность(1);
		КонецЕсли;
	ИначеЕсли НомерСтраницы=Последняя Тогда
		Форма.Вперед.Доступность(0);
		СпрРабМест.УдалитьСтроки();
		Для Стр=1 по спФискРегЭККА.РазмерСписка() цикл
			СпрРабМест.НоваяСтрока();
			СпрРабМест.ЭККА = спФискРегЭККА.ПолучитьЗначение(Стр);
			СпрРабМест.Режим = "Фискальный регистратор";
		КонецЦикла;
		Для Стр=1 по спОфЛЭККА.РазмерСписка() цикл
			СпрРабМест.НоваяСтрока();
			СпрРабМест.ЭККА = спОфЛЭККА.ПолучитьЗначение(Стр);
			СпрРабМест.Режим = "OFF Line";
		КонецЦикла;
		Для Стр=1 по спАвтЭККА.РазмерСписка() цикл
			СпрРабМест.НоваяСтрока();
			СпрРабМест.ЭККА = спАвтЭККА.ПолучитьЗначение(Стр);
			СпрРабМест.Режим = "Автономный";
		КонецЦикла;
	Иначе
		Если (НомерСтраницы = 3) и (спФискРегЭККА.РазмерСписка() = 0) тогда
			Форма.Удалить3.Доступность(0);
		ИначеЕсли (НомерСтраницы = 4) и (спОфЛЭККА.РазмерСписка() = 0) тогда
			Форма.Удалить4.Доступность(0);
		ИначеЕсли (НомерСтраницы = 5) и (спАвтЭККА.РазмерСписка() = 0) тогда              
			Форма.Удалить5.Доступность(0);
		Иначе
			Форма.Удалить3.Доступность(1);
			Форма.Удалить4.Доступность(1);
			Форма.Удалить5.Доступность(1);
			спФискРегЭККА.ТекущаяСтрока(1);
			спОфЛЭККА.ТекущаяСтрока(1);
			спАвтЭККА.ТекущаяСтрока(1);
		КонецЕсли;	
	КонецЕсли;            
	УстДоступность();	
КонецПроцедуры

//==================================
Процедура ИзмРабМесто()                  
	Если СокрЛП(РабМесто) <> ИмяКомпьютера() Тогда
		глКомментарий("Выбранное рабочее место не является текущим. Драйвера не будут инициализированы !!!",,,);
	КонецЕсли;	
	спФискРегЭККА.УдалитьВсе();
	спОфЛЭККА.УдалитьВсе();
	спАвтЭККА.УдалитьВсе();
	УстановитьКнопки();    
	УстРабМесто();
	УстДоступность();	
КонецПроцедуры

//==================================
Процедура Удалить()       
	Если НомерСтраницы = 3 тогда
		Если спФискРегЭККА.ТекущаяСтрока() = 0 тогда
			Возврат;
		КонецЕсли;		
		спФискРегЭККА.УдалитьЗначение(спФискРегЭККА.ТекущаяСтрока());              
	ИначеЕсли НомерСтраницы = 4 тогда
		Если спОфЛЭККА.ТекущаяСтрока() = 0 тогда
			Возврат;
		КонецЕсли;		
		спОфЛЭККА.УдалитьЗначение(спОфЛЭККА.ТекущаяСтрока());              
	ИначеЕсли НомерСтраницы = 5 тогда
		Если спАвтЭККА.ТекущаяСтрока() = 0 тогда
			Возврат;
		КонецЕсли;		
		спАвтЭККА.УдалитьЗначение(спАвтЭККА.ТекущаяСтрока());              
	ИначеЕсли НомерСтраницы = 6 тогда
		СпрРабМест.УдалитьСтроку();	
	КонецЕсли;		
	УстановитьКнопки();	
КонецПроцедуры	

//==================================
Процедура Добавить()
	Перем ВыбЭККА;
	ОткрытьПодбор("Справочник.ЭККА");     
КонецПроцедуры	

//==================================
Процедура ОбработкаПодбора(ВыбЭККА)
	Если спФискРегЭККА.НайтиЗначение(ВыбЭККА) <> 0 тогда
		Возврат;				
	ИначеЕсли спОфЛЭККА.НайтиЗначение(ВыбЭККА) <> 0 тогда
		Возврат;				
	ИначеЕсли спАвтЭККА.НайтиЗначение(ВыбЭККА) <> 0 тогда
		Возврат;				
	КонецЕсли;	
	Если НомерСтраницы = 3 тогда
		Если ВыбЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Регистрация тогда
			спФискРегЭККА.ДобавитьЗначение(ВыбЭККА,Строка(ВыбЭККА));              
		Иначе
			глКомментарий("ЭККА "+ВыбЭККА+" не может быть подключен к данному компьютеру, как фискальный регистратор.");
		КонецЕсли;	
	ИначеЕсли НомерСтраницы = 4 тогда
		Если ВыбЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Отчет тогда
			спОфЛЭККА.ДобавитьЗначение(ВыбЭККА,Строка(ВыбЭККА));            
		Иначе
			глКомментарий("ЭККА "+ВыбЭККА+" не может быть подключен к данному компьютеру, в режиме OffLine.");
		КонецЕсли;	
	ИначеЕсли НомерСтраницы = 5 тогда
		спАвтЭККА.ДобавитьЗначение(ВыбЭККА,Строка(ВыбЭККА));                 
	КонецЕсли;			
	УстановитьКнопки();
КонецПроцедуры	

// ===============================
Процедура ИзмРаботаСК()
	УстДоступность();
КонецПроцедуры

//==================================
Процедура Вперед()
	Форма.ИспользоватьСлой("Слой"+НомерСтраницы,0);
	НомерСтраницы = НомерСтраницы+1;
	Форма.ИспользоватьСлой("Слой"+НомерСтраницы,1);
	Форма.Заголовок("Шаг "+НомерСтраницы,1);
	УстановитьКнопки();
КонецПроцедуры

//==================================
Процедура Назад()
	Форма.ИспользоватьСлой("Слой"+НомерСтраницы,0);
	НомерСтраницы = НомерСтраницы-1;
	Форма.ИспользоватьСлой("Слой"+НомерСтраницы,1);
	Форма.Заголовок("Шаг "+НомерСтраницы,1);
	УстановитьКнопки();
КонецПроцедуры

//==================================
Процедура ПриОткрытии()
	НомерСтраницы = 1;
	Форма.ИспользоватьСлой("Общий",2);
	Форма.ИспользоватьСлой("Слой"+НомерСтраницы,1);
	УстановитьКнопки();
КонецПроцедуры

//*******************************************
Процедура Выполнить()
	Если НомерСтраницы <> Последняя тогда
		НомерСтраницы=Последняя;
		УстановитьКнопки();
	КонецЕсли;	
	СпрЭККАРабМесто = СоздатьОбъект("Справочник.ЭККАРабочегоМеста");	
	СпрЭККАРабМесто.ИспользоватьВладельца(РабМесто); 
	СпрЭККАРабМесто.ВыбратьЭлементы();
	Пока СпрЭККАРабМесто.ПолучитьЭлемент()=1 цикл
		СпрЭККАРабМесто.Удалить();
	КонецЦикла;	     
	
	СписокФискальныхРегистраторов=СоздатьОбъект("СписокЗначений");
	СписокЭККА_OFFLine=СоздатьОбъект("СписокЗначений");
	СписокЭККА_Автономно=СоздатьОбъект("СписокЗначений");
	
	ПереченьДрайверов = СоздатьОбъект("СписокЗначений");

	СпрРабМест.ВыбратьСтроки();
	Пока СпрРабМест.ПолучитьСтроку()=1 цикл
		СпрЭККАРабМесто.Новый();
		СпрЭККАРабМесто.Наименование = Строка(СпрРабМест.ЭККА);
		СпрЭККАРабМесто.ЭККА= СпрРабМест.ЭККА;
		СпрЭККАРабМесто.Записать();
	КонецЦикла;
	
	СпрРабМесто = СоздатьОбъект("Справочник.ТорговоеОборудование");
	СпрРабМесто.НайтиЭлемент(РабМесто);
	СпрРабМесто.РаботаСК = РаботаСК;
	СпрРабМесто.ТипСканера = ТипСканера;
	СпрРабМесто.ЕстьПрефикс = ЕстьПрефикс;
	СпрРабМесто.Записать();	

	Если СокрЛП(РабМесто) = ИмяКомпьютера() Тогда
		Расшифровка = СоздатьОбъект("СписокЗначений");
		Расшифровка.ДобавитьЗначение("Подключить","Парам");
		глРасшифровка = Расшифровка;
		ОткрытьФорму("Обработка.ПодключениеТорговогоОборудования"+"#");
	КонецЕсли;
	
КонецПроцедуры         


СпрРабМест.ВставитьКолонку("ЭККА");
СпрРабМест.ВставитьКолонку("Режим");

Последняя=6;