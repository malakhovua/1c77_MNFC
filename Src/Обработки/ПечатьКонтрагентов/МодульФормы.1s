//*******************************************
Процедура Выполнить()
	СписД = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По списДоп.РазмерСписка() Цикл
		Если списДоп.Пометка(Инд) = 1 Тогда
			Стр = "";
			Зн = списДоп.ПолучитьЗначение(Инд, Стр);
		    СписД.ДобавитьЗначение(Зн, Стр);			
		КонецЕсли;
	КонецЦикла;

	ТекстЗ =
	"//{{ЗАПРОС(Сформировать)"
	+?(фВклУд = 0,"
	|Обрабатывать НеПомеченныеНаУдаление;","") + "
	|ТекЭл = Справочник.Контрагенты.ТекущийЭлемент;
	|Наименование = Справочник.Контрагенты.Наименование;
	|ПолнНаименование = Справочник.Контрагенты.ПолнНаименование;
	|Функция Сч = Счётчик();
	|Условие(ТекЭл в списКонтрагенты);
	|Группировка ТекЭл Упорядочить по ТекЭл.Наименование;
	|"//}}ЗАПРОС
	;
	
	Запр = СоздатьОбъект("Запрос");
	Если Запр.Выполнить(ТекстЗ) = 0 Тогда
		Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Шапка|Основная");
	Для Инд = 1 По списД.РазмерСписка() Цикл
		Стр = "";
		СписД.ПолучитьЗначение(Инд, Стр);
		Таб.ПрисоединитьСекцию("Шапка|Доп");
	КонецЦикла;
	Если фДоговор = 1 Тогда
	    Таб.ВывестиСекцию("Договор|Основная");
		Дог = СоздатьОбъект("Документ");
	КонецЕсли;	
	
	Пока Запр.Группировка(1) = 1 Цикл
		Если Запр.ТекЭл.ЭтоГруппа() = 1 Тогда
			Таб.ВывестиСекцию("Группа|Основная");
		Иначе			
		    Таб.ВывестиСекцию("Строка|Основная");
			Для Инд = 1 По списД.РазмерСписка() Цикл
				Атр = СписД.ПолучитьЗначение(Инд);
				Если Метаданные.Справочник("Контрагенты").Реквизит(Атр).Периодический = 1 Тогда
				    Зн = Запр.ТекЭл.ПолучитьАтрибут(Атр).Получить(РабочаяДата());
				Иначе
					Зн = Запр.ТекЭл.ПолучитьАтрибут(Атр);
				КонецЕсли;
				Таб.ПрисоединитьСекцию("Строка|Доп");
			КонецЦикла;
			Если фДоговор = 1 Тогда
				Дог.ВыбратьПоЗначению(,,"ДоговораКонтрагентов", Запр.ТекЭл);
			    Пока Дог.ПолучитьДокумент() = 1 Цикл
			    	Если (Дог.ПометкаУдаления() = 1) и (фВклУд = 0) Тогда
						Продолжить;
					КонецЕсли;
					Таб.ВывестиСекцию("Дог|Основная");
			    КонецЦикла;
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Список контрагентов");
КонецПроцедуры


Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "Контрагенты" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списКонтрагенты);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
	    списКонтрагенты.ДобавитьЗначение(Форма.Параметр);
	КонецЕсли;
КонецПроцедуры

Инд = 1;
Пока Метаданные.Справочник("Контрагенты").Реквизит(Инд).Выбран() = 1 Цикл
	ИмяРекв = Метаданные.Справочник("Контрагенты").Реквизит(Инд).Идентификатор;
	Синоним = Метаданные.Справочник("Контрагенты").Реквизит(Инд).Синоним;
	
	списДоп.ДобавитьЗначение(ИмяРекв, Синоним);
	Инд = Инд + 1;		
КонецЦикла;


