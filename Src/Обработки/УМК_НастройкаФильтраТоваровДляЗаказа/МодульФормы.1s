Перем НастройкиФайл;
Перем СписокПодбора,СправочникПодбора;
Перем БазИмяФайла;
Перем ТекИмяФильтра;

Процедура ПрочитатьФильтр(ТолькоИмя = 0) Далее

//====================================================================== //--- УМК Сандомирский В.Ю. (31.10.14)
Процедура Выполнить()
КонецПроцедуры

Процедура ПрочитатьТекФильтр()
	СВыбТМЦ = глПрочитатьФильтр("" + КаталогИБ() + БазИмяФайла + ТекИмяФильтра + "1.txt", фСохрСорт);	
	СТМЦИсключение = глПрочитатьФильтр("" + КаталогИБ() + БазИмяФайла + ТекИмяФильтра + "2.txt");	
	Попытка
		СВыбТМЦ.Выгрузить(СписокВыбТМЦ);
		СТМЦИсключение.Выгрузить(СписокВыбТМЦИсключение);
	Исключение
	КонецПопытки;
	Форма.Заголовок("Фильтр товаров в заказе " + ТекИмяФильтра);
КонецПроцедуры

//====================================================================== //--- УМК Сандомирский В.Ю. (31.10.14)
Процедура ПриОткрытии()	
	Если ТипЗначенияСтр(Форма.Параметр) = "Строка" Тогда
		Если Форма.Параметр = "выбрать" Тогда
			ПрочитатьФильтр(1);
			Форма.Параметр = ТекИмяФильтра;
			Форма.Закрыть();
			Возврат;
		КонецЕсли;
	Иначе
		Если (НазваниеНабораПрав() <> "Администратор") И (НазваниеНабораПрав() <> "ПрочиеЗаказ") Тогда
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ПрочитатьТекФильтр();
КонецПроцедуры // ПриОткрытии()

//====================================================================== //--- УМК Сандомирский В.Ю. (31.10.14)
// Название: РаботаСоСписком
// Параметры:
// 	Режим - строка, принимающая 4 значения:
//		"Добавить"
//		"ДобавитьНесколько"
// 		"Удалить"
// 		"УдалитьВсе"
//	Список - список значений, в котором задается множественный фильтр
//	ТипСправочника - строка, содержащая идентификатор справочнника, по
//						которому осуществляется мноджественный фильтр
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   кнопок работы с множественными фильтрами ("...",".....","X","XX")
// Наименование,.
// Описание:
// процедура предназначена для добавления и удаления элементов
// из множественных фильтров
Процедура РаботаСоСписком(Режим,Список,ТипСправочника)
	
	Перем ТекПоз;
	Перем ТекЭлемент;
	Перем Фрм;
                                     
	Если ТипСправочника = "Категории" Тогда
		ТипСправочника = "ВидыКатегорий";
	КонецЕсли;	
	
	ТекПоз = Список.ТекущаяСтрока();
	Если ТекПоз>0 Тогда
		ТекЭлемент=Список.ПолучитьЗначение(ТекПоз);
	КонецЕсли;

	Если Режим="Добавить" Тогда		// добавляем в список один элемент
		СписокПодбора = Список;
		СправочникПодбора = ВРег(ТипСправочника);
		// открываем окно подбора
		ОткрытьПодбор("Справочник."+ТипСправочника,,Фрм,0,ТекЭлемент);
		Фрм.ВыборГруппы(1);
	ИначеЕсли Режим="ДобавитьНесколько" Тогда  // добавляем в список несколько элементов
		СписокПодбора = Список;
		СправочникПодбора = ВРег(ТипСправочника);
		// открываем окно подбора
		ОткрытьПодбор("Справочник."+ТипСправочника,,Фрм,1,ТекЭлемент);
		Фрм.ВыборГруппы(1);
 	ИначеЕсли Режим="УдалитьВсе" Тогда	// удаляем все элементы из списка
		Список.УдалитьВсе();
	ИначеЕсли Режим="Удалить" Тогда	// удаляем из списка один элемент
		Если ТекПоз>0 Тогда
			Список.УдалитьЗначение(ТекПоз);
		КонецЕсли;		
	КонецЕсли;
КонецПроцедуры		// работа со списком

//====================================================================== //--- УМК Сандомирский В.Ю. (31.10.14)
Процедура ОбработкаПодбора(Значение) 	
	Перем ПредставлениеЗначения;
	
	Если (ВРег(Значение.Вид())=СправочникПодбора) И (СписокПодбора.НайтиЗначение(Значение)=0) Тогда
		ПредставлениеЗначения = Строка(Значение);
		Если СписокПодбора.НайтиЗначение(Значение) <> 1 Тогда
			СписокПодбора.ДобавитьЗначение(Значение,ПредставлениеЗначения);
		КонецЕсли;
		СписокПодбора.ТекущаяСтрока(СписокПодбора.РазмерСписка());
	КонецЕсли;	
КонецПроцедуры

//======================================================================
Процедура ЗаписатьВФайл(НазваниеФайла, Спис, ПисатьСортировку = 0)
	НастройкиФайл=СоздатьОбъект("Текст");	
	Если ФС.СуществуетФайл(НазваниеФайла)=1  Тогда
	    НастройкиФайл.Открыть(НазваниеФайла);
		НастройкиФайл.Очистить(); 
		//НастройкиФайл.КодоваяСтраница();     
	КонецЕсли;	
		
    Для НомерСтроки = 1 По Спис.РазмерСписка() Цикл            
		НастройкиФайл.ДобавитьСтроку(Спис.ПолучитьЗначение(НомерСтроки).Код);				
	КонецЦикла;
	Если ПисатьСортировку = 1 Тогда
		НастройкиФайл.ДобавитьСтроку("Сортировка:" + фСохрСорт);
	КонецЕсли;
	
	НастройкиФайл.Записать(НазваниеФайла);	
КонецПроцедуры // ЗаписатьВФайл

//====================================================================== //--- УМК Сандомирский В.Ю. (31.10.14)
Процедура ЗаписатьФильтр()
	//--- заполняем фильтр
	ЗаписатьВФайл("" + КаталогИБ() + БазИмяФайла + ТекИмяФильтра + "1.txt", СписокВыбТМЦ, 1);
	ЗаписатьВФайл("" + КаталогИБ() + БазИмяФайла + ТекИмяФильтра + "2.txt", СписокВыбТМЦИсключение);
	Форма.Заголовок("Фильтр товаров в заказе " + ТекИмяФильтра);
КонецПроцедуры // ЗаписатьФильтр

//======================================================================
Процедура ЗаписатьФильтрКак()
	Если ВвестиСтроку(ТекИмяФильтра, "Введите имя фильтра", 50) = 1 Тогда
		ТекИмяФильтра = СтрЗаменить(ТекИмяФильтра, "*", "");
		ТекИмяФильтра = СтрЗаменить(ТекИмяФильтра, "\", "");
		ТекИмяФильтра = СтрЗаменить(ТекИмяФильтра, "/", "");
		ТекИмяФильтра = СтрЗаменить(ТекИмяФильтра, "?", "");
		ТекИмяФильтра = СтрЗаменить(ТекИмяФильтра, ":", "");
		
		ЗаписатьФильтр();
	КонецЕсли;
КонецПроцедуры // Записать

//======================================================================
Процедура ПрочитатьФильтр(ТолькоИмя = 0)
	ФС.УстТекКаталог(КаталогИБ());
	Рез = ФС.НайтиПервыйФайл(БазИмяФайла + "*1.txt");
	Если Рез <> "" Тогда
		Спис = СоздатьОбъект("СписокЗначений");
		Спис.ДобавитьЗначение("!!!Основной фильтр!!!");
		Пока Рез <> "" Цикл
			Если Рез <> БазИмяФайла + "1.txt" Тогда
				ИмяФайлаДляЗаписи = Сред(Рез, СтрДлина(БазИмяФайла) + 1);
				ИмяФайлаДляЗаписи = СтрЗаменить(ИмяФайлаДляЗаписи, "1.txt", "");
				Спис.ДобавитьЗначение(ИмяФайлаДляЗаписи);
			КонецЕсли;
			Рез = ФС.НайтиСледующийФайл();
		КонецЦикла;
		
		Спис.Сортировать();
		Зн = "";
		Если Спис.РазмерСписка() <= 1 Тогда
			ТекИмяФильтра = "";
			ПрочитатьТекФильтр();
		Иначе
			Если Спис.ВыбратьЗначение(Зн) = 1 Тогда
				ТекИмяФильтра = Зн;
				Если ТекИмяФильтра = "!!!Основной фильтр!!!" Тогда
					ТекИмяФильтра = ""
				КонецЕсли;
				Если ТолькоИмя = 0 Тогда
					ПрочитатьТекФильтр();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры // ПрочитатьФильтр

//======================================================================
Процедура Сдвиг(Позиций)
	Если СписокВыбТМЦ.ТекущаяСтрока() <> 0 Тогда
		СписокВыбТМЦ.СдвинутьЗначение(Позиций, СписокВыбТМЦ.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры // Сдвиг

БазИмяФайла = "filter_set";
ТекИмяФильтра = "";