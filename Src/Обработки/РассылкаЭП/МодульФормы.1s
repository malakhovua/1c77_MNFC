Перем ИмяОбъекта;
Перем АвтономныйРежим;
Перем Адрес;
Перем СообщениеОтправка;
Перем   cdoAutoPromoteBodyParts,
		cdoFlushBuffersOnWrite,
		cdoHTTPCookies,
		cdoLanguageCode,
		cdoNNTPAccountName,
		cdoNNTPAuthenticate,
		cdoNNTPConnectionTimeout,
		cdoNNTPServer,
		cdoNNTPServerPickupDirectory,
		cdoNNTPServerPort,
		cdoNNTPUseSSL,
		cdoPostEmailAddress,
		cdoPostPassword,
		cdoPostUserName,
		cdoPostUserReplyEmailAddress,
		cdoPostUsingMethod,
		cdoSaveSentItems,
		cdoSendEmailAddress,
		cdoSendPassword,
		cdoSendUserName,
		cdoSendUserReplyEmailAddress,
		cdoSendUsingMethod,
		cdoSMTPAccountName,
		cdoSMTPAuthenticate,
		cdoSMTPConnectionTimeout,
		cdoSMTPServer,
		cdoSMTPServerPickupDirectory,
		cdoSMTPServerPort,
		cdoSMTPUseSSL,
		cdoURLGetLatestVersion,
		cdoURLProxyBypass,
		cdoURLProxyServer,
		cdoUseMessageResponseText; 


Процедура Сформировать()
	Попытка
		iConf = createObject("CDO.Configuration");
		iMsg  = createObject("CDO.Message");
	Исключение 
		Сообщить("Не удалось создать объекты <CDO> для работы с электронной почтой...!"); 
		Возврат;
	КонецПопытки; 
	
	iConf.fields(cdoSendUsingMethod).value = 2;
	//cdoSendUsingPickup    1  Send message using the local SMTP service pickup directory. 
	//cdoSendUsingPort      2  Send the message using the network (SMTP protocol over the
	//                         network).  
	//cdoSendUsingExchange  3  Send the message using the Exchange mail submission URI.
	//                         This URI is found in the user's 
	//                         urn:schemas:httpmail:sendmsg mailbox folder property. 
	iConf.fields(cdoSMTPServer).value         = СокрЛП(ПараметрыЭП.SMTP);
	iConf.fields(cdoSMTPServerPort).value     = Число(СокрЛП(ПараметрыЭП.Порт));
	iConf.fields(cdoSendUserName).value       = СокрЛП(ПараметрыЭП.Логин);
	iConf.fields(CdoSendPassword).value       = СокрЛП(ПараметрыЭП.Пароль);
	iConf.fields(cdoSMTPAuthenticate).value   = 1-ПустоеЗначение(СокрЛП(ПараметрыЭП.Логин));
	
	iConf.fields(cdoSendEmailAddress).value   = СокрЛП(ПараметрыЭП.Адрес);
	iConf.fields(cdoSMTPUseSSL).value         = ПараметрыЭП.SSL;  //Secure Sockets Layer 

	iConf.fields.update();
	
	iMsg.configuration = iConf; 
	
  	iMsg.from 	  = СокрЛП(ПараметрыЭП.Адрес);
  	
  	СписАдресов = СоздатьОбъект("СписокЗначений");
  	Если ТипЗначенияСтр(Адрес) = "Строка" Тогда
  		Если Адрес <> "" Тогда
  			СписАдресов.ДобавитьЗначение(Адрес);
  		КонецЕсли;
  	КонецЕсли;
  	
  	Если СписАдресов.РазмерСписка() = 0 Тогда
	  	СпрР = СоздатьОбъект("Справочник.РассылкаЭП");
	  	
	  	СпрР.ВыбратьЭлементы();
		Пока СпрР.ПолучитьЭлемент() = 1 Цикл
			Если СпрР.ПометкаУдаления() = 0 Тогда
				Если Найти(СпрР.ЧтоРассылать, ИмяОбъекта + ",") <> 0 Тогда
				    СписАдресов.ДобавитьЗначение(СокрЛП(СпрР.Наименование));
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если СписАдресов.РазмерСписка() = 0 Тогда
	    Сообщить("Не выбрано ни одного адреса для рассылки");
		Возврат;
	КонецЕсли;

	Для Инд = 1 По СписАдресов.РазмерСписка() Цикл
		Если Инд = 1 Тогда
			iMsg.to  = СписАдресов.ПолучитьЗначение(Инд);
		Иначе
			iMsg.bcc = iMsg.bcc + ?(СокрЛП(iMsg.bcc) = "","",",") + СписАдресов.ПолучитьЗначение(Инд);
		КонецЕсли;
	КонецЦикла;

	iMsg.subject  = Тема; 
    iMsg.textBody = Текст; 
	iMsg.textBodypart.charset = "windows-1251"; //"iso-8859-5";//либо "koi8-r"
												
	СообщениеОтправка = "Идёт отправка...";
	Форма.Обновить();
	Попытка iMsg.send(); 
		СообщениеОтправка = "Письмо отправлено...";
	Исключение	
		СообщениеОтправка = "Ошибка! Письмо не ушло!";
		Сообщить("> Ошибка"+ОписаниеОшибки(),"!");
	КонецПопытки;
	Форма.Обновить();
КонецПроцедуры

Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
		ПараметрыЭП = Форма.Параметр.Получить("ПараметрыЭП");
		Текст = Форма.Параметр.Получить("Текст");
		Тема = Форма.Параметр.Получить("Тема");		
		ИмяОбъекта = Форма.Параметр.Получить("ИмяОбъекта");
		Адрес = Форма.Параметр.Получить("Адрес");
		Сформировать();
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

//CDO 
cdoSendPassword              = "http://schemas.microsoft.com/cdo/configuration/sendpassword";      
cdoSendEmailAddress          = "http://schemas.microsoft.com/cdo/configuration/sendemailaddress";   
cdoAutoPromoteBodyParts      = "http://schemas.microsoft.com/cdo/configuration/autopromotebodyparts" ;
cdoFlushBuffersOnWrite       = "http://schemas.microsoft.com/cdo/configuration/flushbufferson;write" ;
cdoHTTPCookies               = "http://schemas.microsoft.com/cdo/configuration/httpcookies" ;
cdoLanguageCode              = "http://schemas.microsoft.com/cdo/configuration/languagecode" ;
cdoNNTPAccountName           = "http://schemas.microsoft.com/cdo/configuration/nntpaccountname" ;
cdoNNTPAuthenticate          = "http://schemas.microsoft.com/cdo/configuration/nntpauthenticate" ;
cdoNNTPConnectionTimeout     = "http://schemas.microsoft.com/cdo/configuration/nntpconnectiontimeout" ;
cdoNNTPServer                = "http://schemas.microsoft.com/cdo/configuration/nntpserver" ;
cdoNNTPServerPickupDirectory = "http://schemas.microsoft.com/cdo/configuration/nntpserverpickupdirectory" ;
cdoNNTPServerPort            = "http://schemas.microsoft.com/cdo/configuration/nntpserverport" ;
cdoNNTPUseSSL                = "http://schemas.microsoft.com/cdo/configuration/nntpusessl" ;
cdoPostEmailAddress          = "http://schemas.microsoft.com/cdo/configuration/postemailaddress" ;
cdoPostPassword              = "http://schemas.microsoft.com/cdo/configuration/postpassword" ;
cdoPostUserName 		     = "http://schemas.microsoft.com/cdo/configuration/postusername" ;
cdoPostUserReplyEmailAddress = "http://schemas.microsoft.com/cdo/configuration/postuserreplyemailaddress" ;
cdoPostUsingMethod           = "http://schemas.microsoft.com/cdo/configuration/postusing" ;
cdoSaveSentItems             = "http://schemas.microsoft.com/cdo/configuration/savesentitems" ;
cdoSendEmailAddress          = "http://schemas.microsoft.com/cdo/configuration/sendemailaddress" ;
cdoSendPassword              = "http://schemas.microsoft.com/cdo/configuration/sendpassword" ;
cdoSendUserName              = "http://schemas.microsoft.com/cdo/configuration/sendusername" ;
cdoSendUserReplyEmailAddress = "http://schemas.microsoft.com/cdo/configuration/senduserreplyemailaddress" ;
cdoSendUsingMethod           = "http://schemas.microsoft.com/cdo/configuration/sendusing" ;
cdoSMTPAccountName           = "http://schemas.microsoft.com/cdo/configuration/smtpaccountname" ;
cdoSMTPAuthenticate          = "http://schemas.microsoft.com/cdo/configuration/smtpauthenticate" ;
cdoSMTPConnectionTimeout     = "http://schemas.microsoft.com/cdo/configuration/smtpconnectiontimeout" ;
cdoSMTPServer                = "http://schemas.microsoft.com/cdo/configuration/smtpserver";
cdoSMTPServerPickupDirectory = "http://schemas.microsoft.com/cdo/configuration/smtpserverpickupdirectory";
cdoSMTPServerPort            = "http://schemas.microsoft.com/cdo/configuration/smtpserverport";
cdoSMTPUseSSL                = "http://schemas.microsoft.com/cdo/configuration/smtpusessl";
cdoURLGetLatestVersion       = "http://schemas.microsoft.com/cdo/configuration/urlgetlatestversion";
cdoURLProxyBypass            = "http://schemas.microsoft.com/cdo/configuration/urlproxybypass";
cdoURLProxyServer            = "http://schemas.microsoft.com/cdo/configuration/urlproxyserver";
cdoUseMessageResponseText    = "http://schemas.microsoft.com/cdo/configuration/usemessageresponsetext"; 
СообщениеОтправка = "";
ДатаР = ТекущаяДата();
АвтономныйРежим = 0;