Перем ВыгружаемыйДокумент;
Перем СхемаЭкспортаУпаковок;

Перем СписокПодбора,СправочникПодбора;

//======================================================================
Процедура ПриОткрытии()
	
	ДатаЦен = РабочаяДата();
	
КонецПроцедуры // ПриОткрытии

//======================================================================
Процедура Выполнить()
	
	СпрРазрешенныеВидыУпаковкиТМЦ = СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ");
	
	
	ТекстЗапросаПоТМЦ = 
	"//{{ЗАПРОС(СпрТМЦ)
	|Период с ДатаЦен;
	|Обрабатывать НеПомеченныеНаУдаление;
	|ТМЦ 			= Справочник.ТМЦ.ТекущийЭлемент;
	|Группировка ТМЦ без групп;	
	|Условие (ТМЦ в  СписокВыбТМЦ);
	|";
	
	
	ЗапросПоТМЦ = СоздатьОбъект("Запрос");
	Если ЗапросПоТМЦ.Выполнить(ТекстЗапросаПоТМЦ) = 0 Тогда
		Возврат;
	КонецЕсли;
	

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Экспорт");
	Таб.ВывестиСекцию("Шапка|Основная");
	Для Инд = 1 По СписДопВУП.РазмерСписка() Цикл
		Если СписДопВУП.Пометка(Инд) = 1 Тогда
			Таб.ПрисоединитьСекцию("Шапка|ДопРеквизит");
		КонецЕсли;
	КонецЦикла;
	
	Пока ЗапросПоТМЦ.Группировка(1) = 1 Цикл		
		
		//Сообщить(ЗапросПоТМЦ.ТМЦ); //--- технологически
		
		СпрРазрешенныеВидыУпаковкиТМЦ.ИспользоватьВладельца(ЗапросПоТМЦ.ТМЦ.ТекущийЭлемент());
		СпрРазрешенныеВидыУпаковкиТМЦ.ВыбратьЭлементы();
		Пока СпрРазрешенныеВидыУпаковкиТМЦ.ПолучитьЭлемент() = 1 Цикл	
			ПечПометкаУдаления = СпрРазрешенныеВидыУпаковкиТМЦ.ПометкаУдаления();				
			ТМЦ 			= ЗапросПоТМЦ.ТМЦ;	
			Упаковка 		= СпрРазрешенныеВидыУпаковкиТМЦ.ТекущийЭлемент();
			ВидУпаковки 	= СпрРазрешенныеВидыУпаковкиТМЦ.ВидУпаковки;	
			Таб.ВывестиСекцию("Строка|Основная");	
			Для Инд = 1 По СписДопВУП.РазмерСписка() Цикл
				Если СписДопВУП.Пометка(Инд) = 1 Тогда
					ИмяР = СписДопВУП.ПолучитьЗначение(Инд);
					Если Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(ИмяР).Периодический = 1 Тогда
						Зн = Упаковка.ПолучитьАтрибут(ИмяР).Получить(ДатаЦен);
					Иначе
						Зн = Упаковка.ПолучитьАтрибут(ИмяР);
					КонецЕсли;
					Если ТипЗначенияСтр(Зн) = "Перечисление" Тогда
						Зн = Зн.Идентификатор();			    
					КонецЕсли;
					Таб.ПрисоединитьСекцию("Строка|ДопРеквизит");
				КонецЕсли;
			КонецЦикла;
					
		КонецЦикла;
		
	КонецЦикла;	
		
	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Экспорт упаковок","");	
	
КонецПроцедуры

//======================================================================
Процедура ОбработкаПодбора(Значение) 	
	Перем ПредставлениеЗначения;
	
	Если (ВРег(Значение.Вид())=СправочникПодбора) И (СписокПодбора.НайтиЗначение(Значение)=0) Тогда
		ПредставлениеЗначения = Строка(Значение);
		Если СписокПодбора.НайтиЗначение(Значение) <> 1 Тогда
			СписокПодбора.ДобавитьЗначение(Значение,ПредставлениеЗначения);
		КонецЕсли;
		СписокПодбора.ТекущаяСтрока(СписокПодбора.РазмерСписка());
	КонецЕсли;
	
КонецПроцедуры

//======================================================================
// Название: РаботаСоСписком
// Параметры:
// 	Режим - строка, принимающая 4 значения:
//		"Добавить"
//		"ДобавитьНесколько"
// 		"Удалить"
// 		"УдалитьВсе"
//	Список - список значений, в котором задается множественный фильтр
//	ТипСправочника - строка, содержащая идентификатор справочнника, по
//						которому осуществляется мноджественный фильтр
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   кнопок работы с множественными фильтрами ("...",".....","X","XX")
// Наименование,.
// Описание:
// процедура предназначена для добавления и удаления элементов
// из множественных фильтров
Процедура РаботаСоСписком(Режим,Список,ТипСправочника)
	
	Перем ТекПоз;
	Перем ТекЭлемент;
	Перем Фрм;
                                     
	Если ТипСправочника = "Категории" Тогда
		ТипСправочника = "ВидыКатегорий";
	КонецЕсли;	
	
	ТекПоз = Список.ТекущаяСтрока();
	Если ТекПоз>0 Тогда
		ТекЭлемент=Список.ПолучитьЗначение(ТекПоз);
	КонецЕсли;

	Если Режим="Добавить" Тогда		// добавляем в список один элемент
		СписокПодбора = Список;
		СправочникПодбора = ВРег(ТипСправочника);
		// открываем окно подбора
		ОткрытьПодбор("Справочник."+ТипСправочника,,Фрм,0,ТекЭлемент);
		Фрм.ВыборГруппы(1);

	ИначеЕсли Режим="ДобавитьНесколько" Тогда  // добавляем в список несколько элементов
		СписокПодбора = Список;
		СправочникПодбора = ВРег(ТипСправочника);
		// открываем окно подбора
		ОткрытьПодбор("Справочник."+ТипСправочника,,Фрм,1,ТекЭлемент);
		Фрм.ВыборГруппы(1);

 	ИначеЕсли Режим="УдалитьВсе" Тогда	// удаляем все элементы из списка
		Список.УдалитьВсе();

	ИначеЕсли Режим="Удалить" Тогда	// удаляем из списка один элемент
		Если ТекПоз>0 Тогда
			Список.УдалитьЗначение(ТекПоз);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры		// работа со списком


//--- УМК Сандомирский В.Ю. (26.11.14) схема экспорта подчиненного справочника ТМЦ
Инд = 1;
Пока Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Выбран() = 1 Цикл
	ИмяРекв = Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Идентификатор;
	Синоним = Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Синоним;
	ТипЗнач = Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Тип;

	Если (ТипЗнач = "Строка") ИЛИ (ТипЗнач = "Число") ИЛИ (ТипЗнач = "Перечисление") ИЛИ (ТипЗнач = "Счет") Тогда
		СписДопВУП.ДобавитьЗначение(ИмяРекв, Синоним);
	КонецЕсли;		
	Инд = Инд + 1;		
КонецЦикла;
СписДопВУП.СортироватьПоПредставлению();
