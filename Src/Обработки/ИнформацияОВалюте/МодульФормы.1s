// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем КонтекстДокумента; // Переменная, в которой запоминаем контекст редактируемого документа
Перем ВидДок, ВидДокДляПоиска;

Перем СтараяВалюта;      
Перем ДокумнетыГривня;

Перем РежимРаботы;

// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
// Функция проверки изменеия параметров документа в форме обработки.
// Если изменились, спрашиваем установить ли новые значения, 
// если да, то возвращаем "Да", иначе - "Нет".
Функция Изменили()
	
	Результат = Нет;
	
	Если КонтекстДокумента.Валюта <> Валюта Тогда
		Результат = Да;
	КонецЕсли;

	Если КонтекстДокумента.Курс <> Курс Тогда;
		Результат = Да;
	КонецЕсли;                          
	
	Если глЕстьРеквизитШапки("Дата_Курса", ВидДок) = Да Тогда
		Если КонтекстДокумента.Дата_Курса <> Дата_Курса Тогда;
			Результат = Да;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции  //Изменили

// ===============================
Функция УстВалюта()
	// обработаем исключительные ситуации
	Если ВидДок = "ПриходнаяНакладнаяГТД" Тогда
		Если КонтекстДокумента.Контрагент.Выбран() = 1 Тогда
			//Если КонтекстДокумента.Контрагент.ВидКонтрагента <> Перечисление.ВидыКонтрагентов.Нерезидент Тогда
			//	Предупреждение("В документе ПриходнаяНакладнаяГТД контрагент должен быть Нерезидентом! 
			//					|Выбор валюты невозможен.");
			//	Возврат 0;
			//КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	// Инициализируем текущие значения
	Если глЕстьРеквизитШапки("Дата_Курса", ВидДок) = Да Тогда
		Дата_Курса   = КонтекстДокумента.Дата_Курса;
	Иначе
		Дата_Курса   = КонтекстДокумента.ДатаДок;
	КонецЕсли;	                 
	Курс 	= КонтекстДокумента.Курс;       
	Валюта 	= КонтекстДокумента.Валюта;
	Если ПустоеЗначение(Валюта) = 1 Тогда
	    Если ВидДок <> "ПриходнаяНакладнаяГТД" Тогда
			Валюта  		= Гривня;
			Курс			= 1;
		КонецЕсли;
		Возврат 1;
	КонецЕсли;

	Если (Валюта <> Гривня) И (Найти(ДокумнетыГривня,ВидДокДляПоиска)>0) Тогда // неправильно задана валюта
		глКомментарий(""+Валюта+" не может быть валютой этого документа! Установлена валюта гривня.",0,,"!");
		Валюта  		= Гривня;
		Курс			= 1;
		Возврат 1;
	КонецЕсли;	
	Если (Валюта = Гривня) И (ВидДок = "ПриходнаяНакладнаяГТД") Тогда
		глКомментарий("Гривня не может быть валютой этого документа! Валюта очищена.",0,,"!");
		Валюта 	= 0;
		Курс	= 0;
	КонецЕсли;
	//Если глЕстьРеквизитШапки("Контрагент", ВидДок) = Да Тогда     
	//	Если КонтекстДокумента.Контрагент.Выбран() = 1 Тогда
	//		Если (КонтекстДокумента.Контрагент.ВидКонтрагента <> Перечисление.ВидыКонтрагентов.Нерезидент) 
	//				И (Валюта <> Гривня) Тогда
	//			глКомментарий("Валюту, отличную от "+Гривня+", можно выбирать только для контрагентов с видом Нерезидент! Установлена валюта гривня.",0,,"!");
	//			Валюта = Гривня;
	//			Курс			= 1;
	//			Возврат 1;
	//		КонецЕсли;		
	//	КонецЕсли;
	//КонецЕсли;	         
	Возврат 1;
КонецФункции


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Функция УстДоступность()
	ДостВал 	= ?(Найти(ДокумнетыГривня,ВидДокДляПоиска)>0,0,1);
	ДостВал 	= Мин(РежимРаботы,ДостВал);
	ВидимКурс 	= ?(Валюта = Гривня,0,1);
	Форма.тВалюта.		Доступность(ДостВал);
	Форма.Валюта.		Доступность(ДостВал);
    Форма.тТекстКурса.	Видимость(ВидимКурс);
    Форма.тТекстГривня.	Видимость(ВидимКурс);
    Форма.Курс.			Видимость(ВидимКурс);
    Форма.тДата_Курса.	Видимость(ВидимКурс);
    Форма.Дата_Курса.	Видимость(ВидимКурс);
КонецФункции //ТекстКурса

// ===============================
Функция ТекстКурса()
	Возврат ""+Валюта.Кратность.Получить(Дата_Курса)+" "+ Валюта.Кратко + " = ";
КонецФункции //ТекстКурса

// ===============================
Процедура ИзмВалюта()

	Если ПустоеЗначение(Валюта) = 1 Тогда
		СтараяВалюта = 0;
		Возврат;
	КонецЕсли;

	Если ПустоеЗначение(Дата_Курса) = 1 Тогда
		Возврат;
	КонецЕсли;                
	
	Если (Валюта = Гривня) И (ВидДок = "ПриходнаяНакладнаяГТД") Тогда
		Предупреждение("Нельзя выбирать валюту "+Гривня+" в качестве валюты документа ГТД.");
		Валюта = СтараяВалюта;
		Возврат;
	КонецЕсли;

	//Если глЕстьРеквизитШапки("Контрагент", ВидДок) = Да Тогда     
	//	Если КонтекстДокумента.Контрагент.Выбран() = 1 Тогда
	//		Если (КонтекстДокумента.Контрагент.ВидКонтрагента <> Перечисление.ВидыКонтрагентов.Нерезидент) 
	//				И (Валюта <> Гривня) Тогда
	//			Предупреждение("Валюту, отличную от "+Гривня+", можно выбирать только для контрагентов с видом Нерезидент.");
	//			Валюта = Гривня;
	//			Возврат;
	//		КонецЕсли;		
	//	КонецЕсли;
	//КонецЕсли;	
	// При смене валюты зачитываем текущий курс на дату
	Курс = глКурсДляВалюты(Валюта, Дата_Курса);
	СтараяВалюта = Валюта;
	
КонецПроцедуры

// ===============================
// Процедура установки выбранных значений в документе
Процедура ЗаписатьИзменения()   
	Перем Валюта_Прежн, Курс_Прежн, Цены_Прежн;
	// Запомним старые значения 
	Валюта_Прежн = КонтекстДокумента.Валюта;
	Курс_Прежн   = КонтекстДокумента.Курс;
	
	// А теперь запомним новые значения
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Предупреждение("Не задана валюта!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	КонтекстДокумента.Валюта       = Валюта;

	Если ПустоеЗначение(Курс) = 1 Тогда
		Предупреждение("Не задан курс валюты!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	КонтекстДокумента.Курс         = Курс;

	Если ПустоеЗначение(Дата_Курса) = 1 Тогда
		Предупреждение("Не задана дата курса валюты!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;                 
	
	Если глЕстьРеквизитШапки("Дата_Курса", ВидДок) = Да Тогда
		КонтекстДокумента.Дата_Курса   = Дата_Курса;
	КонецЕсли;	
	
	// Тепрерь сделаем необходимые пересчеты
	// Если изменилась валюта или курс
	Если (Валюта_Прежн <> Валюта) Или (Курс_Прежн <> Курс) Тогда
		Если (глЕстьРеквизитМнЧ("ЦенаСНДС", ВидДок) = Да) Или 
				(глЕстьРеквизитМнЧ("ЦенаБезНДС", ВидДок) = Да) Тогда
			глУстан_Вал(КонтекстДокумента, Валюта, Валюта_Прежн, Курс, Курс_Прежн);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
	

// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()  // Предопределенная процедура
	// Запоминаем параметр (Обработка всегда должна вызываться с параметром)
	КонтекстДокумента = Форма.Параметр;  

	Если ТипЗначенияСтр(КонтекстДокумента) <> "ГрупповойКонтекст" Тогда
		Предупреждение("Обработка вызывается только из формы документов!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	ВидДок = КонтекстДокумента.Вид();
	ВидДокДляПоиска = "="+ВРег(ВидДок)+"=";
	РежимРаботы = ?(Найти(ДокумнетыГривня,ВидДокДляПоиска)>0,0,1); // редакт. или просмотр в зависимости от вида док.
	Если УстВалюта()=0 Тогда // вызов обработки запрещен
		СтатусВозврата(0);
		Возврат;                                     
	КонецЕсли;
	СтараяВалюта = Валюта;
	
	// Меняем заголовок формы
	Форма.Заголовок("Информация о валюте");
	//Инструментальная линейка не нужна
	Форма.ПанельИнструментов(0);
КонецПроцедуры

// ===============================
Процедура ПриЗакрытии() // Предопределенная процедура
	// Если нужно записать изменения: спрашиваем и записываем
	Если Изменили() = Да Тогда
		Если Вопрос("Сохранить изменения?", "Да+Нет") = "Да" Тогда
			ЗаписатьИзменения();
		КонецЕсли   
	КонецЕсли;   
КонецПроцедуры
	                       
// ===============================
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтандОбр) // Предопределенная процедура
	Если ИдентЭлемДиалога = "Валюта" Тогда
		// проверим возможность редактирования валюты
		Если глЕстьРеквизитШапки("Договор", ВидДок) = Да Тогда     
			Если КонтекстДокумента.Договор.Выбран() = 1 Тогда
				Если глЕстьРеквизитШапки("Валюта", КонтекстДокумента.Договор.Вид()) = Да Тогда
					Валюта 		= КонтекстДокумента.Валюта;
					Курс 		= КонтекстДокумента.Курс;
					Дата_Курса	= КонтекстДокумента.Дата_Курса;
					РежимРаботы = 0; // только просмотр
					глКомментарий("Валюту нужно изменять в договоре!",1,,"!");
					ФлагСтандОбр = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

ДокумнетыГривня = "=ВОЗВРАТРОЗНИЦА=РАСХОДНАЯРОЗНИЧНАЯ=ОТЧЕТКА=ПРИХОДНАЯНАКЛАДНАЯЗАПАСЫ=ПРИХОДНАЯНАКЛАДНАЯПРОЧИЕ=";	

