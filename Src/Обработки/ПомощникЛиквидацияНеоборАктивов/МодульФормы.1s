// переменные для управления работой помощника
Перем Шаг, ПоследнийШаг, стрСовет, стрОписание, Списание, Док, ЭтоНоваяСтрока;
// переменные для отображения реквизитов табличной части
Перем стрПервСтоимость, стрИзнос, стрЦенаБезНДС, стрСуммаБезНДС, стрНДС, стрСуммаСНДС;

Перем СтарыйДокОснование, СтарыйДоговор;

// ======================================
// Рисует слои помощника в зависимости от текущего шага
Процедура НарисоватьСлои()
	Форма.ИспользоватьСлой("Общий, Слой"+Строка(Шаг),2);
КонецПроцедуры 

// ======================================
Процедура ПоказатьСовет()
	стрСовет = "";
	Если Шаг = 1 Тогда
	    стрСовет = "Здесь будут показываться советы и комментарии по заполнению реквизитов, находящихся в окне";
	ИначеЕсли Шаг = 2 Тогда
	    стрСовет = "В зависимости от выбора вида ликвидации строятся различные схемы проводок";
	ИначеЕсли Шаг = 3 Тогда
	    стрСовет = "Кнопка ""Добавить"" создает новую строку в табличной части, ""Редактировать"" и ""Удалить"" - позволяет соответственно изменить или удалить существующие строки";
	ИначеЕсли Шаг = 4 Тогда
	    стрСовет = "Кнопка ""Сохранить"" позволяет запомнить изменения, ""Сохранить и создать новую строку"" - после сохранения открывает новую строку, ""Не сохранять"" - отменяет сделанные изменения";
	ИначеЕсли Шаг = 5 Тогда
	    стрСовет = "Виновное лицо может быть сотрудником нашей организации, физическим или юридическим (Контрагент) лицом";
	ИначеЕсли Шаг = 6 Тогда
	    стрСовет = "Эти реквизиты необходимы для создания и печати расходной ракладной покупателю";
	ИначеЕсли Шаг = 7 Тогда
	    стрСовет = "Эти реквизиты необходимы для создания и печати расходной ракладной покупателю";
	ИначеЕсли Шаг = 8 Тогда
	    стрСовет = "В поле ""Примечание"" можно ввести комментарий к создаваемому документу";
	ИначеЕсли Шаг = 9 Тогда
	    стрСовет = "";
	КонецЕсли;
КонецПроцедуры 

// ===============================
// Выполняет проверку корректности заполнения реквизитов после каждого шага
Функция ПроверитьШаг()
    Если Шаг = 2 Тогда
		Если ПустоеЗначение(Фирма)=1 Тогда
		    стрСовет = "Не выбрана фирма";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(ВидНДС)=1 Тогда
		    стрСовет = "Не выбран вид НДС";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(ВидЛиквидации)=1 Тогда
		    стрСовет = "Не выбран вид ликвидации необоротного актива";
			Возврат 0;
		КонецЕсли;
		Если ВидЛиквидации = Списание Тогда
			Если ПустоеЗначение(ТипСписания)=1 Тогда
			    стрСовет = "Не выбран тип списания необоротного актива";
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Шаг = 3 Тогда
		Если ТабличнаяЧасть.КоличествоСтрок()=0 Тогда
		    стрСовет = "Не заполнена табличная часть документа";
			Возврат 0;
		КонецЕсли;
	ИначеЕсли Шаг = 4 Тогда
		Если ПустоеЗначение(НеоборотныйАктив)=1 Тогда
		    стрСовет = "Не выбран необоротный актив";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг = 5 Тогда
		Если ПустоеЗначение(Виновный)=1 Тогда
		    стрСовет = "Не выбрано виновное лицо";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(ВидДеятельности)=1 Тогда
		    стрСовет = "Не выбран вид деятельности";
			Возврат 0;
		КонецЕсли;
	ИначеЕсли Шаг = 6 Тогда
		Если ПустоеЗначение(Фирма)=1 Тогда
		    стрСовет = "Не выбран покупатель";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(ВидТорговли)=1 Тогда
		    стрСовет = "Не выбран вид торговли";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(СчетКонтрагента)=1 Тогда
		    стрСовет = "Не выбран счет контрагента";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(Валюта)=1 Тогда
		    стрСовет = "Не выбрана валюта";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг = 7 Тогда
		Если ПустоеЗначение(Отпустил)=1 Тогда
		    стрСовет = "Не выбран сотрудник, который отпускает ценности";
			Возврат 0;
		КонецЕсли;
    ИначеЕсли Шаг = 8 Тогда
		Если ПустоеЗначение(СубконтоВалДох)=1 Тогда
		    стрСовет = "Не выбрано субконто валовых доходов";
			Возврат 0;
		КонецЕсли;
		Если ПустоеЗначение(СубконтоВалРасх)=1 Тогда
		    стрСовет = "Не выбрано субконто валовых расходов";
			Возврат 0;
		КонецЕсли;
		Если ВидЛиквидации = Списание Тогда
			Если ПустоеЗначение(ВидЗатрат)=1 Тогда
			    стрСовет = "Не выбрана статья затрат";
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат 1;
КонецФункции

// ===============================
Процедура кОтмена()
	Если Вопрос("Прекратить работу помощника?",4)=6 Тогда
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ПоказатьРеквизитыСтроки()
	ТабличнаяЧасть.ПолучитьСтрокуПоНомеру(ТабличнаяЧасть.ТекущаяСтрока());
	стрПервСтоимость = Формат(ТабличнаяЧасть.ПервСтоимость, "Ч12.2");
	стрИзнос = Формат(ТабличнаяЧасть.Износ, "Ч12.2");
	стрЦенаБезНДС = Формат(ТабличнаяЧасть.ЦенаБезНДС, "Ч12.2");
	стрСуммаБезНДС = Формат(ТабличнаяЧасть.СуммаБезНДС, "Ч12.2");
	стрНДС = Формат(ТабличнаяЧасть.НДС, "Ч12.2");
	стрСуммаСНДС = Формат(ТабличнаяЧасть.СуммаСНДС, "Ч12.2");
КонецПроцедуры 

// ======================================
Процедура ВыгрузитьИзТаблицы()
	НеоборотныйАктив = ТабличнаяЧасть.НеоборотныйАктив;
	МестоХранения = ТабличнаяЧасть.МестоХранения;
	Кво = ТабличнаяЧасть.Кво;
	ПервСтоимость = ТабличнаяЧасть.ПервСтоимость;
	Износ = ТабличнаяЧасть.Износ;
	ЦенаБезНДС = ТабличнаяЧасть.ЦенаБезНДС;
	СуммаБезНДС = ТабличнаяЧасть.СуммаБезНДС;
	НДС = ТабличнаяЧасть.НДС;
	СуммаСНДС = ТабличнаяЧасть.СуммасНДС;
КонецПроцедуры 

// ======================================
Процедура ЗагрузитьВТаблицу()
	ТабличнаяЧасть.НеоборотныйАктив = НеоборотныйАктив;
	ТабличнаяЧасть.МестоХранения = МестоХранения;
	ТабличнаяЧасть.Кво = Кво;
	ТабличнаяЧасть.ПервСтоимость = ПервСтоимость;
	ТабличнаяЧасть.Износ = Износ;
	ТабличнаяЧасть.ЦенаБезНДС = ЦенаБезНДС;
	ТабличнаяЧасть.СуммаБезНДС = СуммаБезНДС;
	ТабличнаяЧасть.НДС = НДС;
	ТабличнаяЧасть.СуммаСНДС = СуммасНДС;
КонецПроцедуры 

// ===============================
// Добавление новой строки в документ и вызов ее на редактирование
Процедура кДобавить()
	ТабличнаяЧасть.НоваяСтрока();
	ТабличнаяЧасть.ТекущаяСтрока(ТабличнаяЧасть.КоличествоСтрок());
	ТабличнаяЧасть.ПолучитьСтрокуПоНомеру(ТабличнаяЧасть.КоличествоСтрок());
	ВыгрузитьИзТаблицы();
	ЭтоНоваяСтрока = 1;
	// переходим на 4 шаг мастера - редактирование реквизитов строки табличной части
	Шаг = 4;
	НарисоватьСлои();
	ПоказатьСовет();
КонецПроцедуры

// ===============================
// Редактирование текущей строки табличной части
Процедура кРедактировать()
	Если ТабличнаяЧасть.ТекущаяСтрока() <> 0 Тогда
		ТабличнаяЧасть.ПолучитьСтрокуПоНомеру(ТабличнаяЧасть.ТекущаяСтрока());
		ВыгрузитьИзТаблицы();
		ЭтоНоваяСтрока = 0;
		// переходим на 4 шаг мастера - редактирование реквизитов строки табличной части
		Шаг = 4;
		НарисоватьСлои();
		ПоказатьСовет();
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура кТабличнаяЧасть()
	// если двойной щелчок на табличной части, и она пуста, добавим строку,
	// иначе - редактируем текущую
	Если ТабличнаяЧасть.КоличествоСтрок() = 0 Тогда
		кДобавить();
	Иначе
		кРедактировать();
	КонецЕсли;
КонецПроцедуры 

// ===============================
// Удаление текущей строки табличной части
Процедура кУдалить()
	Если ТабличнаяЧасть.ТекущаяСтрока() <> 0 Тогда
		Если Вопрос("Удалить строку табличной части?","Да+Нет")="Да" Тогда
			ТабличнаяЧасть.УдалитьСтроку(ТабличнаяЧасть.ТекущаяСтрока());
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
// Запомнить текущие значения реквизитов строки табл. части
Процедура кСохранить()
	Если ПроверитьШаг() = 1 Тогда
		ЗагрузитьВТаблицу();
		// возврат из редактирования значений строки табличной части
		Шаг = 3;
		НарисоватьСлои();
		ПоказатьСовет();
	КонецЕсли;
КонецПроцедуры

// ===============================
// Запомнить текущие значения реквизитов строки табл. части и создать новую строку
Процедура кСохранитьИСоздатьНовую()
	Если ПроверитьШаг() = 1 Тогда
		ЗагрузитьВТаблицу();
		кДобавить();
	КонецЕсли;
КонецПроцедуры

// ===============================
// Выход из режима редактирования строки без ее сохранения
Процедура кНеСохранять()
	Если ЭтоНоваяСтрока = 1 Тогда
		ТабличнаяЧасть.УдалитьСтроку();
	КонецЕсли;
	// возврат из редактирования значений строки табличной части
	Шаг = 3;
	НарисоватьСлои();
	ПоказатьСовет();
КонецПроцедуры

// ======================================
Процедура ПоказатьОписание()
	стрОписание = "";
    Если Шаг = 1 Тогда
        стрОписание = "Здесь будут показываться все выбранные параметры в процессе работы помощника";
	ИначеЕсли Шаг = 2 Тогда
		Если ВидЛиквидации = Списание Тогда
			Если ТипСписания=Перечисление.ТипыСписанияНеоборАктивов.ПоРешениюНалогоплательщикаИлиКМУ Тогда
		        стрОписание = "В случае ликвидации основных фондов группы 1 по решению " +
							"налогоплательщика или Кабинета Министров Украины, балансовая " +
							"стоимость такого объекта приравнивается к нулю, амортизационные " +
							"отчисления не начисляются. При ликвидации объектов групп 2 и 3 " +
							"балансовая стоимость не изменяется";
			ИначеЕсли ТипСписания=Перечисление.ТипыСписанияНеоборАктивов.ВследствиеОбстоятельствНепреодолимойСилы Тогда
		        стрОписание = "К обстоятельствам непреодолимой силы относятся следствия " + 
							"чрезвычайных ситуаций техногенного, природного или экологического " + 
							"характера (большие аварии в системах водо-, теплообеспечения и пр., " +
							"разрушения, произошедшие из-за землетрясений, наводнений, буреломами " +
							"и пр.), который делают невозможным дальнейшую эксплуатацию " +
							"необоротных средств";
			ИначеЕсли ТипСписания=Перечисление.ТипыСписанияНеоборАктивов.ВынужденнаяЗаменаЕслиВиновныйУстановлен Тогда
		        стрОписание = "Тип списания ""Вынужденная замена, когда виновный установлен"" " +
							"применяется в тех случаях, когда необоротный актив был " +
							"приведен в негодность поврежден, похищен по вине известного " +
							"физического или юридического лица. В этом случае виновный " +
							"возмещает причиненный ущерб.";
			ИначеЕсли ТипСписания=Перечисление.ТипыСписанияНеоборАктивов.ПоДругойПричине Тогда
		        стрОписание = "Тип списания необоротных активов ""По другой причине"" подразумевает, " +
							"что ликвидируется объект основных фондов группы 1, не связанное с " +
							"обстоятельствами непреодолимой силы, а также все другие случаи, не " +
							"подходящие под описание остальных типов списания";
			КонецЕсли;
		Иначе
	        стрОписание = "Вид ликвидации необоротных активов ""Реализация"" подразумевает, " +
						"что объекты продаются, или передаются безоплатно на сторону";
		КонецЕсли;
	КонецЕсли;
	Для НомерШага = 3 по Шаг цикл
		// Описание строится по уже пройденным шагам помощника, которые были доступны
		// при выбранных типах ликвидации и списания
		Если НомерШага=3 Тогда
			стрОписание = "Документ №"+СокрЛП(НомерДок)+
						" от "+СокрЛП(Строка(ДатаДок)) + " по фирме "+Строка(Фирма)+
						". Вид ликвидации - "+ ВидЛиквидации;
			Если ВидЛиквидации = Списание Тогда
				стрОписание = стрОписание + ". Тип списания - " + СокрЛП(Строка(ТипСписания));
			КонецЕсли;
			стрОписание = стрОписание + ". Вид НДС: " + ВидНДС;
		ИначеЕсли НомерШага=7 Тогда
			Если (ВидЛиквидации <> Списание) Тогда
				стрОписание = стрОписание + ". Покупатель "+Контрагент+". Вид торговли "+ВидТорговли+
							". Валюта продажи - "+Валюта;
			КонецЕсли;
		ИначеЕсли НомерШага=8 Тогда
			Если (ВидЛиквидации = Списание) Тогда
				Если ТипСписания = Перечисление.ТипыСписанияНеоборАктивов.ВынужденнаяЗаменаЕслиВиновныйУстановлен Тогда
					стрОписание = стрОписание + ". Виновное лицо - "+Виновный+
								". Сумма возмещения виновным "+СуммаВозмещения+" грн";
				КонецЕсли;
			Иначе
				стрОписание = стрОписание + ". Сотрудник, который отпустил ценности: "+Отпустил;
			КонецЕсли;
		ИначеЕсли НомерШага=9 Тогда
			Если (ВидЛиквидации = Списание) Тогда
				стрОписание = стрОписание + ". Вид затрат: "+ВидЗатрат;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ======================================
Процедура ИзмТипСписания()
	ПоказатьОписание();
КонецПроцедуры 

// ======================================
Процедура ИзмВидЛиквидации()
	ПоказатьОписание();
КонецПроцедуры 

// ======================================
Процедура ЗаписатьДокумент()
	Док.Фирма = Фирма;
	Док.НомерДок = НомерДок;
	Док.ДатаДок = ДатаДок;
	
	Док.Валюта = Валюта;
	Док.ВидДеятельности = ВидДеятельности;
	Док.ВидЗатрат = ВидЗатрат;
	Док.ВидЛиквидации = ВидЛиквидации;
	Док.ВидНДС = ВидНДС;
	Док.ВидТорговли = ВидТорговли;
	Док.Виновный = Виновный;
	Док.ДовДата = ДовДата;
	Док.ДовНомер = ДовНомер;
	Док.ДовСерия = ДовСерия;
	Док.ДокументОснование = ДокументОснование;
	Док.Договор = Договор;
	Док.Контрагент = Контрагент;
	Док.Курс = Курс;
	Док.Отпустил = Отпустил;
	Док.Подразделение = Подразделение;
	Док.Получил = Получил;
	Док.СубконтоВалДох = СубконтоВалДох;
	Док.СубконтоВалРасх = СубконтоВалРасх;
	Док.СуммаВозмещения = СуммаВозмещения;
	Док.СчетКонтрагента = СчетКонтрагента;
	Док.ТипСписания = ТипСписания;
		
	ТабличнаяЧасть.ВыбратьСтроки();
	Пока ТабличнаяЧасть.ПолучитьСтроку()=1 Цикл
		Док.НоваяСтрока();
		Док.НеоборотныйАктив = ТабличнаяЧасть.НеоборотныйАктив;
		Док.МестоХранения = ТабличнаяЧасть.МестоХранения;
		Док.ПервСтоимость = ТабличнаяЧасть.ПервСтоимость;
		Док.Износ = ТабличнаяЧасть.Износ;
		Док.Кво = ТабличнаяЧасть.Кво;
		Док.ЦенаБезНДС = ТабличнаяЧасть.ЦенаБезНДС;
		Док.СуммаБезНДС = ТабличнаяЧасть.СуммаБезНДС;
		Док.НДС = ТабличнаяЧасть.НДС;
		Док.СуммаСНДС = ТабличнаяЧасть.СуммаСНДС;
	КонецЦикла;
	Док.Примечание = Примечание;
	Док.Автор = глПользователь; 
	Док.ОтборСчетаКонтрагент = ?(ПустоеЗначение(ДокументОснование)=1, Контрагент, 0);
	Если ВидЛиквидации <> Списание Тогда
		Если Договор.Выбран() = 0 Тогда
		    ОтборСчетаКонтрагент = Контрагент;
		КонецЕсли;
	КонецЕсли;
	глПроверкаДатыДок(Док,"Запись");
	глСохранитьЗначение("ЛиквидацияНеоборАктивов","СубконтоВалДох",СубконтоВалДох);
	глСохранитьЗначение("ЛиквидацияНеоборАктивов","СубконтоВалРасх",СубконтоВалРасх);
	глСохранитьЗначение("ЛиквидацияНеоборАктивов","ВидЗатрат",ВидЗатрат);
	Док.Записать();
КонецПроцедуры 

// ===============================
// Обрабатывает нажатие на кнопки Назад, Дальше
// Дельта - направление движения
Процедура ИзмШаг(Дельта)
	Если Дельта = 1 Тогда
		Если ПроверитьШаг()<>1 Тогда
			Возврат;
		КонецЕсли;
		Если Шаг = ПоследнийШаг Тогда
    	    ЗаписатьДокумент();
			Форма.Закрыть();
			Если ОткрытьДокумент = 1 Тогда
				// откроем форму документа, не проводя его
				ОткрытьФорму(Док.ТекущийДокумент());
			Иначе
				// документ уже записан, проведем его
				Док.Провести();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Шаг = Шаг + Дельта;
	// пропустим те шаги, которые относятся к другому виду ликвидации НА
	Если ВидЛиквидации = Списание Тогда
		Если ТипСписания = Перечисление.ТипыСписанияНеоборАктивов.ВынужденнаяЗаменаЕслиВиновныйУстановлен Тогда
			// пропускаем: 6 и 7 шаги - относятся к реализации, 4 шаг - редактирование строки документа
			// 5 шаг доступен
			Пока (Шаг = 4) или (Шаг = 6) или (Шаг = 7) Цикл
				Шаг = Шаг + Дельта;
			КонецЦикла;
		Иначе
			// пропускаем: 6 и 7 шаги - относятся к реализации, 4 шаг - редактирование строки документа
			// 5 шаг доступен только если установлено виновное лицо, пропустим
			Пока (Шаг = 4) или (Шаг = 5) или (Шаг = 6) или (Шаг = 7) Цикл
				Шаг = Шаг + Дельта;
			КонецЦикла;
		КонецЕсли;
	Иначе
		// пропускаем: 5 шаг - относится к списанию, 4 шаг - редактирование строки документа
		Пока (Шаг = 4) или (Шаг = 5) Цикл
			Шаг = Шаг + Дельта;
		КонецЦикла;
	КонецЕсли;
	НарисоватьСлои();
	ПоказатьСовет();
	ПоказатьОписание();
КонецПроцедуры

// ===============================
Процедура ИзмНДС()
	СуммаСНДС = СуммаБезНДС + НДС;
КонецПроцедуры

// ===============================
Процедура ИзмСуммаБезНДС()
	НДС = СуммаБезНДС * ВидНДС.Ставка.Получить(ДатаДок);
	ИзмНДС();
КонецПроцедуры

// ===============================
Процедура ИзмЦенаБезНДС()
	СуммаБезНДС = ЦенаБезНДС * Кво;
	ИзмСуммаБезНДС();
КонецПроцедуры

// ===============================
Процедура ИзмКво()
	ИзмЦенаБезНДС();
КонецПроцедуры

// ===============================
Процедура УстЦенаБезНДС()
	СумПерв = глПересчет(ПервСтоимость-Износ,Гривня,Валюта,ДатаДок,Курс);
	Если СумПерв = 0 Тогда
		ЦенаБезНДС = глПересчет(НеоборотныйАктив.Цена_Прих,Гривня,Валюта,ДатаДок,Курс);
	Иначе
		ЦенаБезНДС = СумПерв;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмНеоборотныйАктив()
	Если НеоборотныйАктив.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	ПервСтоимость = 0;
	Износ = 0;
	Кво = 0;
	
	// определим первичную стоимость, количество и место хранения
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы, НеоборотныйАктив, 2);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения);
	СчетИзноса = СчетПоКоду("13."+Строка(Число(Сред(НеоборотныйАктив.Счет.Получить(Док.ДатаДок).Код,1,2))-9));
	Ит.ВыполнитьЗапрос(ДатаДок,,НеоборотныйАктив.Счет);
	Ит.ВыбратьСубконто(1);
	Если Ит.ПолучитьСубконто(1) = 1 Тогда
		ПервСтоимость = Ит.СКД();
		Кво = Ит.СКД("К");
	КонецЕсли;
	Ит.ВыбратьСубконто(2);
	Если Ит.ПолучитьСубконто(2) = 1 Тогда
		МестоХранения = Ит.Субконто(2);
	КонецЕсли;
	
	// определим износ объекта в бухгалтерском учете
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы, НеоборотныйАктив, 2);
	СчетИзноса = СчетПоКоду("13."+Строка(Число(Сред(НеоборотныйАктив.Счет.Получить(Док.ДатаДок).Код,1,2))-9));
	Ит.ВыполнитьЗапрос(ДатаДок,,СчетИзноса);
	Износ = Ит.СКК();
	
	Кво = ?(Кво = 0, 1, Кво);
	Если ВидЛиквидации <> Списание Тогда
		УстЦенаБезНДС();
		ИзмЦенаБезНДС();
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмКурс()
	Если ТабличнаяЧасть.КоличествоСтрок() > 0 Тогда
		Если Вопрос("Пересчитать суммы?","Да+Нет")="Да" Тогда
			ТабличнаяЧасть.ВыбратьСтроки();
			Пока ТабличнаяЧасть.ПолучитьСтроку()=1 Цикл
				ВыгрузитьИзТаблицы();
				УстЦенаБезНДС();
				ИзмСуммаБезНДС();
				ЗагрузитьВТаблицу();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран() = 1 Тогда
		Если Договор.Выбран() = 1 Тогда
			Если Контрагент <> Договор.Контрагент Тогда
			    Договор = 0;
			КонецЕсли;
		КонецЕсли;
		
		Если ДокументОснование.Выбран() = 1 Тогда
			Если Контрагент <> ДокументОснование.Контрагент Тогда
			    ДокументОснование = 0;
			КонецЕсли;
		КонецЕсли;

		Если (Договор.Выбран() = 0) И (Фирма = Контрагент.БазДоговор.Фирма) Тогда
			Договор = Контрагент.БазДоговор;
		КонецЕсли;

		Если Договор.Выбран() = 1 Тогда
			Если глЕстьРеквизитШапки("ВидТорговли", Договор.Вид()) = Да Тогда
				ВидТорговли = Договор.ВидТорговли;
			КонецЕсли;	
		КонецЕсли;	

		Если ПустоеЗначение(ВидТорговли) = 1 Тогда
			ВидТорговли = Контрагент.ВидТорговли;      
		КонецЕсли;

		Если ПустоеЗначение(ВидТорговли) = 1 Тогда
			ВидТорговли = Перечисление.ВидыТорговли.Предоплата;
		КонецЕсли;                                             		
	Иначе
		ВидТорговли = Перечисление.ВидыТорговли.Предоплата;
		ДокументОснование = 0;
		Договор = 0;                                       
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмВалюта()
	Если ПустоеЗначение(Договор) = 0 Тогда
		Если Валюта <> Договор.Валюта Тогда
			глКомментарий("Валюту нужно изменять в договоре!",1,,"!");
			Валюта = Договор.Валюта;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Курс = Валюта.Курс.Получить(ДатаДок);
	ИзмКурс();
	Если (ПустоеЗначение(СчетКонтрагента)=1) или (СчетКонтрагента.Валютный = ?(Валюта=Гривня,1,0)) Тогда
		// Установим счет, если он не выбран
		// а также когда при валюте Гривня выбран валютный счет
		// и при валюте не Гривня выбран гривневый счет
		СчетКонтрагента = СчетПоКоду(?(Валюта = Гривня, "37.7.2", "37.7.4"));
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	Док.Фирма = Фирма;
	глУстановитьНомер(Док);
	НомерДок = Док.НомерДок;
	ДатаДок = Док.ДатаДок;
КонецПроцедуры       

// ===============================
// сравнивает два значения
Функция НеРавны(Значение1,Значение2)
	Если ( (ПустоеЗначение(Значение1)=0) Или (ПустоеЗначение(Значение2)=0) ) И (Значение1 <> Значение2) Тогда
		Возврат 1;
	КонецЕсли;	 	
	Возврат 0;
КонецФункции

// ===============================
Процедура ПриУдаленииДокОснования()

	ДокументОснование  	= 0;
	СтарыйДокОснование 	= 0;
	Форма.кХДокументОснование.Доступность(0);

КонецПроцедуры

// ===============================
Процедура ПриВыбореДоговора()

	Если ПустоеЗначение(Договор) = 0 Тогда

		Если НеРавны(Договор.Фирма,Фирма)=1 Тогда	
			глКомментарий("Нельзя выбирать договор, выписаный на другую фирму",1);
			Договор = СтарыйДоговор;
			Возврат;
		КонецЕсли;  
		
		Если Договор.ВидТорговли = Перечисление.ВидыТорговли.Кредит Тогда
			глКомментарий("Нельзя выбирать договоры с видом торговли ""Товарный кредит"".",1);
			Договор = СтарыйДоговор;
			Возврат;
		КонецЕсли;

		Контрагент = Договор.Контрагент;
		ВидТорговли = Договор.ВидТорговли;
		Доступность = 1;
	Иначе 
		Доступность = 0;
	КонецЕсли;
             
	Форма.кХДоговор.Доступность(Доступность);
	СтарыйДоговор = Договор;

КонецПроцедуры

// ===============================
Процедура ПриВыбореДокумента()

	Если ПустоеЗначение(ДокументОснование) = 1 Тогда // не выбран док. основание
		ПриУдаленииДокОснования();
		Возврат;
	КонецЕсли;
	
	Если НеРавны(ДокументОснование.Фирма,Фирма)=1 Тогда	
		глКомментарий("Нельзя выбирать документ-основание, выписаный на другую фирму",1);
		// восстановим старые занчения полей
		ДокументОснование = СтарыйДокОснование;
		Возврат;
	КонецЕсли;	
        
	Если глЕстьРеквизитШапки("ВидТорговли",ДокументОснование.Вид()) = Да Тогда    
		Если ДокументОснование.ВидТорговли = Перечисление.ВидыТорговли.Кредит Тогда
			глКомментарий("Нельзя выбирать документ-основание с видом торговли ""Товарный кредит"".",1);
			ДокументОснование = СтарыйДокОснование;
			Возврат;
		КонецЕсли;
	КонецЕсли;		
	
	ВидДокОсн = ДокументОснование.Вид();
	Контрагент = ДокументОснование.Контрагент;
	
	Если ПустоеЗначение(Договор) = 1 Тогда
		Если глЕстьРеквизитШапки("Договор", ВидДокОсн) = Да Тогда
			Договор = ДокументОснование.Договор;
			ПриВыбореДоговора();
		ИначеЕсли глЕстьРеквизитШапки("Заказ", ВидДокОсн) = Да Тогда
			Договор = ДокументОснование.Заказ;
			ПриВыбореДоговора();
		КонецЕсли;
	КонецЕсли;
    	
	СтарыйДокОснование	= ДокументОснование;
	Форма.кХДокументОснование.Доступность(1);
	
КонецПроцедуры //ПриВыбореДокумента()

// ======================================
Процедура ПриОткрытии()
	Док = СоздатьОбъект("Документ.ЛиквидацияНеоборАктивов");
	Док.Новый();
	Фирма = глВосстановитьЗначение(,"БазФирма");
	Док.Фирма = Фирма;
	глУстановитьНомер(Док);
	НомерДок = Док.НомерДок;
	ДатаДок = Док.ДатаДок;
	Шаг = 1;
	ПоследнийШаг = 9;
	ПоказатьСовет();
	ПоказатьОписание();
	НарисоватьСлои();
	Форма.ПанельИнструментов(0);
    Списание = Перечисление.ВидыЛиквидацииНеоборАктивов.Списание;
	ВидЛиквидации = Списание;
	СубконтоВалДох = глВосстановитьЗначение("ЛиквидацияНеоборАктивов","СубконтоВалДох");
	СубконтоВалРасх = глВосстановитьЗначение("ЛиквидацияНеоборАктивов","СубконтоВалРасх");
	ВидЗатрат = глВосстановитьЗначение("ЛиквидацияНеоборАктивов","ВидЗатрат");
	ВидНДС = глВосстановитьЗначение(,"БазНДС");
	Отпустил = глВосстановитьЗначение(,"БазОтпустил");
	Валюта = Гривня;
	ИзмВалюта();
	ТипСписания = Перечисление.ТипыСписанияНеоборАктивов.ПоРешениюНалогоплательщикаИлиКМУ;
	// выбирать группы элементов справочников нельзя
	НеоборотныйАктив.ВыборГруппы(0);
	МестоХранения.ВыборГруппы(0);
	Виновный.ВыборГруппы(0);
	СчетКонтрагента.ВыборГруппы(0);
	Контрагент.ВыборГруппы(0);
	Отпустил.ВыборГруппы(0);
	СубконтоВалДох.ВыборГруппы(0);
	СубконтоВалРасх.ВыборГруппы(0);
	ВидЗатрат.ВыборГруппы(0);
	// здесь будем хранить табличную часть документа
	ТабличнаяЧасть.НоваяКолонка("НеоборотныйАктив","Справочник.НеоборотныеАктивы",,,"Необоротный актив",30);
	ТабличнаяЧасть.НоваяКолонка("МестоХранения","Справочник.МестаХранения",,,"Место хранения",20);
	ТабличнаяЧасть.НоваяКолонка("Кво","Число",10,3,"Кол-во",10);
	ТабличнаяЧасть.НоваяКолонка("ПервСтоимость","Число",12,2);
	ТабличнаяЧасть.НоваяКолонка("Износ","Число",12,2);
	ТабличнаяЧасть.НоваяКолонка("ЦенаБезНДС","Число",12,2);
	ТабличнаяЧасть.НоваяКолонка("СуммаБезНДС","Число",12,2);
	ТабличнаяЧасть.НоваяКолонка("НДС","Число",12,2);
	ТабличнаяЧасть.НоваяКолонка("СуммаСНДС","Число",12,2);
	ТабличнаяЧасть.ВидимостьКолонки("ПервСтоимость",0);
	ТабличнаяЧасть.ВидимостьКолонки("Износ",0);
	ТабличнаяЧасть.ВидимостьКолонки("ЦенаБезНДС",0);
	ТабличнаяЧасть.ВидимостьКолонки("СуммаБезНДС",0);
	ТабличнаяЧасть.ВидимостьКолонки("НДС",0);
	ТабличнаяЧасть.ВидимостьКолонки("СуммаСНДС",0);
КонецПроцедуры

// ======================================
Функция УстДоступность()
	Форма.кНазад.Доступность(1);
	Форма.кДальше.Доступность(1);
	Форма.кДальше.Заголовок("&Дальше >");
	Если Шаг = 1 Тогда
		Форма.кНазад.Доступность(0);
	ИначеЕсли Шаг = 2 Тогда
		Если ВидЛиквидации = Списание Тогда
			Форма.ТипСписания.Видимость(1);
			Форма.тТипСписания.Видимость(1);
		Иначе
			Форма.ТипСписания.Видимость(0);
			Форма.тТипСписания.Видимость(0);
		КонецЕсли;
	ИначеЕсли Шаг = 3 Тогда
		Если ТабличнаяЧасть.ТекущаяСтрока() > 0 Тогда
			ТабличнаяЧасть.ПолучитьСтрокуПоНомеру(ТабличнаяЧасть.ТекущаяСтрока());
			ПоказатьРеквизитыСтроки();
		КонецЕсли;
		// Эти реквизиты используются только при реализации
		Если ВидЛиквидации = Списание Тогда
			Форма.тЦенаБезНДС.Видимость(0);
			Форма.тСуммаБезНДС.Видимость(0);
			Форма.тНДС.Видимость(0);
			Форма.тСуммаСНДС.Видимость(0);
			Форма.стрЦенаБезНДС.Видимость(0);
			Форма.стрСуммаБезНДС.Видимость(0);
			Форма.стрНДС.Видимость(0);
			Форма.стрСуммаСНДС.Видимость(0);
		Иначе
			Форма.тЦенаБезНДС.Видимость(1);
			Форма.тСуммаБезНДС.Видимость(1);
			Форма.тНДС.Видимость(1);
			Форма.тСуммаСНДС.Видимость(1);
			Форма.стрЦенаБезНДС.Видимость(1);
			Форма.стрСуммаБезНДС.Видимость(1);
			Форма.стрНДС.Видимость(1);
			Форма.стрСуммаСНДС.Видимость(1);
		КонецЕсли;
	ИначеЕсли Шаг = 4 Тогда
		// 4 слой используется исключительно для редактирования строки документа
		// выход из этого режима делается по кнопке Сохранить, сделаем недоступными
		// кнопки перемещения на предыдущий/следующий шаг мастера
		Форма.кНазад.Доступность(0);
		Форма.кДальше.Доступность(0);
		// Эти реквизиты используются только при реализации
		Если ВидЛиквидации = Списание Тогда
			Форма.трЦенаБезНДС.Видимость(0);
			Форма.трСуммаБезНДС.Видимость(0);
			Форма.трНДС.Видимость(0);
			Форма.трСуммаСНДС.Видимость(0);
			Форма.ЦенаБезНДС.Видимость(0);
			Форма.СуммаБезНДС.Видимость(0);
			Форма.НДС.Видимость(0);
			Форма.СуммаСНДС.Видимость(0);
		Иначе
			Форма.трЦенаБезНДС.Видимость(1);
			Форма.трСуммаБезНДС.Видимость(1);
			Форма.трНДС.Видимость(1);
			Форма.трСуммаСНДС.Видимость(1);
			Форма.ЦенаБезНДС.Видимость(1);
			Форма.СуммаБезНДС.Видимость(1);
			Форма.НДС.Видимость(1);
			Форма.СуммаСНДС.Видимость(1);
		КонецЕсли;
	ИначеЕсли Шаг = 5 Тогда
		//
	ИначеЕсли Шаг = 6 Тогда
		//
	ИначеЕсли Шаг = 7 Тогда
		
	ИначеЕсли Шаг = 8 Тогда
		Если ВидЛиквидации = Списание Тогда
			Форма.ВидЗатрат.Видимость(1);
			Форма.тВидЗатрат.Видимость(1);
			Форма.рВидЗатрат.Видимость(1);
		Иначе
			Форма.ВидЗатрат.Видимость(0);
			Форма.тВидЗатрат.Видимость(0);
			Форма.рВидЗатрат.Видимость(0);
		КонецЕсли;
	Иначе
		// последний шаг, меняем название кнопки
		Форма.кДальше.Заголовок("&Готово");
	КонецЕсли;
	Возврат "";
КонецФункции

// ===============================
Процедура ВыбратьПоКонтрагенту()
	
	Фрм = СоздатьОбъект("СписокЗначений");
	Фрм.ДобавитьЗначение(Контрагент,"Контрагент");
	Фрм.ДобавитьЗначение("СчетаКонтрагентов","ГрафаОтбора");

	ОткрытьФормуМодально("Журнал.ПолныйЖурнал.ДляВыбораДоговоровИСчетов",Фрм,);

	Если ПустоеЗначение(Фрм.Получить("Документ")) = 0 Тогда
		ДокументОснование = Фрм.Получить("Документ");
		ПриВыбореДокумента();
	КонецЕсли;
	
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ФлагСтандОбр = 0;
	ИначеЕсли Рекв = "Отпустил" Тогда
		ФлагСтандОбр = 0;
		КонтФирмы = Фирма;
		ОткрытьФорму("Справочник.Сотрудники.ДляВыбора",КонтФирмы);
	ИначеЕсли Рекв = "СубконтоВалДох" Тогда
		Если ПустоеЗначение(СубконтоВалДох) = 1 Тогда
			ГрВалДох = СоздатьОбъект("Справочник.ВалДоходыРасходы");
			Если ГрВалДох.НайтиПоНаименованию("Валовi доходи",0,1) = 1 Тогда
				СубконтоВалДох.ИспользоватьРодителя(ГрВалДох.ТекущийЭлемент());
			КонецЕсли;
			ГрВалДох = 0;
		КонецЕсли;
	ИначеЕсли Рекв = "СубконтоВалРасх" Тогда
		Если ПустоеЗначение(СубконтоВалРасх) = 1 Тогда
			ГрВалРасх = СоздатьОбъект("Справочник.ВалДоходыРасходы");
			Если ГрВалРасх.НайтиПоНаименованию("Валовi витрати",0,1) = 1 Тогда
				СубконтоВалРасх.ИспользоватьРодителя(ГрВалРасх.ТекущийЭлемент());
			КонецЕсли;
			ГрВалРасх = 0;
		КонецЕсли;
	ИначеЕсли Рекв = "Договор" Тогда
	    ФлагСтандОбр = 0;
		
		Фрм = СоздатьОбъект("СписокЗначений");
		Фрм.ДобавитьЗначение(Контрагент,"Контрагент");
		Фрм.ДобавитьЗначение("ДоговораКонтрагентов","ГрафаОтбора"); // значения "Счета" или "Договора"
		
		ОткрытьФормуМодально("Журнал.ПолныйЖурнал.ДляВыбораДоговоровИСчетов",Фрм,);

		Если ПустоеЗначение(Фрм.Получить("Документ")) = 0 Тогда
			Договор=Фрм.Получить("Документ");
		КонецЕсли;
	ИначеЕсли Рекв = "ДокументОснование" Тогда
	    ФлагСтандОбр = 0;

		Если ПустоеЗначение(ДокументОснование) = 0 Тогда
			ОткрытьФорму(ДокументОснование,,1);
		Иначе
			ВыбратьПоКонтрагенту();
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры                 
