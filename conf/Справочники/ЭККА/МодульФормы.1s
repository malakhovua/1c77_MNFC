
// ===============================
Процедура ИзмТипЭККА()
	Тип = спТипыЭККА.ПолучитьЗначение(спТипыЭККА.ТекущаяСтрока());
КонецПроцедуры
                           
// ===============================
Функция Проверка(Зн)
	Если (Найти(Зн,РазделительСтрок)<>0) или (Найти(Зн,"=")<>0) Тогда
	    глКомментарий("Введены недопустимые символы!",1);
    	Возврат 0;
    КонецЕсли;
    Возврат 1;
КонецФункции

// ===============================
Процедура Обновить()
	ТПараметры.НоваяКолонка("Название", "Строка", 20, , "Название", 20);
	ТПараметры.НоваяКолонка("Значение", "Строка", 16, , "Значение", 20);

	СтрПарс = ПараметрыИнициализации;
	Инд = Найти(СтрПарс,РазделительСтрок);
	Пока Инд<>0 Цикл
		ТекСтр = Сред(СтрПарс,1,Инд-1);
		СтрПарс = Сред(СтрПарс,Инд+1,СтрДлина(СтрПарс)-Инд);
		
		ИндРавно = Найти(ТекСтр,"=");
		Если ИндРавно = 0 Тогда
		    Продолжить;
		КонецЕсли;
		Пар = Сокрлп(Сред(ТекСтр,1,ИндРавно-1));
		Зна = Сокрлп(Сред(ТекСтр,ИндРавно+1,СтрДлина(ТекСтр)-ИндРавно));
		
		ТПараметры.НоваяСтрока();
		ТПараметры.Название = Пар;
		ТПараметры.Значение = Зна;

		Инд = Найти(СтрПарс,РазделительСтрок);
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура Сохранить()
	Дополнительно = "";
	Стр = "";
	ТПараметры.ВыбратьСтроки();
	Пока ТПараметры.ПолучитьСтроку() = 1 Цикл
		Пар = СокрЛП(ТПараметры.Название);
		Зна = СокрЛП(ТПараметры.Значение);
		Стр = Стр+Пар+"="+Зна+РазделительСтрок;
	КонецЦикла;
	ПараметрыИнициализации = Стр;
КонецПроцедуры

// ===============================
Процедура Изменить()
	Ном = ТПараметры.ТекущаяСтрока();
	Если Ном = 0 Тогда
	    Возврат;
	КонецЕсли;                       
	Кол = ТПараметры.ТекущаяКолонка();
	Если Кол = "Название" Тогда
		Стр = ТПараметры.Название;
		Дл = 20;
	Иначе
		Стр = ТПараметры.Значение;
		Дл = 16;
	КонецЕсли;
	Если ВвестиСтроку(Стр,"Введите " + нРег(Кол) + " параметра", Дл) = 0 Тогда
	    Возврат;
	КонецЕсли;
	Если Проверка(Стр)=0 Тогда
	    Возврат;
	КонецЕсли;
	ТПараметры.УстановитьЗначение(Ном, Кол, Стр);

	Сохранить();
КонецПроцедуры

// ===============================
Процедура Добавить()
	Пар = ""; Зна = "";
	Если ВвестиСтроку(Пар,"Введите название параметра",20)=0 Тогда
	    Возврат;
	КонецЕсли;
	Если Проверка(Пар)=0 Тогда
	    Возврат;
	КонецЕсли;
	ВвестиСтроку(Зна,"Введите значение параметра",16);
	Если Проверка(Зна)=0 Тогда
	    Возврат;
	КонецЕсли;
	
	ТПараметры.НоваяСтрока();
	ТПараметры.Название = Пар;
	ТПараметры.Значение = Зна;

	Сохранить();
КонецПроцедуры

// ===============================
Процедура Удалить()
	Ном = ТПараметры.ТекущаяСтрока();
	Если Ном = 0 Тогда
	    Возврат;
	КонецЕсли;
	ТПараметры.УдалитьСтроку(Ном);
	
	Сохранить();
КонецПроцедуры


// ===============================
Процедура ПриОткрытии ()         
	// Выбираем обработки "ЭККА"
	Для Инд=1 по Метаданные.Обработка() Цикл
		МДОбработка=Метаданные.Обработка(Инд);
		Если Найти(МДОбработка.Идентификатор,"ОбслуживаниеЭККА_")<>0 тогда
			спТипыЭККА.ДобавитьЗначение(СокрЛП(СтрЗаменить(МДОбработка.Идентификатор,"ОбслуживаниеЭККА_","")),СокрЛП(МДОбработка.Синоним));
		КонецЕсли;
	КонецЦикла;          
	Позиция = спТипыЭККА.НайтиЗначение(СокрЛП(Тип));
	Если Позиция = 0 Тогда
		Позиция = спТипыЭККА.НайтиЗначение("КассовыйАппарат");
	КонецЕсли;
	спТипыЭККА.ТекущаяСтрока(Позиция);
	ИзмТипЭККА();
	Обновить();
КонецПроцедуры                     

// ===============================
Процедура ПриЗаписи ()         
	глВсеВыбрано = 1;
	глВыбранЛи(РежимРаботы, "Режим работы");
	
	Если глВсеВыбрано = 0 Тогда
		Сообщить("Элемент справочника не записан!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;     
КонецПроцедуры                     
