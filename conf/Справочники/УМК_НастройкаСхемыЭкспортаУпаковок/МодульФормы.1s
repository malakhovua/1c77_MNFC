Перем ВыгружаемыйДокумент;
Перем ЭтоЭкспорт;
Перем СписокРеквизитовИзВЭ;
Перем ИмСпис;

Процедура Экспор() Далее

//======================================================================
Процедура УстМодифицированность()
	Если Форма.кЭкспорт.Видимость() = 0 Тогда
		глЗаписатьСписокТоваров(СтрФильтр, списТовар);
	КонецЕсли;	
КонецПроцедуры // 

//======================================================================
Процедура РазобратьИмяРеквизита(ИмяРекв, СтруктураРеквизита)
	Поз = Найти(ИмяРекв, "#");
	ПозДоллар = Найти(ИмяРекв, "$"); 
	Если Поз <> 0 Тогда // фикс. значение
		СтруктураРеквизита.Установить("Имя", Лев(ИмяРекв, Поз-1));
		СтруктураРеквизита.Установить("Тип", "ФиксЗначение");
		Знч = Сред(ИмяРекв, Поз+1);  
		СтруктураРеквизита.Установить("Значение", ?(Прав(Знч, 1) = "&", Лев(Знч, СтрДлина(Знч) - 1), Знч));
	ИначеЕсли ПозДоллар <> 0 Тогда // фикс. значение
		СтруктураРеквизита.Установить("Имя", Лев(ИмяРекв, ПозДоллар-1));
		СтруктураРеквизита.Установить("Тип", "Процент");
		Знч = Сред(ИмяРекв, ПозДоллар+2);
		СтруктураРеквизита.Установить("Значение", ?(Прав(Знч, 1) = "&", Лев(Знч, СтрДлина(Знч) - 1), Знч));
		СтруктураРеквизита.Установить("Округление", Сред(ИмяРекв, ПозДоллар+1, 1));
	Иначе
		СтруктураРеквизита.Установить("Имя", ?(Прав(ИмяРекв, 1) = "&", Лев(ИмяРекв, СтрДлина(ИмяРекв) - 1), ИмяРекв));
		СтруктураРеквизита.Установить("Тип", "Реквизит");
		СтруктураРеквизита.Установить("Значение", "");
	КонецЕсли;
	СтруктураРеквизита.Установить("ТолькоНовый", "");
	Если Прав(ИмяРекв, 1) = "&" Тогда
		СтруктураРеквизита.Установить("ТолькоНовый", "&");
	КонецЕсли;	
		
	//ЕстьРешетка = 0;
	//Поз = Найти(ИмяРекв, "#"); Зн = "";
	//Если Поз <> 0 Тогда
	//	Зн = Сред(ИмяРекв, Поз+1);
	//	ИмяРекв = Лев(ИмяРекв, Поз-1);		
	//	ЕстьРешетка = 1;
	//КонецЕсли;	
КонецПроцедуры // 

//======================================================================
Процедура ВводНового(ПризнакКопирования, Элем)
	ВидУпаковки = 1; //--- Всегда импортируется	
	//Форма.ПолучитьАтрибут();
КонецПроцедуры // гл

//======================================================================
Процедура УстВидимость()
	Форма.кЭкспорт.Видимость(ЭтоЭкспорт);
	Форма.кОК.Видимость(1-ЭтоЭкспорт);
	Форма.ВДок.Видимость(ЭтоЭкспорт);
	Форма.тЦеныНаДату.Видимость(ЭтоЭкспорт);
	Форма.ДатаЦен.Видимость(ЭтоЭкспорт);
КонецПроцедуры // УстВидимость

Процедура ПриОткрытии()
Перем Зн, ЕстьРешетка;
	Форма.ИспользоватьСлой("Общий,Основной", 2);
	Если (ТипЗначенияСтр(Форма.Параметр) = "ГрупповойКонтекст") ИЛИ (ТипЗначенияСтр(Форма.Параметр) = "Документ") Тогда
		ЭтоЭкспорт = 1;
		ДатаЦен = Форма.Параметр.ДатаДок;
		ВыгружаемыйДокумент = Форма.Параметр;
	КонецЕсли;
	
	глЗаполнитьСписокТоваров(СтрФильтр, списТовар);
	глЗаполнитьСписокТоваров(СтрФильтрНЕ, списТоварНЕ);
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной");
	Форма.Закладки.ДобавитьЗначение("Дополнительно");	

	УстВидимость();
	СРеквизитов = СоздатьОбъект("СписокЗначений");
	СРеквизитов.ИзСтрокиСРазделителями(СписРеквизитов);
	СтруктураРеквизита = СоздатьОбъект("СписокЗначений");
	
	Для Инд = 1 По СРеквизитов.РазмерСписка() Цикл
		// разберём строку
		Рекв = СРеквизитов.ПолучитьЗначение(Инд);
		Если Рекв <> "" Тогда			
			РазобратьИмяРеквизита(Рекв, СтруктураРеквизита);
			Представление = "";
			Поз = СписДоп.НайтиЗначение(СтруктураРеквизита.Получить("Имя"));	
			СписДоп.ПолучитьЗначение(Поз, Представление);
			Если Поз <> 0 Тогда				
				СписДоп.Пометка(Поз, 1);
				ПредставлениеТолькоНовый = ?(СтруктураРеквизита.Получить("ТолькоНовый") = "&"," (только нов.)", "");
				Если СтруктураРеквизита.Получить("Тип") = "ФиксЗначение" Тогда
					СписДоп.УстановитьЗначение(Поз, СтруктураРеквизита.Получить("Имя") + "#" + СтруктураРеквизита.Получить("Значение") + СтруктураРеквизита.Получить("ТолькоНовый"), Представление + " фикс. зн.: " 
					 + СтруктураРеквизита.Получить("Значение") + ПредставлениеТолькоНовый);
				ИначеЕсли СтруктураРеквизита.Получить("Тип") = "Процент" Тогда
					СписДоп.УстановитьЗначение(Поз, СтруктураРеквизита.Получить("Имя") + "$" + СтруктураРеквизита.Получить("Округление") + СтруктураРеквизита.Получить("Значение") + СтруктураРеквизита.Получить("ТолькоНовый"), 
					 Представление + " %: " + СтруктураРеквизита.Получить("Значение") + " окр. " + СтруктураРеквизита.Получить("Округление") + ПредставлениеТолькоНовый);
				Иначе 
					СписДоп.УстановитьЗначение(Поз, СтруктураРеквизита.Получить("Имя") + СтруктураРеквизита.Получить("ТолькоНовый"), Представление + ПредставлениеТолькоНовый);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Строка" Тогда
		Если Форма.Параметр = "Экспорт" Тогда
			Форма.Параметр = СоздатьОбъект("СписокЗначений");
			Форма.Параметр.Установить("списТовар", списТовар);
			СписД = СоздатьОбъект("СписокЗначений");
			Для Инд = 1 По СписДоп.РазмерСписка() Цикл
				Если СписДоп.Пометка(Инд) = 1 Тогда
					СписД.ДобавитьЗначение(СписДоп.ПолучитьЗначение(Инд));
				КонецЕсли;
			КонецЦикла;
			Форма.Параметр.Установить("СписДоп", СписД);
			Форма.Закрыть();
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЭкспорт = 1 Тогда
		//глДобавитьИИ(ТекущийЭлемент(), списТоварНЕ);
	КонецЕсли;
	
	Если (ЭтоЭкспорт = 1) И (Лев(ВыгружаемыйДокумент.НомерДок, 1) = "Т") Тогда		
		Экспор();
//		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ПриВыбореЗакладки(Номер, Имя)
	Форма.ИспользоватьСлой("Общий," + Имя, 2);
	УстВидимость();
КонецПроцедуры // ПриВыбореЗакладки

//======================================================================
Процедура ПриЗаписи()
	Если Форма.кЭкспорт.Видимость() = 0 Тогда
		глЗаписатьСписокТоваров(СтрФильтр, списТовар);
		глЗаписатьСписокТоваров(СтрФильтрНЕ, списТоварНЕ);
	КонецЕсли;
	
	СписРеквизитов = "";
	Для Инд = 1 По СписДоп.РазмерСписка() Цикл
		Если СписДоп.Пометка(Инд) = 1 Тогда
			Рекв = СписДоп.ПолучитьЗначение(Инд);
			СписРеквизитов = СписРеквизитов + """" + Рекв + """" + ",";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ПриЗаписи

//======================================================================
Процедура УстановитьЗначениеПечати(Элт, РеквизитПечати, ИмяРеквизитаСправочника, Периодический, СтрокаДействийФилиала, ТекСхема)
	Если Периодический = 1 Тогда
		ЗначениеЭкспорта = Элт.ПолучитьАтрибут(ИмяРеквизитаСправочника).Получить(ДатаЦен);
	Иначе
		ЗначениеЭкспорта = Элт.ПолучитьАтрибут(ИмяРеквизитаСправочника);
	КонецЕсли;

	Если ИмяРеквизитаСправочника = "Цена" Тогда
		ФлагТекущий  = 1;
		ФлагФилиала = ТекСхема.ПолучитьАтрибут(ИмяРеквизитаСправочника);
		ТолькоНовый = ТекСхема.ЦенаТН;
	Иначе
		ФлагТекущий = ТекСхема.ПолучитьАтрибут(ИмяРеквизитаСправочника);
		ФлагФилиала = ТекСхема.ПолучитьАтрибут(ИмяРеквизитаСправочника + "Филиала");
		ТолькоНовый = ТекСхема.ПолучитьАтрибут(ИмяРеквизитаСправочника + "ФилиалаТН");
	КонецЕсли;
	
	Если ФлагТекущий = 1 Тогда
		РеквизитПечати = Строка(ЗначениеЭкспорта);		
	КонецЕсли;	
	
	СтрокаДействийФилиала = Строка(ТолькоНовый) + "##";
	
	РеквизитПечати = ЗначениеЭкспорта;	
	Если ФлагФилиала = 1 Тогда // устанавливаем такое же значение
		СтрокаДействийФилиала = СтрокаДействийФилиала + Строка(ЗначениеЭкспорта);
	ИначеЕсли ФлагФилиала = 2 Тогда // устанавливаем фикс. значение
		СтрокаДействийФилиала = СтрокаДействийФилиала + Строка(ТекСхема.ПолучитьАтрибут("ФиксЗначение" + ИмяРеквизитаСправочника));
	ИначеЕсли ФлагФилиала = 3 Тогда // устанавливаем значение из текущего, умноженное на коэф.
		СтрокаДействийФилиала = СтрокаДействийФилиала + Строка(ЗначениеЭкспорта * ТекСхема.ПолучитьАтрибут("ФиксЗначение" + ИмяРеквизитаСправочника));
	Иначе // не устанавливам
		СтрокаДействийФилиала = СтрокаДействийФилиала + "";
	КонецЕсли;
КонецПроцедуры //

//======================================================================
Процедура Экспор()
Перем Зн, ЕстьРешетка;
	СпрРВУ = СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ");
	ТСхем = СоздатьОбъект("ТаблицаЗначений");
	ТСхем.НоваяКолонка("Схема", "Справочник.УМК_НастройкаСхемыЭкспортаУпаковок");
	ТСхем.НоваяКолонка("списТМЦ", "СписокЗначений");
	ТСхем.НоваяКолонка("списТМЦНЕ", "СписокЗначений");

	СпрСхем = СоздатьОбъект("Справочник.СхемыЭкспорта");
	СпрСхем.ИспользоватьВладельца(ВыгружаемыйДокумент.Контрагент);
	СпрСхем.ВыбратьЭлементы();
	Пока СпрСхем.ПолучитьЭлемент() = 1 Цикл
		Если (СпрСхем.ПометкаУдаления() = 0) И (СпрСхем.СхемаЭкспорта <> ТекущийЭлемент()) Тогда
			ТСхем.НоваяСтрока();
			ТСхем.Схема = СпрСхем.СхемаЭкспорта;			
			ТСхем.списТМЦ = СоздатьОбъект("СписокЗначений");
			ТСхем.списТМЦНЕ = СоздатьОбъект("СписокЗначений");
			глЗаполнитьСписокТоваров(ТСхем.Схема.СтрФильтр, ТСхем.списТМЦ);
			глЗаполнитьСписокТоваров(ТСхем.Схема.СтрФильтрНЕ, ТСхем.списТМЦНЕ);
		КонецЕсли;
	КонецЦикла;
	СпрВЭ = СоздатьОбъект("Справочник.Этикетки");
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Экспорт");
	Таб.ВывестиСекцию("Шапка|Основная");
	Таб.ПрисоединитьСекцию("Шапка|ТМЦ");
	Для Инд = 1 По СписДоп.РазмерСписка() Цикл
		Если СписДоп.Пометка(Инд) = 1 Тогда
			ИмяРекв = СписДоп.ПолучитьЗначение(Инд);
			ХвостРекв = ?(Прав(ИмяРекв, 1) = "&", "&", "");
			Поз = Найти(ИмяРекв, "#");
			Если Поз <> 0 Тогда
				ИмяРекв = Лев(ИмяРекв, Поз-1);
			КонецЕсли;
			Поз = Найти(ИмяРекв, "$");
			Если Поз <> 0 Тогда
				ИмяРекв = Лев(ИмяРекв, Поз-1);
			КонецЕсли;
			ИмяРекв = ?(Прав(ИмяРекв, 1) = "&", Лев(ИмяРекв, СтрДлина(ИмяРекв) - 1), ИмяРекв) + ХвостРекв;
			
			Таб.ПрисоединитьСекцию("Шапка|Реквизит");
		КонецЕсли;
	КонецЦикла;
	
	ДатаЦ = ?(ПустоеЗначение(ВыгружаемыйДокумент.ДатаЦ) = 1, ВыгружаемыйДокумент.ДатаДок, ВыгружаемыйДокумент.ДатаЦ);
	ВыгружаемыйДокумент.ВыбратьСтроки();
	Пока ВыгружаемыйДокумент.ПолучитьСтроку() = 1 Цикл		
		ПечЦенаУпаковкиРН			= "";	
		ПечЦенаУпаковки				= "";
		ПечПримечаниеУпаковки		= "";
		ПечКоэфУвВеса				= "";
		ПечСкидкаНаВес				= "";
		ПечДляПечати				= "";
		ПечВесУпаковки				= "";
		ПечПометкаУдаления = 0;
		ЦенаФилиалаД = "";
		ПримечаниеФилиалаД = "";
		КоэфУвВесаФилиалаД = "";
		СкидкаНаВесФилиалаД = "";
		ДляПечатиФилиалаД = "";
		ВесУпаковкиФилиалаД	= "";
		Если (ПустоеЗначение(ВыгружаемыйДокумент.ВидУпаковки) = 1) ИЛИ (ВыгружаемыйДокумент.ВидУпаковки = НетУп) Тогда // --- УМК Сандомирский В.Ю, (20.11.14)
			//--- не заполняем	
		Иначе	
			СпрРВУ.ИспользоватьВладельца(ВыгружаемыйДокумент.ТМЦ);
			Если СпрРВУ.НайтиПоРеквизиту("ВидУпаковки",ВыгружаемыйДокумент.ВидУпаковки,0) = 1 Тогда
				// выгружаем значения реквизитов, в соответствии с правилами. Для этого нам достаточно передать наименование реквизита, поскольку они везде одинаковые.
				ТекСхема = ТекущийЭлемент();
				ТСхем.ВыбратьСтроки();
				Пока ТСхем.ПолучитьСтроку() = 1 Цикл
					Если (ТСхем.списТМЦ.Принадлежит(ВыгружаемыйДокумент.ТМЦ) = 1) И (ТСхем.списТМЦНЕ.Принадлежит(ВыгружаемыйДокумент.ТМЦ) = 0) Тогда
						ТекСхема = ТСхем.Схема;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				УстановитьЗначениеПечати(СпрРВУ.ТекущийЭлемент(), ПечЦенаУпаковки, "Цена", 1, ЦенаФилиалаД, ТекСхема);
				ПечЦенаУпаковкиРН = ПечЦенаУпаковки;
				УстановитьЗначениеПечати(СпрРВУ.ТекущийЭлемент(), ПечПримечаниеУпаковки, "Примечание", 0, ПримечаниеФилиалаД, ТекСхема);
				УстановитьЗначениеПечати(СпрРВУ.ТекущийЭлемент(), ПечКоэфУвВеса, "КоэфУвВеса", 0, КоэфУвВесаФилиалаД, ТекСхема);
				УстановитьЗначениеПечати(СпрРВУ.ТекущийЭлемент(), ПечСкидкаНаВес, "СкидкаНаВес", 0, СкидкаНаВесФилиалаД, ТекСхема);
				УстановитьЗначениеПечати(СпрРВУ.ТекущийЭлемент(), ПечДляПечати, "ДляПечати", 1, ДляПечатиФилиалаД, ТекСхема);
				УстановитьЗначениеПечати(СпрРВУ.ТекущийЭлемент(), ПечВесУпаковки, "ВесУпаковки", 1, ВесУпаковкиФилиалаД, ТекСхема);
				ПечПометкаУдаления = СпрРВУ.ПометкаУдаления();
			КонецЕсли;	
		КонецЕсли;
		
		//--- получаем цену прайсовой категории цен УМК Сандомирский В.Ю, (04.12.14)
		ПечПрайсоваяЦена = глВернутьЦену(ВыгружаемыйДокумент.ТМЦ, КатегорияЦен, ДатаЦ);
		СтруктураРеквизита = СоздатьОбъект("СписокЗначений");
		
		Таб.ВывестиСекцию("ЭкспортФ|Основная");
		Таб.ПрисоединитьСекцию("ЭкспортФ|ТМЦ");
		Для Инд = 1 По СписДоп.РазмерСписка() Цикл
			Если СписДоп.Пометка(Инд) = 1 Тогда
				ИмяРекв = СписДоп.ПолучитьЗначение(Инд);
				РазобратьИмяРеквизита(ИмяРекв, СтруктураРеквизита);
				ИмяРекв = СтруктураРеквизита.Получить("Имя");

				Если СтруктураРеквизита.Получить("Тип") = "ФиксЗначение" Тогда
					ЗначениеРекв = СтруктураРеквизита.Получить("Значение");
				ИначеЕсли СтруктураРеквизита.Получить("Тип") = "Процент" Тогда
					Если Метаданные.Справочник("ТМЦ").Реквизит(ИмяРекв).Периодический = 1 Тогда
						ЗначениеРек = ВыгружаемыйДокумент.ТМЦ.ПолучитьАтрибут(ИмяРекв).Получить(ВыгружаемыйДокумент.ДатаДок);
					Иначе
						ЗначениеРек = ВыгружаемыйДокумент.ТМЦ.ПолучитьАтрибут(ИмяРекв);
					КонецЕсли;
					ЗнР = Число(ЗначениеРекв);
					ЗначениеРекв = Окр(ЗначениеРек + ЗначениеРек * (Число(СтруктураРеквизита.Получить("Значение")) / 100), Число(СтруктураРеквизита.Получить("Округление")));
				Иначе
					ЕстьРеквизит = 0;
					Если (Найти(СписокРеквизитовИзВЭ, ИмяРекв + ",") <> 0) и (ПустоеЗначение(ВидВесов) = 0) Тогда
						Если СпрВЭ.Владелец <> ВыгружаемыйДокумент.ТМЦ Тогда
							СпрВЭ.ИспользоватьВладельца(ВыгружаемыйДокумент.ТМЦ);						
							Если СпрВЭ.НайтиПоРеквизиту("ВидВесов", ВидВесов, 0) = 0 Тогда
								Сообщить("Указан вид весов для выгрузки, но для продукции: " + Строка(ВыгружаемыйДокумент.ТМЦ) + " не найдена этикетка. Реквизиты этикетки будут взяты из продукции.");
							КонецЕсли;
						КонецЕсли;
						
						Если (СпрВЭ.Владелец = ВыгружаемыйДокумент.ТМЦ) И (ПустоеЗначение(СпрВЭ.ТекущийЭлемент()) = 0) Тогда
							ЗначениеРекв = СпрВЭ.ПолучитьАтрибут(ИмяРекв);
							ЕстьРеквизит = 1;							
						КонецЕсли;
					КонецЕсли;
					
					Если ЕстьРеквизит = 0 Тогда
						Если Метаданные.Справочник("ТМЦ").Реквизит(ИмяРекв).Периодический = 1 Тогда
							ЗначениеРекв = ВыгружаемыйДокумент.ТМЦ.ПолучитьАтрибут(ИмяРекв).Получить(ВыгружаемыйДокумент.ДатаДок);
						Иначе
							ЗначениеРекв = ВыгружаемыйДокумент.ТМЦ.ПолучитьАтрибут(ИмяРекв);
						КонецЕсли;
						//Если ТипЗначенияСтр(ЗначениеРекв) = "Перечисление" Тогда
						//	ЗначениеРекв = ЗначениеРекв.Идентификатор();
						//КонецЕсли;						
					КонецЕсли;
				КонецЕсли;
				Таб.ПрисоединитьСекцию("ЭкспортФ|Реквизит");
			КонецЕсли;
		КонецЦикла;				
		
		Если СтрокаДоп = 1 Тогда
			Таб.ВывестиСекцию("СтрокаДоп|Основная");
			Таб.ПрисоединитьСекцию("СтрокаДоп|ТМЦ");			
		КонецЕсли;
	КонецЦикла;

	ИмяФайла = СокрЛП(ВыгружаемыйДокумент.Контрагент.ПутьДляЭкспортаРН) + "РН_" + СокрЛП(ВыгружаемыйДокумент.НомерДок) + "_" + 
		СтрЗаменить(Строка(ВыгружаемыйДокумент.ДатаДок), ".", "") + ".mxl";
//	Таб.Показать();
	Таб.Записать(ИмяФайла);
	Если ФС.СуществуетФайл(ИмяФайла) = 1 Тогда
	    Сообщить("Экспорт выполнен успешно");
	Иначе
		Сообщить("Экспорт не удался");
	КонецЕсли;
	
	Форма.Закрыть(0);
КонецПроцедуры // Экспорт

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		Если ИмСпис = "" Тогда
			ДобавитьвСписок(Элт, списТовар);
		Иначе
			ДобавитьвСписок(Элт, списТоварНЕ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//======================================================================
Функция НазначитьЗначениеПоТипуРеквизита(Рекв, ТипРеквизита, ВидРеквизита)
	ПредставлениеРеквизита = Метаданные.Справочник("ТМЦ").Реквизит(Рекв).Представление();
	ТипРеквизита = Метаданные.Справочник("ТМЦ").Реквизит(Рекв).Тип;
	ВидРеквизита = Метаданные.Справочник("ТМЦ").Реквизит(Рекв).Вид;
	Длина = 0; Точность = 0;
	Если (ТипРеквизита = "Строка") ИЛИ (ТипРеквизита = "Число") Тогда
		Длина = Метаданные.Справочник("ТМЦ").Реквизит(Рекв).Длина;
	КонецЕсли;
	Если (ТипРеквизита = "Число") Тогда
		Точность = Метаданные.Справочник("ТМЦ").Реквизит(Рекв).Точность;
	КонецЕсли;	
	
	Форма.Значение.НазначитьТип(ТипРеквизита + ?(ВидРеквизита = "", "", "." + ВидРеквизита), Длина, Точность);
	Возврат ПредставлениеРеквизита;
КонецФункции // 

//======================================================================
Функция ТекРеквизит()
Перем ИмяРекв, Зн, ТипРеквизита, ВидРеквизита, ЕстьРешетка;
	Форма.ОкруглятьДоЗнаков.Видимость(0);
	Форма.тОкруглятьДо.Видимость(0);
	Форма.флКопироватьНовым.Видимость(0);		

	Если Форма.Закладки.ТекущаяСтрока() = 2 Тогда
		СтруктураРеквизита = СоздатьОбъект("СписокЗначений");	
		
		Если СписДоп.ТекущаяСтрока() = 0 Тогда
			Форма.ВариантИспользования.Видимость(0);
			Форма.ВариантФикс.Видимость(0);
			Форма.Значение.Видимость(0);
			Возврат "";
		Иначе		
			Рекв = СписДоп.ПолучитьЗначение(СписДоп.ТекущаяСтрока());
			РазобратьИмяРеквизита(Рекв, СтруктураРеквизита);
			Рекв = СтруктураРеквизита.Получить("Имя");
			Если Прав(Рекв, 1) = "&" Тогда
				Рекв = Лев(Рекв, СтрДлина(Рекв) - 1);
			КонецЕсли;
			ПредставлениеРеквизита = НазначитьЗначениеПоТипуРеквизита(Рекв, ТипРеквизита, ВидРеквизита);
			Зн = СтруктураРеквизита.Получить("Значение");
			
			Если СтруктураРеквизита.Получить("Тип") = "ФиксЗначение" Тогда
				ВариантИспользования = 2;				
				Если ТипРеквизита = "Число" Тогда
					Значение = Число(Зн);
				ИначеЕсли ТипРеквизита = "Дата" Тогда
					Значение = Дата(Зн);
				ИначеЕсли ТипРеквизита = "Счет" Тогда
					Значение = СчетПоКоду(Зн);
				ИначеЕсли Найти(ТипРеквизита, "Перечисление") <> 0 Тогда
					Значение = Перечисление.ПолучитьАтрибут(ВидРеквизита).ЗначениеПоИдентификатору(Зн);
				Иначе
					Значение = Зн;				
				КонецЕсли;
				Форма.Значение.Видимость(1);
			ИначеЕсли СтруктураРеквизита.Получить("Тип") = "Процент" Тогда
				ВариантИспользования = 3;
				ОкруглятьДоЗнаков = Число(СтруктураРеквизита.Получить("Округление"));
				Форма.Значение.НазначитьТип("Число", 7, 2);
				Значение = Число(Зн);
				Форма.ОкруглятьДоЗнаков.Видимость(1);
				Форма.тОкруглятьДо.Видимость(1);
				Форма.Значение.Видимость(1);
			Иначе
				ВариантИспользования = 1;
				Форма.Значение.Видимость(0);
			КонецЕсли;
			
			Если СтруктураРеквизита.Получить("ТолькоНовый") = "&" Тогда
				флКопироватьНовым = 1;
			Иначе
				флКопироватьНовым = 0;
			КонецЕсли;
			
			Форма.ВариантИспользования.Видимость(1);
			Форма.ВариантФикс.Видимость(1);
			Форма.ВариантИспользованияПроцент.Видимость(1);
			Форма.флКопироватьНовым.Видимость(1);
			Возврат ПредставлениеРеквизита;		
		КонецЕсли;
	Иначе
		Форма.ВариантИспользования.Видимость(0);
		Форма.ВариантФикс.Видимость(0);
		Форма.ВариантИспользованияПроцент.Видимость(0);
		Форма.Значение.Видимость(0);
	КонецЕсли;
КонецФункции // ТекРевизит()

//======================================================================
Процедура УстРеквизит(ЭтоЗначение = 0)
Перем ТипРеквизита, ВидРеквизита;

	Форма.Значение.Видимость(0);
	Форма.тОкруглятьДо.Видимость(0);
	Форма.ОкруглятьДоЗнаков.Видимость(0);
	
	Представление = "";
	ТекСтр = СписДоп.ТекущаяСтрока(); 
	Если ТекСтр <> 0 Тогда
		ЗначениеРеквизита = СписДоп.ПолучитьЗначение(ТекСтр, Представление);
		ЗначениеРеквизита = ?(Прав(ЗначениеРеквизита, 1) = "&", Лев(ЗначениеРеквизита, СтрДлина(ЗначениеРеквизита) - 1), ЗначениеРеквизита);
	КонецЕсли;
	Если ВариантИспользования >= 2 Тогда
		Форма.Значение.Видимость(1);
	КонецЕсли;
	Если ВариантИспользования = 3 Тогда
		Форма.тОкруглятьДо.Видимость(1);
		Форма.ОкруглятьДоЗнаков.Видимость(1);
	КонецЕсли;
	
	Если ТекСтр <> 0 Тогда
		ПозР = Найти(ЗначениеРеквизита, "#");
		Если ПозР <> 0 Тогда
			ЗначениеРеквизита = Лев(ЗначениеРеквизита, ПозР-1);			
		КонецЕсли;
		ПозР = Найти(ЗначениеРеквизита, "$");
		Если ПозР <> 0 Тогда
			ЗначениеРеквизита = Лев(ЗначениеРеквизита, ПозР-1);			
		КонецЕсли;
		Представление = Метаданные.Справочник("ТМЦ").Реквизит(ЗначениеРеквизита).Синоним;		
		
		Если ВариантИспользования = 2 Тогда
			Если ЭтоЗначение = 0 Тогда
				НазначитьЗначениеПоТипуРеквизита(ЗначениеРеквизита, ТипРеквизита, ВидРеквизита);
			КонецЕсли;
			
			Если ТипЗначенияСтр(Значение) = "Перечисление" Тогда
				Зн = Значение.Идентификатор();
			Иначе
				Зн = Значение;
			КонецЕсли;				
			ЗначениеРеквизита = ЗначениеРеквизита + "#" + Строка(Зн);
			Представление = Представление + " фикс. зн.:" + Строка(Значение);
		ИначеЕсли ВариантИспользования = 3 Тогда
			Если ЭтоЗначение = 0 Тогда
				НазначитьЗначениеПоТипуРеквизита(ЗначениеРеквизита, ТипРеквизита, ВидРеквизита);
			КонецЕсли;
			
			Зн = Значение;
			ЗначениеРеквизита = ЗначениеРеквизита + "$" + Строка(ОкруглятьДоЗнаков) + Строка(Зн);
			Представление = Представление + " %: " + Строка(Зн) + " окр. " + Строка(ОкруглятьДоЗнаков);
		КонецЕсли;
		
		СписДоп.УстановитьЗначение(ТекСтр, ЗначениеРеквизита + ?(флКопироватьНовым = 1, "&", ""), Представление + ?(флКопироватьНовым = 1, " (только нов.)", ""));
		УстМодифицированность();
	КонецЕсли;	
КонецПроцедуры // УстРеквизит()

//======================================================================
Процедура ДобавитьИИ()
	глДобавитьИИ(ТекущийЭлемент(), списТоварНЕ);
	УстМодифицированность();
КонецПроцедуры // ДобавитьИИ
ВыгружаемыйДокумент = ПолучитьПустоеЗначение("Документ.РасходнаяНакладная");

Инд = 1;
Пока Метаданные.Справочник("ТМЦ").Реквизит(Инд).Выбран() = 1 Цикл
	ИмяРекв = Метаданные.Справочник("ТМЦ").Реквизит(Инд).Идентификатор;
	Синоним = Метаданные.Справочник("ТМЦ").Реквизит(Инд).Синоним;
	ТипЗнач = Метаданные.Справочник("ТМЦ").Реквизит(Инд).Тип;

	Если (ТипЗнач = "Строка") ИЛИ (ТипЗнач = "Число") ИЛИ (ТипЗнач = "Перечисление") ИЛИ (ТипЗнач = "Счет") Тогда
		списДоп.ДобавитьЗначение(ИмяРекв, Синоним);
	КонецЕсли;		
	Инд = Инд + 1;		
КонецЦикла;

списДоп.СортироватьПоПредставлению();
ЭтоЭкспорт = 0;
СписокРеквизитовИзВЭ = "УсловияХранения,УсловияХраненияВП,УсловияХраненияВС,УсловияХраненияВЦ,УсловияХраненияГ,ОписаниеЭ,НаименованиеЭ,ВидЭ,";
