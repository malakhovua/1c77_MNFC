Перем СтарыйЭлемент;
Перем ИнформационнаяСтрока;
Перем ТекГруппа;
                                          
//********************************************************************************
Процедура ПоискЭлемента()	
	Перем СписокПараметров;
	Перем Флаг;
	Перем СписокРеквизитов,СтрокаСпискаРеквизитов;
	
	// нужно сделать настройку, в которой нужно
	// сохранять значение флага.
	// Пока задается константно.
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("Контрагенты","Вид");
	СписокПараметров.ДобавитьЗначение(Нет,"Флаг");
	СписокПараметров.ДобавитьЗначение(Контекст,"Контекст");
	
	// добавим реквизиты для поиска (здесь только по артикулу)
	СписокРеквизитов=СоздатьОбъект("СписокЗначений");
	// полное наименование
	СписокРеквизитов.ДобавитьЗначение("ПолнНаименование","полному наименованию");
	// физический адрес
	СписокРеквизитов.ДобавитьЗначение("ФизическийАдрес","физическому адресу");
	// юридический адрес
	СписокРеквизитов.ДобавитьЗначение("ЮридическийАдрес","юридическому адресу");
	
	СтрокаСпискаРеквизитов = ЗначениеВСтрокуВнутр(СписокРеквизитов);
	СписокПараметров.ДобавитьЗначение(СтрокаСпискаРеквизитов,"ДобАтрибуты");
	
	ОткрытьФормуМодально("Обработка.ПоискЭлемента",СписокПараметров);
	
	Если ТипЗначенияСтр(СписокПараметров)="Справочник" Тогда
	    АктивизироватьОбъект(СписокПараметров);
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ПриПереносеЭлементаВДругуюГруппу(Элемент, Группа)
	
	//глОтправитьУведомлениеЭП("Справочник.Контрагенты", "Перенос контрагента между группами", "Контрагенты: перенос " + Строка(Элемент) + " из группы " + Строка(ТекГруппа) + " в группу: " + Строка(Группа));	
	
	// + umk
	Если ЭтоОсновнойПартнер = 1 Тогда
		СтатусВозврата(0);
		Сообщить("Запрещено переносить основного партнера!");
		Возврат;
	КонецЕсли;
	
	Если Элемент.ЭтоГруппа() = 0 Тогда
		ОсновнойПартнер = глОсновнойКонтрагентИерархии(Группа,Элемент,1);
	КонецЕсли;
	// - umk
	
	Уровень = Группа.Уровень()+1;
	
	//Обновим уровни элементов входящих в иерархию.
	Если Элемент.ЭтоГруппа() = 1 Тогда
		
		СмещениеУровня = Уровень - Элемент.Уровень();
	
		глУстановитьУровеньПодчиненных(Элемент, "Контрагенты", СмещениеУровня);
		
	КонецЕсли;
	
КонецПроцедуры // 

// ===============================
// Название: СформироватьИнформационнуюСтроку()
// Параметры: 
// НЕТ
// Возвращаемое значение:
// Строка с дополнительной информацией 
// Описание:
// процедура предназначена для формирования информационной
// строки для текущего элемента в списке. К такой
// может относится информация которую из-за большого объема
// нет смысла помещать в список, либо при размещении ее в
// колонке будет заметная ухудшение производительности
// (к примеру сложновычисляемая информация из регистра и т.д.)
Функция СформироватьИнформационнуюСтроку()
	
	Если ТекущийЭлемент()<>СтарыйЭлемент Тогда
		// новый элемент не равен текущему
		// так как функция будет вызываться каждый раз при перерисовки формы
		// (необязательно, что в этот момент будет выбран другой элемент в списке),
		// то чтобы не выполнять ненужные вычисления проверяем, что в списке
		// выбран элемент, отличный от того, для которого была сформирована
		// информыционная строка
		Если ТекущийЭлемент().ЭтоГруппа()=0 Тогда
			// не группа
			// а в этом месте можно что-нибудь и вычислить
		    ИнформационнаяСтрока = ?(ПустоеЗначение(ФизическийАдрес)=0," Адрес: "+глАдресСтрокой(ФизическийАдрес),"");
		Иначе
			ИнформационнаяСтрока = "";
		КонецЕсли;
	КонецЕсли;
	
	СтарыйЭлемент = ТекущийЭлемент();
    
	Возврат ИнформационнаяСтрока;
КонецФункции

// ===============================
// Название: ПоказатьИсторию()
// Параметры: 
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопка "История",.
// Описание:
// Открывается форма обработки для редактирования периодических реквизитов
Процедура ПоказатьИсторию()
	ТекЭлемент = ТекущийЭлемент();
	Если ПустоеЗначение(ТекЭлемент) = 1 Тогда
	     Предупреждение("Не выбран контрагент.");
		Возврат;
	КонецЕсли;

	Если ТекЭлемент.ЭтоГруппа() = 1 Тогда
	     Предупреждение("Выберите элемент, а не группу.");
		Возврат;
	КонецЕсли;

	глРедактироватьИсториюЗначений(Контекст,
	"",
	"История периодических реквизитов ("+ТекущийЭлемент()+")");
КонецПроцедуры // ПоказатьИсторию

// ===============================
// Название: СформироватьОтчет()
// Параметры: 
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элемента диалога:
// кнопка "Взаиморасчеты"
// НЕТ
// Описание:
// печать отчета "Взаиморасчеты"
Процедура СформироватьОтчет()
	// пока заглушка
	Если ПустоеЗначение(ТекущийЭлемент()) = 1 Тогда
		Предупреждение("Контрагент не выбран!");
	КонецЕсли;
	
	// печатаем общий упр. отчет по взаиморасчетам
	// по текущему контрагенту или группе 
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Обновить",1);         
	
    ПустойСписок = СоздатьОбъект("СписокЗначений");
	                                     
	// все настройки помещаем в список
	Расшифровка.Установить("Объект", "Взаиморасчеты");
	Расшифровка.Установить("Дата1", Мин(РабочаяДата(),ПолучитьДатуТА(),Константа.ОсновнаяДатаНачалаОтчета));
    Расшифровка.Установить("Дата2", Мин(РабочаяДата(),ПолучитьДатуТА()));
	
	Расшифровка.Установить("ВидОтчета", 1);		// общие взаиморасчеты
	
	Расшифровка.Установить("ФлагДиаграммы", 0);
	
	Расшифровка.Установить("ВыбКонтрагент", ТекущийЭлемент());
	Расшифровка.Установить("ВыбДоговор", ПолучитьПустоеЗначение("Документ"));          
	
	
	Группировки = СоздатьОбъект("СписокЗначений"); // для расшифровки отчета по движениям
	
	Группировки.ДобавитьЗначение("Контрагент", 		"Контрагент");           
	Группировки.ДобавитьЗначение("Договор", 		"Договор");           
	Группировки.ДобавитьЗначение("Счет",	 		"Документ-счет");           
	//Группировки.ДобавитьЗначение("Документ",		"Документ движения");

	Группировки.Пометка(1,1);
	Группировки.Пометка(2,1);
	Группировки.Пометка(3,1);

	Расшифровка.Установить("Группировки",Группировки);
	
	Расшифровка.Установить("фНачОст",1);
	Расшифровка.Установить("фКонОст",1);
	Расшифровка.Установить("фПриход",1);
	Расшифровка.Установить("фРасход",1);
		
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = Число(Расшифровка.Получить("Обновить"));
	глТаблица = 0;
	
	ОткрытьФорму("Отчет."+Расшифровка.Получить("Объект")+"#");
	
	глФлагРасшифровки = 0;
	глРасшифровка = 0;
	глОбновить = 0;	
КонецПроцедуры	// СформироватьОтчет

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	СтарыйЭлемент = ПолучитьПустоеЗначение("Справочник.Контрагенты");      
	Если глПроверяемыеНаборы.НайтиЗначение(НазваниеНабораПрав()) = 1 Тогда
		ИерархическийСписок(1,0);
	КонецЕсли;	
	
	Форма.кнИстория.Видимость(1);
	Форма.кнВзаиморасчеты.Видимость(1);
	Форма.кнГрупповаяОбработка.Видимость(1);
	глУстановитьНачальнуюГруппуПользователя(Контекст, глГруппыДоступаКонтрагенты);
КонецПроцедуры

// ===============================
Процедура ПриНачалеРедактированияСтроки()
	// практически все элементы справочника имеют много реквизитов.
	// поэтому редактировать элемент или создавать новый в списке
	// является нецелесообразным.
	СтатусВозврата(0);
КонецПроцедуры

Процедура ПечатьСписка()
	Род = Родитель;
	ОткрытьФорму("Обработка.ПечатьКонтрагентов", Род);
КонецПроцедуры

Процедура ПриВыбореРодителя(Элт)
	СтатусВозврата(глПроверитьДоступностьГруппы(Элт));
	Если глПользователь.НакладыватьФильтрыПриОткрытии = 1 Тогда
		СтатусВозврата(глГруппыДоступаКонтрагенты.Принадлежит(Элт));
	КонецЕсли;	
	Если СтатусВозврата() = 1 Тогда
		ТекГруппа = Элт;
	КонецЕсли;
КонецПроцедуры

ИнформационнаяСтрока = "";
