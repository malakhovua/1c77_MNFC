Перем ТоварВладелец;
Перем ДатаЗаписи;

// ===============================
// Название: ПоказатьИсторию()
// Параметры: 
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопка "История",.
// Описание:
// Открывается форма обработки для редактирования периодических реквизитов
Процедура ПоказатьИсторию()
	Если ПустоеЗначение(ТекущийЭлемент())=0 Тогда
		глРедактироватьИсториюЗначений(Контекст,
		"Единица,Цена,Валюта,СхемаЦенообразования",					//--- УМК Сандомирский В.Ю, (цены через схемы ) 03.04.14 г.
		"История периодических реквизитов ("+Владелец+")");
	КонецЕсли;
КонецПроцедуры // ПоказатьИсторию

// ===============================
Процедура ВыбратьДату()
	ВвестиДату(ДатаЗаписи, "Введите дату цен");
	ИспользоватьДату(ДатаЗаписи);
КонецПроцедуры //ПриИзмененииДаты

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	ТоварВладелец = Форма.Параметр;
	ДатаЗаписи = ИспользоватьДату();
	ИспользоватьВладельца(ТоварВладелец);
	СохранениеПериодическихРеквизитов(2, "*");
	ИерархическийСписок(1,0);
КонецПроцедуры	// ПриОткрытии

// ===============================
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтандОбр)
	Перем ЕдИзм;
	Если ИдентЭлемДиалога = "Единица" Тогда
		ЕдИзм = СоздатьОбъект("Справочник.Единицы");
		ЕдИзм.ИспользоватьВладельца(Владелец);
		Если ЕдИзм.Выбрать("Единицы измерения", "ФормаСписка") = 1 Тогда
			Единица = ЕдИзм.ТекущийЭлемент();
		КонецЕсли;
		ФлагСтандОбр = 0;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриРедактированииНовойСтроки()
	ДатаЗаписи =Константа.ДатаНачалаРаботы;
	ИспользоватьДату(ДатаЗаписи);
	Форма.Обновить();
	ВладелецЕдиницы = ТоварВладелец;
КонецПроцедуры

// ===============================
Процедура ПриНачалеРедактированияСтроки()
	СохранениеПериодическихРеквизитов(2, "*");
	//ВладелецЕдиницы = ТекущийЭлемент().Владелец;
КонецПроцедуры

// ===============================
Процедура ПриВыбореВладельца(ЭлементВладельца)
	ТоварВладелец = ЭлементВладельца;
	ИспользоватьВладельца(ТоварВладелец);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()	// Предопределенная процедура
	// в процедуре при записи проверятся заполнение обязательных реквизитов
	Перем Описание;
	Перем ФлагОшибки;
	Перем ВыборкаЦен;
	
	Описание = "Не заполнено поле ";
	                
	// изначально считаем, что не все обязательные поля заполнены
	ФлагОшибки = 0;
	
	// проверка обязательных полей
	Если ПустоеЗначение(Валюта) = 1 Тогда
	    Описание = Описание + "'Валюта'";
	ИначеЕсли ПустоеЗначение(КатегорияЦены) = 1 Тогда
	    Описание = Описание + "'Тип цены'";
	ИначеЕсли ПустоеЗначение(Единица) = 1 Тогда
	    Описание = Описание + "'Единица измерения'";
	Иначе
		// все обязательные поля заполнены
		ФлагОшибки = 1;
	КонецЕсли;
	
	Если ФлагОшибки=0 Тогда
	    
		// не заполнено обязательное поле
		Предупреждение(Описание);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	// проверяем, есть ли уже цена с такой категорией
	ВыборкаЦен = СоздатьОбъект("Справочник.Цены");
	ВыборкаЦен.ИспользоватьВладельца(Владелец);
	ВыборкаЦен.ВыбратьЭлементы();
	Пока ВыборкаЦен.ПолучитьЭлемент()=1 Цикл
		Если КатегорияЦены = ВыборкаЦен.КатегорияЦены Тогда
			Если Выбран()=0 Тогда
				// для новой цена сравниваем только совпадение категории
				ФлагОшибки = 0;
				Прервать;
			Иначе
				// для сохраненного элемента проверить несовпадение категорий
				// недостаточно, т.к. из выборки можно получить редактируемый элемент
				// дублирование категорий может произойти только если совпадают
				// категории у разных элементов
				Если (ТекущийЭлемент() <> ВыборкаЦен.ТекущийЭлемент()) Тогда
					ФлагОшибки = 0;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ФлагОшибки = 0 Тогда
		Предупреждение("У товара уже есть '"+КатегорияЦены+"' цена"+
		              ?(ВыборкаЦен.ПометкаУдаления()=1," (помечена на удаление).",""));
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Наименование = КатегорияЦены.Наименование;

	глЗаписьПериодическихРеквизитов(Контекст);
КонецПроцедуры	// ПриЗаписи

НоваяЦена = 0;