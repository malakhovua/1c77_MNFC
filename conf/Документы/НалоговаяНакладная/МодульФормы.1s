Перем НачальнаяДатаДокумента;
Перем СтарЧтоПродаем;
Перем СтарыйКонтрагент;
Перем СписокДействий;
Перем СтараяДата;            
Перем СтарВидНДС;
Перем СтарКатегорияЦен;

// ===============================
Процедура РассчитатьБазуНДС()
    НачВалюта = ?(ПустоеЗначение(Валюта)=1,Гривня,Валюта);
	СуммаНДС = глПересчет(Итог("НДС"),НачВалюта,Гривня,Курс,ДатаДок);
	БазаНДС  = глПересчет(Итог("СуммаБезНДС"),НачВалюта,Гривня,Курс,ДатаДок);
КонецПроцедуры

// ===============================
// Назначение: Пересчет сумм в строке документа
//		
// Аргументы: НоваяЦена - новая цена для текущей строки документа
//		
Процедура ПересчетСтроки(НоваяЦена=0)
	Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаБезНДС Тогда
		Если ПустоеЗначение(НоваяЦена) = 0 Тогда                                  
			// определим ставку НДС
			Если ВидНДС = ОсновнаяСтавкаНДС Тогда
				СтавкаНДС = ТМЦ.СтавкаНДС;
			Иначе
				СтавкаНДС = ВидНДС;
			КонецЕсли;
			// устанавливаем цену без НДС
			ЦенаБезНДС = НоваяЦена*100/(глПроцентНДС(СтавкаНДС,ДатаДок)+100);
		КонецЕсли;
		глВыч_Суммы_Накл(Контекст,-1);
	Иначе                     
		Если ПустоеЗначение(НоваяЦена) = 0 Тогда
			ЦенаСНДС = НоваяЦена;
		КонецЕсли;
		глВыч_Суммы_Накл(Контекст,1);
	КонецЕсли;
КонецПроцедуры //ПересчетСтроки

// Назначение: пересчитывает суммы в строках документа при изменении реквизитов шапки
//		
// Аргументы: Реквизит - наименование измененного реквизита
//		
// ===============================
Процедура ИзмРеквизитШапки(Реквизит)
	// пересчитаем суммы НДС в строках табличной части
	Если (СтарВидНДС=ВидНДС) И (СтарКатегорияЦен=КатегорияЦен) Тогда
		// ничего не изменилось
	    Возврат;     
	Иначе
		СтарВидНДС		=ВидНДС;
		СтарКатегорияЦен=КатегорияЦен;
	КонецЕсли;
	Если КоличествоСтрок() = 0 Тогда
	    Возврат;
	КонецЕсли;
	текОтвет = Вопрос("В документе изменен реквизит """+Реквизит+""". Перезаполнить цены заново?","Да+Нет");
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если текОтвет = "Да" Тогда
			// изменим цены
			ЦенаТовара = глВернутьЦену(ТМЦ, КатегорияЦен);
			Если ПустоеЗначение(ЦенаТовара) = 0 Тогда
				// получим параметры цены
				ЦенаТовара.ИспользоватьДату(ДатаДок);
				ЦенаЦены   = ЦенаТовара.Цена;
				ВалютаЦены = ЦенаТовара.Валюта;
				ЕдЦены	   = ЦенаТовара.Единица;
				ЦенаЦены   = ЦенаЦены * Коэффициент / ЕдЦены.Коэффициент;			
				ЦенаЦены   = глПересчет(ЦенаЦены,ВалютаЦены,ДатаДок,Валюта,Курс,Дата_курса);
				ПересчетСтроки(ЦенаЦены);                                                   
			Иначе
				ПересчетСтроки();
			КонецЕсли;
		ИначеЕсли Реквизит = "ВидНДС" Тогда
			ПересчетСтроки();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ===============================
Процедура ИзмДоговор()
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Вид() = "Договор" Тогда
			Валюта_Прежн = Валюта;
			Валюта = Договор.Валюта;
			Если Валюта <> Валюта_Прежн Тогда
				Дата_Курса=ДатаДок;
				Курс_Прежн = Курс;
				Курс = глКурсДляВалюты(Валюта,ДатаДок);
				Если ((Валюта_Прежн <> Валюта) Или (Курс_Прежн <> Курс)) Тогда
					// что-то изменилось, надо пересчитать цены
					глУстан_Вал(Контекст, Валюта, Валюта_Прежн, Курс, Курс_Прежн);
				КонецЕсли;
			КонецЕсли;
			ВидТорговли = Договор.ВидТорговли;
			ВидНДС = Договор.ВидНДС;
			ИзмРеквизитШапки("ВидНДС");
		КонецЕсли;
	КонецЕсли;    
КонецПроцедуры

// ===============================
Функция ФРМЦена(ЧислЗнач)
	// без разделения на триады, не меньше двух и не больше пяти знаков после запятой
    Стр=СокрЛ(Формат(Окр(ЧислЗнач,5),"Ч15.5"));
	Для Инд=1 по 3 Цикл
		Если Прав(Стр,1)="0" Тогда
		    Стр = Лев(Стр,СтрДлина(Стр)-1);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Стр;
КонецФункции

// ===============================
Функция ФРМСумма(ЧислЗнач)
	// без разделения на Триады
    Возврат СокрЛ(Формат(ЧислЗнач,"Ч15.2"));
КонецФункции

// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в документе
	ОткрытьФормуМодально("Обработка.ИнформацияОВалюте", Контекст);
КонецПроцедуры	

// ======================================
Процедура Заполнить(Док=0)
	ДокОснование = Док;
	
	Если ПустоеЗначение(Док) = 1 Тогда
		Если (РодительскийДокумент.Выбран() = 1) И (Найти("Счет,РасходнаяНакладная,ЛиквидацияНеоборАктивов,ОказаниеУслуг",РодительскийДокумент.Вид()) > 0) Тогда
			ДокОснование = РодительскийДокумент;
		Иначе
			ДокОснование = ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустоеЗначение(ДокОснование) = 1 Тогда
		// не удалось выбрать основание для заполнения
		Возврат;
	КонецЕсли;
	
	Если Вопрос("Заполнить накладную номенклатурой из документа-основания?","Да+Нет") = "Нет" Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСтроки();
	ЧтоПродаем = Перечисление.ЧтоПродаем.ТМЦ;
	Если ДокОснование.Вид() = "Счет" Тогда
		ЧтоПродаем = ДокОснование.ЧтоПродаем;
	ИначеЕсли ДокОснование.Вид() = "ЛиквидацияНеоборАктивов" Тогда
		ЧтоПродаем = Перечисление.ЧтоПродаем.НеоборотныеАктивы;
	КонецЕсли;
	ДокОснование.ВыбратьСтроки();
	// пересчитаем возвратную тару
	СуммаВозвратнойТары = 0;
	Пока ДокОснование.ПолучитьСтроку() = 1 Цикл
		// добавим строку
		НоваяСтрока();
		Если ЧтоПродаем = Перечисление.ЧтоПродаем.НеоборотныеАктивы Тогда
			НазначитьВид(ТМЦ,"НеоборотныеАктивы");
		Иначе
			НазначитьВид(ТМЦ,"ТМЦ");
		КонецЕсли;
		Форма.ТМЦ.НеИзменятьВид(1);
		Если ДокОснование.Вид() = "ЛиквидацияНеоборАктивов" Тогда
			ТМЦ = ДокОснование.НеоборотныйАктив;
		ИначеЕсли ДокОснование.Вид() = "ОказаниеУслуг" Тогда
			ТМЦ = ДокОснование.Услуга;
		Иначе
			ТМЦ = ДокОснование.ТМЦ;
		КонецЕсли;
		Если ТМЦ.Вид() = "ТМЦ" Тогда
			Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Тара Тогда
				Если (глЕстьРеквизитШапки("ВидТары",ДокОснование.Вид()) = Да) Тогда
					Если (ДокОснование.ВидТары <> Перечисление.ВидыТары.Покупная) Тогда
						// возвратная тара не должна попадать в табличную часть
						СуммаВозвратнойТары = СуммаВозвратнойТары + ДокОснование.СуммаБезНДС;
						УдалитьСтроку();
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		СуммаСНДС = ДокОснование.СуммаСНДС;
		Если ДокОснование.Вид() = "ЛиквидацияНеоборАктивов" Тогда
			ЦенаБезНДС = ДокОснование.ЦенаБезНДС;
			ЦенаСНДС = ЦенаБезНДС * (1 + ВидНДС.Ставка.Получить(ДатаДок));
			СуммаБезНДС = ДокОснование.СуммаБезНДС;
			СуммаБезСкидки = СуммаБезНДС;
			НДС = ДокОснование.НДС;
			Кво = 1;
			глУстановкаБазЕд(Контекст,,ТМЦ.БазЕдиница);
		Иначе
			// счета и накладные
			Кво = ДокОснование.Кво;
			Ед = ДокОснование.Ед;
			Коэффициент = ДокОснование.Коэффициент;
			Если ДокОснование.Вид() = "РасходнаяНакладная" Тогда
				ЦенаБезНДС = ДокОснование.ЦенаБезНДС;
				ЦенаСНДС = ДокОснование.ЦенаСНДС;
				Скидка = ДокОснование.Скидка;
				СуммаБезСкидки = ДокОснование.СуммаБезСкидки;
				СуммаБезНДС = ДокОснование.СуммаБезНДС;
				НДС = СуммаСНДС - СуммаБезНДС;
			ИначеЕсли ДокОснование.Вид() = "РасходнаяРозничная" Тогда
				ЦенаСНДС = ДокОснование.ЦенаСНДС;
				Если ДокОснование = ОсновнаяСтавкаНДС Тогда
					СтНДС = ТМЦ.СтавкаНДС.Ставка.Получить(ДатаДок);
				Иначе
					СтНДС = ДокОснование.ВидНДС.Ставка.Получить(ДатаДок);
				КонецЕсли;
				ЦенаБезНДС = ЦенаСНДС - ЦенаСНДС*СтНДС/(1+СтНДС);
				НДС = СуммаСНДС*СтНДС/(1+СтНДС);
				СуммаБезНДС = СуммаСНДС - НДС;
				СуммаБезСкидки = СуммаБезНДС;
			Иначе
				ЦенаБезНДС = ДокОснование.ЦенаБезНДС;
				ЦенаСНДС = ДокОснование.ЦенаСНДС;
				Скидка = ДокОснование.Скидка;
				СуммаБезСкидки = ДокОснование.СуммаБезСкидки;
				СуммаБезНДС = ДокОснование.СуммаБезНДС;
				НДС = ДокОснование.НДС;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в документе
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
	Если (ДокументОснование.Выбран() = 1) и (Найти("Счет,РасходнаяНакладная,РасходнаяРозничная",ДокументОснование.Вид()) > 0) Тогда
		Заполнить(ДокументОснование);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры //

// ===============================
Процедура ИзмТМЦ()
	Если ТМЦ.Выбран() = 1 Тогда
		Если ТМЦ.Вид()="ТМЦ" Тогда
			глПриИзмененииТовара(Контекст);
		Иначе
			// необоротные активы
			глУстановкаБазЕд(Контекст, , ТМЦ.БазЕдиница);
			ЦенаБезНДС = глПересчет(ТМЦ.Цена_Прих,Гривня,Валюта,ДатаДок,Курс);
			глВыч_Суммы_Накл(Контекст,-1);
		КонецЕсли;                       
	Иначе
		глПриИзмененииТовара(Контекст);
	КонецЕсли;
КонецПроцедуры
                          
// ===============================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;	
	Если СтарыйКонтрагент <> Контрагент Тогда
		// изменили Контрагента
		Если Договор.Выбран() = 1 Тогда
			Если Договор.Контрагент <> Контрагент Тогда
				Договор = 0;
			КонецЕсли;
		КонецЕсли;    
		Если Договор.Выбран() = 0 Тогда
			Если (Фирма = Контрагент.БазДоговор.Фирма) и (глВосстановитьЗначение(,"ПодставлятьОсновнойДоговор")=Да) Тогда
				Договор = Контрагент.БазДоговор;
			КонецЕсли;
			Если ПустоеЗначение(Контрагент.ВидТорговли) = 0 Тогда
				ВидТорговли = Контрагент.ВидТорговли;
			ИначеЕсли ПустоеЗначение(ВидТорговли) = 1 Тогда
				ВидТорговли = глВосстановитьЗначение(,"ОсновнойВидТорговли");
			КонецЕсли;	
			ИзмДоговор();
		КонецЕсли;       
		// проверим документ-основание
		Если ПустоеЗначение(ДокументОснование) = 0 Тогда
			Если ДокументОснование.Контрагент <> Контрагент Тогда
				ДокументОснование = 0;
			КонецЕсли;	
		КонецЕсли;	
		Если ПустоеЗначение(Контрагент.КатегорияЦен.Получить(ДатаДок)) = 0 Тогда
			КатегорияЦен = Контрагент.КатегорияЦен.Получить(ДатаДок);
		Иначе	
			КатегорияЦен = глВосстановитьЗначение(,"ОсновнаяКатегорияЦен");
		КонецЕсли;     		
	КонецЕсли;	
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры
                    
// ===============================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Форма.Заголовок(глЗаголовок(Контекст,"Налоговая накладная"));

	Если (ЧтоПродаем = Перечисление.ЧтоПродаем.ТМЦ) Тогда
	   	НазначитьВид(ТМЦ,"ТМЦ");
	Иначе
	   	НазначитьВид(ТМЦ,"НеоборотныеАктивы");
	КонецЕсли;

	Форма.ТМЦ.НеИзменятьВид(1);
	
	Если (ЧтоПродаем = Перечисление.ЧтоПродаем.ТМЦ) Тогда
	    Форма.Ед.Доступность(1);
	    Форма.кПодбор.Доступность(1-Форма.ТолькоПросмотр());
	Иначе
		Форма.Ед.Доступность(0);
		Форма.кПодбор.Доступность(0);
	КонецЕсли;

	Если РодительскийДокумент.Выбран() = 1 Тогда
		// накладная выписана документом, значит не итоговая
		Если Итоговая = 1 Тогда
		    Итоговая = 0;
		КонецЕсли;
	    Форма.Итоговая.Доступность(0);
		Форма.кОчиститьРодительскийДокумент.Доступность(1);
	Иначе
		Форма.Итоговая.Доступность(1);
		Форма.кОчиститьРодительскийДокумент.Доступность(0);
	КонецЕсли;

	Если ((РодительскийДокумент.Выбран() = 1) И (Найти("Счет,РасходнаяНакладная,ЛиквидацияНеоборАктивов,ОказаниеУслуг,РасходнаяРозничная",РодительскийДокумент.Вид()) > 0))
	Или ((ДокументОснование.Выбран() = 1) И (Найти("Счет,РасходнаяНакладная,ЛиквидацияНеоборАктивов,ОказаниеУслуг,РасходнаяРозничная",ДокументОснование.Вид()) > 0)) Тогда
		Форма.кЗаполнить.Доступность(1);
	Иначе
		Форма.кЗаполнить.Доступность(0);
	КонецЕсли;

	Если ЧтоПродаем=Перечисление.ЧтоПродаем.НеоборотныеАктивы Тогда
		Форма.Скидка.Доступность(0);
	ИначеЕсли глВосстановитьЗначение(,"ИспользоватьСкидку") = Да Тогда
		Форма.Скидка.Доступность(1);
	КонецЕсли;

	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кПодбор.Доступность(0);
		Форма.кЗаполнить.Доступность(0);
		Форма.кВалюта.Доступность(0);
		Форма.кОснование.Доступность(0);
		Форма.кОчиститьРодительскийДокумент.Доступность(0);
		Форма.кФирма.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	РассчитатьБазуНДС();
	Форма.ОперДата.Видимость(Итоговая);
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмЧтоПродаем()
	Если (КоличествоСтрок()<>0) и (СтарЧтоПродаем<>ЧтоПродаем) и (СокрЛП(СтарЧтоПродаем)<>"") Тогда
		Рез = Вопрос("Для изменения нужно очистить табличную часть. Удалить строки?","Да+Нет");
		Если Рез = "Да" Тогда
		    УдалитьСтроки();
		Иначе
			ЧтоПродаем = СтарЧтоПродаем;
		КонецЕсли;
	КонецЕсли;
	СтарЧтоПродаем = ЧтоПродаем;
КонецПроцедуры

// ===============================
Процедура ЗаполнитьПоУмолчанию()
	Валюта = Гривня;
	Дата_Курса = ДатаДок;
	Курс = глКурсДляВалюты(Валюта,Дата_Курса);
	Контрагент=глВосстановитьЗначение(,"ОсновнойПокупатель");
	ИзмКонтрагент();
	ВидНДС = глВосстановитьЗначение(,"БазНДС");
	ЧтоПродаем = Перечисление.ЧтоПродаем.ТМЦ;
	Выписал = глВосстановитьЗначение(,"БазВыписалНН");
КонецПроцедуры

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
	Иначе
		глУстановитьФирму(Контекст);
		глЗаполнитьШапку(Контекст);
		ЗаполнитьПоУмолчанию();
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
    // пустой договор должен строго иметь тип "Документ", важно для отчета АнализНДС
    Если ПустоеЗначение(Договор) = 1 Тогда
        Договор = ПолучитьПустоеЗначение("Документ");
    КонецЕсли;
	РассчитатьБазуНДС();
КонецПроцедуры

// ===============================
Процедура Подбор()
	глПодбор(Контекст,"ТМЦ","ДляПодбора");
КонецПроцедуры

// ===============================
Процедура ОбработкаПодбора(Выб)
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(ДокОснование)
	Параметр = СоздатьОбъект("СписокЗначений");
	Параметр.ДобавитьЗначение(ДокОснование,"ДокументОснование");
	Параметр.ДобавитьЗначение(Контекст,"Контекст");
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов#",Параметр);
	СтатусВозврата(Параметр.Получить("СтатусВозврата"));
КонецПроцедуры

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение)
	Форма.ИспользоватьСлой("Общий,"+Значение);
	Если Значение = "Основной" Тогда
		Если Константа.ИспользоватьСкидку <> Да Тогда
			Форма.СуммаБезСкидки.Видимость(0);
			Форма.Скидка.Видимость(0);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
                                
// ===============================
Процедура Печать(Режим=0)
	
Перем ПечЦенаБезНДС, ПечСуммаБезСкидки, ПечСуммаБезТранс, ПечСуммаТранс, ПечВозвТара,
	ПечСуммаНадбавки, ПечСуммаСкидки, ПечИтогСуммаБезНДС, ПечИтогНДС, ПечИтогСуммаСНДС, ПечВсего, НФ;
                             
	глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС"));
	
	НомерДокумента = глНомерБезПрефикса(НомерДок);
	ФирмаНалогНомер = Фирма.ИНН;
	ФирмаНомСвид = Фирма.НомерСвидетельства;
	КонтрагентНалогНомер = ?(ПустоеЗначение(Контрагент.ИНН)=1,"ХХХХХХХХХХХХ",Контрагент.ИНН);
	КонтрагентНомСвид = ?(ПустоеЗначение(Контрагент.НомерСвидетельства)=1,"ХХХХХХХХХХХХ",Контрагент.НомерСвидетельства);
	Приложение1 = "";

	Таб = СоздатьОбъект( "Таблица" );
	Если Валюта = Гривня Тогда
		Если Сокрлп(ВидНДС.Код) = "БезНДС" Тогда
			Таб.ИсходнаяТаблица("БезНДС");
		ИначеЕсли Сокрлп(ВидНДС.Код) = "НДС0" Тогда
			Таб.ИсходнаяТаблица("НДС0");
		Иначе
			Таб.ИсходнаяТаблица("НДС20");
		КонецЕсли;
	Иначе
		// экспорт
		Таб.ИсходнаяТаблица("НДСЭкспорт");
	КонецЕсли;
    
		ТаблТМЦ = СоздатьОбъект("ТаблицаЗначений");
		ВыгрузитьТабличнуюЧасть(ТаблТМЦ);
		                        
		ТаблТМЦ.Свернуть("ТМЦ,"+
  			?(Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС,"ЦенаСНДС","ЦенаБезНДС")+
  			",Ед,Коэффициент","Кво,СуммаБезСкидки,СуммаБезНДС,НДС,СуммаСНДС,ОперДата");
		
		ОбщСуммаНадбавки = ?(Итог("Скидка") < 0, -Итог("Скидка"), 0);
		ОбщСуммаСкидки = ?(Итог("Скидка") >= 0, Итог("Скидка"), 0);
		
		КоэфБезНДС = 100/(100 + глПроцентНДС(ВидНДС,ДатаДок));
		Если (Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС) Тогда
			ОбщСуммаНадбавки = Окр(КоэфБезНДС * ОбщСуммаНадбавки,2);
			ОбщСуммаСкидки = Окр(КоэфБезНДС * ОбщСуммаСкидки,2);
		КонецЕсли;
		
		Для Страница = 1 По 2 Цикл
			Таб.ВывестиСекцию( "Пробел" );
			Если Страница = 1 Тогда
				Таб.ВывестиСекцию( "Оригинал" );
			ИначеЕсли Страница = 2 Тогда
				Таб.ВывестиСекцию( "ПерваяКопия" );
			Иначе
				Таб.ВывестиСекцию( "ВтораяКопия" );
			КонецЕсли;
			Таб.ВывестиСекцию( "Шапка" );
			
			ПерваяСтрока = 1;
			Таб.ВывестиСекцию( "РазделI1" );
			
			ТрансСуммаБезНДС = 0;
			ТрансСуммаСНДС = 0;
			ТрансНДС = 0;
			
			ИтСуммаБезСкидки = 0;
			ТаблТМЦ.ВыбратьСтроки();
			Пока ТаблТМЦ.ПолучитьСтроку() > 0 Цикл
				Если Итоговая=1 Тогда
					Дата2 = ?(ПустоеЗначение(ТаблТМЦ.ОперДата)=0, ТаблТМЦ.ОперДата, ДатаДок);
				Иначе
					Дата2 = ДатаДок;
				КонецЕсли;
				Если ТаблТМЦ.ТМЦ.Вид() = "ТМЦ" Тогда
					Если ТаблТМЦ.ТМЦ.Транспорт = 0 Тогда
						ПечТМЦ = ТаблТМЦ.ТМЦ.ПолнНаименование;
						ЕдИзм = ТаблТМЦ.Ед;
						ПечКво = ТаблТМЦ.Кво;                
						Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС Тогда
						   ТекСуммаБезСкидки = ?(Константа.ИспользоватьСкидку <> Да,ТаблТМЦ.СуммаСНДС,ТаблТМЦ.СуммаБезСкидки);
					       СуммаБезСкидкиГрн = глПересчет(Окр(ТекСуммаБезСкидки * КоэфБезНДС,2),Валюта,Курс,Гривня,Дата_курса,Дата_курса);
					       ПечСуммаБезСкидки = глФРМ3(СуммаБезСкидкиГрн,Гривня,0);
					       ЦенаСНДСГрн		 = глПересчет(ТаблТМЦ.ЦенаСНДС,Валюта,Курс,Гривня,Дата_курса,Дата_курса);
					       ТочноеКво		 = ТекСуммаБезСкидки / ТаблТМЦ.ЦенаСНДС;
					       ПечЦенаБезНДС	 = ФРМЦена(СуммаБезСкидкиГрн/?(ПустоеЗначение(ТочноеКво)=1,1,ТочноеКво));
					       ПечКво			 = ФРМЦена(ТочноеКво);
						Иначе
						   ТекСуммаБезСкидки = ?(Константа.ИспользоватьСкидку <> Да,ТаблТМЦ.СуммаБезНДС,ТаблТМЦ.СуммаБезСкидки);
					       СуммаБезСкидкиГрн = глПересчет(ТекСуммаБезСкидки,Валюта,Курс,Гривня,Дата_курса,Дата_курса);
					       ПечСуммаБезСкидки = глФРМ3(СуммаБезСкидкиГрн,Гривня,0);
					       ЦенаБезНДСГрн	 = глПересчет(ТаблТМЦ.ЦенаБезНДС,Валюта,Курс,Гривня,Дата_курса,Дата_курса);
					       ПечЦенаБезНДС	 = ФРМСумма(ЦенаБезНДСГрн);
					       ПечКво			 = ФРМЦена(СуммаБезСкидкиГрн/ЦенаБезНДСГрн);
						КонецЕсли;
						ИтСуммаБезСкидки = ИтСуммаБезСкидки + СуммаБезСкидкиГрн;						
						Таб.ВывестиСекцию("РазделI2");
					Иначе
						ТрансСуммаБезНДС = ТрансСуммаБезНДС + ТаблТМЦ.СуммаБезНДС;
						ТрансСуммаСНДС = ТрансСуммаСНДС + ТаблТМЦ.СуммаСНДС;
						ТрансНДС = ТрансНДС + ТаблТМЦ.НДС;
					КонецЕсли;
				Иначе
					ПечТМЦ = ТаблТМЦ.ТМЦ.ПолнНаименование;
					ЕдИзм = ТаблТМЦ.ТМЦ.БазЕдиница;
					ПечКво = ТаблТМЦ.Кво;
					ПечЦенаБезНДС = Формат(глПересчет(ТаблТМЦ.ЦенаБезНДС*ТаблТМЦ.Коэффициент,Валюта,Гривня,Курс,ДатаДок),"Ч12.2");
					ПечСуммаБезСкидки = Формат(глПересчет(ТаблТМЦ.СуммаБезСкидки,Валюта,Гривня,Курс,ДатаДок),"Ч12.2");
					Таб.ВывестиСекцию("РазделI2");
				КонецЕсли;
			КонецЦикла;

			ПечСуммаБезТранс = ФРМСумма(ИтСуммаБезСкидки);
			ПечСуммаТранс = ФРМСумма(глПересчет(ТрансСуммаБезНДС,Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			ПечВозвТара = ФРМСумма(глПересчет(СуммаВозвратнойТары,Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			
			ПечСуммаНадбавки = ФРМСумма(глПересчет(ОбщСуммаНадбавки,Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			ПечСуммаСкидки = ФРМСумма(глПересчет(ОбщСуммаСкидки,Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			
			ПечИтогСуммаБезНДС = ФРМСумма(Число(ПечСуммаБезТранс) + Число(ПечСуммаТранс) + Число(ПечСуммаНадбавки) - Число(ПечСуммаСкидки));
			ПечИтогНДС = ФРМСумма(глПересчет(Итог("НДС"),Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			ПечИтогСуммаСНДС = ФРМСумма(глПересчет(Итог("СуммаСНДС"),Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			ПечВсего = ФРМСумма(глПересчет(Итог("СуммаСНДС")+СуммаВозвратнойТары,Валюта,Курс,Гривня,Дата_курса,Дата_курса));
			
			Таб.ВывестиСекцию( "РазделIВсего" );
			Таб.ВывестиСекцию( "ТовТранс" );
			Таб.ВывестиСекцию( "ВозврТара" );
			Таб.ВывестиСекцию( "Надбавка" );
			Таб.ВывестиСекцию( "ВсегоПоI_II_IV" );
			Таб.ВывестиСекцию( "НДС" );
			Таб.ВывестиСекцию( "ОбщаяСумма" );
			Таб.ВывестиСекцию( "Подвал" );
			
			Если Страница <> 2 Тогда
				Таб.НоваяСтраница();
			КонецЕсли;
		КонецЦикла;
	Таб.Опции(0, 0, 0, 0) ;
	Таб.ПараметрыСтраницы(1,,,,,,,,);
	Если Режим = 1 Тогда
		Таб.КоличествоЭкземпляров(глВосстановитьЗначение(,"ПечКолЭкзННПриБыстройПродаже"));
		Таб.Напечатать(0);
	Иначе	
		Таб.Защита(Константа.ФлагЗащитыТаблиц);
		Таб.ТолькоПросмотр(1);
		Таб.Показать("Печать налоговой накладной","");
	КонецЕсли;	
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	СтараяДата = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
	Форма.ИспользоватьСлой("Общий,Основной");
	                          
	Если Константа.ИспользоватьСкидку <> Да Тогда
		Форма.СуммаБезСкидки.Видимость(0);
		Форма.Скидка.Видимость(0);
	КонецЕсли;
	
	глУстВидимостьЦен(Контекст);
		
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр)="Строка" Тогда
			Если ВРег(Форма.Параметр)="БЫСТРАЯПЕЧАТЬ" Тогда
				Печать(1);
				СтатусВозврата(0); 
				Возврат;                            
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	СтарЧтоПродаем = ЧтоПродаем;
	СтарыйКонтрагент = Контрагент;
	СтарВидНДС		=ВидНДС;
	СтарКатегорияЦен=КатегорияЦен;
КонецПроцедуры // ПриОткрытии()
                                  
// ===============================
Процедура ПриРедактированииНовойСтроки()
	Если ПустоеЗначение(ОперДата) = 1 Тогда
		ОперДата = ДатаДок;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ИзмРеквизитШапки("ВидНДС");
		ФлагСтандОбр = 0;
	ИначеЕсли Рекв = "Выписал" Тогда
		ФлагСтандОбр = 0;
		КонтФирма = Фирма;
		ОткрытьФорму("Справочник.Сотрудники.ДляВыбора",КонтФирма);
	КонецЕсли;
КонецПроцедуры

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|Подчиненные");