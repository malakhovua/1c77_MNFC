//==================Область объявления переменных==============
Процедура ПодготовитьТаблицуПродаж(ТаблицаПродаж) Далее
Процедура ПолучитьДочернииОрганизации() Далее

// ============Предопределенные процедуры======================

Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
КонецПроцедуры

// ============================================================
Процедура ВводНового(ПризнакКопирования)
	
	глУстановитьФирму(Контекст);
	
	ПериодНачало = НачМесяца(ДатаДок);
	ПериодОкончание = КонМесяца(ДатаДок)
	
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()	
	
	Если ТекущийДокумент().Выбран() = 0 Тогда
		глПроверкаДатыДок(Контекст,"Запись");
	КонецЕсли;
	
	Если глКонтрольДатыДокумента(Контекст, ДатаДок)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	Автор = глПользователь;															
	
КонецПроцедуры

//=============Служебные процедуры===============================
Процедура ЗаполнитьТабличнуюЧасть()
	
	Если ПустоеЗначение(НачалоПериода) = 1 Тогда
		Предупреждение("Не заполнено Начало периода");
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ОкончаниеПериода) = 1 Тогда
		Предупреждение("Не заполнено Окончание периода");
		Возврат;
	КонецЕсли;
	
	Если НачалоПериода > ОкончаниеПериода Тогда
		Предупреждение("Начало периода больше Окончания периода");
		Возврат;
	КонецЕсли;
	
	УдалитьСтроки();
	
	//Обходим даты периода
	
	Период = НачалоПериода;
	
	//Предварительная таблица. Для расчета.
	ТабИндекс = СоздатьОбъект("ИндексированнаяТаблица");
	ТабИндекс.НоваяКолонка("Контрагент");
	ТабИндекс.НоваяКолонка("Собственник");
	ТабИндекс.НоваяКолонка("Объект");
	ТабИндекс.НоваяКолонка("СхемаРасчетаБонуса");
	ТабИндекс.НоваяКолонка("ЛимитБазы");
	ТабИндекс.НоваяКолонка("ПроцентРасчета");
	ТабИндекс.НоваяКолонка("ПериодНачало");
	ТабИндекс.НоваяКолонка("ПериодОкончание");
	
	ИтогТаб = СоздатьОбъект("ИндексированнаяТаблица");
	ИтогТаб.НоваяКолонка("Контрагент");
	ИтогТаб.НоваяКолонка("Собственник");
	ИтогТаб.НоваяКолонка("Объект");
	ИтогТаб.НоваяКолонка("СхемаРасчетаБонуса");
	ИтогТаб.НоваяКолонка("ПериодНачало");
	ИтогТаб.НоваяКолонка("ПериодОкончание");
	ИтогТаб.НоваяКолонка("ЛимитБазы");
	ИтогТаб.НоваяКолонка("БазаРасчета");
	ИтогТаб.НоваяКолонка("ПроцентРасчета");
	ИтогТаб.НоваяКолонка("СуммаБонусов");
	
	Пока Период <= ОкончаниеПериода Цикл
		
		рс = СоздатьОбъект("ODBCRecordset");
		
		ТекстЗапроса = "
		|SELECT Контрагенты.ID [Контрагент $Справочник.Контрагенты]
		|   , $СхемаРасчетаБонусаСтроки.Собственник [Собственник $Справочник]
		|	, $СхемаРасчетаБонусаСтроки.Объект [Объект $Справочник]
		|	, $ПоследнееЗначение.Контрагенты.СхемаРасчетаБонуса(Контрагенты.ID, :ВыбДата) [СхемаРасчетаБонуса $Документ.СхемаРасчетаБонуса]
		|	, $СхемаРасчетаБонусаСтроки.ЛимитБазы ЛимитБазы
		|	, $СхемаРасчетаБонусаСтроки.Зн ПроцентРасчета
		|FROM $Справочник.Контрагенты AS Контрагенты
		|	INNER JOIN $ДокументСтроки.СхемаРасчетаБонуса AS СхемаРасчетаБонусаСтроки ON $ПоследнееЗначение.Контрагенты.СхемаРасчетаБонуса(Контрагенты.ID, :ВыбДата) = СхемаРасчетаБонусаСтроки.IDDOC
		|WHERE ($СхемаРасчетаБонусаСтроки.База = :БазаСхемы)
		|";
		рс.УстановитьТекстовыйПараметр("БазаСхемы", Перечисление.БазыДляРасчетаБонуса.РасчетЗаПериод);
		рс.УстановитьТекстовыйПараметр("ВыбДата", Период);
		
		
		//Добавим колонки в ТабИндекс
		Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);		
		
		Результат.ВыбратьСтроки();
		
		Пока Результат.ПолучитьСтроку() = 1 Цикл
			
			ТабИндекс.НоваяСтрока();
			ТабИндекс.Контрагент = Результат.Контрагент;
			ТабИндекс.Собственник  = Результат.Собственник;
			ТабИндекс.Объект = Результат.Объект;
			ТабИндекс.СхемаРасчетаБонуса = Результат.СхемаРасчетаБонуса;
			ТабИндекс.ЛимитБазы = Результат.ЛимитБазы;
			ТабИндекс.ПроцентРасчета = Результат.ПроцентРасчета;
			ТабИндекс.ПериодНачало = Период;
			
		КонецЦикла; // По выборке
		
		
		Период = Период + 1;
		
	КонецЦикла; // По периодам
	
	ТабИндекс.ДобавитьИндекс("Ключ_индекс","Контрагент, Собственник, СхемаРасчетаБонуса");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент, Собственник, СхемаРасчетаБонуса");
	
	//Заполняем итоговую таблицу.
	ТабИндекс.ВыбратьСтроки();
	
	Пока ТабИндекс.ПолучитьСтроку() = 1  Цикл
		
		ДобавитьСтроку = 1;
		
		//Поиск уже добавленных строк
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ТабИндекс.Контрагент);
		Фильтр.ДобавитьЗначение(ТабИндекс.Собственник);
		Фильтр.ДобавитьЗначение(ТабИндекс.СхемаРасчетаБонуса);
		
		ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ИтогТаб.ВыбратьСтроки("Ключ_индекс");
		
		Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			ДобавитьСтроку = 0;	
			//Если найдена строка и она соотв. текущему периоду тогда добавляем 
			Если ИтогТаб.ПериодНачало = ТабИндекс.ПериодНачало Тогда
				ДобавитьСтроку = 1;
			КонецЕсли;
		КонецЦикла; // по фильтру
		
		Если ДобавитьСтроку = 1 Тогда
			
			ИтогТаб.НоваяСтрока();
			ИтогТаб.Контрагент = ТабИндекс.Контрагент;
			ИтогТаб.Собственник  = ТабИндекс.Собственник;
			ИтогТаб.Объект = ТабИндекс.Объект;
			ИтогТаб.СхемаРасчетаБонуса = ТабИндекс.СхемаРасчетаБонуса;
			ИтогТаб.ЛимитБазы = ТабИндекс.ЛимитБазы;
			ИтогТаб.ПроцентРасчета = ТабИндекс.ПроцентРасчета;
			ИтогТаб.ПериодНачало = ТабИндекс.ПериодНачало;
			
		КонецЕсли;
		
	КонецЦикла; //По таблице индексу
	
	//Удалим индекс и добавим новый без схемы
	ИтогТаб.УдалитьИндекс("Ключ_индекс");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент, Собственник");
	
	//Установим периоды окончания
	ИтогТаб.ВыбратьСтроки();
	
	Пока ИтогТаб.ПолучитьСтроку() = 1  Цикл
		
		ПериодНачало =  ИтогТаб.ПериодНачало;
		//Поиск уже добавленных строк
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ИтогТаб.Контрагент);
		Фильтр.ДобавитьЗначение(ИтогТаб.Собственник);
		
		ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ИтогТаб.ВыбратьСтроки("Ключ_индекс");
		
		Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			
			//Если найдена строка и она соотв. текущему периоду устанавливаем период окончания
			Если ПериодНачало > ИтогТаб.ПериодНачало Тогда
				ИтогТаб.УстановитьЗначение(,"ПериодОкончание",ПериодНачало-1); //изменена схема. Период вкючительно	
			Иначе
				ИтогТаб.УстановитьЗначение(,"ПериодОкончание",ОкончаниеПериода);
			КонецЕсли;
			
		КонецЦикла; // по фильтру
		
	КонецЦикла; //По таблице Итогов
	
	//Заполним продажи
	ТаблицаПродаж = СоздатьОбъект("ИндексированнаяТаблица");
	
	ИтогТаб.Выгрузить(ТаблицаПродаж,,"Контрагент,ПериодНачало,ПериодОкончание",1);
	//Свернем по периодам контрагентам
	ТаблицаПродаж.НоваяКолонка("Продажи");
	ТаблицаПродаж.Свернуть("Контрагент,ПериодНачало,ПериодОкончание", "Продажи");
	ТаблицаПродаж.Сортировать("Контрагент, ПериодНачало");
	
	ПодготовитьТаблицуПродаж(ТаблицаПродаж);
	
	ТаблицаПродаж.ДобавитьИндекс("Ключ_индекс","Контрагент, ПериодНачало, ПериодОкончание");
	
	//Заполним БазаРасчета(продажи) в итоговой таблице
	
	ИтогТаб.ВыбратьСтроки();
	
	Пока ИтогТаб.ПолучитьСтроку() = 1  Цикл
		БазаРасчета_ = 0;
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ИтогТаб.Контрагент);
		Фильтр.ДобавитьЗначение(ИтогТаб.ПериодНачало);
		Фильтр.ДобавитьЗначение(ИтогТаб.ПериодОкончание);
		
		ТаблицаПродаж.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ТаблицаПродаж.ВыбратьСтроки("Ключ_индекс");
		
		Пока ТаблицаПродаж.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			
			БазаРасчета_ = ТаблицаПродаж.Продажи;
				
		КонецЦикла; // по фильтру
		
		ИтогТаб.УстановитьЗначение(,"БазаРасчета",БазаРасчета_);
		
		//Расчет бонусов
		Если ИтогТаб.БазаРасчета <= ИтогТаб.ЛимитБазы Тогда
			СуммаБонусов_ = ИтогТаб.БазаРасчета * ИтогТаб.ПроцентРасчета * 0.01;
			ИтогТаб.УстановитьЗначение(,"СуммаБонусов",СуммаБонусов_);
		КонецЕсли;
		
	КонецЦикла; //По таблице Продаж
	
	ИтогТаб.Сортировать("Контрагент, Собственник, Объект, ПериодНачало");
	
	ИтогТаб.ВыбратьСтроку();
	
	
	ЗагрузитьТабличнуюЧасть(ИтогТаб);
	
КонецПроцедуры


Процедура ПодготовитьТаблицуПродаж(ТаблицаПродаж)
	
	ТаблицаПродаж.ВыбратьСтроки();
	
	рс = СоздатьОбъект("ODBCRecordset");
	
	Пока  ТаблицаПродаж.ПолучитьСтроку() = 1 Цикл
		
		ТекстЗапроса = 
		"SELECT Sum(ОборотыОбороты.ДоходОборот) СуммаДоходОборот
		|FROM $РегистрОбороты.Обороты(:НачДата,
		|		:КонДата~,,,	(Покупатель = :Контрагент),,
		|		Доход) AS ОборотыОбороты";
		рс.УстановитьТекстовыйПараметр("НачДата", ТаблицаПродаж.ПериодНачало);
		рс.УстановитьТекстовыйПараметр("КонДата", ТаблицаПродаж.ПериодОкончание);
		рс.УстановитьТекстовыйПараметр("Контрагент", ТаблицаПродаж.Контрагент);
		
		Продажи = рс.ВыполнитьСкалярный(ТекстЗапроса);		
		
		ТаблицаПродаж.УстановитьЗначение(,"Продажи",Продажи);
		
	КонецЦикла;
	
КонецПроцедуры //ПодготовитьТаблицуПродаж()

Процедура ПолучитьДочернииОрганизации()
	
КонецПроцедуры


