//==================Область объявления переменных==============
Перем СписокДействий;
Процедура ПодготовитьТаблицуПродаж(ТаблицаПродаж) Далее
Процедура ПолучитьДочернииОрганизации(ИтогТаб) Далее

// ============Предопределенные процедуры======================

Процедура ПриОткрытии()
	
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основное");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
	Форма.ИспользоватьСлой("Основной",2);
	
КонецПроцедуры

Процедура ПриВыбореЗакладки(Номер,Значение)
	
	ОтображаемыеСлои = Значение;           
	Форма.ИспользоватьСлой(ОтображаемыеСлои,2);	
	
КонецПроцедуры

// ============================================================
Процедура ВводНового(ПризнакКопирования)
	
	глУстановитьФирму(Контекст);
	
	НачалоПериода = НачМесяца(ДатаДок);
	ОкончаниеПериода = КонМесяца(ДатаДок);
	Счзатрат = СчетПоКоду("93");
	ВидЗатрат = Константа.ВидЗатратМаркетинг;
	Подразделение = Константа.БазПодразделениеПродажи;
	ВидДеятельности =  Константа.БазВидДеятельности;
	
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()	
	
	Если ТекущийДокумент().Выбран() = 0 Тогда
		глПроверкаДатыДок(Контекст,"Запись");
	КонецЕсли;
	
	Если глКонтрольДатыДокумента(Контекст, ДатаДок)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	Автор = глПользователь;															
	
КонецПроцедуры

//=============Служебные процедуры===============================
Процедура ЗаполнитьТабличнуюЧасть()
	
	Если ПустоеЗначение(НачалоПериода) = 1 Тогда
		Предупреждение("Не заполнено Начало периода");
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ОкончаниеПериода) = 1 Тогда
		Предупреждение("Не заполнено Окончание периода");
		Возврат;
	КонецЕсли;
	
	Если НачалоПериода > ОкончаниеПериода Тогда
		Предупреждение("Начало периода больше Окончания периода");
		Возврат;
	КонецЕсли;
	
	УдалитьСтроки();
	
	//Обходим даты периода
	
	Период = НачалоПериода;
	
	Состояние("Подготовка таблиц");
	//Предварительная таблица. Для расчета.
	ТабИндекс = СоздатьОбъект("ИндексированнаяТаблица");
	ТабИндекс.НоваяКолонка("Контрагент");
	ТабИндекс.НоваяКолонка("Объект");
	ТабИндекс.НоваяКолонка("СхемаРасчетаБонуса");
	ТабИндекс.НоваяКолонка("ЛимитБазыОт");
	ТабИндекс.НоваяКолонка("ЛимитБазыДо");
	ТабИндекс.НоваяКолонка("ПроцентРасчета");
	ТабИндекс.НоваяКолонка("ПериодНачало");
	ТабИндекс.НоваяКолонка("ПериодОкончание");
	
	ИтогТаб = СоздатьОбъект("ИндексированнаяТаблица");
	ИтогТаб.НоваяКолонка("Контрагент");
	ИтогТаб.НоваяКолонка("Владелец");
	ИтогТаб.НоваяКолонка("Объект");
	ИтогТаб.НоваяКолонка("СхемаРасчетаБонуса");
	ИтогТаб.НоваяКолонка("ПериодНачало");
	ИтогТаб.НоваяКолонка("ПериодОкончание");
	ИтогТаб.НоваяКолонка("ЛимитБазыОт");
	ИтогТаб.НоваяКолонка("ЛимитБазыДо");
	ИтогТаб.НоваяКолонка("БазаРасчета");
	ИтогТаб.НоваяКолонка("ПроцентРасчета");
	ИтогТаб.НоваяКолонка("СуммаБонусов");
	
	//Для загрузки в табл. часть документа
	ИтогТабДок = СоздатьОбъект("ТаблицаЗначений");
	ИтогТабДок.НоваяКолонка("Контрагент");
	ИтогТабДок.НоваяКолонка("Владелец");
	ИтогТабДок.НоваяКолонка("Объект");
	ИтогТабДок.НоваяКолонка("СхемаРасчетаБонуса");
	ИтогТабДок.НоваяКолонка("ПериодНачало");
	ИтогТабДок.НоваяКолонка("ПериодОкончание");
	ИтогТабДок.НоваяКолонка("ЛимитБазыОт");
	ИтогТабДок.НоваяКолонка("ЛимитБазыДо");
	ИтогТабДок.НоваяКолонка("БазаРасчета");
	ИтогТабДок.НоваяКолонка("ПроцентРасчета");
	ИтогТабДок.НоваяКолонка("СуммаБонусов");
	
	Пока Период <= ОкончаниеПериода Цикл
		
		рс = СоздатьОбъект("ODBCRecordset");
		
		ТекстЗапроса = "
		|SELECT Контрагенты.ID [Контрагент $Справочник.Контрагенты]
		|	, $СхемаРасчетаБонусаСтроки.Объект [Объект $Справочник]
		|	, $ПоследнееЗначение.Контрагенты.СхемаРасчетаБонуса(Контрагенты.ID, :ВыбДата) [СхемаРасчетаБонуса $Документ.СхемаРасчетаБонуса]
		|	, $СхемаРасчетаБонусаСтроки.ЛимитБазыОт ЛимитБазыОт
		|	, $СхемаРасчетаБонусаСтроки.ЛимитБазыДо ЛимитБазыДо
		|	, $СхемаРасчетаБонусаСтроки.Зн ПроцентРасчета
		|FROM $Справочник.Контрагенты AS Контрагенты
		|	INNER JOIN $ДокументСтроки.СхемаРасчетаБонуса AS СхемаРасчетаБонусаСтроки ON $ПоследнееЗначение.Контрагенты.СхемаРасчетаБонуса(Контрагенты.ID, :ВыбДата) = СхемаРасчетаБонусаСтроки.IDDOC
		|WHERE ($СхемаРасчетаБонусаСтроки.База = :БазаСхемы)
		|";
		рс.УстановитьТекстовыйПараметр("БазаСхемы", Перечисление.БазыДляРасчетаБонуса.РасчетЗаПериод);
		рс.УстановитьТекстовыйПараметр("ВыбДата", Период);
		
		
		//Добавим колонки в ТабИндекс
		Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);		
		
		Результат.ВыбратьСтроки();
		
		Пока Результат.ПолучитьСтроку() = 1 Цикл
			
			ТабИндекс.НоваяСтрока();
			ТабИндекс.Контрагент = Результат.Контрагент;
			ТабИндекс.Объект = Результат.Объект;
			ТабИндекс.СхемаРасчетаБонуса = Результат.СхемаРасчетаБонуса;
			ТабИндекс.ЛимитБазыОт = Результат.ЛимитБазыОт;
			ТабИндекс.ЛимитБазыДо = Результат.ЛимитБазыДо;
			ТабИндекс.ПроцентРасчета = Результат.ПроцентРасчета;
			ТабИндекс.ПериодНачало = Период;
			
		КонецЦикла; // По выборке
		
		Период = Период + 1;
		
	КонецЦикла; // По периодам
	
	ТабИндекс.ДобавитьИндекс("Ключ_индекс","Контрагент, СхемаРасчетаБонуса");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент, СхемаРасчетаБонуса");
	
	//Заполняем итоговую таблицу.
	ТабИндекс.ВыбратьСтроки();
	
	Пока ТабИндекс.ПолучитьСтроку() = 1  Цикл
		
		ДобавитьСтроку = 1;
		
		//Поиск уже добавленных строк
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ТабИндекс.Контрагент);
		Фильтр.ДобавитьЗначение(ТабИндекс.СхемаРасчетаБонуса);
		
		ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ИтогТаб.ВыбратьСтроки("Ключ_индекс");
		
		Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			ДобавитьСтроку = 0;	
			//Если найдена строка и она соотв. текущему периоду тогда добавляем 
			Если ИтогТаб.ПериодНачало = ТабИндекс.ПериодНачало Тогда
				ДобавитьСтроку = 1;
			КонецЕсли;
		КонецЦикла; // по фильтру
		
		Если ДобавитьСтроку = 1 Тогда
			
			ИтогТаб.НоваяСтрока();
			ИтогТаб.Контрагент = ТабИндекс.Контрагент;
			ИтогТаб.Объект = ТабИндекс.Объект;
			ИтогТаб.СхемаРасчетаБонуса = ТабИндекс.СхемаРасчетаБонуса;
			ИтогТаб.ЛимитБазыОт = ТабИндекс.ЛимитБазыОт;
			ИтогТаб.ЛимитБазыДо = ТабИндекс.ЛимитБазыДо;
			ИтогТаб.ПроцентРасчета = ТабИндекс.ПроцентРасчета;
			ИтогТаб.ПериодНачало = ТабИндекс.ПериодНачало;
			
		КонецЕсли;
		
	КонецЦикла; //По таблице индексу
	
	//Удалим индекс и добавим новый без схемы
	ИтогТаб.УдалитьИндекс("Ключ_индекс");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент");
	
	//Установим периоды окончания
	ИтогТаб.ВыбратьСтроки();
	
	Пока ИтогТаб.ПолучитьСтроку() = 1  Цикл
		
		ПериодНачало =  ИтогТаб.ПериодНачало;
		//Поиск уже добавленных строк
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ИтогТаб.Контрагент);
		
		ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ИтогТаб.ВыбратьСтроки("Ключ_индекс");
		
		Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			
			//Если найдена строка и она соотв. текущему периоду устанавливаем период окончания
			Если ПериодНачало > ИтогТаб.ПериодНачало Тогда
				ИтогТаб.УстановитьЗначение(,"ПериодОкончание",ПериодНачало-1); //изменена схема. Период вкючительно	
			Иначе
				ИтогТаб.УстановитьЗначение(,"ПериодОкончание",ОкончаниеПериода);
			КонецЕсли;
			
		КонецЦикла; // по фильтру
		
	КонецЦикла; //По таблице Итогов
	
	Состояние("Добаляем дочернии компании");
	//Добавим дочернии компании и заполним колонку Владелец
	ПолучитьДочернииОрганизации(ИтогТаб);
	
	//Заполним продажи
	ТаблицаПродаж = СоздатьОбъект("ИндексированнаяТаблица");
	
	ИтогТаб.Выгрузить(ТаблицаПродаж,,"Контрагент,ПериодНачало,ПериодОкончание",1);
	//Свернем по периодам контрагентам
	ТаблицаПродаж.НоваяКолонка("Продажи");
	ТаблицаПродаж.Свернуть("Контрагент,ПериодНачало,ПериодОкончание", "Продажи");
	ТаблицаПродаж.Сортировать("Контрагент, ПериодНачало");
	
	Состояние("Подготовка сумм продаж");
	ПодготовитьТаблицуПродаж(ТаблицаПродаж);
	
	ТаблицаПродаж.ДобавитьИндекс("Ключ_индекс","Контрагент, ПериодНачало, ПериодОкончание");
	
	//Заполним БазаРасчета(продажи) в итоговой таблице
	
	Состояние("Расчет бонусов");
	
	ИтогТаб.ВыбратьСтроки();
	
	Пока ИтогТаб.ПолучитьСтроку() = 1  Цикл
		пБазаРасчета = 0;
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ИтогТаб.Контрагент);
		Фильтр.ДобавитьЗначение(ИтогТаб.ПериодНачало);
		Фильтр.ДобавитьЗначение(ИтогТаб.ПериодОкончание);
		
		ТаблицаПродаж.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ТаблицаПродаж.ВыбратьСтроки("Ключ_индекс");
		
		Пока ТаблицаПродаж.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			
			пБазаРасчета = ТаблицаПродаж.Продажи;
			
		КонецЦикла; // по фильтру
		
		ИтогТаб.УстановитьЗначение(,"БазаРасчета",пБазаРасчета);
		
		//Расчет бонусов
		Если (ИтогТаб.БазаРасчета > ИтогТаб.ЛимитБазыОт) И (ИтогТаб.БазаРасчета <= ИтогТаб.ЛимитБазыДо) Тогда
			пСуммаБонусов = ИтогТаб.БазаРасчета * ИтогТаб.ПроцентРасчета * 0.01;
			ИтогТаб.УстановитьЗначение(,"СуммаБонусов",пСуммаБонусов);
		КонецЕсли;
		
	КонецЦикла; //По таблице Продаж
	
	ИтогТаб.Сортировать("Контрагент, Объект, ПериодНачало");
	ИтогТаб.Выгрузить(ИтогТабДок);
	
	ЗагрузитьТабличнуюЧасть(ИтогТабДок);
	
КонецПроцедуры

Процедура ПодготовитьТаблицуПродаж(ТаблицаПродаж)
	
	ТаблицаПродаж.ВыбратьСтроки();
	
	рс = СоздатьОбъект("ODBCRecordset");
	
	Пока  ТаблицаПродаж.ПолучитьСтроку() = 1 Цикл
		
		ТекстЗапроса = 
		"SELECT Sum(ОборотыОбороты.ДоходОборот) СуммаДоходОборот
		|FROM $РегистрОбороты.Обороты(:НачДата,
		|		:КонДата~,,,	(Покупатель = :Контрагент),,
		|		Доход) AS ОборотыОбороты";
		рс.УстановитьТекстовыйПараметр("НачДата", ТаблицаПродаж.ПериодНачало);
		рс.УстановитьТекстовыйПараметр("КонДата", ТаблицаПродаж.ПериодОкончание);
		рс.УстановитьТекстовыйПараметр("Контрагент", ТаблицаПродаж.Контрагент);
		
		Продажи = рс.ВыполнитьСкалярный(ТекстЗапроса);		
		
		ТаблицаПродаж.УстановитьЗначение(,"Продажи",Продажи);
		
	КонецЦикла;
	
КонецПроцедуры //ПодготовитьТаблицуПродаж()

Процедура ПолучитьДочернииОрганизации(ИтогТаб)
	
	Перем ТабВладельцы;
	
	//Таблица строк к добавлению
	ТабДобавитьСтроки = СоздатьОбъект("ИндексированнаяТаблица");
	ТабДобавитьСтроки.НоваяКолонка("Контрагент");
	ТабДобавитьСтроки.НоваяКолонка("Владелец");
	ТабДобавитьСтроки.НоваяКолонка("Объект");
	ТабДобавитьСтроки.НоваяКолонка("СхемаРасчетаБонуса");
	ТабДобавитьСтроки.НоваяКолонка("ПериодНачало");
	ТабДобавитьСтроки.НоваяКолонка("ПериодОкончание");
	ТабДобавитьСтроки.НоваяКолонка("ЛимитБазыОт");
	ТабДобавитьСтроки.НоваяКолонка("ЛимитБазыДо");
	ТабДобавитьСтроки.НоваяКолонка("БазаРасчета");
	ТабДобавитьСтроки.НоваяКолонка("ПроцентРасчета");
	ТабДобавитьСтроки.НоваяКолонка("СуммаБонусов");
	
	//Удалим индекс и добавим новый со схемой
	ИтогТаб.УдалитьИндекс("Ключ_индекс");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент, СхемаРасчетаБонуса");
	
	ИтогТаб.ВыбратьСтроки();
	
	рс = СоздатьОбъект("ODBCRecordset");
	
	Пока  ИтогТаб.ПолучитьСтроку() = 1 Цикл
		
		пВладелец = ИтогТаб.Контрагент;
		
		пОбъект = ИтогТаб.Объект;
		пСхемаРасчетаБонуса = ИтогТаб.СхемаРасчетаБонуса;
		пЛимитБазыОт = ИтогТаб.ЛимитБазыОт;
		пЛимитБазыДо = ИтогТаб.ЛимитБазыДо;
		пПроцентРасчета = ИтогТаб.ПроцентРасчета;
		пПериодНачало = ИтогТаб.ПериодНачало;	
		пПериодОкончание = ИтогТаб.ПериодОкончание;
		
		//Запрос дочерних организаций по владельцу	
		ТекстЗапроса = 
		"SELECT Контрагенты.ID [Контрагент $Справочник.Контрагенты]
		|, $Контрагенты.Хозяин [Владелец $Справочник]
		|FROM $Справочник.Контрагенты AS Контрагенты
		|WHERE ($Контрагенты.Хозяин = :Владелец~)";
		
		рс.УстановитьТекстовыйПараметр("Владелец", пВладелец);
		
		ТабВладельцы = рс.ВыполнитьИнструкцию(ТекстЗапроса);	
		
		ТабВладельцы.ВыбратьСтроки();
		
		Пока  ТабВладельцы.ПолучитьСтроку() = 1 Цикл
			
			//Поиск уже добавленных контрагентов-дочерних владельцу
			//С такой же схемой
			//Список-фильтр
			Фильтр = СоздатьОбъект("СписокЗначений");
			Фильтр.ДобавитьЗначение(ТабВладельцы.Контрагент);
			Фильтр.ДобавитьЗначение(пСхемаРасчетаБонуса);
			
			ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
			ИтогТаб.ВыбратьСтроки("Ключ_индекс");
			
			СтрокаНеНайдена = 1;
			
			Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
				
				СтрокаНеНайдена = 0;
				
				//Самого себя не добавляем
				Если ИтогТаб.Контрагент = пВладелец Тогда
					Продолжить;
				КонецЕсли;
				
				//Если строка найдена, то устанавливаем значение колонки Владелец
				ИтогТаб.УстановитьЗначение(,"Владелец", пВладелец);
				
			КонецЦикла; // по фильтру
			
			//Строка не найдена, добавляем дочерние организации в таблицу итоги
			Если СтрокаНеНайдена = 1 Тогда
				ТабДобавитьСтроки.НоваяСтрока();
				ТабДобавитьСтроки.Контрагент = ТабВладельцы.Контрагент;
				ТабДобавитьСтроки.Владелец = пВладелец;
				ТабДобавитьСтроки.Объект = пОбъект;
				ТабДобавитьСтроки.СхемаРасчетаБонуса = пСхемаРасчетаБонуса;
				ТабДобавитьСтроки.ЛимитБазыОт = пЛимитБазыОт;
				ТабДобавитьСтроки.ЛимитБазыДо = пЛимитБазыДо;
				ТабДобавитьСтроки.ПроцентРасчета = пПроцентРасчета;
				ТабДобавитьСтроки.ПериодНачало = пПериодНачало;	
				ТабДобавитьСтроки.ПериодОкончание = пПериодОкончание;
			КонецЕсли;
			
		КонецЦикла; //По ТабВладельцы
		
	КонецЦикла; //По ИтогТаб
	
	//Добавляем строки в ИтогТаб
	ТабДобавитьСтроки.ВыбратьСтроки();
	
	Пока  ТабДобавитьСтроки.ПолучитьСтроку() = 1  Цикл
		ИтогТаб.НоваяСтрока();
		ИтогТаб.Контрагент = ТабДобавитьСтроки.Контрагент;
		ИтогТаб.Владелец = ТабДобавитьСтроки.Владелец;
		ИтогТаб.Объект = ТабДобавитьСтроки.Объект;
		ИтогТаб.СхемаРасчетаБонуса = ТабДобавитьСтроки.СхемаРасчетаБонуса;
		ИтогТаб.ЛимитБазыОт = ТабДобавитьСтроки.ЛимитБазыОт;
		ИтогТаб.ЛимитБазыДо = ТабДобавитьСтроки.ЛимитБазыДо;
		ИтогТаб.ПроцентРасчета = ТабДобавитьСтроки.ПроцентРасчета;
		ИтогТаб.ПериодНачало = ТабДобавитьСтроки.ПериодНачало;	
		ИтогТаб.ПериодОкончание = ТабДобавитьСтроки.ПериодОкончание;
	КонецЦикла;
		
КонецПроцедуры

Процедура УдалитьПустыеСтроки()
	
	ТекСтрока = КоличествоСтрок();

	Пока ТекСтрока <> 0 Цикл
		ПолучитьСтрокуПоНомеру(ТекСтрока);
		Если СуммаБонусов = 0 Тогда
			УдалитьСтроку();	
		КонецЕсли;
		ТекСтрока = ТекСтрока - 1;		
	КонецЦикла;	
	
КонецПроцедуры

СписокДействий = глПолучитьСписокДействий("
			|СтруктураПодчиненности,
			|ДвиженияДокумента,
			|ВводНаОсновании,
			|ОткрытьВЖурнале,
			|Подчиненные");	  