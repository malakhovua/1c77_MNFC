//==================Область объявления переменных==============
Перем СписокДействий;
Процедура ПодготовитьТаблицуПродаж(ТаблицаПродаж) Далее
Функция ПолучитьДочернииОрганизации(пВладелец) Далее

// ============Предопределенные процедуры======================

Процедура ПриОткрытии()
	
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основное");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
	Форма.ИспользоватьСлой("Основной",2);
	
КонецПроцедуры

Процедура ПриВыбореЗакладки(Номер,Значение)
	
	ОтображаемыеСлои = Значение;           
	Форма.ИспользоватьСлой(ОтображаемыеСлои,2);	
	
КонецПроцедуры

// ============================================================
Процедура ВводНового(ПризнакКопирования)
	
	глУстановитьФирму(Контекст);
	
	НачалоПериода = НачМесяца(ДатаДок);
	ОкончаниеПериода = КонМесяца(ДатаДок);
	Счзатрат = СчетПоКоду("93");
	ВидЗатрат = Константа.ВидЗатратМаркетинг;
	Подразделение = Константа.БазПодразделениеПродажи;
	ВидДеятельности =  Константа.БазВидДеятельности;
	
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()	
	
	Если ТекущийДокумент().Выбран() = 0 Тогда
		глПроверкаДатыДок(Контекст,"Запись");
	КонецЕсли;
	
	Если глКонтрольДатыДокумента(Контекст, ДатаДок)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	Автор = глПользователь;															
	
КонецПроцедуры

//=============Служебные процедуры===============================
Процедура ЗаполнитьТабличнуюЧасть()
	
	Если ПустоеЗначение(НачалоПериода) = 1 Тогда
		Предупреждение("Не заполнено Начало периода");
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ОкончаниеПериода) = 1 Тогда
		Предупреждение("Не заполнено Окончание периода");
		Возврат;
	КонецЕсли;
	
	Если НачалоПериода > ОкончаниеПериода Тогда
		Предупреждение("Начало периода больше Окончания периода");
		Возврат;
	КонецЕсли;
	
	УдалитьСтроки();
	
	//Обходим даты периода
	
	Период = НачалоПериода;
	
	Состояние("Подготовка таблиц");
	//Предварительная таблица. Для расчета.
	ТабИндекс = СоздатьОбъект("ИндексированнаяТаблица");
	ТабИндекс.НоваяКолонка("Контрагент");
	ТабИндекс.НоваяКолонка("Объект");
	ТабИндекс.НоваяКолонка("СхемаРасчетаБонуса");
	ТабИндекс.НоваяКолонка("ЛимитБазыОт");
	ТабИндекс.НоваяКолонка("ЛимитБазыДо");
	ТабИндекс.НоваяКолонка("ПроцентРасчета");
	ТабИндекс.НоваяКолонка("ПериодНачало");
	ТабИндекс.НоваяКолонка("ПериодОкончание");
	
	ИтогТаб = СоздатьОбъект("ИндексированнаяТаблица");
	ИтогТаб.НоваяКолонка("Контрагент");
	ИтогТаб.НоваяКолонка("Владелец");
	ИтогТаб.НоваяКолонка("Объект");
	ИтогТаб.НоваяКолонка("СхемаРасчетаБонуса");
	ИтогТаб.НоваяКолонка("ПериодНачало");
	ИтогТаб.НоваяКолонка("ПериодОкончание");
	ИтогТаб.НоваяКолонка("ЛимитБазыОт");
	ИтогТаб.НоваяКолонка("ЛимитБазыДо");
	ИтогТаб.НоваяКолонка("БазаРасчета");
	ИтогТаб.НоваяКолонка("ПроцентРасчета");
	ИтогТаб.НоваяКолонка("СуммаБонусов");
	
	//Для загрузки в табл. часть документа
	ИтогТабДок = СоздатьОбъект("ТаблицаЗначений");
	ИтогТабДок.НоваяКолонка("Контрагент");
	ИтогТабДок.НоваяКолонка("Владелец");
	ИтогТабДок.НоваяКолонка("Объект");
	ИтогТабДок.НоваяКолонка("СхемаРасчетаБонуса");
	ИтогТабДок.НоваяКолонка("ПериодНачало");
	ИтогТабДок.НоваяКолонка("ПериодОкончание");
	ИтогТабДок.НоваяКолонка("ЛимитБазыОт");
	ИтогТабДок.НоваяКолонка("ЛимитБазыДо");
	ИтогТабДок.НоваяКолонка("БазаРасчета");
	ИтогТабДок.НоваяКолонка("ПроцентРасчета");
	ИтогТабДок.НоваяКолонка("СуммаБонусов");
	
	Пока Период <= ОкончаниеПериода Цикл
		
		рс = СоздатьОбъект("ODBCRecordset");
		
		ТекстЗапроса = "
		|SELECT Контрагенты.ID [Контрагент $Справочник.Контрагенты]
		|	, $СхемаРасчетаБонусаСтроки.Объект [Объект $Справочник]
		|	, $ПоследнееЗначение.Контрагенты.СхемаРасчетаБонуса(Контрагенты.ID, :ВыбДата) [СхемаРасчетаБонуса $Документ.СхемаРасчетаБонуса]
		|	, $СхемаРасчетаБонусаСтроки.ЛимитБазыОт ЛимитБазыОт
		|	, $СхемаРасчетаБонусаСтроки.ЛимитБазыДо ЛимитБазыДо
		|	, $СхемаРасчетаБонусаСтроки.Зн ПроцентРасчета
		|FROM $Справочник.Контрагенты AS Контрагенты
		|	INNER JOIN $ДокументСтроки.СхемаРасчетаБонуса AS СхемаРасчетаБонусаСтроки ON $ПоследнееЗначение.Контрагенты.СхемаРасчетаБонуса(Контрагенты.ID, :ВыбДата) = СхемаРасчетаБонусаСтроки.IDDOC
		|WHERE ($СхемаРасчетаБонусаСтроки.База = :БазаСхемы)
		|";
		рс.УстановитьТекстовыйПараметр("БазаСхемы", Перечисление.БазыДляРасчетаБонуса.РасчетЗаПериод);
		рс.УстановитьТекстовыйПараметр("ВыбДата", Период);
		
		
		//Добавим колонки в ТабИндекс
		Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);		
		
		Результат.ВыбратьСтроки();
		
		Пока Результат.ПолучитьСтроку() = 1 Цикл
			
			ТабИндекс.НоваяСтрока();
			ТабИндекс.Контрагент = Результат.Контрагент;
			ТабИндекс.Объект = Результат.Объект;
			ТабИндекс.СхемаРасчетаБонуса = Результат.СхемаРасчетаБонуса;
			ТабИндекс.ЛимитБазыОт = Результат.ЛимитБазыОт;
			ТабИндекс.ЛимитБазыДо = Результат.ЛимитБазыДо;
			ТабИндекс.ПроцентРасчета = Результат.ПроцентРасчета;
			ТабИндекс.ПериодНачало = Период;
			
		КонецЦикла; // По выборке
		
		Период = Период + 1;
		
	КонецЦикла; // По периодам
	
	ТабИндекс.ДобавитьИндекс("Ключ_индекс","Контрагент, СхемаРасчетаБонуса");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент, СхемаРасчетаБонуса");
	
	//Заполняем итоговую таблицу.
	ТабИндекс.ВыбратьСтроки();
	
	Пока ТабИндекс.ПолучитьСтроку() = 1  Цикл
		
		ДобавитьСтроку = 1;
		
		//Поиск уже добавленных строк
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ТабИндекс.Контрагент);
		Фильтр.ДобавитьЗначение(ТабИндекс.СхемаРасчетаБонуса);
		
		ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ИтогТаб.ВыбратьСтроки("Ключ_индекс");
		
		Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			ДобавитьСтроку = 0;	
			//Если для контрагента меняется схема, то мы не попадает в условия фильтра,
			//соотв. строка добавляется, последующие строки для этого периода и контрагента 
			//будут добавлены по условиям фильтра.
			Если ИтогТаб.ПериодНачало = ТабИндекс.ПериодНачало Тогда
				ДобавитьСтроку = 1;
			КонецЕсли;
		КонецЦикла; // по фильтру
		
		Если ДобавитьСтроку = 1 Тогда
			
			ИтогТаб.НоваяСтрока();
			ИтогТаб.Контрагент = ТабИндекс.Контрагент;
			ИтогТаб.Владелец = ТабИндекс.Контрагент.Хозяин;
			ИтогТаб.Объект = ТабИндекс.Объект;
			ИтогТаб.СхемаРасчетаБонуса = ТабИндекс.СхемаРасчетаБонуса;
			ИтогТаб.ЛимитБазыОт = ТабИндекс.ЛимитБазыОт;
			ИтогТаб.ЛимитБазыДо = ТабИндекс.ЛимитБазыДо;
			ИтогТаб.ПроцентРасчета = ТабИндекс.ПроцентРасчета;
			ИтогТаб.ПериодНачало = ТабИндекс.ПериодНачало;
			
		КонецЕсли;
		
	КонецЦикла; //По таблице индексу
	
	//Удалим индекс и добавим новый без схемы
	ИтогТаб.УдалитьИндекс("Ключ_индекс");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент");
	
	//Установим периоды окончания
	ИтогТаб.ВыбратьСтроки();
	
	Пока ИтогТаб.ПолучитьСтроку() = 1  Цикл
		
		пПериодНачало =  ИтогТаб.ПериодНачало;
		//Поиск уже добавленных строк
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ИтогТаб.Контрагент);
		
		ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ИтогТаб.ВыбратьСтроки("Ключ_индекс");
		
		Пока ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			//Ищем строки по отбору-контрагент, где изменился период и соотв. Схема
			//Если найдена строка и она соотв. условиям текущего периода, то устанавливаем период окончания
			Если пПериодНачало > ИтогТаб.ПериодНачало Тогда
				ИтогТаб.УстановитьЗначение(,"ПериодОкончание",пПериодНачало-1); //Изменена схема. Период - предыдущий день вкючительно.		
			Иначе
				ИтогТаб.УстановитьЗначение(,"ПериодОкончание",ОкончаниеПериода);
			КонецЕсли;
			
		КонецЦикла; // по фильтру
		
	КонецЦикла; //По таблице Итогов
	
	//Заполним продажи
	ТаблицаПродаж = СоздатьОбъект("ИндексированнаяТаблица");
	
	ИтогТаб.Выгрузить(ТаблицаПродаж,,"Контрагент,Владелец,ПериодНачало,ПериодОкончание",1);
	
	//Свернем по периодам контрагентам
	ТаблицаПродаж.НоваяКолонка("Продажи");
	ТаблицаПродаж.Свернуть("Контрагент,Владелец,ПериодНачало,ПериодОкончание", "Продажи");
	ТаблицаПродаж.Сортировать("Контрагент, ПериодНачало");
	
	Состояние("Подготовка сумм продаж");
	
	ПодготовитьТаблицуПродаж(ТаблицаПродаж);
	
	ТаблицаПродаж.ДобавитьИндекс("Ключ_индекс","Контрагент, ПериодНачало, ПериодОкончание");
	
	//Заполним БазаРасчета(продажи) в итоговой таблице
	
	Состояние("Расчет бонусов");
	
	ИтогТаб.ВыбратьСтроки();
	
	Пока ИтогТаб.ПолучитьСтроку() = 1  Цикл
		пБазаРасчета = 0;
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(ИтогТаб.Контрагент);
		Фильтр.ДобавитьЗначение(ИтогТаб.ПериодНачало);
		Фильтр.ДобавитьЗначение(ИтогТаб.ПериодОкончание);
		
		ТаблицаПродаж.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ТаблицаПродаж.ВыбратьСтроки("Ключ_индекс");
		
		Пока ТаблицаПродаж.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			
			пБазаРасчета = ТаблицаПродаж.Продажи;
			
		КонецЦикла; // по фильтру
		
		ИтогТаб.УстановитьЗначение(,"БазаРасчета",пБазаРасчета);
		
		//Расчет бонусов
		Если (ИтогТаб.БазаРасчета > ИтогТаб.ЛимитБазыОт) И (ИтогТаб.БазаРасчета <= ИтогТаб.ЛимитБазыДо) Тогда
			пСуммаБонусов = ИтогТаб.БазаРасчета * ИтогТаб.ПроцентРасчета * 0.01;
			ИтогТаб.УстановитьЗначение(,"СуммаБонусов",пСуммаБонусов);
		КонецЕсли;
		
	КонецЦикла; //По таблице Продаж
	
	ИтогТаб.Сортировать("Владелец, Контрагент, Объект, ПериодНачало");
	
	
	ИтогТаб.Выгрузить(ИтогТабДок);
	
	//Теперь необходимо удалить из списка тех контрагентов которые имеют владельца, в том случае 
	//если владелец уже добавлен в ИтогТаб как контрагент, т.к. он уже содержит суммарные
	//показатели по всем дочерним контрагентам.
	
	//Удалим индекс и добавим новый со схемой
	ИтогТаб.УдалитьИндекс("Ключ_индекс");
	ИтогТаб.ДобавитьИндекс("Ключ_индекс","Контрагент, СхемаРасчетаБонуса");
	
	ИтогТабДок.ВыбратьСтроки();
	
	Пока ИтогТабДок.ПолучитьСтроку() = 1  Цикл
			
		ДобавитьСтроку = 1;
		
		Если (ПустоеЗначение(ИтогТабДок.Владелец) = 0) И (ИтогТабДок.Владелец <> ИтогТабДок.Контрагент) Тогда
		
			//Пробуем найти Кнтрагента = Владельцу с такой же схемой.
			Фильтр = СоздатьОбъект("СписокЗначений");
			Фильтр.ДобавитьЗначение(ИтогТабДок.Владелец); //Контрагент
			Фильтр.ДобавитьЗначение(ИтогТабДок.СхемаРасчетаБонуса);
			
			ИтогТаб.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
			ИтогТаб.ВыбратьСтроки("Ключ_индекс");
			
			Если ИтогТаб.ПолучитьСтроку("Ключ_индекс") = 1 Тогда
				ДобавитьСтроку = 0;
			КонецЕсли; // по фильтру
			
			ИтогТаб.ВыключитьФильтр("Ключ_индекс");
			
		КонецЕсли;
		
		Если ДобавитьСтроку = 1 Тогда
			
			НоваяСтрока();
			Контрагент = ИтогТабДок.Контрагент;
			Владелец = ИтогТабДок.Владелец;
			Объект = ИтогТабДок.Объект;
			СхемаРасчетаБонуса = ИтогТабДок.СхемаРасчетаБонуса;
			ПериодНачало = ИтогТабДок.ПериодНачало;
			ПериодОкончание = ИтогТабДок.ПериодОкончание;
			ЛимитБазыОт = ИтогТабДок.ЛимитБазыОт;
			ЛимитБазыДо = ИтогТабДок.ЛимитБазыДо;
			БазаРасчета  = ИтогТабДок.БазаРасчета;
			ПроцентРасчета = ИтогТабДок.ПроцентРасчета;
			СуммаБонусов  = ИтогТабДок.СуммаБонусов;
			
		КонецЕсли;
		
	КонецЦикла; //По таблице Итогов
		
КонецПроцедуры

Процедура ПодготовитьТаблицуПродаж(ТаблицаПродаж)
	
	ТаблицаПродаж.ВыбратьСтроки();
	
	рс = СоздатьОбъект("ODBCRecordset");
	
	Пока  ТаблицаПродаж.ПолучитьСтроку() = 1 Цикл
		
		ДочернииКомпании = ПолучитьДочернииОрганизации(ТаблицаПродаж.Контрагент);
		
		ТекстЗапроса = 
		"SELECT Sum(ВзаиморасчетыПокупателей.ДолгОснПриход) СуммаДолгОснПриход
		|FROM $РегистрОбороты.ВзаиморасчетыПокупателей(:НачДата,
		|		:КонДата~,,,	(" + ?(ДочернииКомпании.РазмерСписка()>0," Контрагент IN (SELECT Val FROM #ДочернииКомпании)","Контрагент = :Контрагент") +"),,
		|		ДолгОсн) AS ВзаиморасчетыПокупателей";
		рс.УстановитьТекстовыйПараметр("НачДата", ТаблицаПродаж.ПериодНачало);
		рс.УстановитьТекстовыйПараметр("КонДата", ТаблицаПродаж.ПериодОкончание);
		рс.УстановитьТекстовыйПараметр("Контрагент", ТаблицаПродаж.Контрагент);
		
		Если ДочернииКомпании.РазмерСписка()>0 Тогда
			рс.УложитьСписокОбъектов(ДочернииКомпании, "#ДочернииКомпании","Контрагенты");
		КонецЕсли;
		
		Продажи = рс.ВыполнитьСкалярный(ТекстЗапроса);		
		
		ТаблицаПродаж.УстановитьЗначение(,"Продажи",Продажи);
		
	КонецЦикла;
	
КонецПроцедуры //ПодготовитьТаблицуПродаж()

Функция ПолучитьДочернииОрганизации(пВладелец)
		
	рс = СоздатьОбъект("ODBCRecordset");
	
	ДочернииКомпании = СоздатьОбъект("СписокЗначений");
	
	//Запрос дочерних организаций по владельцу	
	ТекстЗапроса = 
	"SELECT Контрагенты.ID [Контрагент $Справочник.Контрагенты]
	|, $Контрагенты.Хозяин [Владелец $Справочник]
	|FROM $Справочник.Контрагенты AS Контрагенты
	|WHERE ($Контрагенты.Хозяин = :Владелец~)";
	
	рс.УстановитьТекстовыйПараметр("Владелец", пВладелец);
	
	ТабВладельцы = рс.ВыполнитьИнструкцию(ТекстЗапроса);

    ТабВладельцы.Выгрузить(ДочернииКомпании,,,"Контрагент");

	Если ДочернииКомпании.РазмерСписка() >  0 Тогда //Добавим в список самого Владельца
		ДочернииКомпании.ДобавитьЗначение(пВладелец);
	КонецЕсли;
	
	ТабВладельцы.ВыбратьСтроки();
	
	Возврат ДочернииКомпании;
		
КонецФункции

Процедура УдалитьПустыеСтроки()
	
	ТекСтрока = КоличествоСтрок();

	Пока ТекСтрока <> 0 Цикл
		ПолучитьСтрокуПоНомеру(ТекСтрока);
		Если СуммаБонусов = 0 Тогда
			УдалитьСтроку();	
		КонецЕсли;
		ТекСтрока = ТекСтрока - 1;		
	КонецЦикла;	
	
КонецПроцедуры

СписокДействий = глПолучитьСписокДействий("
			|СтруктураПодчиненности,
			|ДвиженияДокумента,
			|ВводНаОсновании,
			|ОткрытьВЖурнале,
			|Подчиненные");	  