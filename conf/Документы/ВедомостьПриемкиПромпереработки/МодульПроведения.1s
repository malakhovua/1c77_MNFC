Перем ТекСклад;

// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");  
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
	Если ПустоеЗначение(ТекСклад) = 1 Тогда
		ТекСклад = Перемещение.НовоеМестоХранения;
	ИначеЕсли ТекСклад <> Перемещение.НовоеМестоХранения Тогда
		Сообщить("В строке: " + Строка(НомерСтроки) + " Новый склад отличается от остальных в этой ведомости");	
	КонецЕсли;
    глВсеВыбрано = 1;
	глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);
	Возврат глВсеВыбрано;
КонецФункции

//======================================================================
Процедура ДобавитьСтрокуТЗ(ТЗСп, ТЗ, Разница)
	ТЗСп.НоваяСтрока();
	ТЗСп.ТМЦ = ТЗ.ТМЦ;
	ТЗСп.Склад = ТекСклад;
	ТЗСп.КвоСписания = Разница;
	ТЗСп.Коэффициент = ТЗ.ТМЦ.ЕдиницаПоУмолчанию.Коэффициент;
	ТЗСп.Ед = ТЗ.ТМЦ.ЕдиницаПоУмолчанию;
	ТЗСп.ДатаСписания = ДатаДок;
	ТЗСп.МестоХранения = ТекСклад;
КонецПроцедуры //

//======================================================================
Процедура СоздатьДокументы()
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТЗ);
	ТЗ.НоваяКолонка("Склад");
	ТЗ.Свернуть("ТМЦ", "Кво,Принято");

	ТЗСп = СоздатьОбъект("ТаблицаЗначений");
	ТЗСп.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗСп.НоваяКолонка("Склад", "Справочник.МестаХранения");
	ТЗСп.НоваяКолонка("КвоСписания", "Число", 15, 3);
	ТЗСп.НоваяКолонка("ДопКвоСписания", "Число", 15, 3);
	ТЗСп.НоваяКолонка("Коэффициент", "Число", 15, 3);
	ТЗСп.НоваяКолонка("Ед", "Справочник.Единицы");
	ТЗСп.НоваяКолонка("ДатаСписания", "Дата");
	ТЗСп.НоваяКолонка("МестоХранения", "Справочник.МестаХранения");	

	ТЗОприходование = СоздатьОбъект("ТаблицаЗначений");	
	ТЗСп.Выгрузить(ТЗОприходование);
	
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Разница = ТЗ.Принято - ТЗ.Кво;
		Если Разница < 0 Тогда
			ДобавитьСтрокуТЗ(ТЗСп, ТЗ, -Разница);
		ИначеЕсли Разница > 0 Тогда
			ДобавитьСтрокуТЗ(ТЗОприходование, ТЗ, Разница);
		КонецЕсли;
	КонецЦикла;
	
	глСформироватьСписаниеПоДатам(ТекущийДокумент(), ТЗСп, Константа.БазПодразделениеПроизводство, 1);
	глСформироватьОприходованиеПоДатам(ТекущийДокумент(), ТЗОприходование, Константа.БазПодразделениеПроизводство, 1);
КонецПроцедуры // СоздатьДокументы()

Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);
	ТекСклад = ПолучитьПустоеЗначение("Справочник.МестаХранения");
	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст); 
		Возврат;
	КонецЕсли;     
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
	КонецЦикла;         

	СоздатьДокументы();	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры

//======================================================================
Процедура ОбработкаУдаленияПроведения()
	НачатьТранзакцию();
	Док = СоздатьОбъект("Документ");
	СписДок = СоздатьОбъект("СписокЗначений");
	Док.ВыбратьПодчиненныеДокументы(,, ТекущийДокумент());
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если ((Док.Вид() = "СписаниеТМЦ") ИЛИ (Док.Вид() = "ОприходованиеИзлишков")) И (Док.Проведен() = 1) Тогда
			СписДок.ДобавитьЗначение(Док.ТекущийДокумент());
		КонецЕсли;
	КонецЦикла;
			
	Для Инд = 1 По СписДок.РазмерСписка() Цикл
		Док.НайтиДокумент(СписДок.ПолучитьЗначение(Инд));
		Док.СделатьНеПроведенным();					
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
КонецПроцедуры // ОбработкаУдаления