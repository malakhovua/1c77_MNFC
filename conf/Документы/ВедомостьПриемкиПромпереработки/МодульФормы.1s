Перем СтараяДата;
Перем НачальнаяДатаДокумента;
Перем СписокДействий;

//======================================================================
Процедура ОбновитьТЗПриемки(Пересчитать = 0, Перерисовать = 1)
	ТП = СоздатьОбъект("ТаблицаЗначений");	
	ТЗПриемки.Выгрузить(ТП);
	
	Если Перерисовать = 1 Тогда
		ТЗПриемки.УдалитьСтроки();
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ТЗПриемки.НоваяСтрока();
			ТЗПриемки.ТМЦ = ТМЦ;
			ТЗПриемки.Принято = Принято;
			ТЗПриемки.Кво = Кво;
			ТЗПриемки.Разница = ТЗПриемки.Принято - ТЗПриемки.Кво;			
		КонецЦикла;
		ТЗПриемки.Свернуть("ТМЦ", "Принято,Кво,Разница,Процент");
		ТЗПриемки.ВыбратьСтроки();
		Пока ТЗПриемки.ПолучитьСтроку() = 1 Цикл
			ТЗПриемки.Процент = ТЗПриемки.Разница / ?(ТЗПриемки.Кво = 0, 1, ТЗПриемки.Кво) * 100;
		КонецЦикла;
		ТЗПриемки.Сортировать("ТМЦ");
	КонецЕсли;
	
	Если Пересчитать = 1 Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Принято = 0;
		КонецЦикла;		
		
		ТП.ВыбратьСтроки();
		Пока ТП.ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			Если ТЗПриемки.НайтиЗначение(ТП.ТМЦ, Стр, "ТМЦ") = 1 Тогда
				ТЗПриемки.УстановитьЗначение(Стр, "Принято", ТП.Принято);
				ТЗПриемки.УстановитьЗначение(Стр, "Разница", ТЗПриемки.ПолучитьЗначение(Стр, "Принято") - ТЗПриемки.ПолучитьЗначение(Стр, "Кво"));
				ТЗПриемки.УстановитьЗначение(Стр, "Процент", ТЗПриемки.ПолучитьЗначение(Стр, "Разница") / ?(ТЗПриемки.ПолучитьЗначение(Стр, "Кво") = 0, 1, ТЗПриемки.ПолучитьЗначение(Стр, "Кво")) * 100);
				ВыбратьСтроки();
				Пока ПолучитьСтроку() = 1 Цикл
					Если ТМЦ = ТП.ТМЦ Тогда
						Принято = ТП.Принято;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры // 

// ===============================
Процедура ВводНового(Скопирован) //Предопределенная процедура
	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;         	
КонецПроцедуры

Процедура ПриОткрытии()	
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;                                                                                     	
	ПриЗаписиПерепроводить(1);
	СтараяДата = ДатаДок;
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	
	ОбновитьТЗПриемки();
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
		Форма.кПодбор.Доступность(0);
		Форма.кЗаполнить.Доступность(0);
		
		Форма.кФирма.Доступность(0);               		
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	
КонецПроцедуры

// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи() //Предопределенная процедура
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;             
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ПриУдаленииСтроки()
	СтатусВозврата(0);
	УдалитьСтроку();
	ОбновитьТЗПриемки(1);
КонецПроцедуры // 

Процедура ПриОкончанииРедактированияСтроки()
	ОбновитьТЗПриемки(1);
КонецПроцедуры

//======================================================================
Процедура ИзмТЗ()
	ТекКол = ТЗПриемки.ТекущаяКолонка();
	ТекСтр = ТЗПриемки.ТекущаяСтрока();
	Если (ТекСтр <> 0) И (ТекКол = "Принято") Тогда
		ТПринято = ТЗПриемки.ПолучитьЗначение(ТекСтр, "Принято");
		Если ВвестиЧисло(ТПринято, "Введите вес принятого", 15, 2) = 1 Тогда
			ТЗПриемки.УстановитьЗначение(ТекСтр, "Принято", ТПринято);
			ТЗПриемки.УстановитьЗначение(ТекСтр, "Разница", ТПринято - ТЗПриемки.ПолучитьЗначение(ТекСтр, "Кво"));
			ОбновитьТЗПриемки(1, 0);
			//ТП = СоздатьОбъект("ТаблицаЗначений");
			//ТЗПриемки.Выгрузить(ТП);
			//
			//ВыбратьСтроки();
			//Пока ПолучитьСтроку() = 1 Цикл
			//	Принято = 0; Стр = 0;
			//	Если ТП.НайтиЗначение(ТМЦ, Стр, "ТМЦ") = 1 Тогда
			//		Принято = ТП.ПолучитьЗначение(Стр, "Принято");
			//		ТП.УдалитьСтроку(Стр);
			//	КонецЕсли;
			//КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ИзмТЗ

//======================================================================
Процедура ОбработкаПодбора(Элт, КФормы)
	ЕстьУже = 0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Перемещение = Элт Тогда
			ЕстьУже = 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	
	Если ЕстьУже = 1 Тогда
		Если Вопрос("Это перемещение уже есть в табличной части. Обновить данные из него?", "Да+Нет") = "Да" Тогда			
			ВыгрузитьТабличнуюЧасть(ТЗ);
			
			Инд = 1;
			Пока Инд <= ТЗ.КоличествоСтрок() Цикл
				ТЗ.ПолучитьСтрокуПоНомеру(Инд);
				Если ТЗ.Перемещение = Элт Тогда
					ТЗ.УдалитьСтроку(Инд);
				Иначе
					Инд = Инд + 1;
				КонецЕсли;
			КонецЦикла;
			ЗагрузитьТабличнуюЧасть(ТЗ);
		Иначе
			Возврат;			
		КонецЕсли;
	КонецЕсли;
	
	Элт.ВыбратьСтроки();
	Пока Элт.ПолучитьСтроку() = 1 Цикл
		Если Элт.ТМЦ <> Элт.Стало Тогда
			НоваяСтрока();
			Перемещение = Элт;
			ТМЦ = Элт.Стало;
			Кво = Элт.Кво;
		КонецЕсли;
	КонецЦикла;	
	
	ВыгрузитьТабличнуюЧасть(ТЗ);
	ТЗ.Свернуть("Перемещение, ТМЦ", "Кво, Принято");
	ЗагрузитьТабличнуюЧасть(ТЗ);
	ОбновитьТЗПриемки(1, 1);
КонецПроцедуры // ОбработкаПодбора

//======================================================================
Процедура Подбор()
	ОткрытьПодбор("Журнал.Перемещения", "ФормаСписка",, 1);
КонецПроцедуры // Подбор()

//======================================================================
Процедура Заполнить()
	НДата = ДатаДок - 7;
	КДата = ДатаДок;
	Если ВвестиПериод(НДата, КДата, "Введите период для заполнения") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если КоличествоСтрок() > 0 Тогда
		Отв = Вопрос("Удалить строки", "Да+Нет+Отмена");
		Если Отв = "Да" Тогда
			УдалитьСтроки();
		ИначеЕсли Отв = "Отмена" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТЗ);
	ТЗЧ = СоздатьОбъект("ТаблицаЗначений");
	
	Док = СоздатьОбъект("Документ.Перемещение");
	Док.УстановитьФильтр(1, 0);
	Док.ВыбратьДокументы(НДата, КДата);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Стр = 0;
		Если ТЗ.НайтиЗначение(Док.ТекущийДокумент(), Стр, "Перемещение") = 0 Тогда
			ПД = СоздатьОбъект("Документ");
			ПД.ВыбратьПодчиненныеДокументы(,, Док);
			ЕстьВедомость = 0;
			Пока ПД.ПолучитьДокумент() = 1 Цикл
				Если (ПД.Вид() = "ВедомостьПриемкиПромпереработки") И (ПД.ПометкаУдаления() = 0) Тогда
					ЕстьВедомость = 1;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьВедомость = 0 Тогда
				Док.ВыгрузитьТабличнуюЧасть(ТЗЧ);
				ТЗЧ.Свернуть("Стало", "Кво");
				
				ТЗЧ.ВыбратьСтроки();
				Пока ТЗЧ.ПолучитьСтроку() = 1 Цикл
					Если глСписокПП.НайтиЗначение(ТЗЧ.Стало) <> 0 Тогда
						НоваяСтрока();
						ТМЦ = ТЗЧ.Стало;
						Кво = ТЗЧ.Кво;
						Перемещение = Док.ТекущийДокумент();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьТЗПриемки(1, 1);
КонецПроцедуры // Заполнить

//======================================================================
Процедура ПриИзмененииПорядкаСтрок()
//	ОбновитьТЗПриемки(1);
КонецПроцедуры // гл

ТЗПриемки.НоваяКолонка("ТМЦ", "Справочник.ТМЦ", ,, "ТМЦ", 15);
ТЗПриемки.НоваяКолонка("Кво", "Число", 15, 3, "К приёму", 5);
ТЗПриемки.НоваяКолонка("Принято", "Число", 15, 3, "Принято", 5);
ТЗПриемки.НоваяКолонка("Разница", "Число", 15, 3, "Разница", 5);
ТЗПриемки.НоваяКолонка("Процент", "Число", 15, 2, "% пр.", 5);

СписокДействий = глПолучитьСписокДействий("
	|ВводНаОсновании,
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале,
	|Подчиненные");
