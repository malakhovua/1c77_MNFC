Перем ТекФормаПодбора;

Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
КонецПроцедуры

//******************************************************************************
Процедура ВводНового(Скопирован, ОбъектКопирования)
	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	ДатаДок=РабочаяДата();	
КонецПроцедуры

Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
КонецПроцедуры

Процедура ИзмТовар()
	глПриИзмененииТовара(Контекст);
КонецПроцедуры
	
Процедура Заполнить()
	Если КоличествоСтрок() > 0 Тогда
		Если Вопрос("Удалить строки?", "Да+Нет") = "Да" Тогда
			УдалитьСтроки();
		КонецЕсли;
	КонецЕсли;
	
	СпрТМЦ = СоздатьОбъект("Справочник.ТМЦ");
	Если ГруппаТ.Выбран() = 1 Тогда
	    СпрТМЦ.ИспользоватьРодителя(ГруппаТ);
		СпрТМЦ.ВыбратьЭлементы(1);
	Иначе
		СпрТМЦ.ВыбратьЭлементы(0);
	КонецЕсли;
	
	Пока СпрТМЦ.ПолучитьЭлемент() = 1 Цикл
		Если СпрТМЦ.ЭтоГруппа() = 0 Тогда
		    НоваяСтрока();
			ТМЦ = СпрТМЦ.ТекущийЭлемент();
			ИзмТовар();
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

//{{ПРОЦЕДУРА_ПЕЧАТИ(Печать)

//Данный фрагмент построен конструктором.
//При повторном использовании конструктора, внесенные вручную изменения будут потеряны!!!

Процедура Печать()
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Печать");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0,0,0);
	Ном = 1;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");
		Ном = Ном + 1;
	КонецЦикла;
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать Установка цен","");
КонецПроцедуры
//}}ПРОЦЕДУРА_ПЕЧАТИ

Процедура Подбор()
	ОткрытьПодбор("Справочник.ТМЦ", "ФормаСписка",,1);
	ТекФормаПодбора = "ДляТЧ";
КонецПроцедуры

Процедура ОбработкаПодбора(Элемент, Конт)
	Если ТекФормаПодбора = "ДляТЧ" Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если Элемент = ТМЦ Тогда
			    Возврат;
			КонецЕсли;
		КонецЦикла;
	
		НоваяСтрока();
		ТМЦ = Элемент;
		ИзмТовар();
	Иначе
		//Стр = 0;
		//Если ТЗ.НайтиЗначение(Элемент, Стр, "ТМЦ") = 0 Тогда
		//	Ц = 0;
		//	Если ВвестиЧисло(Ц, "Введите цену", 12, 2) = 0 Тогда
		//		Возврат;
		//	КонецЕсли;
		//	ТЗ.НоваяСтрока();
		//	ТЗ.ТМЦ = Элемент;
		//	ТЗ.Цена = Ц;
		//КонецЕсли;		
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(0);
	ТекФормаПодбора = "ДляСписка";
КонецПроцедуры

//Процедура УдалитьЗначениеСписка()
//	Если ТЗ.ТекущаяСтрока() <> 0 Тогда
//		ТЗ.УдалитьЗначение(ТЗ.ТекущаяСтрока());
//	КонецЕсли;
//КонецПроцедуры

Процедура ОбработатьСписок(ТМЦС, Запр, НЦена, ТЗПоП)
	Запр.ВНачалоВыборки();
	ТЗП = СоздатьОбъект("ТаблицаЗначений");
	ТЗП.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗП.НоваяКолонка("КвоС", "Число", 12, 2);
	ТЗП.НоваяКолонка("СумС", "Число", 12, 2);
	
	Если Запр.Получить(ТМЦС, ) = 1 Тогда
		Пока Запр.Группировка(2) = 1 Цикл
			ТЗП.НоваяСтрока();
			ТЗП.ТМЦ = Запр.Продукция;
			ТЗП.КвоС = Запр.КвоПриход;
			ТЗП.СумС = Запр.СуммаПриход;
		КонецЦикла;
	КонецЕсли;
	
	Если ТЗП.КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// добавляем ТМЦ в ТЧ
	НоваяСтрока();
	ТМЦ = ТМЦС;
	Ед = ТМЦС.ЕдиницаПоУмолчанию;
	Цена = НЦена;
	
	// теперь перебираем зависимую продукцию, пересчитываем цену и идём по рекурсии
	ТЗП.ВыбратьСтроки();
	Пока ТЗП.ПолучитьСтроку() = 1 Цикл
		// определяем новую цену это общая сумма списанного на продукцию - старая сумма списания + к-во списанного * новую цену
		Стр = 0; Выпущено = 0;
		Если ТЗПоП.НайтиЗначение(ТЗП.ТМЦ, Стр, "Продукция") = 1 Тогда
			Выпущено = ТЗПоП.ПолучитьЗначение(Стр, "КвоВып");
			СтараяСумма = ТЗПоП.ПолучитьЗначение(Стр, "СуммаПриход");
		КонецЕсли;
		Если Выпущено <> 0 Тогда
			НоваяЦена = (СтараяСумма - ТЗП.СумС + ТЗП.КвоС * НЦена) / Выпущено;
			ОбработатьСписок(ТЗП.ТМЦ, Запр, НоваяЦена, ТЗПоП);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьДатуЗаказа()
	ТекстЗ =
	"//{{ЗАПРОС(Фарш)
	|Период с НачДата по КонДата;
	|Продукция = Документ.Заказ.Продукция;
	|Заказ = Документ.Заказ.ТекущийДокумент;
	|КвоКутеров = Документ.Заказ.КвоКутеров;
	|Функция КвоКутеровС = Сумма(КвоКутеров);
	|Группировка Продукция без упорядочивания без групп все;
	|"//}}ЗАПРОС
	;
	
	ЗапрФ = СоздатьОбъект("Запрос");
	Если ЗапрФ.Выполнить(ТекстЗ) = 0 Тогда
		Возврат КонДата;
	КонецЕсли;
	
	КДата = КонДата;
	Док = СоздатьОбъект("Документ");
	Пока ЗапрФ.Группировка(1) = 1 Цикл
		Если ПустоеЗначение(ЗапрФ.Заказ) = 0 Тогда
		    Док.ВыбратьПодчиненныеДокументы(,,ЗапрФ.Заказ);
			Док.УстановитьФильтр(1,0);
			Пока Док.ПолучитьДокумент() = 1 Цикл
				Если Док.Вид() = "ВыпускПродукции" Тогда
				    КДата = Макс(КДата, Док.ДатаДок);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КДата;
КонецФункции

Функция ПорядокВЗаказе(Заказ, Продукция, ТекДОк)
	Если ПустоеЗначение(Заказ) = 1 Тогда
		Если (Продукция.ВидТМЦ <> Перечисление.ВидыТМЦ.Продукция) и (Продукция.ВидТМЦ <> Перечисление.ВидыТМЦ.Полуфабрикат) Тогда
			Если (ТекДок.ДатаДок <= КонДата) и (ТекДок.ДатаДок >= НачДата) Тогда
				Возврат 1;
			Иначе
				Возврат 0;
			КонецЕсли;			
		КонецЕсли;
	    Возврат 0;
	КонецЕсли;

	Если ТипЗначенияСтр(Заказ) = "Документ" Тогда
		Если (Заказ.ДатаДок <= КонДата) и (Заказ.ДатаДок >= НачДата) Тогда
		    Возврат 1;
		Иначе
			Возврат 0;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

Функция ПолучитьТМясо(ТЗ)
	ТМясо = СоздатьОбъект("ТаблицаЗначений");
	//ТМясо.НоваяКолонка("Материал", "Справочник.ТМЦ");
	ТМясо.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТМясо.НоваяКолонка("СуммаН", "Число", 12, 2);
	ТМясо.НоваяКолонка("Кво", "Число", 15, 3);
	
	ДокПер = СоздатьОбъект("Документ.ПереработкаМяса");
	ТЗСп = СоздатьОбъект("ТаблицаЗначений");
	ТЗИз = СоздатьОбъект("ТаблицаЗначений");
	ДокПер.УстановитьФильтр(1,0);
	ДокПер.ВыбратьДокументы(НачДата, КонДата);	
	Пока ДокПер.ПолучитьДокумент() = 1 Цикл
		// заполним и рассчитаем новую сумму
		ДокПер.ВыгрузитьТабличнуюЧасть(ТЗСп);
		ДокПер.ДокОснование.ВыгрузитьТабличнуюЧасть(ТЗИз);
		ТЗИз.ВыбратьСтроки();
		ИтКво = 0;
		Пока ТЗИз.ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			Если ТЗ.НайтиЗначение(ТЗИз.ТМЦ, Стр, "ТМЦ") = 1 Тогда
				ТЗИз.СуммаБезНДС = ТЗ.ПолучитьЗначение(Стр, "Цена") * ТЗИз.Кво;
			КонецЕсли;
			ИтКво = ИтКво + ТЗИз.Кво * ТЗИз.Коэффициент;
		КонецЦикла;
		
		// теперь пересчитаем ТЧ переработки		
		СС = ТЗИз.Итог("СуммаБезНДС");
		ИтСуммаБезКоэф = 0;
		ИтМассаСКоэф = 0;
		НСтрКоэф1 = 0;
		
		ТЗСп.ВыбратьСтроки();
		Пока ТЗСп.ПолучитьСтроку() = 1 Цикл
			Если ТЗСп.Коэф = 0 Тогда
				ИтСуммаБезКоэф = ИтСуммаБезКоэф + ТЗСп.СуммаБезНДС;
			Иначе
				Если ТЗСп.Коэф = 1 Тогда
					НСтрКоэф1 = ТЗСп.НомерСтроки;
				КонецЕсли;
				ИтМассаСКоэф = ИтМассаСКоэф + ТЗСп.ВесФакт * ТЗСп.Коэф;
				Если ТЗСп.ВесФакт <> 0 Тогда
					ПоследСтрокаСКоэф = ТЗСп.НомерСтроки;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если НСтрКоэф1 = 0 Тогда
			Сообщить("Нет ТМЦ с коэффициентом 1 " + ДокПер.ТекущийДокумент());
			Продолжить;
		КонецЕсли;
		
		ЦенаБазовая = (СС - ИтСуммаБезКоэф) / ИтМассаСКоэф;
		
		ТЗСп.ВыбратьСтроки();
		Пока ТЗСп.ПолучитьСтроку() = 1 Цикл
			Если ТЗСп.Коэф > 0 Тогда
				ТЗСп.ЦенаБезНДС = ЦенаБазовая * ТЗСп.Коэф;		
				ТЗСп.СуммаБезНДС = ТЗСп.ЦенаБезНДС * ТЗСп.ВесФакт;
				Стр = 0;
				ТЗИз.ВыбратьСтроки();
				Пока ТЗИз.ПолучитьСтроку() = 1 Цикл
					Если ТЗ.НайтиЗначение(ТЗИз.ТМЦ, Стр, "ТМЦ") = 1 Тогда
						ТМясо.НоваяСтрока();
						ТМясо.ТМЦ = ТЗСп.ТМЦ;
						ТМясо.СуммаН = ТЗСп.СуммаБезНДС;
						ТМясо.Кво = ТЗСп.ВесФакт;
					КонецЕсли;					
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	
	ТМясо.Свернуть("ТМЦ", "СуммаН,Кво");
	Возврат ТМясо;
КонецФункции

Процедура Рассчитать()
	Если (ПустоеЗначение(НачДата) = 1) или (ПустоеЗначение(КонДата) = 1) Тогда
		Предупреждение("Нужно задать начальную и конечную дату");
		Возврат;
	КонецЕсли;

	СпМеню = СоздатьОбъект("СписокЗначений");
	СпМеню.ДобавитьЗначение(1, "Рассчитать приходные цены");
	СпМеню.ДобавитьЗначение(2, "Добавить зависимые для переработки");
	СпМеню.ДобавитьЗначение(4, "Добавить связаные ТМЦ для списания");
	СпМеню.ДобавитьЗначение(3, "Добавить зависимые для продукции");
	Зн = 1;
	Если СпМеню.ВыбратьЗначение(Зн,,,,1) = 0 Тогда
		Возврат;
	КонецЕсли;	
	
    СписТ = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписТ, "ТМЦ");
	Если КоличествоСтрок() = 0 Тогда
		Предупреждение("В таблице должны быть ТМЦ");
		Возврат;		
	КонецЕсли;

	
	Если Зн = 1 Тогда
		ТекстЗ = 
		"//{{ЗАПРОС(Партии)
		|Период с НачДата по КонДата;
		|ТМЦ = Регистр.Партии.ТМЦ;
		|Стоимость = Регистр.Партии.Стоимость;
		|ОстатокТовара = Регистр.Партии.ОстатокТовара;
		|Функция СтНачОст = НачОст(Стоимость);
		|Функция СтПриход = Приход(Стоимость);
		|Функция КвоНО = НачОст(ОстатокТовара);
		|Функция КвоПриход = Приход(ОстатокТовара);
		|Группировка ТМЦ без упорядочивания без групп;
		|Условие(ТМЦ в списТ);
		|"//}}ЗАПРОС
		;
		
		Запр = СоздатьОбъект("Запрос");
		Если Запр.Выполнить(ТекстЗ) = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Запр.ВНачалоВыборки();
			Если Запр.Получить(ТМЦ) = 1 Тогда
			    Если Запр.КвоПриход <> 0 Тогда
			        ЦенаП = Запр.СтПриход / Запр.КвоПриход;
				ИначеЕсли Запр.КвоНО <> 0 Тогда
					ЦенаП = Запр.СтНачОст / Запр.КвоНО;
				КонецЕсли;
				Цена = ЦенаП;	
			КонецЕсли;
		КонецЦикла;
		Возврат;
	ИначеЕсли Зн = 2 Тогда
		ТЗ = СоздатьОбъект("ТаблицаЗначений");
		ТЗ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
		ТЗ.НоваяКолонка("Цена", "Число", 12, 2);
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если Цена <> 0 Тогда
				ТЗ.НоваяСтрока();
				ТЗ.ТМЦ = ТМЦ;
				ТЗ.Цена = Цена;
			КонецЕсли;
		КонецЦикла;
		
		ТМясо = ПолучитьТМясо(ТЗ);
		ТМясо.ВыбратьСтроки();
		Пока ТМясо.ПолучитьСтроку() = 1 Цикл
			НЦена = ?(ТМясо.Кво <> 0,ТМясо.СуммаН / ТМясо.Кво,0);
			Если НЦена <> 0 Тогда
				Поз = СписТ.НайтиЗначение(ТМясо.ТМЦ);
				Если Поз <> 0 Тогда
				    ПолучитьСтрокуПоНомеру(Поз);
				Иначе
					НоваяСтрока();
					ТМЦ = ТМясо.ТМЦ;
					Ед = ТМЦ.ЕдиницаПоУмолчанию;
					СписТ.ДобавитьЗначение(ТМЦ);
				КонецЕсли;
				Цена = НЦена;				
			КонецЕсли;
		КонецЦикла;
		Возврат;
	ИначеЕсли Зн = 3 Тогда	    
		ТМясо = СоздатьОбъект("ТаблицаЗначений");
		//ТМясо.НоваяКолонка("Материал", "Справочник.ТМЦ");
		ТМясо.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
		ТМясо.НоваяКолонка("СуммаН", "Число", 12, 2);
		ТМясо.НоваяКолонка("Кво", "Число", 15, 3);

		КДата = ПолучитьДатуЗаказа();
		
		// теперь нам нужно выполнить запрос по ПЗ, чтобы понять, что на что списывалось и определить зависимые ТМЦ
		// далее просматриваем строки ТЗ ищем зависимые ТМЦ, вычитаем старую цену, прибавляем новую цену, получаем результат и повторяем, пока не дойдём до продукции, которая никуда не списывалась.
		ТекстЗ = "//{{ЗАПРОС(Сформировать)
		|Период с НачДата по КДата;
		|Продукция = Регистр.ПроизводственныеЗатраты.Продукция;
		|Материал = Регистр.ПроизводственныеЗатраты.Материал;
		|Количество = Регистр.ПроизводственныеЗатраты.Количество;
		|Заказ = Регистр.ПроизводственныеЗатраты.Заказ;
		|Сумма = Регистр.ПроизводственныеЗатраты.Сумма;
		|ТекДок = Регистр.ПроизводственныеЗатраты.ТекущийДокумент;
		|Функция КвоПриход = Приход(Количество) когда (ПорядокВЗаказе(Заказ,Продукция,ТекДок) = 1);
		|Функция СуммаПриход = Приход(Сумма) когда (ПорядокВЗаказе(Заказ,Продукция,ТекДок) = 1);
		|Группировка Материал без упорядочивания без групп;
		|Группировка Продукция без упорядочивания без групп;
		|Условие (ТекДок.Вид() <> ""ПереработкаМяса"");
		|Условие (ПустоеЗначение(Продукция) = 0);
		|"//}}ЗАПРОС
		;
		
		ТЗПоП = СоздатьОбъект("ТаблицаЗначений");
		Запр = СоздатьОбъект("Запрос");
		Если Запр.Выполнить(ТекстЗ) = 0 Тогда
			Возврат;
		КонецЕсли;
		Запр.Выгрузить(ТЗПоП, 0, 0);
		ТЗПМ = СоздатьОбъект("ТаблицаЗначений");
		Запр.Выгрузить(ТЗПМ, 0, 0);
		ТЗПМ.Сортировать("Материал");
		ТЗПоП.Свернуть("Продукция", "СуммаПриход");
		
		// теперь нужно определить, сколько было выпущено какждой продукции, чтобы посчитать потом её себестоимость
		// выпуски берём по периоду заказов
		ТекстЗ = 
		"//{{ЗАПРОС(Выпуск)
		|Период с НачДата по КДата;
		|Продукция = Регистр.ВыпускПродукции.Продукция;
		|Количество = Регистр.ВыпускПродукции.Количество;
		|Сумма = Регистр.ВыпускПродукции.Сумма;
		|Заказ = Регистр.ВыпускПродукции.Заказ;
		|ТекДок = Регистр.ВыпускПродукции.ТекущийДокумент;
		|Функция КвоСумма = Сумма(Количество) когда (ПорядокВЗаказе(Заказ,Продукция,ТекДок) = 1);
		|Функция СумСумма = Сумма(Количество) когда (ПорядокВЗаказе(Заказ,Продукция,ТекДок) = 1);
		|Группировка Продукция без упорядочивания без групп;
		|Условие (ТекДок.Вид() <> ""ПереработкаМяса"");
		|"//}}ЗАПРОС
		;
		ЗапрВып = СоздатьОбъект("Запрос");
		Если ЗапрВып.Выполнить(ТекстЗ) = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТЗПоП.НоваяКолонка("КвоВып", "Число", 12, 2);
		ТЗПоП.НоваяКолонка("ИзПерераб", "Число", 1);
		
		Пока ЗапрВып.Группировка(1) = 1 Цикл
			ТЗПоП.НоваяСтрока();
			ТЗПоП.Продукция = ЗапрВып.Продукция;
			ТЗПоП.КвоВып = ЗапрВып.КвоСумма;
		КонецЦикла;
		ТЗПоП.Свернуть("Продукция", "КвоВып,СуммаПриход,КвоПриход");
	
		//ТЗ.ВыбратьСтроки();
		//Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		//	ОбработатьСписок(ТЗ.ТМЦ, Запр, ТЗ.Цена, ТЗПоП);
		//КонецЦикла;
	
		// получим таблицу переработок мяса, поскольку для них себестоимость считается по другому
		списНПер = СоздатьОбъект("СписокЗначений");
		// добавим в ТМясо то, что у нас в ТЗ
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ТМясо.НоваяСтрока();
			ТМясо.ТМЦ = ТМЦ;
			ТМясо.СуммаН = Цена;
			ТМясо.Кво = 1;
		КонецЦикла;
		
		ПервыйПроход = 1;
		Пока ТМясо.КоличествоСтрок() <> 0 Цикл
			ТМясо.ВыбратьСтроки();
			Пока ТМясо.ПолучитьСтроку() = 1 Цикл
				НЦена = ?(ТМясо.Кво <> 0,ТМясо.СуммаН / ТМясо.Кво,0);
				Если НЦена <> 0 Тогда
					Стр = 0;
					Если ТЗПМ.НайтиЗначение(ТМясо.ТМЦ, Стр, "Материал") <> 0 Тогда
						Поз = СписТ.НайтиЗначение(ТМясо.ТМЦ);
						Если Поз <> 0 Тогда
						    ПолучитьСтрокуПоНомеру(Поз);
						Иначе
							НоваяСтрока();
							ТМЦ = ТМясо.ТМЦ;
							Ед = ТМЦ.ЕдиницаПоУмолчанию;
							СписТ.ДобавитьЗначение(ТМЦ);
						КонецЕсли;
						Цена = НЦена;				
						ТЗПМ.ПолучитьСтрокуПоНомеру(Стр);
						Пока ТЗПМ.Материал = ТМясо.ТМЦ Цикл
							ТЗПМ.СуммаПриход = ТЗПМ.КвоПриход * НЦена;
							списНПер.ДобавитьЗначение(ТЗПМ.Продукция);
							
							Стр = Стр + 1;
							Если Стр > ТЗПМ.КоличествоСтрок() Тогда
								Прервать;
							Иначе
								ТЗПМ.ПолучитьСтрокуПоНомеру(Стр);
							КонецЕсли;
						КонецЦикла;
					//ИначеЕсли (ПервыйПроход = 1) Тогда
					//	Стр = 0;
					//	Если ТЗ.НайтиЗначение(ТМясо.ТМЦ, Стр, "ТМЦ") = 1 Тогда
					//		НоваяСтрока();
					//		ТМЦ = ТМясо.ТМЦ;
					//		Цена = НЦена;
					//		Ед = ТМЦ.ЕдиницаПоУмолчанию;
					//	КонецЕсли;				
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			// теперь двигаемся по новой таблице и считаем новую цену продукции
			ТМясо.УдалитьСтроки();
			ТЗПМ.ВыбратьСтроки();
			Пока ТЗПМ.ПолучитьСтроку() = 1 Цикл
				Если (списНПер.НайтиЗначение(ТЗПМ.Продукция) <> 0) Тогда
					ТМясо.НоваяСтрока();
					ТМясо.ТМЦ = ТЗПМ.Продукция;
					ТМясо.СуммаН = ТЗПМ.СуммаПриход;
				КонецЕсли;
			КонецЦикла;
			
			// теперь нужно определить к-во выпущенногго
			ТМясо.Свернуть("ТМЦ", "СуммаН,Кво");
			ТМясо.ВыбратьСтроки();
			Пока ТМясо.ПолучитьСтроку() = 1 Цикл
				Стр = 0;
				Если ТЗПоП.НайтиЗначение(ТМясо.ТМЦ, Стр, "Продукция") = 1 Тогда
					ТМясо.Кво = ТЗПоП.ПолучитьЗначение(Стр, "КвоВып");
				КонецЕсли;
			КонецЦикла;		
			списНПер.УдалитьВсе();
			ПервыйПроход = 0;
		КонецЦикла;		
	ИначеЕсли Зн = 4 Тогда
		СписТ = СоздатьОбъект("СписокЗначений");
		ВыгрузитьТабличнуюЧасть(СписТ, "ТМЦ");
		ТекстЗ = 
		"//{{ЗАПРОС(Зависимые)
		|Период С ДатаДок ПО ДатаДок;
		|ТекЭл = Справочник.ТМЦ.ТекущийЭлемент;
		|ТМЦДляСписания = Справочник.ТМЦ.ТМЦДляСписания;
		|Функция Счётчик = Счётчик();
		|Группировка ТекЭл;
		|Условие(ТМЦДляСписания в списТ);
		|Условие(ПустоеЗначение(ТМЦДляСписания) = 0);
		|"//}}ЗАПРОС
		;
		
		Запрос = СоздатьОбъект("Запрос");
		Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
			Возврат;
		КонецЕсли;

		ТЗИ = СоздатьОбъект("ТаблицаЗначений");
		ТЗ = СоздатьОбъект("ТаблицаЗначений");
		
		ВыгрузитьТабличнуюЧасть(ТЗИ);
		Запрос.Выгрузить(ТЗ,1);
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			Если ТЗИ.НайтиЗначение(ТЗ.ТекЭл.ТМЦДляСписания.Получить(ДатаДок), Стр, "ТМЦ") = 1 Тогда
				ТЗИ.ПолучитьСтрокуПоНомеру(Стр);
			    НоваяСтрока();
				ТМЦ = ТЗ.ТекЭл;
				Цена = ТЗИ.Цена;
				ЦенаП = ТЗИ.ЦенаП;
				Ед = ТЗ.ТекЭл.ЕдиницаПоУмолчанию;
			КонецЕсли;
		КонецЦикла;		
	КонецЕсли;
КонецПроцедуры


//====================================================================== //--- УМК Сандомирский В.Ю. (09.02.15)
Процедура ДобавитьИзДругогоУстановкаЦенНаСырье()
	ДокУстановкаЦенСырья = СоздатьОбъект("Документ.УстановкаЦенСырья");
	
	Если ДокУстановкаЦенСырья.Выбрать("Выберите установку","Журнал.УстановкаЦенТМЦ") = 1 Тогда
		
		ДокУстановкаЦенСырья.ВыбратьСтроки();
		Пока ДокУстановкаЦенСырья.ПолучитьСтроку() = 1 Цикл
			НоваяСтрока();
			ТМЦ 	= ДокУстановкаЦенСырья.ТМЦ;
			ИзмТовар();
			Цена	= ДокУстановкаЦенСырья.Цена;
			Ед		= ДокУстановкаЦенСырья.Ед;
			ЦенаП	= ДокУстановкаЦенСырья.ЦенаП;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьИзДругогоУстановкаЦенНаСырье();

//====================================================================== //--- УМК Сандомирский В.Ю. (09.02.15)
Процедура УвеличитьНовуюЦенуНаКоэфф()
	
	ВыбКоэфф = 0;
	
	Если ВвестиЧисло(ВыбКоэфф,"Введите коэффициент:",10,4) = 1 Тогда
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Цена = Цена * ВыбКоэфф;
		КонецЦикла;
	 
	КонецЕсли;
КонецПроцедуры // УвеличитьНовуюЦенуНаКоэфф()

//====================================================================== //--- УМК Сандомирский В.Ю. (10.02.15)
Процедура УдалитьСтрокиСПустойЦенойИзПрихода()
	
	КвоСтрок = КоличествоСтрок();
	
	Для НС = 0 По КвоСтрок-1 Цикл	
		ПолучитьСтрокуПоНомеру(КвоСтрок - НС);	
		Если ЦенаП = 0 Тогда
           УдалитьСтроку();
        КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры // УдалитьСтрокиСПустойЦеной()

//====================================================================== //--- УМК Сандомирский В.Ю. (10.02.15)
Процедура УдалитьСтрокиСПустойЦенойНовой()
	
	КвоСтрок = КоличествоСтрок();
	
	Для НС = 0 По КвоСтрок-1 Цикл	
		ПолучитьСтрокуПоНомеру(КвоСтрок - НС);	
		Если Цена = 0 Тогда
           УдалитьСтроку();
        КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры // УдалитьСтрокиСПустойЦенойНовой()


//====================================================================== //--- УМК Сандомирский В.Ю. (09.02.15)
Процедура Действия(СписокДействий)
	Варианы= СоздатьОбъект("СписокЗначений");
	Варианы.ДобавитьЗначение(1,"&Увеличить новую цену на коэфф.");
	Варианы.ДобавитьЗначение(2,"&Добавить из другого установка цен сырья");
	Варианы.ДобавитьЗначение(3,"&Удалить строки с пустой ценой из прихода");
	Варианы.ДобавитьЗначение(4,"&Удалить строки с пустой ценой новой");
	
	Результат = 0;
	Если Варианы.ВыбратьЗначение(,,Результат,,1)=1 Тогда
	    Если Результат = 1 Тогда
			УвеличитьНовуюЦенуНаКоэфф();
		ИначеЕсли Результат = 2 Тогда	
			ДобавитьИзДругогоУстановкаЦенНаСырье();
		ИначеЕсли Результат = 3 Тогда	
			УдалитьСтрокиСПустойЦенойИзПрихода();		
		ИначеЕсли Результат = 4 Тогда	
			УдалитьСтрокиСПустойЦенойНовой();		
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры // Действия(СписокДействий)

//====================================================================== УМК Сандомирский В.Ю. (*) //--- заголовок FormEx_ПланРаскраски
Функция Раскраска()
	ТекстПлана = "(BRUSH_S[" + Строка(100*255*100) + "])()()"; //--- добавляя колонку вначало документа до "раскрашек" часов добавить "()"
	Возврат  ТекстПлана;	
КонецФункции




ГруппаТ.ВыборГруппы(1);
//ТЗ.НоваяКолонка("ТМЦ", "Справочник.ТМЦ", ,,,10);
//ТЗ.НоваяКолонка("Цена", "Число", 12, 2, ,5);

// =============================== УМК Сандомирский В.Ю. (09.02.15)
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
    |ТоварныйСостав,
	|ОткрытьВЖурнале");

