Перем СтарДатаДок;      
Перем НачальнаяДатаДокумента;
Перем СписокДействий;


Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, списТовар);
	КонецЕсли;	
КонецПроцедуры

//======================================================================
Процедура ИзменитьТолькоПросмотр()
	Форма.кОК.Доступность(0);
	Форма.кДМ.Доступность(0);
	Форма.кД.Доступность(0);
	Форма.кМ.Доступность(0);
	Форма.кММ.Доступность(0);
	
	Форма.кФирма.Доступность(0);               		
	Форма.КнопкаПоУмолчанию("кЗакрыть");
	СписокДействий = глПолучитьСписокДействий("
		|СтруктураПодчиненности,
		|ОткрытьВЖурнале,
		|Подчиненные");	
КонецПроцедуры //
	
//======================================================================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	СтарДатаДок = ДатаДок;
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр() = 0 Тогда
		Форма.ТолькоПросмотр(ДействиеВыполнено);
	КонецЕсли;
	
	Если Форма.ТолькоПросмотр()=1 Тогда
		ИзменитьТолькоПросмотр();
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
		СписокДействий = глПолучитьСписокДействий("
			|СтруктураПодчиненности,
			|ОткрытьВЖурнале,
			|Подчиненные");		
	КонецЕсли;
	Если СокрЛП(Действие) <> "" Тогда
		списД.ТекущаяСтрока(списД.НайтиЗначение(СокрЛП(Действие)));
	КонецЕсли;
	
	глЗаполнитьСписокТоваров(СтрФильтр, списТовар);
КонецПроцедуры // ПриОткрытии

Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	глЗаписатьСписокТоваров(СтрФильтр, списТовар);
	Автор = глПользователь;
КонецПроцедуры

Процедура ИзмДатаДок()
	Если ДатаДок <> СтарДатаДок Тогда
		СтарДатаДок = ДатаДок;
	КонецЕсли;
КонецПроцедуры

Процедура ИзмФирма()
	Если Фирма <> глВосстановитьЗначение(Контекст,"Фирма") Тогда
		глСохранитьЗначение(Контекст,"Фирма",Фирма);
	КонецЕсли;
КонецПроцедуры 

//======================================================================
Процедура Заполнить()
	Если КоличествоСтрок() > 0 Тогда
		Если Вопрос("Удалить строки?", "Да+Нет") = "Да" Тогда
			УдалитьСтроки();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗ =
	"//{{ЗАПРОС(Норм)
	|Норма = Документ.НормыЗатрат.ТекущийДокумент;
	|Продукция = Документ.НормыЗатрат.Продукция;
	|фДляЗаказа = Документ.НормыЗатрат.фДляЗаказа;
	|Функция Счётчик = Счётчик();
	|Группировка Норма упорядочить по Норма.ДатаДок;
	|Условие(" + ?(ВсеКроме = 1, "(", "") + "Продукция в списТовар" + ?(ВсеКроме = 1, ")", "") + ");
	|Условие(фДляЗаказа = 1);
	|Условие(Продукция.ПометкаУдаления() = 0);
	|"//}}ЗАПРОС
	;
	
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока Запрос.Группировка(1) = 1 Цикл
		НоваяСтрока();
		НормаЗатрат = Запрос.Норма;
	КонецЦикла;
КонецПроцедуры // Заполнить

Процедура ВводНового(Скопирован) //Предопределенная процедура
	ДействиеВыполнено = 0;
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	глЗаполнитьШапку(Контекст);	
	ДатаДок=РабочаяДата();
КонецПроцедуры

//======================================================================
Процедура ИзмДействие()
	Действие = списД.ПолучитьЗначение(списД.ТекущаяСтрока());
КонецПроцедуры // ИзмДействие

//======================================================================
Функция ПолучитьСписТМЦПоНормам()
	СписТМЦ = СоздатьОбъект("СписокЗначений");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		НормаЗатрат.ВыбратьСтроки();
		Пока НормаЗатрат.ПолучитьСтроку() = 1 Цикл
			Если НормаЗатрат.Элемент.Вид() = "ТМЦ" Тогда
				ТМЦ = ?(НормаЗатрат.Элемент.ТМЦДляСписания.Получить(ДатаДок).Выбран() = 1, НормаЗатрат.Элемент.ТМЦДляСписания.Получить(ДатаДок), НормаЗатрат.Элемент);	
				Если СписТМЦ.НайтиЗначение(ТМЦ) = 0 Тогда
					СписТМЦ.ДобавитьЗначение(ТМЦ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СписТМЦ;
КонецФункции // 

//======================================================================
Процедура Выполнить()
	Если ДействиеВыполнено = 1 Тогда
		Предупреждение("Действие уже выполнено.");
//		Если Вопрос("Действие уже выполнено. Ещё раз?", "Да+Нет") = "Нет" Тогда
		Возврат;
//		КонецЕсли;
	КонецЕсли;
	
	Если СокрЛП(Действие) = "" Тогда
		Если списД.ТекущаяСтрока() <> 0 Тогда
			Действие = списД.ПолучитьЗначение(СписД.ТекущаяСтрока());
		КонецЕсли;
		Если СокрЛП(Действие) = "" Тогда
			Предупреждение("Выберите действие");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДокНЗ = СоздатьОбъект("Документ.НормыЗатрат");
	НачатьТранзакцию();
	Если СокрЛП(Действие) = "Обнулить цены" Тогда
		ЗакупЦена = Константа.ЗакупЦена;		
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ДокНЗ.НайтиДокумент(НормаЗатрат);
			ДокНЗ.ВыбратьСтроки();
			Пока ДокНЗ.ПолучитьСтроку() = 1 Цикл
				Если (ДокНЗ.ФиксироватьЦену <> Да) И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.ИзНорм) Тогда
					ДокНЗ.Цена = ?(ДокНЗ.СпособПодстановкиЦены = Перечисление.СпособПодстановкиЦеныВСписание.Закупочная, глВернутьЦену(ДокНЗ.Элемент, ЗакупЦена, ДатаДок, Гривня), 0);
				КонецЕсли;
			КонецЦикла;
			ДокНЗ.Записать();
			Если ДокНЗ.Проведен() = 1 Тогда
				ДокНЗ.Провести();
			КонецЕсли;
			Сообщить(ДокНЗ);
		КонецЦикла;
	ИначеЕсли СокрЛП(Действие) = "Установить из УЦС" Тогда
		Если УЦС.Выбран() = 0 Тогда
			Предупреждение("Выберите документ");
			Возврат;
		КонецЕсли;
		
		ТЗС = СоздатьОбъект("ТаблицаЗначений"); 
		УЦС.ВыгрузитьТабличнуюЧасть(ТЗС);
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ДокНЗ.НайтиДокумент(НормаЗатрат);
			ДокНЗ.ВыбратьСтроки();
			Пока ДокНЗ.ПолучитьСтроку() = 1 Цикл
				Если (ДокНЗ.Цена = 0) И (ДокНЗ.ФиксироватьЦену <> Да) И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.ИзНорм) 
					И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.Закупочная) Тогда
					ТМЦ = ?(ДокНЗ.Элемент.ТМЦДляСписания.Получить(ДатаДок).Выбран() = 1, ДокНЗ.Элемент.ТМЦДляСписания.Получить(ДатаДок), ДокНЗ.Элемент);
					Стр = 0;
					Поз = ТЗС.НайтиЗначение(ТМЦ, Стр, "ТМЦ");
					Если Поз <> 0 Тогда
						ДокНЗ.Цена = ТЗС.ПолучитьЗначение(Стр, "Цена");
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ДокНЗ.Записать();
			Если ДокНЗ.Проведен() = 1 Тогда
				ДокНЗ.Провести();
			КонецЕсли;
			Сообщить(ДокНЗ);
		КонецЦикла;		
	ИначеЕсли СокрЛП(Действие) = "Заполнить цены из остатков на дату документа" Тогда
		СписТМЦ = ПолучитьСписТМЦПоНормам();
		РегПарт = СоздатьОбъект("Регистр.Партии");
		РегПарт.УстановитьЗначениеФильтра("Фирма", Фирма, 1);
		РегПарт.УстановитьЗначениеФильтра("ТМЦ", СписТМЦ, 2);
		ДатаРасчета = ДатаДок;
	
	    ДатаРасчета = Мин(ДатаДок, ПолучитьДатуТА());
		Если ДатаРасчета < ПолучитьДатуТА() Тогда
			РегПарт.ВременныйРасчет(1);
			РассчитатьРегистрыПо(ДатаРасчета);
		КонецЕсли;
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ДокНЗ.НайтиДокумент(НормаЗатрат);
			ДокНЗ.ВыбратьСтроки();
			Пока ДокНЗ.ПолучитьСтроку() = 1 Цикл
				Если (ДокНЗ.Цена = 0) И (ДокНЗ.ФиксироватьЦену <> Да) И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.ИзНорм) 
					И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.Закупочная) Тогда
					Если ДокНЗ.Элемент.Вид() = "ТМЦ" Тогда
						ТМЦ = ?(ДокНЗ.Элемент.ТМЦДляСписания.Получить(ДатаРасчета).Выбран() = 1, ДокНЗ.Элемент.ТМЦДляСписания.Получить(ДатаРасчета), ДокНЗ.Элемент);
						ОстКво = РегПарт.СводныйИтог(Фирма, ДокНЗ.Элемент.Счет, , ТМЦ,,,,"ОстатокТовара");
						ОстСум = РегПарт.СводныйИтог(Фирма, ДокНЗ.Элемент.Счет, , ТМЦ,,,,"Стоимость");
						ДокНЗ.Цена = ?(ОстКво = 0, 0, ОстСум/ОстКво);
					КонецЕсли;
				КонецЕсли;				
			КонецЦикла;

			ДокНЗ.Записать();
			Если ДокНЗ.Проведен() = 1 Тогда
				ДокНЗ.Провести();
			КонецЕсли;
			Сообщить(ДокНЗ);
		КонецЦикла;		
	ИначеЕсли (СокрЛП(Действие) = "Заполнить цены из прихода за период") ИЛИ (СокрЛП(Действие) = "Заполнить цены из расхода за период") Тогда
		Если (ПустоеЗначение(ДатаПо) = 1) ИЛИ (ПустоеЗначение(ДатаС) = 1) Тогда
			Предупреждение("Неверно заполнено период");
			Возврат;
		КонецЕсли;
		СписТМЦ = ПолучитьСписТМЦПоНормам();
		
		СписСт = СоздатьОбъект("СписокЗначений");
		СписСт.ДобавитьЗначение(ПеремещениеВРозницу);
		СписСт.ДобавитьЗначение(ПеремещениеИзРозницы);
		СписСт.ДобавитьЗначение(ПеремещениеМеждуСкладами);
		СписСт.ДобавитьЗначение(ПередачаТарыВозвратнойПокупателю);
		СписСт.ДобавитьЗначение(ВозвратТарыВозвратнойОтПокупателя);
		СписСт.ДобавитьЗначение(ПолучениеТарыВозвратнойОтПоставщика);
		СписСт.ДобавитьЗначение(ВозвратТарыВозвратнойПоставщику);
		СписСт.ДобавитьЗначение(ПередачаТарыЗалоговойПокупателю);
		СписСт.ДобавитьЗначение(ВозвратТарыЗалоговойОтПокупателя);
		СписСт.ДобавитьЗначение(ПолучениеТарыЗалоговойОтПоставщика);
		СписСт.ДобавитьЗначение(ВозвратТарыЗалоговойПоставщику);		
		
		ДКон = Мин(ДатаПо, ПолучитьДатуТА());
		ДНач = Мин(ДатаС, ДКон);
		ТекстЗ = 
		"//{{ЗАПРОС(Сформировать)
		|Период с ДНач по ДКон;
		|ТМЦ = Регистр.Партии.ТМЦ;
		|Фир = Регистр.Партии.Фирма;
		|ОстатокТовара = Регистр.Партии.ОстатокТовара;
		|Стоимость = Регистр.Партии.Стоимость;
		|КодОперации = Регистр.Партии.КодОперации;
		|Функция ОстПриход = Приход(ОстатокТовара);
		|Функция ОстРасход = Расход(ОстатокТовара);
		|Функция СтПриход = Приход(Стоимость);
		|Функция СтРасход = Расход(Стоимость);
		|Группировка ТМЦ без упорядочивания без групп;
		|Условие (НЕ (КодОперации в СписСт));
		|Условие (ТМЦ В СписТМЦ);
		|Условие (Фир = Фирма);
		|"//}}ЗАПРОС
		;
		
		Запрос = СоздатьОбъект("Запрос");
		
		Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
			Сообщить("Не удалось выполнить запрос");
		    Возврат;
		КонецЕсли;
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ДокНЗ.НайтиДокумент(НормаЗатрат);
			ДокНЗ.ВыбратьСтроки();
			Пока ДокНЗ.ПолучитьСтроку() = 1 Цикл
				Если (ДокНЗ.Цена = 0) И (ДокНЗ.ФиксироватьЦену <> Да) И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.ИзНорм) 
					И (ДокНЗ.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.Закупочная) Тогда
					Если ДокНЗ.Элемент.Вид() = "ТМЦ" Тогда
						ТМЦ = ?(ДокНЗ.Элемент.ТМЦДляСписания.Получить(ДатаДок).Выбран() = 1, ДокНЗ.Элемент.ТМЦДляСписания.Получить(ДатаДок), ДокНЗ.Элемент);
						Запрос.ВНачалоВыборки();
						ОстКво = 0; ОстСум = 0;
						Если Запрос.Получить(ТМЦ) = 1 Тогда
							ОстКво = ?(СокрЛП(Действие) = "Заполнить цены из прихода за период", Запрос.ОстПриход, Запрос.ОстРасход);
							ОстСум = ?(СокрЛП(Действие) = "Заполнить цены из прихода за период", Запрос.СтПриход, Запрос.СтРасход);
						КонецЕсли;				
						ДокНЗ.Цена = ?(ОстКво = 0, 0, ОстСум/ОстКво);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ДокНЗ.Записать();
			Если ДокНЗ.Проведен() = 1 Тогда
				ДокНЗ.Провести();
			КонецЕсли;
			Сообщить(ДокНЗ);			
		КонецЦикла;		
	КонецЕсли;
	
	Если Вопрос("Сохранить?", "Да+Нет") = "Да" Тогда
		ДействиеВыполнено = 1;
		
		Записать();		
		ЗафиксироватьТранзакцию();
		Форма.ТолькоПросмотр(ДействиеВыполнено);
		ИзменитьТолькоПросмотр();
	КонецЕсли;
КонецПроцедуры // Выполнить

//======================================================================
Процедура ДобавитьТЗДок(ТЗДок, Причина, НЗ);
	ТЗДок.НоваяСтрока();
	ТЗДок.Материал = НЗ.Элемент;
	ТЗДок.Причина = Причина;
	ТЗДок.НормаЗатрат = НЗ;	
КонецПроцедуры // гл

//======================================================================
Процедура Печать()
	Если ДействиеВыполнено = 0 Тогда
		Предупреждение("Действие не выполнено");
		Возврат;		
	КонецЕсли;
	
	ТЗДок = СоздатьОбъект("ТаблицаЗначений");
	ТЗДок.НоваяКолонка("Материал", "Справочник.ТМЦ");
	ТЗДок.НоваяКолонка("Причина", "Строка");
	ТЗДок.НоваяКолонка("НормаЗатрат", "Документ.НормыЗатрат");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		НормаЗатрат.ВыбратьСтроки();
		Пока НормаЗатрат.ПолучитьСтроку() = 1 Цикл
			Если СокрЛП(Действие) = "Обнулить цены" Тогда
				Если НормаЗатрат.ФиксироватьЦену = Да Тогда
					ДобавитьТЗДок(ТЗДок, "Фикс. цена", НормаЗатрат);
				ИначеЕсли НормаЗатрат.СпособПодстановкиЦены = Перечисление.СпособПодстановкиЦеныВСписание.ИзНорм Тогда
					ДобавитьТЗДок(ТЗДок, "Подстановка из норм", НормаЗатрат);
				ИначеЕсли (НормаЗатрат.СпособПодстановкиЦены = Перечисление.СпособПодстановкиЦеныВСписание.Закупочная) Тогда
					ДобавитьТЗДок(ТЗДок, "Из закуп. цены", НормаЗатрат);				
				ИначеЕсли (НормаЗатрат.СпособПодстановкиЦены <> Перечисление.СпособПодстановкиЦеныВСписание.Закупочная) И (НормаЗатрат.Цена <> 0) Тогда
					ДобавитьТЗДок(ТЗДок, "Не определена", НормаЗатрат);
				КонецЕсли;
			Иначе
				Если НормаЗатрат.Цена = 0 Тогда
					ДобавитьТЗДок(ТЗДок, "Не определена", НормаЗатрат);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТЗДок.Сортировать("Причина");
	СтарПричина = "";
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Шапка" + ?(СокрЛП(Действие) = "Обнулить цены", "", "Н"));
	ТЗДок.ВыбратьСтроки();
	Пока ТЗДок.ПолучитьСтроку() = 1 Цикл
		Если СокрЛП(Действие) = "Обнулить цены" Тогда
			Если СтарПричина <> ТЗДок.Причина Тогда
				Таб.ВывестиСекцию("Причина");
				СтарПричина = ТЗДок.Причина;
			КонецЕсли;
		КонецЕсли;
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);	
	Таб.Показать(?(СокрЛП(Действие) = "Обнулить цены", "Не обнулённые цены", "Нулевые цены"));
КонецПроцедуры // Печать()

//======================================================================
Процедура СписокМатериалов()
	Если ДействиеВыполнено = 0 Тогда
		Предупреждение("Действие не выполнено");
		Возврат;		
	КонецЕсли;
	
	ТЗДок = СоздатьОбъект("ТаблицаЗначений");
	ТЗДок.НоваяКолонка("Материал", "Справочник.ТМЦ");
	ТЗДок.НоваяКолонка("Родитель", "Справочник.ТМЦ");
	ТЗДок.НоваяКолонка("НормаЗатрат", "Документ.НормыЗатрат");
	ТЗДок.НоваяКолонка("Цена", "Число", 15, 2);
	ТЗДок.НоваяКолонка("К", "Число", 15, 2);
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		НормаЗатрат.ВыбратьСтроки();
		Пока НормаЗатрат.ПолучитьСтроку() = 1 Цикл
			ТЗДок.НоваяСтрока();
			ТЗДок.Материал = НормаЗатрат.Элемент;
			ТЗДок.Родитель = НормаЗатрат.Элемент.Родитель;
			ТЗДок.НормаЗатрат = НормаЗатрат;
			ТЗДок.Цена = НормаЗатрат.Цена;
		КонецЦикла;
	КонецЦикла;

	ТЗДок.Свернуть("Материал,Родитель,Цена", "К");	
	ТЗДок.Сортировать("Родитель,Материал");
	СтарПричина = "";
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Список");
	Таб.ВывестиСекцию("Шапка");
	
	ТЗДок.ВыбратьСтроки();
	Пока ТЗДок.ПолучитьСтроку() = 1 Цикл
		Если СтарПричина <> ТЗДок.Родитель Тогда
			Таб.ВывестиСекцию("Причина");
			СтарПричина = ТЗДок.Родитель;
		КонецЕсли;
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);	
	Таб.Показать("Список материалов");
КонецПроцедуры // Печать()

//======================================================================
Процедура ОткрытьНормы()
	ОткрытьФорму(НормаЗатрат);
КонецПроцедуры // ОткрытьНормы()

//======================================================================
Функция Продукция()
	Если ПустоеЗначение(НормаЗатрат) = 0 Тогда
		Возврат НормаЗатрат.Продукция;
	КонецЕсли;
КонецФункции // Продукция

списД.ДобавитьЗначение("Обнулить цены");
списД.ДобавитьЗначение("Установить из УЦС");
списД.ДобавитьЗначение("Заполнить цены из остатков на дату документа");
списД.ДобавитьЗначение("Заполнить цены из прихода за период");
списД.ДобавитьЗначение("Заполнить цены из расхода за период");
