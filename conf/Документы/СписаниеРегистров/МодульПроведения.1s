// ********************
Процедура ОбработкаПроведения()
	РегПарт = СоздатьОбъект("Регистр.Партии");
	РегОст  = СоздатьОбъект("Регистр.Остатки");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	
	РегПарт.УстановитьЗначениеФильтра("Фирма", Фирма, 1);
	Если Склад.Выбран() = 1 Тогда
		Если глПартионныйУчетПоСкладам = Да Тогда
		    РегПарт.УстановитьЗначениеФильтра("МестоХранения", Склад);
		КонецЕсли;	    
		РегОст.УстановитьЗначениеФильтра("МестоХранения", Склад);	
	КонецЕсли;	
	РегОст.УстановитьЗначениеФильтра("Фирма", Фирма, 1);
	СписТов = СоздатьОбъект("СписокЗначений");
	
	Если КоличествоСтрок() > 0 Тогда	    
		ВыгрузитьТабличнуюЧасть(СписТов, "Товар");
		РегОст.УстановитьЗначениеФильтра("ТМЦ", списТов, 2);
		РегПарт.УстановитьЗначениеФильтра("ТМЦ", списТов, 2);
	КонецЕсли;
	
	Если ИтогиАктуальны() = 0 Тогда
	    РегПарт.ВременныйРасчет(1);
		РегОст.ВременныйРасчет(1);
		РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;	
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	РегПарт.ВыгрузитьИтоги(ТЗ);
	ТЗ.Свернуть("Товар", "ОстатокТовара");
	
	РегПарт.ВыбратьИтоги();
	Пока РегПарт.ПолучитьИтог() = 1 Цикл
		Если ТолькоСуммы = 1 Тогда
			Стр = 0;
			Если ТЗ.НайтиЗначение(РегПарт.Товар, Стр, "Товар") = 0 Тогда
				Продолжить;
			Иначе
				Если ТЗ.ПолучитьЗначение(Стр, "ОстатокТовара") <> 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		    
		Регистр.Партии.ДвижениеРасход(РегПарт.Фирма, РегПарт.Счет, РегПарт.МестоХранения, РегПарт.ТМЦ, РегПарт.Поставщик, РегПарт.Поставка, 
		 РегПарт.ПрихДокумент, РегПарт.ОстатокТовара, РегПарт.Стоимость, РегПарт.ПродСтоимость, "");
	КонецЦикла;

	РегОст.ВыбратьИтоги();
	Пока РегОст.ПолучитьИтог() = 1 Цикл
		Если (РегОст.ОстатокТовара <> 0) и (ТолькоСуммы = 1) Тогда
			Продолжить;
		КонецЕсли;
		Регистр.Остатки.ДвижениеРасход(РегОст.Фирма, РегОст.ТМЦ, РегОст.МестоХранения,, РегОст.ОстатокТовара, РегОст.ДопКво, 1, 0, , );
	КонецЦикла;
	
	Если списТов.РазмерСписка() > 0 Тогда
	    Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,списТов, 1, 0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,, 1, 0);
	КонецЕсли;
	Ит.ИспользоватьСубконто(ВидыСубконто.Партии,, 1, 0);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,Склад, 1, 0);	
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ВключатьСубсчета(-1);
	
	Ит.ВыполнитьЗапрос(,ТекущийДокумент());
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Ит.ВыбратьСубконто(1);
		Пока Ит.ПолучитьСубконто(1) = 1 Цикл
			Ит.ВыбратьСубконто(2);
			Пока Ит.ПолучитьСубконто(2) = 1 Цикл
				Кво = ?(Ит.СКД("К") = 0, Ит.СКК("К"), Ит.СКД("К"));
				Сум = ?(Ит.СКД() = 0, Ит.СКК(), Ит.СКД());
				глПроводка(Контекст,"00",Ит.Счет,Сум,"Списание с/с",Кво, ,,,
				Склад,Ит.Субконто(1),Ит.Субконто(2) ,,"ТО",);			
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Операция.ЗаписатьПроводки();	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если списТов.РазмерСписка() > 0 Тогда
	    Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,списТов, 1, 0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,, 1, 0);
	КонецЕсли;
	
	Если Склад.Выбран() = 1 Тогда 
	  	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,Склад, 1, 0);
	Иначе
	  	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,, 1, 0);
	КонецЕсли;
	
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ВключатьСубсчета(-1);
	
	Ит.ВыполнитьЗапрос(,ТекущийДокумент());
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Ит.ВыбратьСубконто(1);
		Пока Ит.ПолучитьСубконто(1) = 1 Цикл
			Ит.ВыбратьСубконто(2);
			Пока Ит.ПолучитьСубконто(2) = 1 Цикл
				Кво = ?(Ит.СКД("К") = 0, Ит.СКК("К"), Ит.СКД("К"));
				Сум = ?(Ит.СКД() = 0, Ит.СКК(), Ит.СКД());
				глПроводка(Контекст,"00",Ит.Счет,Сум,"Списание с/с",Кво, ,,,
				Ит.Субконто(2),Ит.Субконто(1), ,,"ТО",);			
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	Операция.Записать();
КонецПроцедуры
