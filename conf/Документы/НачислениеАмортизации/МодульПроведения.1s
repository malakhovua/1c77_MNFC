Перем СпрНА;
Перем АмортГр[4],НМ;
Перем ОбъектныйУчет[4];
Перем ПонижающийКоэффициент;

// ===============================
// возвращает корень от числа Основание со степенью Степень
Функция Корень(Основание,Степень)
	Если Степень = 1 Тогда
		Возврат(Основание);
	КонецЕсли;
	Факториал = 1; Степень_ = 1; Сум = 0;
	СтепеньЕ = 1/Степень*Лог(Основание);
	Для ии = 1 по 28 Цикл             
		Факториал = Факториал * ии;
		Степень_ = Степень_ * СтепеньЕ;
		Сум = Сум + Степень_/Факториал 
	КонецЦикла;
    Возврат(1+Сум);
КонецФункции

// ===============================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
 	глПроверкаДатыДок(Контекст,"Проведение");
 	глВыбранЛи(Фирма,"Фирма");
 	Если НалоговыйУчет = 1 Тогда
 		Ит = СоздатьОбъект("БухгалтерскиеИтоги");
 		Ит.ИспользоватьРазделительУчета(Фирма);
		Ит.ПериодКВ(ДатаДок);	
		Если (Ит.ОБ(,"ОС.1") + Ит.ОБ(,"ОС.2") + Ит.ОБ(,"ОС.3") + Ит.ОБ(,"ОС.4") + Ит.ОБ(,"НА.2")) <> 0 Тогда
			глКомментарий("За "+ПериодСтр(НачКвартала(ДатаДок),КонКвартала(ДатаДок)) + "амортизация в налоговом учете уже рассчитана!",0);
			глВсеВыбрано = 0;
		КонецЕсли;
	Иначе
 		//
	КонецЕсли;
	ноАмортГр = 0;
	Спис = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По 4 Цикл
		Если глПолучитьНиО(ноАмортГр,"АмортГр"+Инд,"о ставках амортизации "+Инд+" группы!") = 0 Тогда
			Возврат 0;
		КонецЕсли;
		глПарсить(ноАмортГр.Дополнительно.Получить(ДатаДок),Спис);
		ОбъектныйУчет[Инд] = Число(Спис.Получить("Пообъектно"));
	КонецЦикла;
	Возврат глВсеВыбрано;    
КонецФункции

// ===============================
Процедура ЗаполнитьКоэфАморт()
	// коэффициент амортизации
	Расшифровка = СоздатьОбъект( "СписокЗначений" );
	Расшифровка.Установить ("Дата",ДатаДок);
	Расшифровка.Установить("АмортГр1","");
	Расшифровка.Установить("АмортГр2","");
	Расшифровка.Установить("АмортГр3","");
	Расшифровка.Установить("АмортГр4","");
	Расшифровка.Установить("НеоблагМин","");
	
	глПолучитьДанные(Расшифровка);

	АмортГр[1] = Расшифровка.Получить("АмортГр1");
	АмортГр[2] = Расшифровка.Получить("АмортГр2");
	АмортГр[3] = Расшифровка.Получить("АмортГр3");
	АмортГр[4] = Расшифровка.Получить("АмортГр4");
	НМ = Расшифровка.Получить("НеоблагМин");
КонецПроцедуры

// ===============================
// Списывает по налоговому учету остаточную стоимость группы основных средств (2 или 3),
// не содержащую материальных ценностей на начало отчетного периода
Процедура СписатьОстСтоимостьГруппыОС(Знач СчетГруппыОС, Знач ОстСтоимостьГруппыОС)
	глПроводка(Контекст,СчетГруппыОС,,-ОстСтоимостьГруппыОС,"Списание остаточной стоимости",, ,,,
	,,, ,,"НА");
	СпрВДР = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	Если СпрВДР.НайтиПоКоду("2/3/5") = 1 Тогда
	
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		//глПроводка(Контекст,"ВР","ВР",ОстСтоимостьГруппыОС,"Списание остаточной стоимости",, ,,СпрВДР.ТекущийЭлемент(),
		//,,СпрВДР.ТекущийЭлемент(), ,,"НА");
		
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
	Иначе
		глКомментарий("В справочнике валовых доходов/расходов не найден элемент с кодом ""2/3/5"" (І 30.5)!",0,,"!");
		глКомментарий("Не сделана проводка по списанию остаточной стоимости по счету ""ВР""!",0);
	КонецЕсли;
	глКомментарий("Счет " + СчетГруппыОС + " - нет мат. ценностей на начало периода! Остаточная стоимость списана полностью!",2,,"I");
	СпрВДР = 0;
КонецПроцедуры

// ===============================
// Списывает по налоговому учету остаточную стоимость ОС группы 1
Процедура СписатьОстСтоимость(Знач СчетОС, Знач Актив, Знач ОстСтоимостьОС)
	глПроводка(Контекст,СчетОС,,-ОстСтоимостьОС,"Списание остаточной стоимости",, Актив,,,
	           ,,, ,,"НА");
	СпрВДР = СоздатьОбъект("Справочник.ВалДоходыРасходы");
	Если СпрВДР.НайтиПоКоду("2/3/2") = 1 Тогда
		
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		//глПроводка(Контекст,"ВР","ВР",ОстСтоимостьОС,"Списание ост. стоимости: "+Актив,, ,,СпрВДР.ТекущийЭлемент(),
		//,,СпрВДР.ТекущийЭлемент(), ,,"НА");
		
		// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
	Иначе
		глКомментарий("В справочнике валовых доходов/расходов не найден элемент с кодом ""2/3/2"" (І 30.2)!",0,,"!");
		глКомментарий("Не сделана проводка по списанию остаточной стоимости по счету ""ВР""!",0);
	КонецЕсли;
	СпрВДР = 0;
КонецПроцедуры


// ===============================
Процедура ПроводкиНалоговыйУчет()
	Для НомерГруппы = 1 По 4 Цикл
		КоэффициентАмортизации = АмортГр[НомерГруппы];
		Ит = СоздатьОбъект("БухгалтерскиеИтоги");
		Ит.ИспользоватьРазделительУчета(Фирма);
		СчетГруппы = "ОС."+НомерГруппы;
		Если ОбъектныйУчет[НомерГруппы]=1 Тогда // пообъектно
			Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
			Ит.ВыполнитьЗапрос(НачКвартала(ДатаДок),,СчетГруппы);
			Ит.ВыбратьСубконто(1);
			Пока Ит.ПолучитьСубконто(1) = 1 Цикл
				НА = Ит.Субконто(1);
				Если НА.Выбран()=0 Тогда
					Продолжить;
				КонецЕсли;
				НА.ИспользоватьДату(НачКвартала(ДатаДок),1);
				Если (НА.Состояние = Перечисление.СостоянияНеоборАктивов.НаКонсервации) 
				 или (НА.Состояние = Перечисление.СостоянияНеоборАктивов.НаМодернизации) Тогда
				    Продолжить;
				КонецЕсли;
	
				ОстСтоимость = Ит.СНД("С");
				Если (НомерГруппы = 1) И (ОстСтоимость < НМ*100) Тогда
					// для ОС 1 группы ост. стоимость меньше 100 необл. минимумов
					СписатьОстСтоимость(СчетГруппы, НА, ОстСтоимость);
				Иначе
					СуммаИзноса = Окр(ОстСтоимость * КоэффициентАмортизации * ПонижающийКоэффициент, 2);
					Если (СуммаИзноса > ОстСтоимость) Тогда
						// сумма износа больше ост. стоимости 
						СуммаИзноса = ОстСтоимость;
					КонецЕсли;
					глПроводка(Контекст,,СчетГруппы,СуммаИзноса,"Амортизация",, ,,,
					НА,,, ,,"НА");
				КонецЕсли;
			КонецЦикла;
		Иначе
			Ит.ПериодКВ(ДатаДок);	
			ОстСтоимость = Ит.СНД(СчетГруппы);
			ОстКво = Ит.СНД(СчетГруппы,"К");
			Если ОстКво > 0 Тогда
				// начислим износ обычным образом
				СуммаИзноса = Окр(ОстСтоимость * КоэффициентАмортизации * ПонижающийКоэффициент, 2);
				глПроводка(Контекст,,СчетГруппы,СуммаИзноса,"Амортизация",, ,,,
				,,, ,,"НА");
			ИначеЕсли ОстСтоимость > 0 Тогда
				// группа не содержит материальных ценностей, спишем ост. стоимость
				СписатьОстСтоимостьГруппыОС(СчетГруппы,ОстСтоимость);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// НМА
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
	Ит.ВыполнитьЗапрос(НачКвартала(ДатаДок),,"НА.1,НА.2");
	Ит.ВыбратьСубконто(1);
	Пока Ит.ПолучитьСубконто(1) = 1 Цикл
		НА = Ит.Субконто(1);
		НА.ИспользоватьДату(НачКвартала(ДатаДок),1);
		
		ПервСтоимость = 0;
		Если Ит.ПолучитьСчет(,СчетПоКоду("НА.1")) = 1 Тогда
			ПервСтоимость = Ит.СНД("С");
		КонецЕсли;
		ОстСтоимость = ПервСтоимость;
		Если Ит.ПолучитьСчет(,СчетПоКоду("НА.2")) = 1 Тогда
			ОстСтоимость = ПервСтоимость - Ит.СНК("С");
		КонецЕсли;
				
		СрокИспользования = НА.СрокИспользования;
		Если СрокИспользования = 0 Тогда
			глКомментарий("Для НМА "+Ит.Субконто(1)+" не задан срок использования! Амортизация не начислена!",1,,"!");
			Продолжить;
		КонецЕсли;
		СуммаИзноса = Мин(ОстСтоимость,3*Окр(ПервСтоимость / СрокИспользования, 2));
		
		глПроводка(Контекст,,"НА.2",СуммаИзноса,"Амортизация",, ,,,
		НА,,, ,,"НА");
	КонецЦикла;
КонецПроцедуры

// ===============================
// рассчитывает износ по конкретному ОС, НМА, необоротному активу
// по установленному в нем методу
Функция РассчитатьИзносПоЭлементу(Элемент,ПервСтоимость,ОстСтоимость,ПервСтоимостьКв,ОстСтоимостьКв,СчИзноса,Сч)
	Если Элемент.МетодРасчетаИзноса.Выбран() = 0 Тогда
	    глКомментарий(Строка(Элемент)+": не задан метод расчета износа. Износ не начислен!",0,,"!");
		Возврат 0;
	КонецЕсли;
	СуммаИзноса = 0;ИзношеноПолностью = 0;

	Если Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйМесяц Тогда
		// Установим признак "ИзношеноПолностью" если применяется метод "100%"
	ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Налоговый Тогда
		// Установим признак "ИзношеноПолностью" если применяется "Налоговый" метода
	Иначе
		// Установим признак "ИзношеноПолностью" если для всех остальных методов
		Если ((ОстСтоимость) < 0) или (ОстСтоимость<=Элемент.ЛиквидационнаяСтоимость) Тогда
			ИзношеноПолностью = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ИзношеноПолностью = 0 Тогда
		//ПрямолинейноеСписание
		Если Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПрямолинейноеСписание Тогда
			Если Элемент.СрокИспользования = 0 Тогда
				глКомментарий(Строка(Элемент)+": не задан срок полезного использования. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли;    
			СуммаИзноса = Макс(Окр((ПервСтоимость - Элемент.ЛиквидационнаяСтоимость)/Элемент.СрокИспользования,2),0);
			
			//УменьшениеОстатка
			//большая погрешность в случае,если ликвидационная стоимость много больше первоначальной
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.УменьшениеОстатка Тогда
			Если Элемент.СрокИспользования = 0 Тогда
				глКомментарий(Строка(Элемент)+": не задан срок полезного использования. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли; 
			Если Элемент.СрокИспользования < 12 Тогда
				глКомментарий(Строка(Элемент)+": срок полезного использования меньше 1 года. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли; 
			Если ПервСтоимость = 0 Тогда
				глКомментарий(Строка(Элемент)+": первоначальная стоимость равна 0. Износ не начислен!",0,,"!");    
				Возврат 0;
			КонецЕсли;                  
			Если Элемент.ЛиквидационнаяСтоимость = 0 Тогда
				глКомментарий(Строка(Элемент)+": ликвидационная стоимость равна 0. Износ не начислен!",0,,"!");    
				Возврат 0;
			КонецЕсли;
			
			// для данного метода расчитывается коэф. аморт. за год, поэтому при начислении 
			// аморт помесячно необходимо брать остаточную стоимость и износ на начало года
			Ит = СоздатьОбъект("БухгалтерскиеИтоги");
			Ит.ИспользоватьРазделительУчета(Фирма);
			Ит.ПериодМ(?(ДатаМесяц(Элемент.ДатаВвода) < ДатаМесяц(ДатаДок),
						Дата(ДатаГод(ДатаДок),ДатаМесяц(ДобавитьМесяц(Элемент.ДатаВвода,1)),1),
						Дата(ДатаГод(ДатаДок)-1,ДатаМесяц(ДобавитьМесяц(Элемент.ДатаВвода,1)),1)));
		    ИзносГод = Ит.СНК(СчИзноса,"С",,Элемент);
			ОстСтоимостьГод = ПервСтоимость - ИзносГод;
			
			ГодоваяНормаАмортизации = (1-Корень(Элемент.ЛиквидационнаяСтоимость/ПервСтоимость,Элемент.СрокИспользования/12));
			МесячнаяНормаАмортизации = ГодоваяНормаАмортизации/12;
			СуммаИзноса = Макс(ОстСтоимостьГод*МесячнаяНормаАмортизации,0);
			
			Если СпрНА.НайтиЭлемент(Элемент)=1 Тогда
				СпрНА.НормаАмортизации = МесячнаяНормаАмортизации;
				СпрНА.Записать();    
			КонецЕсли;     
			//УскоренноеУменьшениеОстатка
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.УскоренноеУменьшениеОстатка Тогда
			Если Элемент.СрокИспользования = 0 Тогда
				глКомментарий(Строка(Элемент)+": не задан срок полезного использования. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли; 
			Если Элемент.СрокИспользования < 12 Тогда
				глКомментарий(Строка(Элемент)+": срок полезного использования меньше 1 года. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли; 
			Если ПервСтоимость = 0 Тогда
				глКомментарий(Строка(Элемент)+": первоначальная стоимость равна 0. Износ не начислен!",0,,"!");    
				Возврат 0;
			КонецЕсли;
			Если ПустоеЗначение(Элемент.ДатаВвода) = 1 Тогда
				глКомментарий(Строка(Элемент)+": Не задана дата ввода в эксплуатацию. Износ не начислен!",0,,"!");    
				Возврат 0;
			КонецЕсли;
			
			// для данного метода расчитывается коэф. аморт. за год, поэтому при начислении 
			// аморт помесячно необходимо брать остаточную стоимость и износ на начало года
			Ит = СоздатьОбъект("БухгалтерскиеИтоги");
			Ит.ИспользоватьРазделительУчета(Фирма);
			
			ДатаЛиквидации = ДобавитьМесяц(Элемент.ДатаВвода, Элемент.СрокИспользования);
			
			ДокументАбсМесяц   = ДатаГод(ДатаДок)*12        + ДатаМесяц(ДатаДок);
			ЛиквидацияАбсМесяц = ДатаГод(ДатаЛиквидации)*12 + ДатаМесяц(ДатаЛиквидации);
						
			// если это не последний год эксплуатации...
			Если ДокументАбсМесяц <= ЛиквидацияАбсМесяц-12 Тогда
				// ...то применяем алгоритм Double declining balance depreciation method
				Ит.ПериодМ(?(ДатаМесяц(Элемент.ДатаВвода) < ДатаМесяц(ДатаДок),
						Дата(ДатаГод(ДатаДок),ДатаМесяц(ДобавитьМесяц(Элемент.ДатаВвода,1)),1),
						Дата(ДатаГод(ДатаДок)-1,ДатаМесяц(ДобавитьМесяц(Элемент.ДатаВвода,1)),1)));
		    	ИзносГод = Ит.СНК(СчИзноса,"С",,Элемент);                   
				ОстСтоимостьГод = ПервСтоимость - ИзносГод;
				
				ГодоваяНормаАмортизации = (1/Элемент.СрокИспользования*12) * 2;
				МесячнаяНормаАмортизации = ГодоваяНормаАмортизации/12;
				СуммаИзноса = ОстСтоимостьГод*МесячнаяНормаАмортизации;
				
				Если СпрНА.НайтиЭлемент(Элемент)=1 Тогда
					СпрНА.НормаАмортизации = МесячнаяНормаАмортизации;
					СпрНА.Записать();		
				КонецЕсли;  
			Иначе // ...линейно списываем до ликвидационной стоимости
				Ит.ПериодМ(ДатаДок);
				ИзносГод = Ит.СНК(СчИзноса,"С",,Элемент);
				ОстСтоимостьГод = ПервСтоимость - ИзносГод;
								
				ОсталосьМесяцев = Макс(1, ЛиквидацияАбсМесяц - ДокументАбсМесяц + 1);
				СуммаИзноса = (ОстСтоимостьГод-Элемент.ЛиквидационнаяСтоимость)/ОсталосьМесяцев;
			КонецЕсли;
			
			СуммаИзноса = Макс(0, СуммаИзноса);
			
			//СуммЕдиниц
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.СуммЕдиниц Тогда
			// Перечисление.МетодыРасчетаИзноса.СуммЕдиниц
			Если Элемент.РасчетныйОбъемПроизводства = 0 Тогда
				глКомментарий(Строка(Элемент)+": не задан расчетный объем производства. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли;
			Элемент.ИспользоватьДату(КонМесяца(ДатаДок),1);
    		ТекущийОбъемПроизводства = Элемент.ТекущийОбъемПроизводства;
    		Элемент.ИспользоватьДату(НачМесяца(ДатаДок),1);
			Если ТекущийОбъемПроизводства = 0 Тогда
				глКомментарий(Строка(Элемент)+": не задан текущий объем производства. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли;
			СуммаИзноса = Макс(ТекущийОбъемПроизводства*(ПервСтоимость - Элемент.ЛиквидационнаяСтоимость)/Элемент.РасчетныйОбъемПроизводства,0);
			// Кумулятивный
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Кумулятивный Тогда
			Если Элемент.СрокИспользования = 0 Тогда
				глКомментарий(Строка(Элемент)+": не задан срок полезного использования. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли; 
			Если Элемент.СрокИспользования < 12 Тогда
				глКомментарий(Строка(Элемент)+": срок полезного использования меньше 1 года. Износ не начислен!",0,,"!");
				Возврат 0;
			КонецЕсли; 
			Если ПервСтоимость = 0 Тогда
				глКомментарий(Строка(Элемент)+": первоначальная стоимость равна 0. Износ не начислен!",0,,"!");    
				Возврат 0;
			КонецЕсли;
			Если Элемент.ЛиквидационнаяСтоимость = 0 Тогда
				глКомментарий(Строка(Элемент)+": ликвидационная стоимость равна 0. Износ не начислен!",0,,"!");    
				Возврат 0;
			КонецЕсли;
			// определим срок использования в целых годах
			ЛетИспользования = Цел(Элемент.СрокИспользования/12);
			// определим число лет, остающееся до конца срока службы объекта
			ДатаНачала = ДобавитьМесяц(Элемент.ДатаВвода,12);
			ПрошлоЛет = 0;
			Пока ДатаНачала < ДатаДок Цикл
				ПрошлоЛет = ПрошлоЛет + 1;
				ДатаНачала = ДобавитьМесяц(ДатаНачала,12);
			КонецЦикла;
            ОсталосьЛет = ЛетИспользования - ПрошлоЛет;
			// определим сумму лет срока использования объекта
			СуммаЛет = 0;
			Пока ЛетИспользования > 0 Цикл
				СуммаЛет = СуммаЛет + ЛетИспользования;
				ЛетИспользования = ЛетИспользования - 1;
			КонецЦикла;
			// определим кумулятивный коэффициент и сумму износа
			КумулятивныйКоэффициент = ?(СуммаЛет = 0, 0, ОсталосьЛет / СуммаЛет);
			СуммаИзноса = Макс(КумулятивныйКоэффициент*(ПервСтоимость-Элемент.ЛиквидационнаяСтоимость)/12,0);
			//ПервыйМесяц (100%)
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйМесяц Тогда
			// начисление первого месяца (100 %) в документе ВводВЭксплуатацию
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйИПоследнийМесяц Тогда
			// начисление первого месяца (первые 50 %) в документе ВводВЭксплуатацию
		ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Налоговый Тогда
			//НМА
			Если Элемент.ВидНеоборотногоАктива = НМА Тогда
				Если Элемент.СрокИспользования = 0 Тогда
					глКомментарий(Строка(Элемент)+": не задан срок полезного использования. Износ не начислен!",0,,"!");
					Возврат 0;
				КонецЕсли;
				СуммаИзноса = Мин(ОстСтоимость,Окр(ПервСтоимостьКв / Элемент.СрокИспользования, 2));
				//Группа 1
			ИначеЕсли Элемент.Группа = 1 Тогда
				Если КонМесяца(ДатаДок) = КонКвартала(ДатаДок) Тогда 
					СуммаИзноса = Макс(Окр(ОстСтоимостьКв * (АмортГр[1]) * ПонижающийКоэффициент, 2)-(2*Окр(ОстСтоимостьКв * (АмортГр[1]/3) * ПонижающийКоэффициент, 2)),0);
				Иначе
					СуммаИзноса = Макс(Окр(ОстСтоимостьКв * (АмортГр[1]/3) * ПонижающийКоэффициент, 2),0);
				КонецЕсли;
				Если (СуммаИзноса > ОстСтоимость)
				Или (ОстСтоимостьКв < НМ*100) И (ОстСтоимостьКв > 0)
				Или (ОстСтоимость = 0) Тогда
					// сумма износа больше ост. стоимости или ост. стоимость меньше 100 необл. минимумов
					СуммаИзноса = ОстСтоимость;
				КонецЕсли;
				//Группа 2
			ИначеЕсли Элемент.Группа = 2 Тогда
				Если КонМесяца(ДатаДок) = КонКвартала(ДатаДок) Тогда 
					СуммаИзноса = Мин(ОстСтоимость,Окр(ОстСтоимостьКв * (АмортГр[2]) * ПонижающийКоэффициент, 2)-(2*Окр(ОстСтоимостьКв * (АмортГр[2]/3) * ПонижающийКоэффициент, 2)));	
				Иначе
					СуммаИзноса = Мин(ОстСтоимость,Окр(ОстСтоимостьКв * (АмортГр[2]/3) * ПонижающийКоэффициент, 2));
				КонецЕсли;
				//Группа 3
			ИначеЕсли Элемент.Группа = 3 Тогда
				Если КонМесяца(ДатаДок) = КонКвартала(ДатаДок) Тогда 
					СуммаИзноса = Мин(ОстСтоимость,Окр(ОстСтоимостьКв * (АмортГр[3]) * ПонижающийКоэффициент, 2)-(2*Окр(ОстСтоимостьКв * (АмортГр[3]/3) * ПонижающийКоэффициент, 2)));
				Иначе
					СуммаИзноса = Мин(ОстСтоимость,Окр(ОстСтоимостьКв * (АмортГр[3]/3) * ПонижающийКоэффициент, 2));
				КонецЕсли;
				//Группа 4
			ИначеЕсли Элемент.Группа = 4 Тогда
				Если КонМесяца(ДатаДок) = КонКвартала(ДатаДок) Тогда 
					СуммаИзноса = Мин(ОстСтоимость,Окр(ОстСтоимостьКв * (АмортГр[4]) * ПонижающийКоэффициент, 2)-(2*Окр(ОстСтоимостьКв * (АмортГр[4]/3) * ПонижающийКоэффициент, 2)));
				Иначе
					СуммаИзноса = Мин(ОстСтоимость,Окр(ОстСтоимостьКв * (АмортГр[4]/3) * ПонижающийКоэффициент, 2));
				КонецЕсли;
			КонецЕсли;
			Элемент.ЛиквидационнаяСтоимость = 0;
		КонецЕсли;                       
	КонецЕсли;
	
	// копейки
	Если ОстСтоимость - СуммаИзноса < Элемент.ЛиквидационнаяСтоимость Тогда
	    СуммаИзноса = Макс(0, ОстСтоимость - Элемент.ЛиквидационнаяСтоимость);
	КонецЕсли;
	
	Если Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйМесяц Тогда
		// Установим признак "ИзношеноПолностью" если применяется метод "100%"
		//ИзношеноПолностью = 1;
	ИначеЕсли Элемент.МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Налоговый Тогда
		// Установим признак "ИзношеноПолностью" если применяется "Налоговый" метода
		Если (ОстСтоимость - СуммаИзноса <= 0) Тогда
			ИзношеноПолностью = 1;	
		КонецЕсли;	
	Иначе
		// Установим признак "ИзношеноПолностью" если для всех остальных методов
		Если ((ОстСтоимость-СуммаИзноса) < 0) или (ОстСтоимость-СуммаИзноса<=Элемент.ЛиквидационнаяСтоимость) Тогда
			ИзношеноПолностью = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если (ИзношеноПолностью = 1) и (ОстСтоимость > 0) Тогда
		глКомментарий(СокрЛП(Элемент)+" : износ начислен полностью !",2,,"I");
	КонецЕсли;
	Возврат СуммаИзноса;
КонецФункции

// ===============================
// рассчитывает износ по всем субконто на одном счете - ОС, НМА, необоротные активы
Процедура РассчитатьИзносПоСчету(Сч, СчИзноса)
	Сч = ?(ТипЗначенияСтр(Сч)="Строка",СчетПоКоду(Сч),Сч);
	СчИзноса = ?(ТипЗначенияСтр(СчИзноса)="Строка",СчетПоКоду(СчИзноса),СчИзноса);
	
	СпрНА = СоздатьОбъект("Справочник.НеоборотныеАктивы");
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Если ПустоеЗначение(Подразделение)=0 Тогда
		// фильтр по подразделению
		Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения,Подразделение,2);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения);
	КонецЕсли;
	Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы); // ОС, НМА 
	
	Если НачКвартала(ДатаДок) = НачМесяца(ДатаДок) Тогда
		Ит.ВыполнитьЗапрос(НачКвартала(ДатаДок),,Сч);
	Иначе
		Ит.ВыполнитьЗапрос(НачКвартала(ДатаДок),КонМесяца(ДобавитьМесяц(ДатаДок,-1)),Сч);
	КонецЕсли;
	
	ИтИзн = СоздатьОбъект("БухгалтерскиеИтоги");
	ИтИзн.ИспользоватьРазделительУчета(Фирма);
	ИтИзн.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
	Если НачКвартала(ДатаДок) = НачМесяца(ДатаДок) Тогда
		ИтИзн.ВыполнитьЗапрос(НачКвартала(ДатаДок),,СчИзноса);
	Иначе
		ИтИзн.ВыполнитьЗапрос(НачКвартала(ДатаДок),КонМесяца(ДобавитьМесяц(ДатаДок,-1)),СчИзноса);
	КонецЕсли;
	
	Ит.ВыбратьСубконто(2);
	Пока Ит.ПолучитьСубконто(2) = 1 Цикл
		
		ТекПодразделение = Ит.Субконто(1);
		НА = Ит.Субконто(2);
		
		НА.ИспользоватьДату(НачМесяца(ДатаДок),1); 
		
		Если (НА.Состояние = Перечисление.СостоянияНеоборАктивов.НаКонсервации) 
		 или (НА.Состояние = Перечисление.СостоянияНеоборАктивов.НаМодернизации) Тогда
		    Продолжить;
		КонецЕсли;

		// на начало квартала (для метода "Налоговый")
		ПервСтоимостьКв = Ит.СНД("С");
		Кво = Ит.СНД("К");
		Если НачКвартала(ДатаДок) = НачМесяца(ДатаДок) Тогда
			// месяц - начало квартала
			ПервСтоимость = ПервСтоимостьКв;
		Иначе
			//на начало месяца
			ПервСтоимость = Ит.СКД("С");
			Кво = Ит.СКД("К");
		КонецЕсли;
		
		ИзносКв = 0;
		Износ = 0;
		Если ИтИзн.ПолучитьСубконто(1,,НА)=1 Тогда
			// на начало квартала (для метода "Налоговый")
			ИзносКв = ИтИзн.СНК("С");
			Если НачКвартала(ДатаДок) = НачМесяца(ДатаДок) Тогда
				// месяц - начало квартала
				Износ = ИзносКв;
			Иначе
				//на начало месяца
				Износ = ИтИзн.СКК("С");
			КонецЕсли;
		КонецЕсли;
		
		Если (Сч.Код = "10") Тогда
		    Если  Кво <> 1 Тогда
		        глНеПроводить(Контекст,Строка(Ит.Субконто(1))+": остаток количества на счете "+Сч+" не может быть отличным от 1!");
				Возврат;
		    КонецЕсли;
		КонецЕсли;
		
		ОстСтоимость = ПервСтоимость - Износ;
		ОстСтоимостьКв = ПервСтоимостьКв - ИзносКв;
		СуммаИзноса = РассчитатьИзносПоЭлементу(НА,ПервСтоимость,ОстСтоимость,ПервСтоимостьКв,ОстСтоимостьКв,СчИзноса,Сч);
		
		ТекСчетЗатрат = НА.СчетЗатрат;
		ТекВидЗатрат = НА.ВидЗатрат;
		Если ПустоеЗначение(ТекВидЗатрат)=1 Тогда
		    ТекВидЗатрат = ВидЗатрат;
		КонецЕсли;
		
		фАналитикаОК = 1;
		Если (ПустоеЗначение(ТекВидЗатрат)=1) или (ПустоеЗначение(ТекСчетЗатрат)=1) Тогда
		    глКомментарий(Строка(НА)+": не задан счет затрат или вид затрат",0,,"!");
			фАналитикаОК = 0;
		КонецЕсли;
		Если Сред(ТекСчетЗатрат.Код,1,2) = "23" Тогда
			Если глПроверитьАналитикуПоЗатратам(ТекПодразделение,НА.ВидДеятельности,,НА.Продукция,
				"а аналитика ""Подразделение"" для "+НА," реквизит ""Вид деятельности"" для "+НА," реквизит ""Заказ"" для "+НА," реквизит ""Продукция"" для "+НА)=0 Тогда
				фАналитикаОК = 0;
			КонецЕсли;
		КонецЕсли;
		Если фАналитикаОК = 0 Тогда
			глКомментарий("Износ по "+НА+" не начислен!",0,,"!");
			Продолжить;
		КонецЕсли;
		
		глПроводкаПоЗатратам(Контекст,ТекСчетЗатрат,СчИзноса,СуммаИзноса,"Начислен износ",,НА.ВидДеятельности,ТекПодразделение,ТекВидЗатрат,
		НА,,, ,,"НА");
		
		Если Сред(ТекСчетЗатрат.Код,1,2) = "23" Тогда
		    // нужно отразить затраты в регистре 
			Рег  = Регистр.ПроизводственныеЗатраты;
			Рег.Фирма			 = Фирма;
			Рег.ВидДеятельности	 = НА.ВидДеятельности;
			Рег.Подразделение	 = ТекПодразделение;
			Рег.Продукция		 = НА.Продукция;
			Рег.ВидЗатрат		 = ТекВидЗатрат;
			Рег.Сумма			 = СуммаИзноса;
			Рег.КодОперации		 = ОтражениеЗатрат;
			Рег.ДвижениеПриходВыполнить();
		КонецЕсли;
	КонецЦикла; // ОС, НМА ...
КонецПроцедуры

// ===============================
Процедура РассчитатьИзносНаСчетеУчета(Сч)
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Если ПустоеЗначение(Подразделение)=0 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения,Подразделение,2); // фильтр по подразделению
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения);
	КонецЕсли;
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения);
	Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы);
		
	Ит.ВыполнитьЗапрос(НачМесяца(ДатаДок),,Сч);
	
	Ит.ВыбратьСубконто(3);
	Пока Ит.ПолучитьСубконто(3)=1 Цикл
	    Элемент = Ит.Субконто(3);
		Элемент.ИспользоватьДату(НачМесяца(ДатаДок));
		
		Если (ПустоеЗначение(Элемент.ВидЗатрат)=1)
		Или  (ПустоеЗначение(Элемент.СчетЗатрат)=1)
		Тогда
		    глКомментарий(Строка(Элемент)+": не задан счет затрат или вид затрат",0,,"!");
			Продолжить;
		КонецЕсли;
		Если ПустоеЗначение(Элемент.ДатаВвода) = 1 Тогда
			глКомментарий(Строка(Элемент)+": Не задана дата ввода в эксплуатацию. Износ не начислен!",0,,"!");    
			Продолжить;
		КонецЕсли;
		Если Элемент.СрокИспользования = 0 Тогда
			глКомментарий(Строка(Элемент)+": не задан срок полезного использования. Износ не начислен!",0,,"!");
			Продолжить;
		КонецЕсли; 
		
		ДатаЛиквидации		= ДобавитьМесяц(Элемент.ДатаВвода, Элемент.СрокИспользования);
		ДокументАбсМесяц	= ДатаГод(ДатаДок)*12        + ДатаМесяц(ДатаДок);
		ЛиквидацияАбсМесяц	= ДатаГод(ДатаЛиквидации)*12 + ДатаМесяц(ДатаЛиквидации);
		ОсталосьМесяцев		= Макс(1, ЛиквидацияАбсМесяц - ДокументАбсМесяц + 1);
		
		ОстСтоимость = Ит.СНД("С");
		// только прямолинейное списание
		СуммаИзноса = Макс(0, (ОстСтоимость - Элемент.ЛиквидационнаяСтоимость)/ОсталосьМесяцев);
		
		глПроводкаПоЗатратам(Контекст,Элемент.СчетЗатрат,Сч,СуммаИзноса,"Начислен износ",,Элемент.ВидДеятельности,Ит.Субконто(1),Элемент.ВидЗатрат,
		Ит.Субконто(1),Ит.Субконто(2),Ит.Субконто(3), ,,"НА");
		
		Если Сред(Элемент.СчетЗатрат.Код,1,2) = "23" Тогда
		    // нужно отразить затраты в регистре 
			Рег  = Регистр.ПроизводственныеЗатраты;
			Рег.Фирма			 = Фирма;
			Рег.ВидДеятельности	 = Элемент.ВидДеятельности;
			Рег.Подразделение	 = Ит.Субконто(1);
			Рег.Продукция		 = Элемент.Продукция;
			Рег.ВидЗатрат		 = Элемент.ВидЗатрат;
			Рег.Сумма			 = СуммаИзноса;
			Рег.КодОперации		 = ОтражениеЗатрат;
			Рег.ДвижениеПриходВыполнить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура ПроводкиБухгалтерскийУчет()
	// по основным средствам
	РассчитатьИзносПоСчету("10","13.1");
	// по другим необоротным материальным активам
	РассчитатьИзносПоСчету("11","13.2");
	// по нематериальным активам
	РассчитатьИзносПоСчету("12","13.3");
	// гудвил
	РассчитатьИзносНаСчетеУчета("19.1");
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);

	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	
	НиО = СоздатьОбъект("Справочник.ШкалаСтавок");
	Если НиО.НайтиПоКоду("ПонижКоэф") = 1 Тогда
	    ПонижающийКоэффициент = НиО.ТекущийЭлемент().Ставка.Получить(ДатаДок);
	КонецЕсли;
	
	ЗаполнитьКоэфАморт();
	
	Если НалоговыйУчет = 1 Тогда
	    ПроводкиНалоговыйУчет();
	Иначе
		ПроводкиБухгалтерскийУчет();
	КонецЕсли;
	
	Операция.СуммаОперации = 0; // ?
	Операция.Содержание = Примечание;	
	Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
