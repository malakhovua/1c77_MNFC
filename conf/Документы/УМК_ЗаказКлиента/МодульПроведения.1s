//--- УМК Сандомирский В.Ю. (01.04.15) При резервировании товара не обращаем внимание на остатки
Перем ОстТоваров,РезТоваров;
Перем ПервыйДок;
Перем ЕстьВзвешенныеСтроки;
Перем МаксПроцОткл;

// ===========================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Фирма,"Фирма");
    глВыбранЛи(Контрагент,"Контрагент");
    глВыбранЛи(МестоХранения,"Место хранения");
	глВыбранЛи(ВидНДС,"Вид НДС");
  	глВсеВыбрано = ?(глВсеВыбрано = 0, 0, глПроверкаДублейСтрок(Контекст));
  	
	//глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС")); // --- Ц закоментил бо лишнее в этом документе 
	Если Константа.РазрешитьПревышениеКредита = Нет Тогда	
		ЗнакОплаты = -1;
		ВремРег = СоздатьОбъект("Регистры");
		глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,Фирма, ЗнакОплаты, Контрагент, ПервыйДок, Гривня, 0, 0);
		Долг = ВремРег.ВзаиморасчетыПокупателей.СводныйОстаток(Фирма, Контрагент, , , , , , "ДолгОсн");
		ТДолг = "";
		глПолучитьСуммуПросроченногоДолгаКонтрагента(Контрагент, Долг, ТДолг, ДатаДок, 0, 0, -1);
		Если ТДолг.Итог("СуммаП") > 0 Тогда
			Сообщить("Сумма просроченного долга по контрагенту: " + Строка(ТДолг.Итог("СуммаП")));
			глВсеВыбрано = 0;
		КонецЕсли;
	КонецЕсли;
	
    Возврат глВсеВыбрано;
КонецФункции

// ===========================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
    глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);
    глВыбранЛи(ТМЦ.ВидДеятельности,"Вид деятельности в карточке ТМЦ",НомерСтроки);
	Если (Контрагент.ПроверятьДопКво = 1) И (ДокументВзвешен = 1) Тогда
		глВыбранЛи(КоличествоКлиента, "Доп. количество", НомерСтроки);
	КонецЕсли;
	
	глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,ТМЦ,НомерСтроки,1)=Да, глВсеВыбрано, 0);
	Если Лев(ПримечаниеСтроки, 2) = "@@" Тогда
		Сообщить("В строке: " + Строка(НомерСтроки) + " в примечании информация, что вид упаковки не был найден при импорте. Укажите правильный вид упаковки");
		глВсеВыбрано = 0;
	КонецЕсли;
	Если (ЕстьВзвешенныеСтроки = 1) И (СтрокаВзвешена <> Да) Тогда
		глПоказатьПредупреждение("В документе не взвешена строка: " + Строка(НомерСтроки) + ", продукция: " + Строка(ТМЦ), 3);
	КонецЕсли;

	Если СтрокаВзвешена	= Да Тогда
		Разница = КвоФакт - КвоПересчет;
		Разница = ?(Разница < 0, -Разница, Разница);
		ПроцентРазницы = Окр(Разница / КвоПересчет * 100, 2);
		Если ПроцентРазницы > МаксПроцОткл Тогда
			Сообщить("Взвешенное количество товара: " + Строка(ТМЦ) + " в строке: " + Строка(НомерСтроки) + " сильно отличается от количество в заказе. Проверьте правильность");
		КонецЕсли;
		РВУ = глПолучитьРазрешенныйВидУпаковки(ТМЦ, ВидУпаковки);
		Если ПустоеЗначение(РВУ) = 0 Тогда
			Если ПустоеЗначение(РВУ.ТМЦПодарок) = 0 Тогда
				глВыбранЛи(КвоУпаковок,"К-во упаковок",НомерСтроки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	глПроверитьОграниченияНаОтгрузкуПоКонтрагенту(Контекст);		
	
    Возврат глВсеВыбрано;
КонецФункции
                       
// ===============================
Процедура ОбработкаПроведения(ЧастичноПровести = 0)
	глКомментарий("Начало",2,Контекст);
	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;

	МаксПроцОткл = Константа.МаксПроцОтклоненияЗаказа;
	ЕстьВзвешенныеСтроки = 0;
	ФлагЕстьОшибки = 0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл		
		Если СтрокаВзвешена = Да Тогда
			ЕстьВзвешенныеСтроки = 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;		
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл		
		Если ПроверкаСтроки() = 0 Тогда
			ФлагЕстьОшибки = 1;
		КонецЕсли;	
	КонецЦикла;	

	Если ФлагЕстьОшибки = 1 Тогда
		глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	//--- УМК Сандомирский В.Ю. (01.04.15) Движения по регистру резерв 
	Если (ПустоеЗначение(Константа.УМК_ДатаНачалаРаботыРезервов) <> 1) 
		И (Константа.УМК_ДатаНачалаРаботыРезервов <= ДатаДок)  Тогда 	
	
		ВыбратьСтроки();
		Пока ПолучитьСтроку()>0 Цикл
				
			Если ТМЦ.ВидТМЦ=Перечисление.ВидыТМЦ.Услуга Тогда
				// услуги не резервируем
				Продолжить;
			КонецЕсли;       
			
			Если ПустоеЗначение(ВидУпаковки) = 1 Тогда
				ТекВидУпаковки = НетУп;
			Иначе
				ТекВидУпаковки = ВидУпаковки;
			КонецЕсли;
	
			Если КвоШт > 0 Тогда
				Если (ПустоеЗначение(ВидУпаковки) = 1) ИЛИ (ВидУпаковки = НетУп)  Тогда
					ТекКг = Кво + 	(КвоШт * ТМЦ.Вес);
				Иначе	
					РазрешенныйВидУпаковкиТМЦ =  глПолучитьРазрешенныйВидУпаковки(ТМЦ,ВидУпаковки);
					Если ПустоеЗначение(РазрешенныйВидУпаковкиТМЦ) <> 1 Тогда
						ТекКг = Кво + 	(КвоШт * РазрешенныйВидУпаковкиТМЦ.ВесУпаковки.Получить(ДатаДок));	
					КонецЕсли;		
				КонецЕсли;
			Иначе
				ТекКг = Кво;	
			КонецЕсли;
					
			Регистр.Резервы.ПривязыватьСтроку(НомерСтроки);
			Регистр.Резервы.Фирма 			= Фирма;
			Регистр.Резервы.ТМЦ 			= ТМЦ;
			Регистр.Резервы.ВидУпаковки 	= ТекВидУпаковки;
			Если ДатаДок >= '01.06.2016' Тогда
				Регистр.Резервы.ФормаУпаковки	= глПолучитьФормуУпаковки(ТекВидУпаковки, ТМЦ, ДатаДок);			
			КонецЕсли;
			Регистр.Резервы.ДокументРезерва	= ТекущийДокумент();
			Если Ед.Единица = ТМЦ.БазоваяЕдиница Тогда
				Регистр.Резервы.Резерв			= ?((Ед.Единица = Константа.ЕдиницаШтучногоТовара) И (КвоШт <> 0), КвоШт, ТекКг);
			Иначе
				Регистр.Резервы.Резерв			= КвоПересчет;
			КонецЕсли;
			Если ТМЦ.УчетДопКво = 1 Тогда
				Регистр.Резервы.ДопКво		= КвоШт;
			Иначе
				Регистр.Резервы.ДопКво		= 0;
			КонецЕсли;
			Регистр.Резервы.ДвижениеПриходВыполнить();
				
		КонецЦикла;  
	
	КонецЕсли;

	РежимПроведения=0;
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры