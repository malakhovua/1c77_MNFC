Перем ОстаткиТоваров;
Перем НачальнаяДатаДокумента;

// ===============================
Функция УстДоступность()   
	Форма.Заголовок(глЗаголовок(Контекст,"Возврат ТМЦ из производства"));
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	
	Возврат "";
КонецФункции

// ======================================
Процедура ИзмФирма()
	Если Фирма <> глВосстановитьЗначение(Контекст,"Фирма") Тогда
		Если КоличествоСтрок()>0 Тогда
			Рез = Вопрос("Фирма была изменена. Удалить строки?","Да+Нет");
			Если Рез ="Да" Тогда
				УдалитьСтроки();
			Иначе
				Фирма = глВосстановитьЗначение(Контекст,"Фирма");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		глСохранитьЗначение(Контекст,"Фирма",Фирма);
	КонецЕсли;
КонецПроцедуры                      

// ===============================
Процедура ИзмМестоХранения()
	Если МестоХранения.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;   
	
	глСохранитьЗначение(Контекст,"МестоХранения",МестоХранения);
КонецПроцедуры

// ===============================
Процедура ИзмПродукция()
	Если Продукция.Выбран()=1 Тогда
		Если Продукция = ТМЦ Тогда
			Предупреждение("Полуфабрикат не может содержать себя же в качестве затраты!");
			Продукция = 0;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмСуммаДав()
	Если (глЭтоДавальческийЗаказ(Заказ) =1)  Тогда
		Если ((ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Продукция)	или (ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Полуфабрикат))  Тогда
			Если СуммаДав>СуммаБезНДС Тогда
			    СуммаДав = СуммаБезНДС;
			КонецЕсли;
		Иначе
			Если СуммаДав>0 Тогда
			    СуммаДав = СуммаБезНДС;
			КонецЕсли;
		КонецЕсли;
	Иначе
		СуммаДав = 0;
	КонецЕсли;
КонецПроцедуры   

// ======================================
Процедура ИзмСуммаБезНДС()
    СуммаДав = СуммаБезНДС;
	ИзмСуммаДав();
КонецПроцедуры   

// ======================================
Процедура ИзмЦенаБезНДС()
	СуммаБезНДС = ЦенаБезНДС * Кво;
	ИзмСуммаБезНДС();
КонецПроцедуры

// ======================================
Процедура ИзмЕд()
	Перем	СтарКоэф;
	
	СтарКоэф = Коэффициент;
	Коэффициент = Ед.Коэффициент;
	Если СтарКоэф <> 0 Тогда
		// пересчитаем цены
		ЦенаБезНДС = ЦенаБезНДС * Коэффициент / СтарКоэф;
		ИзмЦенаБезНДС();
	КонецЕсли;
КонецПроцедуры    

// ===============================
Процедура ИзмТМЦ()          
	Если глПроверкаТовараВДокументе(Контекст,ТМЦ) = Нет Тогда
		ТМЦ = глВосстановитьЗначение(Контекст,"ТМЦ");
	ИначеЕсли ТМЦ.Выбран()=1 Тогда
		Если ПустоеЗначение(Ед) = 1 Тогда
			глУстановкаБазЕд(Контекст,ТМЦ,ТМЦ.ЕдиницаПоУмолчанию.Единица);
		ИначеЕсли ТМЦ <> Ед.Владелец Тогда
			глУстановкаБазЕд(Контекст,ТМЦ,ТМЦ.ЕдиницаПоУмолчанию.Единица);
		КонецЕсли;
		Если Кво = 0 Тогда
			Кво = 1;
		КонецЕсли;
		
		ВидДеятельности = ТМЦ.ВидДеятельности;
		ВидЗатрат = ТМЦ.ВидЗатрат;
		
		Если ЦенаБезНДС = 0 Тогда
			ЦенаБезНДС = ТМЦ.УчетнаяЦена.Получить(?(Выбран()=1, ТекущийДокумент(), ДатаДок)) * Коэффициент;
			ИзмЦенаБезНДС();
		КонецЕсли;
	Иначе                         
		ВидДеятельности = 0;
		ВидЗатрат = 0;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ОбработкаПодбора(Выб)
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	ИзмФирма();
	
	МестоХранения = глВосстановитьЗначение(Контекст,"ОсновнойСклад");
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1); 
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
//		Форма.кДействия.Доступность(0);          
		Форма.кПодбор.Доступность(0);          
		Форма.кФирма.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");		
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;
	             
	Если Константа.ПоказыватьОстаткиТМЦ = Нет Тогда
	    Форма.тОстатокПолный.Видимость(0);
	    Форма.тОстатокПоСкладу.Видимость(0);
	КонецЕсли;

	глСохранитьЗначение(Контекст,"Фирма",Фирма);
	глСохранитьЗначение(Контекст,"МестоХранения",МестоХранения);
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры
                             
// ===============================
Процедура ПриНачалеРедактированияСтроки()
	глСохранитьЗначение(Контекст,"ТМЦ",ТМЦ);
КонецПроцедуры



//====================================================================== УМК Сандомирский В.Ю. (*) //--- заголовок FormEx_ПланРаскраски
Функция Раскраска()
	ТекстПлана = "(BRUSH_S[" + Строка(100*255*100) + "])()()"; //--- добавляя колонку вначало документа до "раскрашек" часов добавить "()"
	Возврат  ТекстПлана;	
КонецФункции



// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
