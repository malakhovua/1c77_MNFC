Перем Фрм;      
Перем НачальнаяДатаДокумента;

// ===============================
Процедура ИзмРСчет()
	Если РСчет.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры
             
// ===============================
Процедура ИзмСуммаСНДС()
	Ст = Константа.БазНДС.Ставка.Получить(ДатаДок);
	НДС = СуммаСНДС*Ст/(1+Ст);
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.Заголовок(глЗаголовок(Контекст,"Платежное требование-поручение"));
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмДоговор()
	Если Договор.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	ТекДок = ?(Выбран() = 1, ТекущийДокумент(), ДатаДок);
	Если Лев(Счет.Код,2) = "36" Тогда
		ЗнакОплаты = -1;
	ИначеЕсли Лев(Счет.Код,2) = "63" Тогда		
		ЗнакОплаты = 1;
	КонецЕсли;	
	СуммаСНДС = глДолгКонтрагента(Контекст, ЗнакОплаты, Фирма, Контрагент, Договор ,);
	ИзмСуммаСНДС();
КонецПроцедуры

// ===============================
Процедура ОчиститьЗаказ()
	Если Договор.Выбран() = 1 Тогда
	    Договор = 0;
		СуммаСНДС = 0;
		НДС = 0;
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура УстРСчетКонтрагента()
	Если РСчетКонтрагента.Выбран() = 0 Тогда
	    СпрДС = СоздатьОбъект("Справочник.ДенежныеСчета");
		СпрДС.ИспользоватьВладельца(Контрагент);
		СпрДС.ВыбратьЭлементы();
		ДС = 0; КвоДС = 0;
		Пока СпрДС.ПолучитьЭлемент() = 1 Цикл
			Если СпрДС.Валюта <> Рсчет.Валюта Тогда
			    Продолжить;
			КонецЕсли;
			ДС = СпрДС.ТекущийЭлемент();
			КвоДС = КвоДС + 1;
		КонецЦикла;
		Если КвоДС = 1 Тогда
		    РСчетКонтрагента = ДС;
		КонецЕсли;
	КонецЕсли;      
КонецПроцедуры 

// ===============================
Процедура ИзмКонтрагент()
	Если Контрагент.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;    
	Если ПустоеЗначение(Договор) = 0 Тогда
	    Если Договор.Контрагент <> Контрагент Тогда
			Договор = 0;
	    КонецЕсли;
	КонецЕсли;     
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если ДокументОснование.Контрагент <> Контрагент Тогда
			ДокументОснование = 0;
		КонецЕсли;	
	КонецЕсли;	
	Если (Константа.ПодставлятьОсновнойДоговор=Да)
	И (ПустоеЗначение(Контрагент.БазДоговор)=0) 
	И (ПустоеЗначение(Договор)=1) Тогда
		Если Фирма = Контрагент.БазДоговор.Фирма Тогда
			Договор = Контрагент.БазДоговор;
		КонецЕсли;	
	КонецЕсли;                              
	Если РСчетКонтрагента.Владелец <> Контрагент Тогда
	     РСчетКонтрагента = 0;
	КонецЕсли;
	УстРСчетКонтрагента();
	ИзмДоговор();
КонецПроцедуры

// ===============================
Процедура ИзмНазначениеПлатежа()
	Если НазначениеПлатежа.Выбран() = 0 Тогда
	    Возврат;
	КонецЕсли;
	Содержание = НазначениеПлатежа.ПолнНаименование;
	Если НДС = 0 Тогда
	    Содержание = Содержание+"
	    |Без ПДВ.";
	Иначе
	    Содержание = Содержание+"
	    |В т.ч. ПДВ "+СокрЛП(Формат(НДС,"Ч14.2,'"))+" грн.";
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	Если РСчет.Владелец <> Фирма Тогда
		РСчет = Фирма.РС;
		ИзмРСчет();
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура Печать()
	Если РСчет.Валюта <> Гривня Тогда
	    Предупреждение("Предусмотрена печать только гривневых платежек!");
	    Возврат;
	КонецЕсли;
	Таб = СоздатьОбъект("Таблица");
	ИмяФайлаПечатнойФормы = КаталогИБ()+"ExtForms\PrnForms\PTP_ukr.mxl";
	Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы) = 1 Тогда
		Таб.ИсходнаяТаблица(ИмяФайлаПечатнойФормы);
	Иначе
		Таб.ИсходнаяТаблица("Таблица_Укр");
	КонецЕсли;
	// строго на украинском!
	глУстПропись(Гривня, "у"); 
	Эк = 2;
	СуммаСНДС_стр = Сред(Формат(Цел(СуммаСНДС), "ЧПД"),1,Найти(Формат(Цел(СуммаСНДС), "ЧПД"),"гр") - 2) + " грн. " + Формат((СуммаСНДС - Цел(СуммаСНДС))*100,"Ч(0)2") + " коп.";
	// номер платежного требования-поручения возьмем равным номеру,
	// стоящему в номере документа после префикса
	Номер = Сред(НомерДок, Найти(НомерДок, "-")+1);

	Если ВвестиЧисло(Эк,"Введите количество экземпляров",1,0) = 0 Тогда
		Возврат;
	КонецЕсли;  
	Если ПустоеЗначение(ДатаДок) = 0 Тогда
		ЧислоДаты = ?(ДатаЧисло(ДатаДок)<10,""""+"0"+ДатаЧисло(ДатаДок)+"""",""""+ДатаЧисло(ДатаДок)+"""");
		МесяцДаты = Прав(Формат(ДатаДок,"Д ДДММММГГГГ"),СтрДлина(Формат(ДатаДок,"Д ДДММММГГГГ"))-2);
	КонецЕсли;
    Для Ном = 1 По Эк Цикл
		Если Ном > 1 Тогда
		    Таб.НоваяСтраница();
		КонецЕсли;
		Таб.ВывестиСекцию("Форма");
	КонецЦикла;
	Таб.Опции(0,0,0,0,"");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Платежное требование-поручение","");
КонецПроцедуры

// ===============================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	РСчет = Фирма.РС;
	ИзмРСчет();
	Счет = СчетПоКоду("36.1");          
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()    
	НачальнаяДатаДокумента = ДатаДок;
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
	Форма.Счет.ВыборГруппы(0);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кДействия.Доступность(0);
		
		Форма.кФирма.Доступность(0);
		Форма.кОсн.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры       

// ===============================
Процедура ИзмДокументОснование()                      
	Если ПустоеЗначение(ДокументОснование) = 1 Тогда
		Возврат;
	КонецЕсли;	
	глЗаполнитьШапкуНаОсн(Контекст,ДокументОснование);
КонецПроцедуры 

// ===============================
Процедура ВводНаОсновании(Док)
	Если Док.Валюта <> Гривня Тогда
	    Предупреждение("Предусмотрен ввод только гривневых платежек!");
    	СтатусВозврата(0);
	КонецЕсли;
	Фирма = Док.Фирма;
	глУстановитьНомер(Контекст);
	РСчет = Фирма.РС;
	ИзмРСчет();
	Счет = ?(РСчет.Валюта=Гривня,СчетПоКоду("36.1"),СчетПоКоду("36.2"));
	Контрагент = Док.Контрагент;
	УстРСчетКонтрагента();
	Если Док.Вид() = "Договор" Тогда
		СуммаСНДС = Док.СуммаСНДС;
		ИзмСуммаСНДС();
		Договор = Док;
	Иначе
		// расходная накладная или оказание услуг
		СуммаСНДС = Док.Итог("СуммаСНДС");
		НДС = Док.Итог("НДС");
		ДокументОснование = Док;
		ИзмДокументОснование();
	КонецЕсли;   
КонецПроцедуры      

// ===============================
Процедура ВыборОснования()  
	// Процедура по кнопке редактирования основания в докумнете
	СтарДоговор = Договор;
	СтарОснование = ДокументОснование;
	
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если (ДокументОснование <> СтарОснование)
		И (ДокументОснование.Вид() <> "РасходыНаПриобретение") Тогда
			ВводНаОсновании(ДокументОснование);	
		КонецЕсли;	
	КонецЕсли;	          
КонецПроцедуры	

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале");