Перем КассовыйМетодБонусы;
// ********************
//
Процедура ОбработкаПроведения()
	
	глВсеВыбрано = 1;
	
	
	глВыбранЛи(Контрагент,"Контрагент");
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(СчетКонтрагента,"Счет Контрагента");
	глВыбранЛи(СчетКорреспондент,"Счет Корреспондент");
	глВыбранЛи(ПокупательПоставщик,"Вид клиента");
	глВыбранЛи(СуммаДолга,"Сумма долга");
	глВыбранЛи(ПриходРасходДолга,"Вид прихода-расхода долга");
	
	Если глВсеВыбрано = 0 Тогда
		глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	
	КонтрагентРасчетов = Контрагент;
	
	РегистрРасчетов = ?(ПокупательПоставщик = Перечисление.ВидыКлиентов.Покупатель,Регистр.ВзаиморасчетыПокупателей,Регистр.ВзаиморасчетыПоставщиков);
	
	РегистрРасчетов.Фирма = Фирма;
	РегистрРасчетов.Контрагент = Контрагент;
	РегистрРасчетов.Договор = Договор;
	РегистрРасчетов.Счет = СчетКонтрагента;
	РегистрРасчетов.Валюта = Гривня;
	РегистрРасчетов.Долг = СуммаДолга;
	РегистрРасчетов.ДолгОсн = СуммаДолга;
	
	Если ПриходРасходДолга = Перечисление.ПлюсМинус.Плюс Тогда
		РегистрРасчетов.ДвижениеПриходВыполнить();
	Иначе
		РегистрРасчетов.ДвижениеРасходВыполнить()
	КонецЕсли;
	
	
	//бу
	Если НеФормироватьПроводкиБУ = 0 Тогда
		Если ПокупательПоставщик = Перечисление.ВидыКлиентов.Покупатель Тогда //Проводки по кредиту
			глПроводка(Контекст,СчетКорреспондент,СчетКонтрагента, РегистрРасчетов.Долг,"Коригування боргу",,Субконто1,Субконто2,Субконто3,Контрагент,Договор,,,,"НК",,,1);
		Иначе //Проводки по дебету
			глПроводка(Контекст,СчетКонтрагента,СчетКорреспондент, РегистрРасчетов.Долг,"Коригування боргу",, Контрагент,Договор,,Субконто1,Субконто2,Субконто3,,,"НК",,,1);
		КонецЕсли;
	КонецЕсли;
	
	глЗаписатьПроводкиВОперацию(Контекст);
	Операция.СуммаОперации = СуммаДолга;
	Операция.Содержание = Примечание;
		
	//Бонусы
	Если НачислятьБонусы = 1 Тогда
		Если КассовыйМетодБонусы = 1 Тогда	
			ОсновнойПартнер = глОсновнойКонтрагентИерархии(Контрагент.Родитель, Контрагент);
			Если ПустоеЗначение(ОсновнойПартнер) = 0 Тогда
				КонтрагентРасчетов = ОсновнойПартнер;
			КонецЕсли;
		КонецЕсли;
		
		// + umk 
		БазаРасчета = СуммаДолга;
		
		Если КассовыйМетодБонусы = 1 Тогда
			//Выполнить движения по остаткам долгов для бонусов рассчитанных по отгрузке
			глПогаситьДолгОстаткиБонусов(Контекст, КонтрагентРасчетов, СуммаДолга, 1, Договор);
			
			Долг = глОстатокДолгаВзаиморасчетыБонусы(ТекущийДокумент(), Фирма, КонтрагентРасчетов, Договор, Гривня);
			
			Если Долг < СуммаДолга Тогда
				БазаРасчета = СуммаДолга - Долг;
			Иначе
				БазаРасчета = 0;
			КонецЕсли;
		КонецЕсли;
		
		глНачислитьБонусы(Контекст, Контрагент, Договор, 1, БазаРасчета,,1);
		
	КонецЕсли; //Бонусы
	
	Операция.Записать();
	
КонецПроцедуры

КассовыйМетодБонусы = ?((ПустоеЗначение(Константа.КассовыйМетодРасчетаБонусов)=0) И (ДатаДок>=Константа.КассовыйМетодРасчетаБонусов),1,0);
