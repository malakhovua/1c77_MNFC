Перем ВидПокупатель, ВидПоставщик;
Перем НачальнаяДатаДокумента;

// ===============================
Функция УстДоступность()
	
    Форма.СчетПоставщика.Доступность(1);
	Форма.СчетПокупателя.Доступность(1);

	Если ВидКонтрагента = ВидПоставщик Тогда
	    Форма.СуммаВалПоставщик.Доступность(1);
		Форма.СуммаГрнПоставщик.Доступность(1);
	    Форма.СуммаВалПокупатель.Доступность(0);
		Форма.СуммаГрнПокупатель.Доступность(0);
	Иначе
	    Форма.СуммаВалПоставщик.Доступность(0);
		Форма.СуммаГрнПоставщик.Доступность(0);
	    Форма.СуммаВалПокупатель.Доступность(1);
		Форма.СуммаГрнПокупатель.Доступность(1);
	КонецЕсли;
	
    Форма.Заголовок(глЗаголовок(Контекст,"Взаимозачет"));	
	Возврат "";
КонецФункции
    
// ===============================
Процедура ПриРедактированииНовойСтроки()
	ВидКонтрагента = ВидПокупатель;
	глСохранитьЗначение(Контекст,"Счет",Счет);
КонецПроцедуры

// ===============================
Процедура ПриНачалеРедактированияСтроки()
	глСохранитьЗначение(Контекст,"Счет",Счет);
КонецПроцедуры

// ===============================
Процедура ИзмСуммаВалПоставщик()                                                 
	Если Счет.Выбран() = 1 Тогда
		СуммаГрнПоставщик = глПересчет(СуммаВалПоставщик,Валюта,Гривня,Счет.ДатаДок);
	Иначе
		СуммаГрнПоставщик = глПересчет(СуммаВалПоставщик,Валюта,Гривня,ДатаДок);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмСуммаВалПокупатель()
	Если Счет.Выбран() = 1 Тогда
		СуммаГрнПокупатель = глПересчет(СуммаВалПокупатель,Валюта,Гривня,Счет.ДатаДок);
	Иначе                                                                              
		СуммаГрнПокупатель = глПересчет(СуммаВалПокупатель,Валюта,Гривня,ДатаДок);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмСчет()
	Перем НДСЗак;
	Если Счет.Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	Если Счет.Фирма <> Фирма Тогда
		глКомментарий("Документ взаиморасчетов принадлежит другой фирме!",0,,"!");
		Счет= глВосстановитьЗначение(Контекст,"Счет");
		Возврат;
	КонецЕсли;
	Если (Найти("Счет,СчетВходящий,ПриходнаяНакладнаяЗапасы,ПриходнаяНакладнаяПрочие,ПриходнаяНакладнаяГТД,РасходнаяНакладная,ОказаниеУслуг,УслугиСтороннихОрганизаций,УслугиСтороннихОрганизацийПроизвХарактера,ЛиквидацияНеоборАктивов",Счет.Вид()) = 0) Тогда
		глКомментарий("Выберите документ по которому проходят взаиморасчеты!",0,,"!");
		Счет= глВосстановитьЗначение(Контекст,"Счет");
		Возврат;
	КонецЕсли;
КонецПроцедуры
                     
// ===============================
Процедура ИзмВидКонтрагента()
	Если ВидКонтрагента = ВидПокупатель Тогда
	    СуммаВалПоставщик = 0;
		СуммаГрнПоставщик = 0;
		Если ПустоеЗначение(Счет) = 0 Тогда
			Если Счет.Контрагент <> Покупатель Тогда
			    Счет= 0;
			КонецЕсли;
		КонецЕсли;
	Иначе
	    СуммаВалПокупатель = 0;
		СуммаГрнПокупатель = 0;
		Если ПустоеЗначение(Счет) = 0 Тогда
			Если Счет.Контрагент <> Поставщик Тогда
				Счет = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
                                 
// ===============================
Процедура ИзмПокупатель()
	Если ПустоеЗначение(Поставщик)=1 Тогда
	    Поставщик = Покупатель;
	КонецЕсли;
	
	Если ПустоеЗначение(Покупатель)=0 Тогда
	    Если Покупатель.ВидКонтрагента = Перечисление.ВидыКонтрагентов.Нерезидент Тогда
			Если СчетПокупателя = СчетПоКоду("361") Тогда
			    СчетПокупателя = СчетПоКоду("362");
			КонецЕсли;
		Иначе
			Если СчетПокупателя = СчетПоКоду("362") Тогда
			    СчетПокупателя = СчетПоКоду("361");
			КонецЕсли;
	    КонецЕсли;
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ВидКонтрагента = ВидПокупатель Тогда
			Если ПустоеЗначение(Счет) = 0 Тогда
				Попытка
					Если Счет.Контрагент <> Покупатель Тогда
						Счет = ПолучитьПустоеЗначение("Документ");
						СуммаВалПокупатель = 0;
						СуммаГрнПокупатель = 0;
					КонецЕсли;
				Исключение КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура ИзмПоставщик()
	Если ПустоеЗначение(Покупатель)=1 Тогда
	    Покупатель = Поставщик;
	КонецЕсли;
	Если ПустоеЗначение(Поставщик)=0 Тогда
	    Если Поставщик.ВидКонтрагента = Перечисление.ВидыКонтрагентов.Нерезидент Тогда
			Если СчетПоставщика = СчетПоКоду("631") Тогда
			    СчетПоставщика = СчетПоКоду("632");
			КонецЕсли;
		Иначе
			Если СчетПоставщика = СчетПоКоду("632") Тогда
			    СчетПоставщика = СчетПоКоду("631");
			КонецЕсли;
	    КонецЕсли;
	КонецЕсли;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ВидКонтрагента = ВидПоставщик Тогда
			Если ПустоеЗначение(Счет) = 0 Тогда
				Попытка
					Если Счет.Контрагент <> Поставщик Тогда
						Счет = ПолучитьПустоеЗначение("Документ");
						СуммаВалПоставщик = 0;
						СуммаГрнПоставщик = 0;
					КонецЕсли;
				Исключение КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры                  

// ===============================
Процедура ИзмВалюта()
	Если Валюта <> глВосстановитьЗначение(Контекст,"Валюта",Валюта) Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			
			Если Счет.Выбран() = 1 Тогда
				СуммаВалПоставщик = глПересчет(СуммаГрнПоставщик,Гривня,Валюта,Счет.ДатаДок);
				СуммаВалПокупатель = глПересчет(СуммаГрнПокупатель,Гривня,Валюта,Счет.ДатаДок);
			Иначе                                                                              
				СуммаВалПоставщик = глПересчет(СуммаГрнПоставщик,Гривня,Валюта,ДатаДок);
				СуммаВалПокупатель = глПересчет(СуммаГрнПокупатель,Гривня,Валюта,ДатаДок);
			КонецЕсли;
			
		КонецЦикла;
		глСохранитьЗначение(Контекст,"Валюта",Валюта);
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ИзмФирма()
	Если Фирма <> глВосстановитьЗначение(Контекст,"Фирма") Тогда
		Если КоличествоСтрок()>0 Тогда
			Рез = Вопрос("Фирма была изменена. Удалить строки?","Да+Нет");
			Если Рез ="Да" Тогда
				УдалитьСтроки();
			Иначе
				Фирма = глВосстановитьЗначение(Контекст,"Фирма");
				глУстановитьНомер(Контекст);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		глСохранитьЗначение(Контекст,"Фирма",Фирма);
	КонецЕсли;
КонецПроцедуры                      

// ===============================
Процедура ПриЗаписи()
	Автор = глПользователь;
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
//		Форма.кДействия.Доступность(0);          
		Форма.кФирма.Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	глСохранитьЗначение(Контекст,"Валюта",Валюта);
	глСохранитьЗначение(Контекст,"Фирма",Фирма);
КонецПроцедуры

// ======================================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
	СчетПоставщика = СчетПоКоду("631");
	СчетПокупателя = СчетПоКоду("361");
	Валюта = Гривня;
	ВидНДСПок = Константа.БазНДС;
	ВидНДСПост = Константа.БазНДС;
КонецПроцедуры 

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр) 
	Если Рекв = "Счет" Тогда 
		Фрм=СоздатьОбъект("СписокЗначений");
		Если ВидКонтрагента = ВидПоставщик Тогда
			Контрагент = Поставщик;
		Иначе
			Контрагент = Покупатель;
		КонецЕсли;
		Если (ПустоеЗначение(Контрагент) = 0) Тогда
			Фрм.ДобавитьЗначение(Контрагент,"Контрагент");
		Иначе
			Фрм.ДобавитьЗначение(ПолучитьПустоеЗначение("Справочник.Контрагенты"),"Контрагент");
		КонецЕсли;                                                         			   
		
		ОткрытьФормуМодально("Журнал.ПолныйЖурнал.ФормаДляВыбора",Фрм,);
		Счет = Фрм.Получить("ДокументОснование");
		ИзмСчет(); 
		ФлагСтандОбр = 0;
	ИначеЕсли Рекв="СчетПоставщика" Тогда 
		ФлагСтандОбр = 0;             
		ВыбСчет = СчетПоставщика;
		Если глСчетаПоставщиковПокупателей.ВыбратьЗначение(ВыбСчет,"Выберите счет",1,,2) = 1 Тогда
			СчетПоставщика = СчетПоКоду(ВыбСчет);	
		КонецЕсли;	
	ИначеЕсли Рекв="СчетПокупателя" Тогда 
		ФлагСтандОбр = 0;             
		ВыбСчет = СчетПокупателя;
		Если глСчетаПоставщиковПокупателей.ВыбратьЗначение(ВыбСчет,"Выберите счет",1,,2) = 1 Тогда
			СчетПокупателя = СчетПоКоду(ВыбСчет);	
		КонецЕсли;	
	ИначеЕсли (Рекв="ВидНДСПок") или (Рекв="ВидНДСПост") Тогда
	    глВыбратьНДС(Контекст,Рекв);                
		ФлагСтандОбр = 0;	
	ИначеЕсли Рекв = "ДоговорПокупателя" Тогда	// выбираем договор
		Фрм = СоздатьОбъект("СписокЗначений");
		Фрм.ДобавитьЗначение("ДоговораКонтрагентов","ГрафаОтбора");
		Фрм.ДобавитьЗначение(Покупатель,"Контрагент");
		ОткрытьПодбор("Журнал.ПолныйЖурнал","ДляВыбораДоговоровИСчетов",Фрм,0,ДоговорПокупателя);
		ФлагСтандОбр=0;
	ИначеЕсли Рекв = "ДоговорПоставщика" Тогда	// выбираем договор
		Фрм = СоздатьОбъект("СписокЗначений");
		Фрм.ДобавитьЗначение("ДоговораКонтрагентов","ГрафаОтбора");
		Фрм.ДобавитьЗначение(Поставщик,"Контрагент");
		ОткрытьПодбор("Журнал.ПолныйЖурнал","ДляВыбораДоговоровИСчетов",Фрм,0,ДоговорПоставщика);
		ФлагСтандОбр=0;
	КонецЕсли;
КонецПроцедуры            

ВидПокупатель = Перечисление.ВидыКлиентов.Покупатель;
ВидПоставщик = Перечисление.ВидыКлиентов.Поставщик;

// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
