Перем НомерПоследнейСтроки;
Перем НДСПервоеСобытие, НДСВтороеСобытие, ВзаиморасчетыГрн; // для исправления ошибок при округлениях
Перем ВремРег;
Перем спОтбор;
Перем ПервыйДок, ПоЗаказу;

// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");  
	глВыбранЛи(Контрагент,"Контрагент");
	глВыбранЛи(ВидТорговли,"Вид торговли");
	глВыбранЛи(МестоХранения,"Место хранения");			
    глВыбранЛи(СубконтоВалДох,"Субконто валовых доходов");
	глВыбранЛи(Валюта,"Валюта");     
	глВыбранЛи(ВидНДС,"Вид НДС");
  	глВсеВыбрано = ?(глВсеВыбрано = 0, 0, глПроверкаДублейСтрок(Контекст));
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		глВыбранЛи(ЭККА,"ЭККА");
		глВыбранЛи(Касса,"Касса");
	КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
	глВсеВыбрано = 1;
	глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);
	глВыбранЛи(Ед,"Единица",НомерСтроки);
	Если МестоХранения.СуммовойУчет <> 1 Тогда
		глВыбранЛи(Кво,"Количество",НомерСтроки);
	КонецЕсли;	
	глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,ТМЦ,НомерСтроки,1)=Да, глВсеВыбрано, 0);
	ЕстьКалькулируемыеУслуги = 0;
	Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
		Если глПолучитьНормы(ТМЦ,,1,ДатаДок,ПоЗаказу,,1)=1 Тогда // калькулируемая услуга
			ЕстьКалькулируемыеУслуги = 1;
		    глВыбранЛи(ТМЦ.ВидДеятельности,"Вид деятельности в ТМЦ",НомерСтроки);  
			Если глВсеВыбрано = 1 Тогда
				Если (ТМЦ.ВидДеятельности.ПозаказноеПроизводство = Да) И (ПустоеЗначение(ПоЗаказу) = 1) Тогда
					глКомментарий("В строке "+НомерСтроки+" выбрана услуга, в карточке которой указан вид деятельности с позаказным производством!",0,,"!");
					глВсеВыбрано = 0;                         
				ИначеЕсли (ТМЦ.ВидДеятельности.ПозаказноеПроизводство = Нет) И (ПустоеЗначение(ПоЗаказу) = 0) Тогда
					глКомментарий("В строке "+НомерСтроки+" выбрана услуга, в карточке которой указан вид деятельности с непозаказным производством!",0,,"!");
					глВсеВыбрано = 0;                         
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ЕстьКалькулируемыеУслуги = 1 Тогда
		глВыбранЛи(ПодразделениеПроизв,"Подразделение производства");
		глПроверитьТипПодразделения(ПодразделениеПроизв,"подразделения производства");
	КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции
             
// ===============================
Процедура РассчитатьШапку(ЧастичноПровести = 0)
	// определяем заказ в документе для проводок по услугам
	ПоЗаказу = глПолучитьЗаказ(Договор);
	// взаиморасчеты
	ФлагОтгрузки = 1;
	
	//Если ПустоеЗначение(Договор) = 1 Тогда
	//    ПервыйДок = ПолучитьПустоеЗначение("Документ");
	//ИначеЕсли Найти("Договор,Заказ",Договор.Вид()) <> 0 Тогда
	//    ПервыйДок = ?(Контрагент.УчетВРазрезеДоговоров.Получить(ДатаДок)= 1,Договор, ПолучитьПустоеЗначение("Документ")); //  + umk
	//Иначе
	//	ПервыйДок = ПолучитьПустоеЗначение("Документ");
	//КонецЕсли;
	
	 ПервыйДок = ?(Контрагент.УчетВРазрезеДоговоров.Получить(ДатаДок)= 1,Договор, Контрагент.БазДоговор); //  + umk
	
	ЗнакОплаты = -1; 
	ВремРег = СоздатьОбъект("Регистры");	
	// партии
	спОтбор = СоздатьОбъект("СписокЗначений");
	спСчета = СоздатьОбъект("СписокЗначений");
	Если ПустоеЗначение(ЧастичноПровести) = 1 Тогда
		глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,Фирма,ЗнакОплаты, Контрагент, ПервыйДок, Валюта, 1);
		// партии
		// сформируем список счетов для отбора	
		Если МестоХранения.ВидСклада = Перечисление.ВидыСкладов.Розничный Тогда
			спСчета.ДобавитьЗначение(СчетПоКоду("28.2.1"));
			спСчета.ДобавитьЗначение(СчетПоКоду("28.2.2"));
		Иначе     
			ВыбратьСтроки();
			Пока ПолучитьСтроку() = 1 Цикл
				Если спСчета.НайтиЗначение(ТМЦ.Счет) = 0 Тогда
					спСчета.ДобавитьЗначение(ТМЦ.Счет);
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;	
		спОтбор.Установить("Счет",спСчета);
		Если (МестоХранения.ВидСклада = Перечисление.ВидыСкладов.Розничный) или (глПартионныйУчетПоСкладам = Да) Тогда
			спОтбор.Установить("МестоХранения", МестоХранения);
		КонецЕсли;
		Если Договор.Вид()="Заказ" Тогда
		    спОтбор.Установить("Поставка",Договор);
		КонецЕсли;
		глРассчитатьОстаткиПартий(Контекст, ВремРег, Фирма, спОтбор,1);
	КонецЕсли;	
    спОтбор.Установить("МестоХранения",);
	// фильтр по складу нужен только для партий
	глРассчитатьОстаткиИРезервы(Контекст, ВремРег, спОтбор,1,1);	
	Если (МестоХранения.ВидСклада = Перечисление.ВидыСкладов.Розничный) или (глПартионныйУчетПоСкладам = Да) Тогда
		спОтбор.Установить("МестоХранения", МестоХранения);
	КонецЕсли;
КонецПроцедуры
                                 
// ===============================
Процедура ДвиженияОстатки()
	        
	глКомментарий("Выполняются движения остатков ТМЦ",2);

	// определим документ по которому зарезервированы ТМЦ
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если ДокументОснование.Вид() = "Счет" Тогда
			РезервПоСчету = ДокументОснование;
		КонецЕсли;	
	КонецЕсли;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
			Продолжить;
		КонецЕсли;
		ВсегоСписать = Кво * Коэффициент;			
		// движения по регистру Остатки                                                                          
		глПровестиОстатки(Контекст, ВремРег, Фирма, МестоХранения, ТМЦ,, ВсегоСписать, 0, 0,,, 1, РезервПоСчету);	//--- тут нет упаковки
	КонецЦикла;	
	
КонецПроцедуры

// ===============================
Процедура ДвиженияВзаиморасчеты()
	глКомментарий("Выполняются движения по взаиморасчетам",2);
	
	ФлагОтгрузки = 1;
	ЗнакОплаты = -1; 
	тбСуммыПогашения = глПолучитьСуммыДляПогашения(Контекст, Фирма, Валюта);
	тбДолги = глПолучитьИтогиВзаиморасчетов(Контекст, ВремРег, Фирма, ЗнакОплаты, Контрагент, ПервыйДок, Валюта);
	глПровестиПоВзаиморасчетам(Контекст,  ФлагОтгрузки, ЗнакОплаты, 0, Фирма, тбДолги, тбСуммыПогашения, Валюта, Контрагент, ПервыйДок, ВидТорговли, ДокументОснование);
	НДСПервоеСобытие = 0;
	НДСВтороеСобытие = 0;
	ВзаиморасчетыГрн = 0;
	СчетКонтрагента  = ?(Валюта=Гривня,СчетПоКоду("361"),СчетПоКоду("362"));
	РегПоставщики    = ВремРег.ВзаиморасчетыПоставщиков;	
	РегПокупатели    = ВремРег.ВзаиморасчетыПокупателей;	
	РегПокупатели.ВыбратьДвиженияДокумента(ТекущийДокумент());
	Пока РегПокупатели.ПолучитьДвижение() = 1 Цикл
		Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
			 //При оплате наличными формируются 2 движения
			Если РегПокупатели.Приход = 1 Тогда // Продажа 
				Если ((РегПокупатели.КодОперации = АвансоваяОтгрузка)
				ИЛИ (РегПокупатели.КодОперации = ПостОтгрузка)) Тогда     					
					// проводки по взаиморасчетам
					Если Валюта = Гривня Тогда
						глПроводка(Контекст,СчетКонтрагента,,РегПокупатели.Долг,"Реализация",, Контрагент,ПервыйДок,,
						,,, ,,"НК",1,"Реализация",1);					
					Иначе	
						глПроводка(Контекст,СчетКонтрагента,,РегПокупатели.ДолгОсн,"Реализация",, Контрагент,ПервыйДок,,
						,,, РегПокупатели.Валюта,РегПокупатели.Долг,"НК",1,"Реализация",1);					
					КонецЕсли;	  
					ВзаиморасчетыГрн = ВзаиморасчетыГрн + РегПокупатели.ДолгОсн;
				КонецЕсли;	      
				// сформируем проводки по НДС
				Если глДелатьПроводкиПоНалогам(РегПокупатели,"НДС") = 1 Тогда
					глПроводка(Контекст,,"6415",РегПокупатели.НДС,"НДС: оплата наличными",, ,,,
					ВидНДС,,, ,,"РЗ",1,"НДС оплата наличными",1);                              
					НДСПервоеСобытие = НДСПервоеСобытие + РегПокупатели.НДС;
				КонецЕсли;	
				
				// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
				// сформируем проводки по ВД/ВР
				//Если (глДелатьПроводкиПоНалогам(РегПокупатели,"ВД/ВР") = 1)
				//И (СубконтоВалДох <> Константа.НиДоходНиРасход) Тогда
				//	глПроводка(Контекст,"ВД","ВД",РегПокупатели.СуммаСНДС_НУ-РегПокупатели.НДС,"Валовые доходы",, Контрагент,ПервыйДок,СубконтоВалДох,
				//	Контрагент,ПервыйДок,СубконтоВалДох, ,,"РЗ",1,0);
				//КонецЕсли;
				// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
					
			Иначе // Оплата
				Если Константа.ПроводкиПоКассеТолькоОрдерами <> Да Тогда
					Если ((РегПокупатели.КодОперации = АвансоваяОтгрузка)
					ИЛИ (РегПокупатели.КодОперации = ПостОплата)) Тогда     					
						// Проводки по взаиморасчетам. Оплата.
						Если Валюта = Гривня Тогда
							глПроводка(Контекст,"301",СчетКонтрагента,РегПокупатели.ДолгОсн,"Реализация: ",, Касса,,,
							Контрагент,ПервыйДок,, ,,"РЗ",1,0);
						Иначе	
							глПроводка(Контекст,"302",СчетКонтрагента,РегПокупатели.ДолгОсн,"Реализация: ",, Касса,,,
							Контрагент,ПервыйДок,, РегПокупатели.Валюта,РегПокупатели.Долг,"РЗ",1,0);
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;	 
			КонецЕсли;	
		Иначе
			Если ((РегПокупатели.КодОперации = АвансоваяОтгрузка)
			ИЛИ (РегПокупатели.КодОперации = ПостОтгрузка)) Тогда     					
				// проводки по взаиморасчетам
				Если Валюта = Гривня Тогда
					глПроводка(Контекст,СчетКонтрагента,,РегПокупатели.Долг,"Реализация",, Контрагент,ПервыйДок,,
					,,, ,,"НК",1,"Реализация",1);					
				Иначе	
					глПроводка(Контекст,СчетКонтрагента,,РегПокупатели.ДолгОсн,"Реализация",, Контрагент,ПервыйДок,,
					,,, РегПокупатели.Валюта,РегПокупатели.Долг,"НК",1,"Реализация",1);					
				КонецЕсли;	  
				ВзаиморасчетыГрн = ВзаиморасчетыГрн + РегПокупатели.ДолгОсн;
			КонецЕсли;	  
			
			Если РегПокупатели.КодОперации = АвансоваяОтгрузка Тогда     					
				// сформируем проводки по НДС
				Если глДелатьПроводкиПоНалогам(РегПокупатели,"НДС") = 1 Тогда
					// для первого события
					глПроводка(Контекст,,"64.1.5",РегПокупатели.НДС,"НДС: первое событие",, ,,,
					ВидНДС,,, ,,"НК",1,"НДС первого события",1);                                                     
					НДСПервоеСобытие = НДСПервоеСобытие + РегПокупатели.НДС;
				КонецЕсли;	
			ИначеЕсли РегПокупатели.КодОперации = ПостОтгрузка Тогда     					
				// погасим аванс
				Если глВыделятьЛиАвансыПоСчету(СчетКонтрагента) = 1 Тогда
					Если Валюта=Гривня Тогда
						глПроводка(Контекст,"6811",СчетКонтрагента,РегПокупатели.Долг,"Реализация: погашен авансовый платеж",, Контрагент,ПервыйДок,,
						Контрагент,ПервыйДок,, ,,"НК",1,0);
					Иначе
						глПроводка(Контекст,"6812",СчетКонтрагента,РегПокупатели.ДолгОсн,"Реализация за валюту: погашен авансовый платеж",, Контрагент,ПервыйДок,,
						Контрагент,ПервыйДок,, РегПокупатели.Валюта,РегПокупатели.Долг,"НК",1,0);
					КонецЕсли;
				КонецЕсли;      
				// сформируем проводки по НДС
				Если глДелатьПроводкиПоНалогам(РегПокупатели,"НДС") = 1 Тогда
					// для второго события
					глПроводка(Контекст,,"64.3",РегПокупатели.НДС,"НДС: второе событие",, ,,,
					Контрагент,ПервыйДок,, ,,"НК",1,"НДС второго события",1);
					// посчитаем НДС предварительной оплаты
					НДСВтороеСобытие = НДСВтороеСобытие + РегПокупатели.НДС;
				КонецЕсли;                                                                        
			КонецЕсли;        
			
			// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
			//// сформируем проводки по ВД/ВР
			//Если глДелатьПроводкиПоНалогам(РегПокупатели,"ВД/ВР") = 1 Тогда
			//	Если СубконтоВалДох <> Константа.НиДоходНиРасход Тогда
			//		глПроводка(Контекст,"ВД","ВД",РегПокупатели.СуммаСНДС_НУ-РегПокупатели.НДС,"Валовые доходы",, Контрагент,ПервыйДок,СубконтоВалДох,
			//		Контрагент,ПервыйДок,СубконтоВалДох, ,,"НК",1,0);
			//	КонецЕсли;
			//КонецЕсли;
			// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
			
		КонецЕсли;
	КонецЦикла;                                                             
КонецПроцедуры

// ===============================
Процедура ДвиженияРезерв();
	
	Если ДокументОснование.Вид()="Счет" Тогда
		глКомментарий("Выполняется снятие резерва на продаваемые ТМЦ",2);
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			НС = 0;
			ТекущийРезерв = ВремРег.Резервы.Остаток(Фирма,ТМЦ,ДокументОснование,"Резерв");
			СписываемыйРезерв = Мин(ТекущийРезерв,Кво*Коэффициент);
			Если СписываемыйРезерв > 0 Тогда
				Регистр.Резервы.ПривязыватьСтроку(НомерСтроки);
				Регистр.Резервы.Фирма=Фирма;
				Регистр.Резервы.ТМЦ=ТМЦ;
				Регистр.Резервы.ДокументРезерва=ДокументОснование;
				Регистр.Резервы.Резерв=СписываемыйРезерв;
				Регистр.Резервы.ДвижениеРасходВыполнить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры        
                   
// Формирует проводки на основании движений по регистру Партии и Обороты.
// Используются не суммы из движений, а те же суммы которые используются 
// для формирования движений по регистрам.
// ОстатокТовара - количество ТМЦ, которое надо списать
// Стоимость - себестоимость ТМЦ, которую надо списать
// ПродСтоимость - розничная наценка на ТМЦ, которую надо списать
// Донаценка - разница между себестоимостью+розничной наценкой и ценой продажи
// НДС - НДС ТМЦ
// СписываемаяПартия - партия, которую надо списать
// СчетУчета - счет учета ТМЦ
// СчетНаценки - счет учета розничной наценки
// ===============================
Процедура ПроводкиТовар(ОстатокТовара, Стоимость, ПродСтоимость,Донаценка,СписываемаяПартия,СчетУчета,СчетНаценки)
	Если ПустоеЗначение(СчетНаценки) = 0 Тогда
		// спишем наценку
		глПроводка(Контекст,СчетНаценки,СчетУчета,ПродСтоимость-Стоимость+Донаценка,"Розн.продажа: Наценка+НДС",,
		МестоХранения,ТМЦ,СписываемаяПартия,МестоХранения,ТМЦ,СписываемаяПартия,,,"РЗ",1,0);
		// дооценим
		глПроводка(Контекст,СчетУчета,СчетНаценки,Донаценка,"Розн.продажа: Донаценка",,
		МестоХранения,ТМЦ,СписываемаяПартия,МестоХранения,ТМЦ,СписываемаяПартия,,,"РЗ",1,0);
	КонецЕсли;
	// для склада с суммовым учетом реализацию покажет док. ОпределениеТорговойНаценки
	Если МестоХранения.СуммовойУчет <> 1 Тогда
		СчРеал = глСчетРеализации(ТМЦ,1);
		Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
			// услуг
			глПроводка(Контекст,СчРеал,СчетУчета,Стоимость,"Роз.продажа: себестоимость услуги",ОстатокТовара, ТМЦ.ВидДеятельности,ПодразделениеПроизв,,
			ТМЦ.ВидДеятельности,ПодразделениеПроизв,, ,,"РЗ",1,0);  			
		Иначе	    
			// обычных ТМЦ
			глПроводка(Контекст,СчРеал,СчетУчета,Стоимость,"Роз.продажа: себестоимость",ОстатокТовара, ТМЦ.ВидДеятельности,Подразделение,,
			МестоХранения,ТМЦ,СписываемаяПартия, ,,"РЗ",1,0);  			
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры
            
// Наименование: СформироватьДвиженияПартии
// Назначение: рассчитать необходимеы параметры и сформировать движения по регистрам 
// Партии и Обороты. Сформировать проводки.
//		В процессе обработки в процедуре изменятся следующие пареметры:
//															ОсталосьСписать											
//															ОсталосьСуммаСНДСПоСтроке											
//															ОсталосьСуммаБезНДСПоСтроке											
// Параметры: 
// 			тбОстатки - таблица остатков, при списании кол-ва большего, чем есть на складе, этот параметр пустой
//			тбПартии - таблица партий, при списании кол-ва большего, чем есть на складе, этот параметр пустой
//			ОсталосьСписать - количество ТМЦ, которое необходимо списать
// 			ВсегоСписатьПоСтроке - количество ТМЦ, указанное в строке документа в базовых единица
//			ВсегоСуммаСНДСПоСтроке - сумма с НДС указанная в строке документа, пересчитанная по курсу документа в гривни
//			ВсегоСуммаБезНДСПоСтроке - сумма без НДС указанная в строке документа, пересчитанная по курсу документа в гривни
// 			ОсталосьСуммаСНДСПоСтроке - сумма с НДС, которую необходимо списать
//			ОсталосьСуммаБезНДСПоСтроке - сумма без НДС, которую необходимо списать
//			ПоставщикП - поставщик, для проведения по регистрам
//			ПоставкаП - документ поставки, для проведения по регистрам
//			ПрихДокументП - приходный документ, для проведения по регситрам
// ===============================
Процедура СформироватьДвиженияПартии(тбОстатки,тбПартии,ОсталосьСписать,ВсегоСписатьПоСтроке,ВсегоСуммаСНДСПоСтроке,
	ВсегоСуммаБезНДСПоСтроке,ОсталосьСуммаСНДСПоСтроке,ОсталосьСуммаБезНДСПоСтроке,ПоставщикП,ПоставкаП,ПрихДокументП)
	
	Если ПустоеЗначение(тбОстатки) = 1 Тогда
		ОстатокТовараПоПартии = 0;
		// если надо списать больше чем есть на складе
		СписываемыйОстатокТовара = ОсталосьСписать;	
		// себестоимость равна реализации
		СписываемаяСтоимость = ОсталосьСуммаБезНДСПоСтроке;
		СписываемаяПродСтоимость = 0;
		СписываемыйНДС = ОсталосьСуммаСНДСПоСтроке - ОсталосьСуммаБезНДСПоСтроке;
		// определим сумму реализации текущей списываемой партии
		СуммаРеализацииБезНДС = ОсталосьСуммаБезНДСПоСтроке;
		СуммаРеализацииСНДС = СуммаРеализацииБезНДС + СписываемыйНДС;
		Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Тара Тогда
			СчетП = ГлПолучитьСчетУчетаТМЦ("ТараВозвратная",ТМЦ);
		Иначе
			Если МестоХранения.ВидСклада <> Перечисление.ВидыСкладов.Розничный Тогда
				СчетП = ТМЦ.Счет;
			ИначеЕсли ((ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Полуфабрикат) 
			ИЛИ (ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Продукция)) Тогда 
				СчетП = СчетПоКоду("28.2.2");
			Иначе
				СчетП = СчетПоКоду("28.2.1");
			КонецЕсли;	
		КонецЕсли;
	Иначе	                     
		ОстатокТовараПоПартии = тбОстатки.ОстатокТовара;
		// нормальное списание
		СписываемыйОстатокТовара = Мин(ОсталосьСписать, ОстатокТовараПоПартии);
		Если МестоХранения.СуммовойУчет = 1 Тогда
			// для склада с суммовым учетом списывем все что указано в документе
			СписываемаяПродСтоимость = ОсталосьСуммаСНДСПоСтроке;
		Иначе
			КоэфСписания = СписываемыйОстатокТовара / ОстатокТовараПоПартии;
			КоэфРеализации = СписываемыйОстатокТовара / ВсегоСписатьПоСтроке;
			// определим себестоимость текущей списываемой партии
			СписываемаяСтоимость = Окр(тбОстатки.Стоимость * КоэфСписания, 2);
			СписываемаяПродСтоимость = Окр(тбОстатки.ПродСтоимость * КоэфСписания, 2);
		КонецЕсли;
		Если СписываемыйОстатокТовара = ОсталосьСписать Тогда
			// определим сумму реализации текущей списываемой партии
			// если списывается вся сумма указаная в строке документа
			СуммаРеализацииСНДС = ОсталосьСуммаСНДСПоСтроке;
			СуммаРеализацииБезНДС = ОсталосьСуммаБезНДСПоСтроке;
		Иначе                                                
			// определим сумму реализации текущей списываемой партии
			// если списывется часть суммы указаной в строке документа 
			СуммаРеализацииСНДС = Окр(ВсегоСуммаСНДСПоСтроке * КоэфРеализации,2);
			СуммаРеализацииБезНДС = Окр(ВсегоСуммаБезНДСПоСтроке * КоэфРеализации,2);
		КонецЕсли;  
		СчетП = тбПартии.Счет;		
	КонецЕсли;	
	// доход от продажи 
	Доход = СуммаРеализацииБезНДС - СписываемаяСтоимость;
	// осталось списать по суммам
	ОсталосьСуммаБезНДСПоСтроке = ОсталосьСуммаБезНДСПоСтроке - СуммаРеализацииБезНДС;
	ОсталосьСуммаСНДСПоСтроке = ОсталосьСуммаСНДСПоСтроке - СуммаРеализацииСНДС;
	КодОперации = ?(МестоХранения.ВидСклада=Перечисление.ВидыСкладов.Розничный,РозничнаяПродажа,Продажа);                
	МестоХраненияП = МестоХранения;
	глПровестиПартию(Контекст, 0, 0, Фирма, ТМЦ, СчетП, МестоХраненияП, ПоставщикП, 
		ПоставкаП, ПрихДокументП,СписываемыйОстатокТовара, СписываемаяСтоимость, СписываемаяПродСтоимость,
		КодОперации, 1, СуммаРеализацииБезНДС, Доход);
	Если МестоХранения.СуммовойУчет = 1 Тогда
		ПроводкиТовар(СписываемыйОстатокТовара, , СуммаРеализацииСНДС,,ПоставкаП,СчетП,);
	ИначеЕсли МестоХранения.ВидСклада <> Перечисление.ВидыСкладов.Розничный Тогда
		ПроводкиТовар(СписываемыйОстатокТовара, СписываемаяСтоимость, СписываемаяПродСтоимость,СуммаРеализацииСНДС-СписываемаяПродСтоимость,ПоставкаП,СчетП,"");
	ИначеЕсли ((ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Полуфабрикат) 
	ИЛИ (ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Продукция)) Тогда 
		ПроводкиТовар(СписываемыйОстатокТовара, СписываемаяСтоимость, СписываемаяПродСтоимость,СуммаРеализацииСНДС-СписываемаяПродСтоимость,ПоставкаП,СчетП,СчетПоКоду("28.5.2"));
	Иначе
		ПроводкиТовар(СписываемыйОстатокТовара, СписываемаяСтоимость, СписываемаяПродСтоимость,СуммаРеализацииСНДС-СписываемаяПродСтоимость,ПоставкаП,СчетП,СчетПоКоду("28.5.1"));
	КонецЕсли;	
	Если ПустоеЗначение(тбПартии) = 0 Тогда
		глУчестьСписание(тбОстатки,СписываемыйОстатокТовара,СписываемаяСтоимость,СписываемаяПродСтоимость);
	КонецЕсли;	
	ОсталосьСписать = ОсталосьСписать - СписываемыйОстатокТовара;
КонецПроцедуры

// ===============================
Процедура ДвижениеНормативныеЗатраты(СтатьяКалькуляции, Материал, СписываемоеКво, СписываемаяСумма)
	Регистр.НормативныеЗатраты.ПривязыватьСтроку(НомерСтроки);
	Регистр.НормативныеЗатраты.Фирма = Фирма;
	Регистр.НормативныеЗатраты.ВидДеятельности = ТМЦ.ВидДеятельности;
	Регистр.НормативныеЗатраты.Подразделение = ПодразделениеПроизв; // потом поставим производственное
	Регистр.НормативныеЗатраты.Продукция = ТМЦ;
	Регистр.НормативныеЗатраты.СтатьяКалькуляции = СтатьяКалькуляции;
	Регистр.НормативныеЗатраты.Заказ = ПоЗаказу;
	Регистр.НормативныеЗатраты.Материал = Материал;
	Регистр.НормативныеЗатраты.ТипЗатрат = НормыНаВыпуск;
	Регистр.НормативныеЗатраты.Количество = СписываемоеКво;
	Регистр.НормативныеЗатраты.Сумма = СписываемаяСумма; 
	Регистр.НормативныеЗатраты.ДвижениеВыполнить();
КонецПроцедуры

// ===============================
Процедура ДвижениеВыпускПродукции(КвоПродукции,КвоБрака,СуммаПродукции,СуммаДавПродукции,Партия) 
	Регистр.ВыпускПродукции.ПривязыватьСтроку(НомерСтроки);
	Регистр.ВыпускПродукции.Фирма = Фирма;
	Регистр.ВыпускПродукции.ВидДеятельности = ТМЦ.ВидДеятельности;
	Регистр.ВыпускПродукции.Подразделение = ПодразделениеПроизв; // потом поставим производственное
	Регистр.ВыпускПродукции.Заказ = ПоЗаказу;
	Регистр.ВыпускПродукции.Продукция = ТМЦ;
	Регистр.ВыпускПродукции.Партия = Партия; 
	Регистр.ВыпускПродукции.Количество = КвоПродукции;  
	Регистр.ВыпускПродукции.КоличествоБрака = КвоБрака;  
	Регистр.ВыпускПродукции.Сумма = СуммаПродукции; 
	Регистр.ВыпускПродукции.СуммаДав = СуммаДавПродукции; 
	Регистр.ВыпускПродукции.ДвижениеВыполнить();
КонецПроцедуры      

// ===============================
Процедура ДвиженияСтрокаПроизводство(тбНормы,СебестоимостьПродукции)

	СебестоимостьПродукции = 0;

	Если ПустоеЗначение(ПоЗаказу) = 0 Тогда
		Партия = ПоЗаказу;
	ИначеЕсли Константа.МетодРасчетаСебестоимостиПроизводственныхЗапасов.Получить(ДатаДок) = Перечисление.МетодыРасчетаСебестоимости.ПоСреднему Тогда
		Партия = ПолучитьПустоеЗначение("Документ");
	Иначе
		Партия = ТекущийДокумент();
	КонецЕсли;

	СуммаМатЗатратПоПродукции = 0;	//Сумма материальных затрат по нормам на продукцию
	СуммаНеМатЗатратПоПродукции = 0;//Сумма нематериальных затрат по нормам на продукцию
	СуммаВозврОтходовПоПродукции = 0;//Сумма возвратных отходов по нормам на продукцию
	
	тбНормы.ВыбратьСтроки();
	Пока тбНормы.ПолучитьСтроку() = 1 Цикл
		// показываем нормативные затраты
		ДвижениеНормативныеЗатраты(тбНормы.СтатьяКалькуляции,тбНормы.Материал,тбНормы.Кво * ?(тбНормы.ВидЭлемента = Перечисление.ВидыЭлементовСоставаПродукции.ВозвратныйОтход,-1,1),тбНормы.Сумма);
		
		Если тбНормы.ВидЭлемента = Перечисление.ВидыЭлементовСоставаПродукции.ВозвратныйОтход Тогда
			СуммаВозврОтходовПоПродукции = СуммаВозврОтходовПоПродукции + тбНормы.Сумма;
		ИначеЕсли (тбНормы.ВидЭлемента = Перечисление.ВидыЭлементовСоставаПродукции.Материал)
				или (тбНормы.ВидЭлемента = Перечисление.ВидыЭлементовСоставаПродукции.Полуфабрикат) Тогда
			СуммаМатЗатратПоПродукции = СуммаМатЗатратПоПродукции + тбНормы.Сумма;
		ИначеЕсли (тбНормы.ВидЭлемента = Перечисление.ВидыЭлементовСоставаПродукции.СтатьяКалькуляции) Тогда
			СуммаНеМатЗатратПоПродукции = СуммаНеМатЗатратПоПродукции + тбНормы.Сумма;
		КонецЕсли;
	КонецЦикла;

	// Определяем себестоимость заданного количества продукции
	Если Константа.СпособПриходованияПродукции = Перечисление.СпособыПриходованияПродукции.ПоПлановойСебестоимости  Тогда
		//Плановую себестоимость берем из справочинка ТМЦ, УчетнаяЦена
		СебестоимостьПродукции = ТМЦ.УчетнаяЦена.Получить(ДатаДок) * Кво;
	ИначеЕсли Константа.СпособПриходованияПродукции = Перечисление.СпособыПриходованияПродукции.ПоНормативнойСебестоимости  Тогда
		//Считаем себестоимость как общую сумму затрат по нормативам
		СебестоимостьПродукции = СуммаМатЗатратПоПродукции + СуммаНеМатЗатратПоПродукции - СуммаВозврОтходовПоПродукции;
	ИначеЕсли Константа.СпособПриходованияПродукции = Перечисление.СпособыПриходованияПродукции.ПоСебестоимостиФактическогоСостава  Тогда
		//Считаем себестоимость как фактические материальные затраты + плановые прочие затраты по нормативам
		СебестоимостьПродукции = СуммаНеМатЗатратПоПродукции - СуммаВозврОтходовПоПродукции;
	ИначеЕсли Константа.СпособПриходованияПродукции = Перечисление.СпособыПриходованияПродукции.БезСебестоимости Тогда
		СебестоимостьПродукции = 0;
	КонецЕсли;
	//Движения по ВЫПУСКУ ПРОДУКЦИИ
	ДвижениеВыпускПродукции(Кво,0,СебестоимостьПродукции,0,Партия);
КонецПроцедуры //ДвиженияСтрокаПроизводство

// ===============================
Функция ДвиженияПартии()
Перем фОшибка;	// возвращаемое значение
Перем ИтогСуммаСНДС, ИтогНДС;	// базы распределения сумм

	фОшибка = 1;              
	
	глКомментарий("Выполняются движения партий ТМЦ",2);

	Предпочтение = 0;
	тбПартии = 0;
	тбОстатки = 0;
	ИнвСтратегияПредпочтения  = 0;
	ИнвСтратегияПродажи = 0;
	глСформироватьТаблицуПартий(Контекст, ВремРег, Фирма, спОтбор, Предпочтение, 
		тбПартии, тбОстатки, ИнвСтратегияПредпочтения, ИнвСтратегияПродажи);

	// инициализация алгоритмов корректого распределения
	глОчиститьКлючОкр("Взаим");
	глОчиститьКлючОкр("НДС");
	глОчиститьКлючОкр("НДСперв");

	ФлагПрихода = 0;

	// расчитаем базы распределения 
	глОпределитьБазыРаспределения(Контекст, ИтогСуммаСНДС, ИтогНДС);
	ИтогСуммаСНДС       = глПересчет(ИтогСуммаСНДС,Валюта,ДатаДок,Гривня,ДатаДок);
	ИтогНДС             = глПересчет(ИтогНДС      ,Валюта,ДатаДок,Гривня,ДатаДок);
	НДСпоВзаиморасчетам	= НДСПервоеСобытие + НДСВтороеСобытие;
	СкидкаГрн           = глПересчет(Скидка       ,Валюта,ДатаДок,Гривня,ДатаДок);
	КоэфСкидки          = ?(ИтогСуммаСНДС = 0, 0, СкидкаГрн / (ИтогСуммаСНДС + СкидкаГрн));

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		МетодРасчетаСебестоимости = глПолучитьМетодРасчетаСебестоимости(ТМЦ,ДатаДок);
		Если ВидНДС <> ОсновнаяСтавкаНДС Тогда                                     
			ПроцНДС = глПроцентНДС(ВидНДС,ДатаДок);
		Иначе
			ПроцНДС = глПроцентНДС(ТМЦ.СтавкаНДС,ДатаДок);
		КонецЕсли;
		КоэфБезНДС      = 100 / (100 + ПроцНДС);
		ВсегоСписать    = Кво * Коэффициент;			
		ОсталосьСписать = ВсегоСписать;

		ПострСуммаСНДС      = глПересчет(СуммаСНДС,Валюта,ДатаДок,Гривня,ДатаДок);
		ПострСуммаСкидки    = ПострСуммаСНДС * КоэфСкидки;
		ПострСуммаСНДС	    = глОкрКорр("Взаим", ?(ИтогСуммаСНДС = 0, 0, ВзаиморасчетыГрн * (ПострСуммаСНДС - ПострСуммаСкидки) / ИтогСуммаСНДС),2);
		ПострНДС	 	    = ПострСуммаСНДС * (1 - КоэфБезНДС);
		ПострНДС    		= глОкрКорр("НДС", ?(ИтогНДС = 0, 0, НДСпоВзаиморасчетам * ПострНДС / ИтогНДС),2);
		ПострНДСПерв	    = глОкрКорр("НДСперв", ?(НДСпоВзаиморасчетам = 0, 0, НДСПервоеСобытие * ПострНДС / НДСпоВзаиморасчетам),2);
		ПострСуммаБезНДС 	= ПострСуммаСНДС - ПострНДС;
		ОсталосьСуммаСНДС	= ПострСуммаСНДС;
		ОсталосьСуммаБезНДС	= ПострСуммаБезНДС;

		// Формирование проводок по реализации
		Если (ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Продукция) Или (ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Полуфабрикат) Тогда
			ВидТМЦ = "продукция"; 
			СчетДоходов = "701"; 
		ИначеЕсли ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
			ВидТМЦ = "услуги"; 
			СчетДоходов = "703"; 
		Иначе
			ВидТМЦ = "товары"; 
			СчетДоходов = "702"; 
		КонецЕсли;

		// вторая часть сложной проводки по доходам
		глПроводка(Контекст,,СчетДоходов,ПострСуммаСНДС,"Реализация: "+ВидТМЦ,, ,,,
		ТМЦ.ВидДеятельности,Подразделение,МестоХранения, ,,"РЗ",1,"Реализация");

		Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
			глПроводка(Контекст,СчетДоходов,,ПострНДС,"НДС: оплата наличными: "+ВидТМЦ,, ТМЦ.ВидДеятельности,Подразделение,МестоХранения,
			,,, ,,"РЗ",1,"НДС оплата наличными");
		Иначе
			// в случае предоплаты НДС состоит из части по первому событию и остального, 			
			// проводка для первого события
			глПроводка(Контекст,СчетДоходов,,ПострНДСПерв,"НДС: "+СокрЛП(ВидТМЦ)+": первое событие",, ТМЦ.ВидДеятельности,Подразделение,МестоХранения,
				,,, ,,"НК",1,"НДС первого события");                  
			// проводка для второго события
			глПроводка(Контекст,СчетДоходов,,ПострНДС - ПострНДСПерв,"НДС: "+СокрЛП(ВидТМЦ)+": второе событие",, ТМЦ.ВидДеятельности,Подразделение,МестоХранения,
			,,, ,,"НК",1,"НДС второго события");
		Конецесли;
		// проводки и движения по услугам отдельно
		Если ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
			тбНормы = 0; СуммаУчБезНДС = 0;
			// получаем нормы
			Если глПолучитьНормы(ТМЦ,Ед,Кво,ДатаДок,ПоЗаказу,тбНормы) = 1 Тогда 
				// калькулируемая услуга
				ДвиженияСтрокаПроизводство(тбНормы,СуммаУчБезНДС);
				Доход = ОсталосьСуммаБезНДС - СуммаУчБезНДС;
				СчетУч = СчетПоКоду("231");
			Иначе
				Доход = 0;
				СчетУч = ТМЦ.Счет;
			КонецЕсли;
			ПроводкиТовар(ОсталосьСписать, СуммаУчБезНДС, 0,ОсталосьСуммаСНДС,ТекущийДокумент(),СчетУч,"");
			глПровестиПартию(Контекст, 0, 0, Фирма, ТМЦ, СчетУч,
				0,0, ТекущийДокумент(), ТекущийДокумент(), ОсталосьСписать, ОсталосьСуммаБезНДС, 
				0, ПродажаУслуги, 1, ОсталосьСуммаБезНДС,Доход);
			Продолжить;
		КонецЕсли;
		НС=0;
		// находим в тбПартии первую партию, соответсв. текущей строке документа
		Если тбПартии.НайтиЗначение(НомерСтроки,НС,"НомерСтрокиДокумента")=1 Тогда
			тбПартии.ПолучитьСтрокуПоНомеру(НС);
		Иначе                                                                     
			// если такой строки нет, переходим на первую строку таблицы
			тбПартии.ВыбратьСтроки();
			тбПартии.ПолучитьСтроку();
		КонецЕсли;
		// обрабатываем все строки тбПартии с соответсв. номером строки документа
		Пока тбПартии.НомерСтрокиДокумента = НомерСтроки Цикл
			// берем необходимые параметры партии из тбПартии
			Если (тбПартии.ТМЦ.Счет = тбПартии.Счет)
			Или  (МестоХранения.ВидСклада = Перечисление.ВидыСкладов.Розничный)
			Тогда
				МестоХраненияП = тбПартии.МестоХранения;
				ПоставщикП = тбПартии.Поставщик;
				ПоставкаП = тбПартии.Поставка;
				ПрихДокументП = тбПартии.ПрихДокумент;
				СчетП = тбПартии.Счет;
				// проверяем соответствие партиии заказу
				Если глПартияСоответствуетЗаказу(ПоставкаП,Договор,1)=0 Тогда
					Если тбПартии.ПолучитьСтроку()=0 Тогда
						Прервать;
					КонецЕсли;
					Продолжить;
				КонецЕсли;
				НС =0;
				// находим остаток текущей партии в тбОстатки по ключу
				Если тбОстатки.НайтиЗначение(глПолучитьКлючТбОстатков(ПрихДокументП,ПоставкаП,ТМЦ.Код,СчетП),НС,"Ключ")=1 Тогда
					тбОстатки.ПолучитьСтрокуПоНомеру(НС);
					ОстатокТовара = тбОстатки.ОстатокТовара;
					// если остаток 0, то с текущей партии не списываем
					Если ОстатокТовара = 0 Тогда
						Если тбПартии.ПолучитьСтроку()=0 Тогда
							Прервать;
						КонецЕсли;
						Продолжить;
					КонецЕсли;
					// сформируем движения по регистрам и проводки
					СформироватьДвиженияПартии(тбОстатки,тбПартии,ОсталосьСписать,ВсегоСписать,ПострСуммаСНДС
					,ПострСуммаБезНДС,ОсталосьСуммаСНДС,ОсталосьСуммаБезНДС,ПоставщикП,ПоставкаП
					,ПрихДокументП);
				КонецЕсли;
			КонецЕсли;
			Если ОсталосьСписать = 0 Тогда
				Прервать;
			КонецЕсли;   
			Если тбПартии.ПолучитьСтроку()=0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла; 
		Если ОсталосьСписать > 0 Тогда
			глСообщитьОбОтсутствииПартии(0, Фирма, ТМЦ, НомерСтроки);
			Если Константа.РазрешитьОтрицОстатки = Нет Тогда
				фОшибка = 0;
			Иначе   
				ПрихДокументП = ?(МетодРасчетаСебестоимости<>Перечисление.МетодыРасчетаСебестоимости.ПоСреднему
				,ТекущийДокумент(),0);
				// сформируем движения по регистрам и проводки
				СформироватьДвиженияПартии(,,ОсталосьСписать,ВсегоСписать,ПострСуммаСНДС
				,ПострСуммаБезНДС,ОсталосьСуммаСНДС,ОсталосьСуммаБезНДС,,
				,ПрихДокументП);
				глСообщитьОСозданииПартии(ТМЦ, ОсталосьСписать);
			КонецЕсли;
		КонецЕсли; // ОсталосьСписать > 0
	КонецЦикла; // строки документа
	Возврат фОшибка;
КонецФункции

// ===============================
Процедура ОбработкаПроведения(ЧастичноПровести = 0)
	глКомментарий("Начало",2,Контекст);  
	Если Проведен() = 1 Тогда
		глКомментарий("Документ уже проведен. Чек напечатан не будет.",1);
	КонецЕсли;	
	Если ПроверкаШапки() = 0 Тогда
		глНеПроводить(Контекст); 
		Возврат;
	КонецЕсли;                           
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст); 
			Возврат;
		КонецЕсли;
		НомерПоследнейСтроки = НомерСтроки;	
	КонецЦикла;
	РассчитатьШапку(ЧастичноПровести);
	ВремРег.Актуальность(1);
	ДвиженияОстатки();
	ВремРег.Актуальность(0);
	Если ЧастичноПровести = 1 Тогда
		Если РежимПроведения<>1 Тогда
			РежимПроведения=1;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	ДвиженияВзаиморасчеты();
	Если ПустоеЗначение(Договор) = 0 Тогда
		Если Договор.Вид() <> "Заказ" Тогда
			ДвиженияРезерв();
		КонецЕсли;	
	Иначе
		ДвиженияРезерв();
	КонецЕсли;	
	Если ДвиженияПартии() = 0 Тогда
		глТбОперация.УдалитьСтроки();
		глНеПроводить(Контекст); 
		Возврат;
	КонецЕсли;	          
	Если (ВидТорговли = Перечисление.ВидыТорговли.Нал)
	И (ЧекПробит=0) И (СтатусВозврата()=1) 
	И (МестоХранения.СуммовойУчет <> 1)Тогда
		глПечатьЧека(Контекст);
		Если ЧекПробит=0 Тогда
			глНеПроводить(Контекст,"Чек не пробит!");
			глТбОперация.УдалитьСтроки();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	РежимПроведения=0;
	
	глЗаписатьПроводкиВОперацию(Контекст);
	
	Операция.СуммаОперации = Итог("СуммаСНДС");
	Операция.Содержание = Примечание;
	Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры

// ===============================
Процедура ОбработкаУдаленияПроведения()	
	РежимПроведения=0;
КонецПроцедуры