Перем ИтТМЦ, Товар;
Перем ТЗДляПер, ДатаНачалаФормированияПеремещенийПоМаркировке;
Перем ВремРег, спОтбор;
Перем СписСписаний;

// ===========================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Фирма,"Фирма");
    глВыбранЛи(МестоХранения,"Место хранения");
	Если глВсеВыбрано = 1 Тогда
		Если СписаниеМБП <> 1 Тогда
		    глВыбранЛи(СчетСписания,"Счет списания");
			// будем проверять параметры счета списания, если он выбран
			Если  ПустоеЗначение(СчетСписания) = 0 Тогда
				Для Инд = 1 По СчетСписания.КоличествоСубконто() Цикл
					Если Метаданные.ВидСубконто(СчетСписания.ВидСубконто(Инд).Идентификатор()).ПустоеСубконто = 0 Тогда
						глВыбранЛи(ПолучитьАтрибут("СубконтоСписания"+Инд),"Субконто списания "+Инд);
					КонецЕсли;	
				КонецЦикла;
				Если ((Лев(СчетСписания,1)="9")
				ИЛИ ((Лев(СчетСписания,1)="8"))) Тогда
					Если глПроверитьСчетЗатрат(СчетСписания,2,0,"Счет списания")=0 Тогда
					    глВсеВыбрано = 0;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
		Если (МестоХранения.Тип = Перечисление.ТипыМестХранения.Склад) и (МестоХранения.ВидСклада = Перечисление.ВидыСкладов.Розничный) Тогда
			глКомментарий("Нельзя указывать РОЗНИЧНЫЙ склад (магазин)",0,,"!");
			глВсеВыбрано = 0;
		КонецЕсли;
	КонецЕсли;
	глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС"));
  	//глВсеВыбрано = ?(глВсеВыбрано = 0, 0,глПроверкаДублейСтрок(Контекст,1));
    Возврат глВсеВыбрано;
КонецФункции

// ===========================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
    глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);   
	Если глВсеВыбрано = 1 Тогда
	    глВыбранЛи(ТМЦ.Счет,"Счет учета для ТМЦ",НомерСтроки);
	КонецЕсли;                                         
	Если МестоХранения.СуммовойУчет <> 1 Тогда
//		глВыбранЛи(Кво,"Количество",НомерСтроки);
	КонецЕсли;	
	глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,ТМЦ,НомерСтроки,1)=Да, глВсеВыбрано, 0);
    Возврат глВсеВыбрано;
КонецФункции

//======================================================================
Процедура РассчитатьШапку()
	ТЗДляПер = СоздатьОбъект("ТаблицаЗначений");
	ТЗДляПер.НоваяКолонка("ТМЦ", "Справочник.ТМЦ");
	ТЗДляПер.НоваяКолонка("ТМЦМарк", "Справочник.ТМЦ");
	ТЗДляПер.НоваяКолонка("ВидУпаковки_", "Справочник.ВидыУпаковки"); // + umk
	ТЗДляПер.НоваяКолонка("Кво", "Число", 15, 3);
	
	фОшибка = 1;              

	спОтбор = СоздатьОбъект("СписокЗначений");
	спСчета = СоздатьОбъект("СписокЗначений");
	// сформируем список счетов для отбора	
	Если СписаниеМБП  = 1 Тогда
		спСчета.ДобавитьЗначение(СчетПоКоду("МЦ"));
	Иначе	
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если спСчета.НайтиЗначение(ТМЦ.Счет) = 0 Тогда
				спСчета.ДобавитьЗначение(ТМЦ.Счет);
			КонецЕсли;
			
		КонецЦикла;	
	КонецЕсли;	
	спОтбор.Установить("Счет",спСчета);
	
	ВремРег = СоздатьОбъект("Регистры");	
	глПолучитьОтборПоМестамХранения(Контекст, спОтбор);
	
	Если (ПустоеЗначение(Константа.УМК_ДатаНачалаПартииОстатки) <> 1) 
				И (Константа.УМК_ДатаНачалаПартииОстатки <= ДатаДок)  Тогда 
		глРассчитатьОстаткиИРезервы(Контекст, ВремРег, спОтбор,1,1,1,0); //--- УМК Сандомирский В.Ю, (03.10.14) 7ой параметр : (0 - текущий товар ТЧ \ 1-Заменять товаром для списания \ 2-Заменять товаром для приходования)
	Иначе //--- как было  
		глРассчитатьОстаткиИРезервы(Контекст, ВремРег, спОтбор,1,1,1,0,1); //--- УМК Сандомирский В.Ю, (03.10.14) 7ой параметр : (0 - текущий товар ТЧ \ 1-Заменять товаром для списания \ 2-Заменять товаром для приходования)
	КонецЕсли;
	
	Если (глПартионныйУчетПоСкладам <> Да) И (Фирма.ПартионныйУчетПоСкладам = 0) Тогда
		спОтбор.Установить("МестоХранения", "");
		//	    спОтбор.Установить("МестоХранения", МестоХранения);
	КонецЕсли;

	Если ВидОперации = 3 Тогда //Списание ТПЦ по складу МОЛ
		спОтбор.Установить("МестоХранения", МестоХранения);
    КонецЕсли;

	Если (ПустоеЗначение(Константа.УМК_ДатаНачалаПартииОстатки) <> 1) 
				И (Константа.УМК_ДатаНачалаПартииОстатки <= ДатаДок)  Тогда 
		глРассчитатьОстаткиПартий(Контекст, ВремРег, Фирма, спОтбор,,2);//--- УМК Сандомирский В.Ю, (03.10.14) 6ой параметр : (0 - текущий товар ТЧ \ 1-Заменять товаром для списания \ 2-Заменять товаром для приходования)
	Иначе
		глРассчитатьОстаткиПартий(Контекст, ВремРег, Фирма, спОтбор,,0);//--- УМК Сандомирский В.Ю, (03.10.14) 6ой параметр : (0 - текущий товар ТЧ \ 1-Заменять товаром для списания \ 2-Заменять товаром для приходования)	
	КонецЕсли;
	
	// сформируем таблицу того, что нужно допереместить, потому что его не хватает.
	Если ДатаДок >= ДатаНачалаФормированияПеремещенийПоМаркировке Тогда
		// нужно будет сформировать перемещения на продукцию с такой же маркировкой
		ТОстатков = СоздатьОбъект("ТаблицаЗначений"); 
		ВремРег.Остатки.ВыгрузитьИтоги(ТОстатков, 1);
		ТОстатков.Свернуть("ТМЦ", "ОстатокТовара");
		ВыбратьСтроки();
		
		Пока ПолучитьСтроку() = 1 Цикл
			Стр = 0;
			ОдинаковаяМаркировка = ТМЦ.ОдинаковаяПрРазныеМаркИ.Получить(ДатаДок); 
			Если ПустоеЗначение(ОдинаковаяМаркировка) = 0 Тогда
				Стр = 0;
				КвоНеХватает = 0;
				Если ТОстатков.НайтиЗначение(ТМЦ, Стр, "ТМЦ") = 0 Тогда
					КвоНеХватает = Кво * Коэффициент;
				Иначе
					КвоНеХватает = Макс(0, Кво * Коэффициент - ТОстатков.ПолучитьЗначение(Стр, "ОстатокТовара"));
				КонецЕсли;
				Если КвоНеХватает > 0 Тогда
					ТЗДляПер.НоваяСтрока();
					ТЗДляПер.ТМЦ = ТМЦ;
					ТЗДляПер.ВидУпаковки_ = ВидУпаковки; // + umk
					ТЗДляПер.ТМЦМарк = ОдинаковаяМаркировка;
					ТЗДляПер.Кво = КвоНеХватает;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры // РассчитатьШапку

// ===============================
Функция ДвиженияПартии()
	Перем фОшибка;	// возвращаемое значение	
			
	// параметры движения по регситру Партии
	Предпочтение = 0;
	тбПартии = 0;
	тбОстатки = 0;
	ИнвСтартегияПредпочтения  = 0;
	ИнвСтратегияПродажи = 0;
	// парметры движения по регистру Остатки
	ФлагПрихода = 0;
	ФлагВозврата = 0;
	ФлагПеремещения = 2;
	
	Если (ПустоеЗначение(Константа.УМК_ДатаНачалаПартииОстатки) <> 1) 
				И (Константа.УМК_ДатаНачалаПартииОстатки <= ДатаДок)  Тогда 
		глСформироватьТаблицуПартий(Контекст, ВремРег,Фирма, спОтбор, Предпочтение, //--- УМК Сандомирский В.Ю, (03.10.14) 10ый параметр : (0 - текущий товар ТЧ \ 1-Заменять товаром для списания \ 2-Заменять товаром для приходования)
		тбПартии, тбОстатки, ИнвСтартегияПредпочтения, ИнвСтратегияПродажи,2);
	Иначе
		глСформироватьТаблицуПартий(Контекст, ВремРег,Фирма, спОтбор, Предпочтение, //--- УМК Сандомирский В.Ю, (03.10.14) 10ый параметр : (0 - текущий товар ТЧ \ 1-Заменять товаром для списания \ 2-Заменять товаром для приходования)
		тбПартии, тбОстатки, ИнвСтартегияПредпочтения, ИнвСтратегияПродажи,0);
		
	КонецЕсли;	
			
	ВремРег.Актуальность(1);
	ФлагСписания = 0;
	Если ДокОснование.Выбран() = 1 Тогда
	    Если ДокОснование.Вид() = "ВыпускПродукции" Тогда
	        ФлагСписания = 1;
	    КонецЕсли;
	КонецЕсли;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		ВсегоСписаноКво = 0;
		ВсегоСписаноСум = 0;
		ВсегоСписаноНДС = 0;
		ВсегоСписать = Кво * Коэффициент;		
		// движения по регистру Остатки                                                                          
		глПровестиОстатки(Контекст, ВремРег, Фирма, ?(ПустоеЗначение(СкладСписания) = 1, МестоХранения, СкладСписания), ТМЦ, ВидУпаковки, ВсегоСписать, ФлагПрихода, ФлагВозврата,,ФлагПеремещения, 1,,,,ФлагСписания);   //--- Вид упаковки
		ОсталосьСписать = ВсегоСписать;
		Если (Кво = 0) Тогда
			Продолжить;
		КонецЕсли;

		НС=0;
		// находим в тбПартии первую партию, соответсв. текущей строке документа
		Если тбПартии.НайтиЗначение(НомерСтроки,НС,"НомерСтрокиДокумента")=1 Тогда
			тбПартии.ПолучитьСтрокуПоНомеру(НС);
		Иначе                                                                     
			// если такой строки нет, переходим на первую строку таблицы
			тбПартии.ВыбратьСтроки();
			тбПартии.ПолучитьСтроку();
		КонецЕсли;
			
		Если (ПустоеЗначение(Константа.УМК_ДатаНачалаПартииОстатки) <> 1) 	//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
				И (Константа.УМК_ДатаНачалаПартииОстатки <= ДатаДок)  Тогда 			
			ТМЦПартии = ?(ПустоеЗначение(ТМЦ.ТМЦДляПрихода.Получить(ДатаДок)) = 0, ТМЦ.ТМЦДляПрихода.Получить(ДатаДок), ТМЦ);; 			
		Иначе		
			ТМЦПартии = ТМЦ;	
		КонецЕсли;
		
		ОсталосьСписатьСумма = Окр(СуммаСНДС, 2);
		ОсталосьСписатьНДС = НДС;
		// обрабатываем все строки тбПартии с соответсв. номером строки документа
		Пока тбПартии.НомерСтрокиДокумента = НомерСтроки Цикл
			// берем необходимые параметры партии из тбПартии
			Если (тбПартии.ТМЦ.Счет = тбПартии.Счет)
			Или  (тбПартии.Счет = СчетПоКоду("МЦ"))
			Тогда
				МестоХраненияП = тбПартии.МестоХранения;
				ПоставщикП = тбПартии.Поставщик;
				ПоставкаП = тбПартии.Поставка;
				ПрихДокументП = тбПартии.ПрихДокумент;
				СчетП = тбПартии.Счет;
				НС =0;
				// находим остаток текущей партии в тбОстатки по ключу
				Если тбОстатки.НайтиЗначение(глПолучитьКлючТбОстатков(ПрихДокументП,ПоставкаП,ТМЦПартии.Код,СчетП,?(ВидОперации = 3,0,МестоХраненияП)),НС,"Ключ")=1 Тогда //--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
					тбОстатки.ПолучитьСтрокуПоНомеру(НС);
					ОстатокТовара = тбОстатки.ОстатокТовара;
					// если остаток 0, то с текущей партии не списываем
					Если ОстатокТовара = 0 Тогда
						Если тбПартии.ПолучитьСтроку()=0 Тогда
							Прервать;
						КонецЕсли;
						Продолжить;
					КонецЕсли;
					СписываемыйОстатокТовара = Мин(ОсталосьСписать, ОстатокТовара);
					КоэфСписания = СписываемыйОстатокТовара / ОстатокТовара;
					
					Если СписатьСуммуИзТЧ = 1 Тогда
						СписываемаяСтоимость = ?(ОсталосьСписать = СписываемыйОстатокТовара, ОсталосьСписатьСумма, Окр(СуммаСНДС / (Кво * Коэффициент) * СписываемыйОстатокТовара, 2));
						ОсталосьСписатьСумма = ОсталосьСписатьСумма - СписываемаяСтоимость;
					Иначе						
						КоэфРеализации = СписываемыйОстатокТовара / ВсегоСписать;
						// рассчитаем себестоимость текущей списываемой партии				
						СписываемаяСтоимость = ?(БезСС = 1, 0, тбОстатки.Стоимость * КоэфСписания);						
					КонецЕсли;
					
					СписываемаяПродСтоимость = тбОстатки.ПродСтоимость * КоэфСписания;
					// для записи в строку документа
					ВсегоСписаноКво = ВсегоСписаноКво + СписываемыйОстатокТовара;
					ВсегоСписаноСум = ВсегоСписаноСум + СписываемаяСтоимость;
					СписываемыйНДС = 0;                      
					КодОперации = СписаниеИзлишков;
					Регистр.Партии.ПривязыватьСтроку(НомерСтроки);
					// Списываем
					глПровестиПартию(Контекст, 0, 0, Фирма, ТМЦПартии, СчетП, МестоХраненияП, ПоставщикП, //--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
						ПоставкаП, ПрихДокументП,СписываемыйОстатокТовара, СписываемаяСтоимость, СписываемаяПродСтоимость,
						КодОперации, 0 ,0, 0);
					СтавкаНДС = ?(ВидНДС.Ставка.Получить(ДатаДок) = 0, 0, ТМЦПартии.СтавкаНДС.Ставка.Получить(ДатаДок));//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
					НДС = СписываемаяСтоимость * СтавкаНДС;					
					Если СписаниеМБП  = 1 Тогда
						глПроводка(Контекст,,"МЦ",СписываемаяСтоимость,"Списание МБП",СписываемыйОстатокТовара, ,,,	МестоХранения,ТМЦПартии,ПоставкаП, ,,"МЦ"); //--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
					Иначе	
						Если НаСобственныеНужды=1 Тогда
						    // нет прав на налоговый кредит, дооценим себестоимость
							глПроводка(Контекст,СчетП,"64.1.5",НДС,"Списание ТМЦ: дооценка себест. и сторно налогового кредита",, МестоХранения,ТМЦПартии,ПоставкаП,//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
							ВидНДС,,, ,,"СП");
						КонецЕсли;
						Если СчетСписания <> СчетПоКоду("231") Тогда
							глПроводкаПоЗатратам(Контекст,СчетСписания,СчетП,(СписываемаяСтоимость+?(НаСобственныеНужды=1,НДС,0)),"Списание ТМЦ: уч. сумма",СписываемыйОстатокТовара, СубконтоСписания1,СубконтоСписания2,СубконтоСписания3,
							МестоХранения,ТМЦПартии,ПоставкаП, ,,"СП",1,0);						    
						КонецЕсли;
					КонецЕсли;	
					глУчестьСписание(тбОстатки,СписываемыйОстатокТовара,СписываемаяСтоимость,СписываемаяПродСтоимость);
					ОсталосьСписать = ОсталосьСписать - СписываемыйОстатокТовара;
				КонецЕсли;
			КонецЕсли;
			Если ОсталосьСписать = 0 Тогда
				Прервать;
			КонецЕсли;   
			Если тбПартии.ПолучитьСтроку()=0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ОсталосьСписать > 0 Тогда
			глСообщитьОбОтсутствииПартии(0, Фирма, ТМЦПартии, НомерСтроки);//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
			Если Константа.РазрешитьОтрицОстатки = Нет Тогда
				фОшибка = 0;
			Иначе				
				глСообщитьОСозданииПартии(ТМЦПартии, ОсталосьСписать);//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
				МетодРасчетаСебестоимости = глПолучитьМетодРасчетаСебестоимости(ТМЦПартии,ДатаДок);//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
				ПрихДокументП = ?(МетодРасчетаСебестоимости<>Перечисление.МетодыРасчетаСебестоимости.ПоСреднему
				,ТекущийДокумент(),0);
				МестоХраненияП = ?((глПартионныйУчетПоСкладам = Да) ИЛИ (Фирма.ПартионныйУчетПоСкладам = 1), МестоХранения, "");
				СчетП = ТМЦПартии.Счет;//--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
				
				ПоставщикП = "";
				Если СписатьСуммуИзТЧ = 1 Тогда
					СписываемаяСтоимость = ОсталосьСписатьСумма;
				Иначе
					СписываемаяСтоимость = 0;
				КонецЕсли;
				глПровестиПартию(Контекст, 0, 0, Фирма, ТМЦПартии, СчетП, МестоХраненияП, ПоставщикП, //--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
					ПрихДокументП, ПрихДокументП, ОсталосьСписать, СписываемаяСтоимость, 0,	КодОперации, 0 ,0, 0);
				глПроводкаПоЗатратам(Контекст,СчетСписания,СчетП, 0,"Списание ТМЦ: уч. сумма",ОсталосьСписать, СубконтоСписания1,СубконтоСписания2,СубконтоСписания3,
							МестоХранения,ТМЦПартии,ПоставкаП, ,,"СП",1,0);		
			КонецЕсли; 
		КонецЕсли; 
		Если СписатьСуммуИзТЧ = 0 Тогда
			// запишем себестоимость в колонку ...
			СуммаБезНДС = ВсегоСписаноСум;
			ЦенаБезНДС = ?(ВсегоСписаноКво=0, 0, СуммаБезНДС/Кво);
			СтавкаНДС = ?(ВидНДС.Ставка.Получить(ДатаДок) = 0, 0, ТМЦПартии.СтавкаНДС.Ставка.Получить(ДатаДок)); //--- УМК Сандомирский В.Ю. (03.10.14) в остатки тмц в партии ТМЦ для приходования
			НДС = СуммаБезНДС * СтавкаНДС;
			СуммаСНДС = СуммаБезНДС + НДС;
		КонецЕсли;
	КонецЦикла; // строки документа
	ВремРег.Актуальность(0);	
	Возврат фОшибка;
КонецФункции

//======================================================================
Процедура СформироватьТЗПеремещения()
	НачатьТранзакцию();
	Док = СоздатьОбъект("Документ");	
	СписСписаний = СоздатьОбъект("СписокЗначений");
	Док.ВыбратьПодчиненныеДокументы(,,ТекущийДокумент());
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если (Док.Вид() = "Перемещение") Тогда
			Док.Удалить(0);
			СписСписаний.ДобавитьЗначение(Док.ТекущийДокумент());
		КонецЕсли;
	КонецЦикла;

	ЗафиксироватьТранзакцию();
КонецПроцедуры // 

//======================================================================
Процедура СформироватьПеремещение()
	Если ТЗДляПер.КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	ДокПер = СоздатьОбъект("Документ.Перемещение");
	Если СписСписаний.РазмерСписка() > 0 Тогда
		ДокПер.НайтиДокумент(СписСписаний.ПолучитьЗначение(1));
		ДокПер.СнятьПометкуУдаления();
	Иначе
		ДокПер.Новый();
		ДокПер.Фирма = Фирма;
		ДокПер.ДатаДок = ДатаДок;
		глУстановитьНомер(ДокПер, 1);			
	КонецЕсли;
	ДокПер.ДатаДок = ДатаДок;			
	ДокПер.МестоХранения = МестоХранения;
	ДокПер.НовоеМестоХранения = МестоХранения;
	ДокПер.ДокументОснование = ТекущийДокумент();
	ДокПер.СписыватьМатериал = 1;// + umk
	ДокПер.ВидТМЦ = 1;
	ДокПер.Автор = глПользователь;
	ДокПер.Примечание = "Создан автоматически";
	ДокПер.УдалитьСтроки();
	ТЗДляПер.ВыбратьСтроки();
	Пока ТЗДляПер.ПолучитьСтроку() = 1 Цикл
		ДокПер.НоваяСтрока();
		ДокПер.ТМЦ = ТЗДляПер.ТМЦМарк;
		ДокПер.Счет = ДокПер.ТМЦ.Счет;
		ДокПер.Стало = ТЗДляПер.ТМЦ;
		ДокПер.СталоСчет = ДокПер.Стало.Счет;
		ДокПер.Кво = ТЗДляПер.Кво;
		ДокПер.ВидУпаковки = НетУп; 
		ДокПер.ВидУпаковкиСтало = ТЗДляПер.ВидУпаковки_;
	КонецЦикла;
	
	Ч = 0; М = 0; С = 0;
	ТекущийДокумент().ПолучитьВремя(Ч,М,С);
	ДокПер.Записать();
	ДокПер.УстановитьВремя(Ч, М, С-1);
	ДокПер.Записать();
	ОткрытьФормуМодально(ДокПер.ТекущийДокумент(), 1);
	
	ЗафиксироватьТранзакцию();
	Сообщить("Создано Перемещение: " + Строка(ДокПер));
КонецПроцедуры // гл

// ===============================
Процедура ДвиженияРезерв();	
	Если ДокОснование.Вид()="УМК_ЗаказКлиента" Тогда //--- УМК Сандомирский В.Ю. (31.03.15) 		
		//--- УМК Сандомирский В.Ю. (01.04.15) Движения по регистру резерв 
		Если (ПустоеЗначение(Константа.УМК_ДатаНачалаРаботыРезервов) <> 1) 
			И (Константа.УМК_ДатаНачалаРаботыРезервов <= ДатаДок)  Тогда 	
		
			глКомментарий("Выполняется снятие резерва на продаваемые ТМЦ",2);
			ВыбратьСтроки();
			Пока ПолучитьСтроку()=1 Цикл
				НС = 0;
				
				Если ПустоеЗначение(ВидУпаковки) = 1 Тогда
					ТекВидУпаковки = НетУп;
				Иначе
					ТекВидУпаковки = ВидУпаковки;
				КонецЕсли;
				
				//ТекущийРезерв = ВремРег.Резервы.Остаток(Фирма,ТМЦ,ДокументОснование,ТекВидУпаковки,"Резерв");
				
				//СписываемыйРезерв = Мин(ТекущийРезерв,Кво);
				
				//Если СписываемыйРезерв > 0 Тогда
					Регистр.Резервы.ПривязыватьСтроку(НомерСтроки);
					Регистр.Резервы.Фирма			= Фирма;
					Регистр.Резервы.ТМЦ				= ТМЦ;  
					Регистр.Резервы.ВидУпаковки		= ТекВидУпаковки;  
					Если ДокОснование.ДатаДок >= '01.06.2016' Тогда
						Регистр.Резервы.ФормаУпаковки	= глПолучитьФормуУпаковки(ТекВидУпаковки, ТМЦ, ДокОснование.ДатаДок);
					КонецЕсли;
					Регистр.Резервы.ДокументРезерва = ДокОснование;
					
					Регистр.Резервы.Резерв			= Кво * Коэффициент;
					
					Если ТМЦ.УчетДопКво = 1 Тогда
						//ТекущийДопКво = ВремРег.Резервы.Остаток(Фирма,ТМЦ,ДокументОснование,ТекВидУпаковки,"ДопКво");
						//СписываемыйДопКво = Мин(ТекущийДопКво,ДопКво);
						Регистр.Резервы.ДопКво		= ДопКво;//СписываемыйДопКво;
					Иначе
						Регистр.Резервы.ДопКво		= 0;
					КонецЕсли;
					
					Регистр.Резервы.ДвижениеРасходВыполнить();
				//КонецЕсли;
			КонецЦикла;			
		КонецЕсли;		
	КонецЕсли;	
КонецПроцедуры        

//=============================== + umk Товары в рознице передача
//
Процедура ДвижениеТоварыВРознице()
	
	Если ПродажаРозница = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ДатаДок < Константа.ДатаНачалаВеденияУчетаВРознице) ИЛИ (ПустоеЗначение(Константа.ДатаНачалаВеденияУчетаВРознице) = 1) Тогда
		Возврат;
	КонецЕсли;
	
	СтоимостьПродажи = 0;
	Себестоимость = 0;
	
	ВыбратьСтроки();
	
	Пока ПолучитьСтроку() = 1 Цикл	
		
		глПолучитьСтоимостьОстаткаРозница (Контекст, СтоимостьПродажи, Себестоимость,ТМЦ, МестоХранения, Фирма);
		
		Регистр.ТоварыВРознице.ПривязыватьСтроку(НомерСтроки);
		Регистр.ТоварыВРознице.Организация = Фирма;
		Регистр.ТоварыВРознице.МестоХранения =  МестоХранения;
		Регистр.ТоварыВРознице.Номенклатура =  ТМЦ;
		Регистр.ТоварыВРознице.Кол =  Кво*Ед.Коэффициент;
		Регистр.ТоварыВРознице.СтоимостьПродажи = СтоимостьПродажи*Кво*Ед.Коэффициент;
		Регистр.ТоварыВРознице.Себестоимость =  Себестоимость*Кво*Ед.Коэффициент;
		Регистр.ТоварыВРознице.ДвижениеРасходВыполнить();
		
	КонецЦикла;
	
КонецПроцедуры

//================================
//Затраты на ремонт ОС
Процедура ДвиженияЗатратыНаРемонтОС()
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ОсновноеСредство) = 0 Тогда
			Регистр.ЗатратыНаРемонтОС.ПривязыватьСтроку(НомерСтроки);
			Регистр.ЗатратыНаРемонтОС.ОсновноеСредство = ОсновноеСредство;
			Регистр.ЗатратыНаРемонтОС.Сумма = СуммаСНДС;
			Регистр.ЗатратыНаРемонтОС.ДвижениеПриходВыполнить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	Если НазваниеНабораПрав() = "Заказ" Тогда
		Если (МестоХранения.ДляГП = 0) Тогда
			Сообщить("Вам разрешено проводить только с основного склада");
	        СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	глКомментарий("Начало",2,Контекст);

	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;
	
	Если (ПустоеЗначение(Константа.УМК_ДатаНачалаПартииОстатки) <> 1) //--- УМК Сандомирский В.Ю, (09.10.14) (проверка действует с момента разделения регистров)
				И (Константа.УМК_ДатаНачалаПартииОстатки <= ДатаДок)  Тогда 
		Если глКонтрольНаличияТМЦДляСписания(Контекст) = 1 Тогда //---УМК Сандомирский В.Ю, (09.10.14) контролируем наличие ТМЦДляСписания в ТМЦ табличной части контектста
			//--- если категорично то можно поставить в этом месте 
			//глНеПроводить(Контекст);
		    //Возврат;
		КонецЕсли;
	КонецЕсли;		
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
		    глНеПроводить(Контекст);
		    Возврат;
		КонецЕсли;
	КонецЦикла;      

	Если глВсеВыбрано = 1 Тогда
		СформироватьТЗПеремещения();	
		
		РассчитатьШапку();
		Если ДатаДок >= ДатаНачалаФормированияПеремещенийПоМаркировке Тогда			
			СформироватьПеремещение();
		КонецЕсли;
		Если ДвиженияПартии() = 0 Тогда
		    глНеПроводить(Контекст);
			глТбОперация.УдалитьСтроки();
		    Возврат;
		КонецЕсли;
		ДвиженияРезерв();
		глЗаписатьПроводкиВОперацию(Контекст);  
	
		Операция.СуммаОперации = Итог("СуммаБезНДС");
		Операция.Содержание = Примечание;
		Операция.Записать();		
	КонецЕсли;	
	
	ДвижениеТоварыВРознице(); // + umk
	
	Если ВидОперации = 2 Тогда
		ДвиженияЗатратыНаРемонтОС();
	КонецЕсли;
	
	глКомментарий("Окончание",2,Контекст);
	
КонецПроцедуры

// ===============================
Процедура ОбработкаУдаленияПроведения()
	Если НазваниеНабораПрав() = "Заказ" Тогда
		Если (МестоХранения.ДляГП = 0) Тогда
			Сообщить("Вам разрешено проводить только с основного склада");			
	        СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		// очистим реквизиты заполненные при проведении
		СуммаБезНДС = 0;
		НДС = 0;
		СуммаСНДС = 0;
	КонецЦикла;
КонецПроцедуры

ДатаНачалаФормированияПеремещенийПоМаркировке = '01.06.2019';