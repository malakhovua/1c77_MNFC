Перем ОстТоваров,РезТоваров;                   

// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
	глВыбранЛи(Фирма,"Фирма");  
	глВыбранЛи(РСчет,"Расчетный счет");  
	глВыбранЛи(ВидТары,"Вид тары");
	глВыбранЛи(Контрагент,"Контрагент");
	глВыбранЛи(Валюта,"Валюта");         
	глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС"));
  	глВсеВыбрано = ?(глВсеВыбрано = 0, 0, глПроверкаДублейСтрок(Контекст));
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
	глВыбранЛи(ТМЦ,"ТМЦ",НомерСтроки);
	глВыбранЛи(Ед,"Единица",НомерСтроки);
	глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,ТМЦ,НомерСтроки,1)=Да, глВсеВыбрано, 0);
	Возврат глВсеВыбрано;
КонецФункции
               
// ===============================
Функция СвободныйОстаток()
	ВремКолво = ОстТоваров.СводныйОстаток(Фирма,ТМЦ,,"ОстатокТовара");
	ВремРезерв = РезТоваров.СводныйОстаток(Фирма,ТМЦ,,"Резерв");      
	Возврат ВремКолво - ВремРезерв;
КонецФункции

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);
	
	// по Регистру Резервы (резервируем ТМЦ при выписке Счета)
	ВремРегистры = СоздатьОбъект("Регистры");
	ОстТоваров = ВремРегистры.Остатки;
	РезТоваров = ВремРегистры.Резервы;
	                           
	Если ЧтоПродаем=Перечисление.ЧтоПродаем.НеоборотныеАктивы Тогда
		// необоротные активы не резервируются
		Возврат;
	КонецЕсли;

	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст); 
		Возврат;
	КонецЕсли;     
	
	// чтоб для каждой строки не обращаться к константам, сделаем переменную
	Если (Константа.РезервироватьТоварНаСегодня = Нет) 
	И (ПустоеЗначение(СрокРезервирования) = 1) Тогда
		текРезервироватьТовар = Нет;
	Иначе
		текРезервироватьТовар = Да;
	КонецЕсли;       
	текПроверкаРезерва = Константа.РезервироватьТолькоСвободныйТовар;
	
	Если (ИтогиАктуальны()=0) 
	И (текПроверкаРезерва = Да)
	И (текРезервироватьТовар = Да) Тогда           
		спТовар = СоздатьОбъект("СписокЗначений");
		ВыгрузитьТабличнуюЧасть(спТовар,"ТМЦ");
		ОстТоваров.УстановитьЗначениеФильтра("Фирма",Фирма,1);
		ОстТоваров.УстановитьЗначениеФильтра("ТМЦ",спТовар,2);
		ОстТоваров.ВременныйРасчет();
		РезТоваров.УстановитьЗначениеФильтра("Фирма",Фирма,1);
		РезТоваров.УстановитьЗначениеФильтра("ТМЦ",спТовар,2);
		РезТоваров.ВременныйРасчет();
		Если Константа.ПроверятьДублиСтрок = Нет Тогда
			ВремРегистры.Актуальность(1);
		КонецЕсли;
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());		
	КонецЕсли;
	
	Если ПустоеЗначение(Договор) = 0 Тогда
		Если Договор.Вид() = "Заказ" Тогда
			текРезервироватьТовар = 0;
		КонецЕсли;	                      
	КонецЕсли;	                      
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()>0 Цикл
		
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст);
			Возврат;
		КонецЕсли;
		
		Если ТМЦ.ВидТМЦ=Перечисление.ВидыТМЦ.Услуга Тогда
			// услуги не резервируем
			Продолжить;
		КонецЕсли;       
		
		Если текРезервироватьТовар = Да Тогда
			Если текПроверкаРезерва = Да Тогда
				// проверим наличие свободного остатка
				СвободныйОстаток = СвободныйОстаток();
				Если Кво*Коэффициент>СвободныйОстаток Тогда
					глНеПроводить(Контекст,Шаблон("Строка [НомерСтроки]. Недостаточно свободного ТМЦ [ТМЦ]. 
					| Свободный остаток [СвободныйОстаток], необходимо [Кво*Коэффициент]."));
				КонецЕсли;
			КонецЕсли;
			
			Регистр.Резервы.ПривязыватьСтроку(НомерСтроки);
			Регистр.Резервы.Фирма=Фирма;
			Регистр.Резервы.ТМЦ=ТМЦ;
			Регистр.Резервы.ДокументРезерва=ТекущийДокумент();
			Регистр.Резервы.Резерв=Кво*Коэффициент;
			Регистр.Резервы.ДвижениеПриходВыполнить();
		КонецЕсли;	
	КонецЦикла;         
		          
	Если ПустоеЗначение(Договор)=1 Тогда
	     Договор = ПолучитьПустоеЗначение("Документ");
	КонецЕсли; 
	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
