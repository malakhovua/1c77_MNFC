// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем СубконтоВалРасхПоУмолчанию;
Перем НачальнаяДатаДокумента;


// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
Процедура ЗаполнитьСубсчетами(СпСчетов, СчетРодитель) 
	ТекСчет=СчетПоКоду(СчетРодитель,ВыбранныйПланСчетов());
	Если ТекСчет.Выбран()=0 Тогда // нет такого счета
		Возврат;
	КонецЕсли;

	ВремСчет = СоздатьОбъект("Счет");
	ВремСчет.ИспользоватьПланСчетов(ВыбранныйПланСчетов());
	ВремСчет.ИспользоватьРодителя(СчетПоКоду(СчетРодитель,ВыбранныйПланСчетов()));
	ВремСчет.ВыбратьСчета();
	Пока ВремСчет.ПолучитьСчет()=1 Цикл
		ТекСчет=ВремСчет.ТекущийСчет();
		глДобавитьЗначениеБезПовторения(СпСчетов,ТекСчет,ТекСчет.Код);
	КонецЦикла;
КонецПроцедуры //ЗаполнитьСубсчетами

// ======================================
Функция ЗаполнитьМестоНахожденияНеоборотногоАктива()
Перем Ит;

	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Фирма);
	Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,НеоборотныйАктив,2);
	Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения,,,0);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,,,0);
	Ит.Опции(0,0);
	Ит.ВключатьСубсчета(-1);
	Если Ит.ВыполнитьЗапрос(?(Выбран()=1,ТекущийДокумент(),ДатаДок),,НеоборотныйАктив.Счет,,,,,"К")=0 Тогда
		Предупреждение ("Запрос по бухгалтерским итогам не выполнен!");
		Возврат 0;
	КонецЕсли;

	Ит.ВыбратьСубконто(1);
	Если Ит.ПолучитьСубконто(1)=1 Тогда
		Подразделение = 0;
		Ит.ВыбратьСубконто(2);
		Пока Ит.ПолучитьСубконто(2)=1 Цикл
			Если ПустоеЗначение(Подразделение)=0 Тогда
				Сообщить("Необоротный актив "+НеоборотныйАктив+" по бухгалтерским итогам числится на нескольких подразделениях:
							|""" + Подразделение + """ и """ + Ит.Субконто(2)+"""!");
			Иначе
				Подразделение = Ит.Субконто(2);
			КонецЕсли;
		КонецЦикла;
		МестоХранения = 0;
		Ит.ВыбратьСубконто(3);
		Пока Ит.ПолучитьСубконто(3)=1 Цикл
			Если ПустоеЗначение(МестоХранения)=0 Тогда
				Сообщить("Необоротный актив "+НеоборотныйАктив+" по бухгалтерским итогам числится на нескольких местах хранения:
							|""" + МестоХранения + """ и """ + Ит.Субконто(3)+"""!");
			Иначе
				МестоХранения = Ит.Субконто(3);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецФункции


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Процедура Печать()
	Если Итог("Сумма") = 0 Тогда
		Предупреждение("Итоговая сумма по табличной части равна 0!");
	    Возврат;
	КонецЕсли;
	
	СуммаНаУвеличениеСтоимости = 0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Сред(СчетМодернизации.Код,1,2) <> "15" Тогда
		    Продолжить;
		КонецЕсли;
		СуммаНаУвеличениеСтоимости = СуммаНаУвеличениеСтоимости + Сумма;
	КонецЦикла;
	Если СуммаНаУвеличениеСтоимости = 0 Тогда
	    Предупреждение("Вся сумма модернизации отнесена на затраты!");
		Возврат;
	КонецЕсли;
	
	Таб = СоздатьОбъект("Таблица");
	СуффиксТаблицы = "_Укр";
	Таб.ИсходнаяТаблица("Таблица"+СуффиксТаблицы);
	глУстПропись(Гривня,"у");
	Директор = глФИО(Фирма.Руководитель.Получить(ДатаДок),1);
	ГлавБух = глФИО(Фирма.ГлавныйБухгалтер.Получить(ДатаДок),1);
	
	ПолучитьСтрокуПоНомеру(1);
		
	Таб.Вывести();
	Таб.Опции(0,0,0,0);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Модернизация необоротного актива","");
КонецПроцедуры

// ======================================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Форма.Заголовок(глЗаголовок(Контекст,"Модернизация необоротных активов"));
	Форма.Фирма.Видимость(0);

	Доступность = ?(НеоборотныйАктив.ВидНеоборотногоАктива = НМА,1,0);
	Форма.тПодразделение.	Доступность(Доступность);
	Форма.тМестоХранения.	Доступность(Доступность);
	Форма.Подразделение.	Доступность(Доступность);
	Форма.МестоХранения.	Доступность(Доступность);
	
	Если (НеоборотныйАктив.Производственное = 0) Или (НеоборотныйАктив.ВидНеоборотногоАктива = НМА) 
			Или (Итог("Сумма")=0) Тогда
		Доступность = 0;
	Иначе
		Доступность = 1;
	КонецЕсли;
	Форма.тБалСтоимость.	Доступность(Доступность);
	Форма. БалСтоимость.	Доступность(Доступность);
	Форма.рНалоговыйУчет.	Доступность(Доступность);
	Форма. СуммаВалРасх.	Доступность(Доступность);
	Форма.тСуммаВалРасх.	Доступность(Доступность);
	Форма.рВаловыеРасходы.	Доступность(Доступность);

	Доступность = Мин(?(СуммаВалРасх = 0,0,1),Доступность);
	Форма. СубконтоВалРасх.	Доступность(Доступность);
	Форма.тСубконтоВалРасх.	Доступность(Доступность);

	Позиция = Форма.Закладки.НайтиЗначение("Дополнительный");
	Если (НеоборотныйАктив.Производственное = 0) Или (Итог("Сумма")=0) Тогда
		Если Позиция > 0 Тогда
			Форма.Закладки.УдалитьЗначение(Позиция);
		КонецЕсли;
	Иначе
		Если Позиция = 0 Тогда
			Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
		КонецЕсли;
	КонецЕсли;

	Возврат "";

КонецФункции 

// ===============================
Процедура ИзмНалоговыеСуммы(ЭлементДиалога = "")
	
	ИтогСумма = Итог("Сумма");
	Если (НеоборотныйАктив.Производственное = 0) Или (НеоборотныйАктив.ВидНеоборотногоАктива = НМА) 
			Или (ИтогСумма = 0) Тогда
		СуммаВалРасх = 0;
	КонецЕсли;

	Если НеоборотныйАктив.Производственное = 0 Тогда
		БалСтоимость = 0;
		Возврат;
	КонецЕсли;

	Если ПустоеЗначение(ЭлементДиалога)=1 Тогда
		ЭлементДиалога = Форма.АктивныйЭлемент();
	КонецЕсли;
	
	Если ЭлементДиалога = "БалСтоимость" Тогда
		Если БалСтоимость > ИтогСумма Тогда
			Предупреждение("Вы не можете ввеси сумму, превышающую итог по табличной части!");
			БалСтоимость = ИтогСумма;
		КонецЕсли;
		СуммаВалРасх = ИтогСумма - БалСтоимость;

	Иначе
		Если ЭлементДиалога = "Удаление" Тогда
			ИтогСумма = ИтогСумма - Сумма;
		КонецЕсли;

		Если СуммаВалРасх > ИтогСумма Тогда
			Если ЭлементДиалога = "СуммаВалРасх" Тогда
				Предупреждение("Вы не можете ввеси сумму, превышающую итог по табличной части!");
			Иначе
				глКомментарий("Сумма валовых расходов уменьшена до итога по табличной части!",0);
			КонецЕсли;
			СуммаВалРасх = ИтогСумма;
		КонецЕсли;
	    БалСтоимость = ИтогСумма - СуммаВалРасх;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмСчетМодернизации()
	СчетМодернизации = СпСчетовМодернизации.ПолучитьЗначение(СпСчетовМодернизации.ТекущаяСтрока());
КонецПроцедуры //ИзмСчетМодернизации

// ======================================
Процедура ИзмНеоборотныйАктив()
	Перем Ит;
	
	Подразделение = 0;
	МестоХранения = 0;
	ЗаполнитьМестоНахожденияНеоборотногоАктива();

	СубконтоВалРасх = ?((НеоборотныйАктив.Производственное = 0) Или (НеоборотныйАктив.ВидНеоборотногоАктива = НМА),
						0,СубконтоВалРасхПоУмолчанию);
	ИзмНалоговыеСуммы();
КонецПроцедуры 

// ======================================
Процедура ИзмФирма()
КонецПроцедуры 


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================
                        
// ======================================
Процедура ПриУдаленииСтроки()
	ИзмНалоговыеСуммы("Удаление");
КонецПроцедуры

// ======================================
Процедура ПриВыбореЗакладки(Ном,Значение);
	Форма.ИспользоватьСлой(Значение+", Общий",2);
КонецПроцедуры  

// ======================================
Процедура ПриОткрытии()

	глПроверкаДатыДок(Контекст,"Открытие");
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительно");
	Форма.ИспользоватьСлой("Основной,Общий",2);
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.		Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кФирма.	Доступность(0);               		
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли;	

	глСохранитьЗначение(Контекст,"Фирма",		 	Фирма);
	глСохранитьЗначение(Контекст,"НеоборотныйАктив",НеоборотныйАктив);
	глСохранитьЗначение(Контекст,"Подразделение",	Подразделение);
	глСохранитьЗначение(Контекст,"МестоХранения",	МестоХранения);
	
	ЗаполнитьСубсчетами(СпСчетовМодернизации,"15");
	Поз = СпСчетовМодернизации.НайтиЗначение(СчетМодернизации); 
	Если Поз = 0 Тогда
		СпСчетовМодернизации.ТекущаяСтрока(1);
		ИзмСчетМодернизации();
	Иначе
		СпСчетовМодернизации.ТекущаяСтрока(Поз);
	КонецЕсли;
	
	ИзмНалоговыеСуммы();

КонецПроцедуры 

// ======================================
Процедура ВводНового(ПризнакКопирования)
	Если ПризнакКопирования = 1 Тогда
		глУстановитьНомер(Контекст);
		Возврат;
	КонецЕсли;
	глУстановитьФирму(Контекст);
КонецПроцедуры 

// ======================================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Автор = глПользователь;
КонецПроцедуры

// ======================================
Процедура ПриНачалеВыбораЗначения(Рекв, ФлагСтандОбр)
	Если Рекв = "НеоборотныйАктив" Тогда
        ФлагСтандОбр = 0;
		КонтФирмы = Фирма;
		ОткрытьФорму("Справочник.НеоборотныеАктивы.ФормаСписка",КонтФирмы);
		ИзмНеоборотныйАктив();
	КонецЕсли;		
КонецПроцедуры 

// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

Спр = СоздатьОбъект("Справочник.ВалДоходыРасходы");
Если Спр.НайтиПоКоду("2/11") = 1 Тогда
    СубконтоВалРасхПоУмолчанию = Спр.ТекущийЭлемент();
Иначе
    СубконтоВалРасхПоУмолчанию = 0;
КонецЕсли;
СписокДействий = глПолучитьСписокДействий("
	|ДвиженияДокумента,
	|ОткрытьВЖурнале");
