Перем ВремРег, ПЗ;

//===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
    глВыбранЛи(Фирма,"Фирма");
    глВыбранЛи(Подразделение,"Подразделение");
	глПроверитьТипПодразделения(Подразделение);
	Если (ПоВидамДеятельности = 1) и (ПоЗаказам = 0) и (ПоПродукции = 1) Тогда
		глКомментарий("",0);
		глВсеВыбрано = 0;
	КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции  //ПроверкаШапки         

// ===============================
Функция РассчитатьШапку()
	ВремРег = СоздатьОбъект("Регистры");
	ВремРег.Актуальность(1);
	ПЗ = ВремРег.ПроизводственныеЗатраты;
	ПЗ.УстановитьЗначениеФильтра("Фирма",Фирма,1);
	ПЗ.УстановитьЗначениеФильтра("Подразделение",Подразделение,1);
	Если ИтогиАктуальны() = 0 Тогда
		ПЗ.ВременныйРасчет(1);
		ВремРег.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
КонецФункции //РассчитатьШапку
                                
// ===============================
Функция ПолучитьНормы(ЗапрНЗ,Этап,СтатьяК,Материал)
    Если Этап = 1 Тогда
    	Возврат ЗапрНЗ.Получить(СтатьяК,Материал,);
	ИначеЕсли Этап = 2 Тогда
    	Возврат ЗапрНЗ.Получить(ПЗ.ВидДеятельности,СтатьяК,Материал,);
	ИначеЕсли Этап = 3 Тогда
    	Возврат ЗапрНЗ.Получить(ПЗ.ВидДеятельности,ПЗ.Заказ,СтатьяК,Материал,);
    КонецЕсли;
КонецФункции //ПолучитьНормы

// ===============================
Процедура ДвижениеПЗ(ПЗ,РаспрАналитика,ЗначАналитики,Кво,Сумма,СуммаДав)
	// всю аналитику копируем из рагистра ПЗ
	Регистр.ПроизводственныеЗатраты.Фирма = ПЗ.Фирма;
	Регистр.ПроизводственныеЗатраты.Подразделение = ПЗ.Подразделение;
	Регистр.ПроизводственныеЗатраты.ВидДеятельности = ПЗ.ВидДеятельности;
	Регистр.ПроизводственныеЗатраты.Заказ = ПЗ.Заказ;
	Регистр.ПроизводственныеЗатраты.Продукция = ПЗ.Продукция;
	Регистр.ПроизводственныеЗатраты.ВидЗатрат = ПЗ.ВидЗатрат;
	Регистр.ПроизводственныеЗатраты.Материал = ПЗ.Материал;
	// кроме той, которую распределяем
	Регистр.ПроизводственныеЗатраты.УстановитьАтрибут(РаспрАналитика,ЗначАналитики);
	// заполняем ресурсы
	Регистр.ПроизводственныеЗатраты.Количество = Кво;
	Регистр.ПроизводственныеЗатраты.Сумма = Сумма;
	Регистр.ПроизводственныеЗатраты.СуммаДав = СуммаДав;
	// заполняем реквизиты
	Регистр.ПроизводственныеЗатраты.КодОперации = ПерераспрЗатрат;
	// двигаем, всегда прриход, т.к. расход делается только при фактической калькуляции
	Регистр.ПроизводственныеЗатраты.ДвижениеПриходВыполнить();
КонецПроцедуры //

// ===============================
Функция Печ(Значение)
    Если ПустоеЗначение(Значение)=1 Тогда
    	Возврат "<пустое значение>";
	Иначе
		Возврат СокрЛП(Строка(Значение));
    КонецЕсли;
КонецФункции //

// ===============================
Функция РаспределениеЗатрат(Этап)
	// Строим запрос по нормативным затратам 
	ДопГруппировки = "";
	Если Этап = 1 Тогда
		РаспрАналитика 	= "ВидДеятельности";
		АналитикаДляКомментария = "видами деятельности";
		ПустЗнач		= ПолучитьПустоеЗначение("Справочник.ВидыДеятельности");
	ИначеЕсли Этап = 2 Тогда
		РаспрАналитика 	= "Заказ";
		АналитикаДляКомментария = "заказами";
		ПустЗнач		= ПолучитьПустоеЗначение("Документ.Заказ");
		ДопГруппировки 	= "Группировка ВидДеятельности без упорядочивания без групп;";
	ИначеЕсли Этап = 3 Тогда
		РаспрАналитика 	= "Продукция";
		АналитикаДляКомментария = "продукцией";
		ПустЗнач		= ПолучитьПустоеЗначение("Справочник.ТМЦ");
		ДопГруппировки 	= "Группировка ВидДеятельности без упорядочивания без групп;
						 |Группировка Заказ без упорядочивания;";
	КонецЕсли;
	глКомментарий("Производится распределение затрат между "+АналитикаДляКомментария+".",2);
	ДатаС = НачМесяца(ДатаДок);
	УсловиеПо = "";
	Если ИтогиАктуальны()=0 Тогда
		УсловиеПо = " по ТекДок ";
		ТекДок = ТекущийДокумент();
	КонецЕсли;
	ТЗ = "//{{ЗАПРОС(НормыЗатрат)
	|Период с ДатаС "+УсловиеПо+";
	|Фирм = Регистр.НормативныеЗатраты.Фирма;
	|Подр = Регистр.НормативныеЗатраты.Подразделение;
	|ВидДеятельности = Регистр.НормативныеЗатраты.ВидДеятельности;
	|Продукция = Регистр.НормативныеЗатраты.Продукция;
	|УрПередела = Регистр.НормативныеЗатраты.Продукция.УровеньПередела;
	|СтатьяКалькуляции = Регистр.НормативныеЗатраты.СтатьяКалькуляции;
	|Материал = Регистр.НормативныеЗатраты.Материал;
	|Заказ = Регистр.НормативныеЗатраты.Заказ;
	|Сум = Регистр.НормативныеЗатраты.Сумма;
	|Колво = Регистр.НормативныеЗатраты.Количество;
	|Функция Сумма = Сумма(Сум);
	|Функция Кво = Сумма(Колво);
	|"+ДопГруппировки+"
	|Группировка СтатьяКалькуляции без упорядочивания без групп;
	|Группировка Материал без упорядочивания без групп;
	|Группировка "+РаспрАналитика+" без упорядочивания без групп;
	|Условие(Фирм=Фирма);
	|Условие(Подр=Подразделение);
	|Условие(УрПередела <= УровеньПередела);";
	
	ЗапрНЗ = СоздатьОбъект("Запрос");
	Если ЗапрНЗ.Выполнить(ТЗ)=0 Тогда
		Возврат 0;
	КонецЕсли;
	
	// Выбираем распределяемые итоги
	ПЗ.ВыбратьИтоги();
	Пока ПЗ.ПолучитьИтог()=1 Цикл
		Если (ПустоеЗначение(ПЗ.ПолучитьАтрибут(РаспрАналитика))=0) Тогда
			// если нужная аналитика уже заполнена, то пропускаем итог
			Продолжить;
		КонецЕсли;
		// распределяемая затрата должна соответствовать правилам, проверяем её
		Если глПроверитьАналитикуПоЗатратам(ПЗ.Подразделение,ПЗ.ВидДеятельности,ПЗ.Заказ,ПЗ.Продукция,
		"а аналитика ""Подраделение""","а аналитика ""Вид деятельности""","а аналитика ""Заказ""","а аналитика ""Продукция""")=0 Тогда
			глКомментарий("Некорректно отражены затраты в регистре ""Производственные затраты"":",1);
			глКомментарий("Подразделение: "+Печ(ПЗ.Подразделение)+", Вид деятельности: "+Печ(ПЗ.ВидДеятельности)+", Заказ: "+Печ(ПЗ.Заказ)+", Продукция: "+Печ(ПЗ.Продукция),1);
			глНеПроводить(Контекст);
			Возврат 0;
		КонецЕсли;
		Если (Этап = 2) и (ПЗ.ВидДеятельности.ПозаказноеПроизводство = Нет) Тогда
			// для этого вида деятельности по заказам ничего распределять не нужно
			Продолжить;
		КонецЕсли;
		// распределение
		ЗапрНЗ.вНачалоВыборки();
		Если ПолучитьНормы(ЗапрНЗ,Этап,ПЗ.ВидЗатрат.СтатьяКалькуляции,ПЗ.Материал) = 1 Тогда
			// для нематериальных затрат базой является сумма, а для материальных - количество материалов
			БазаВсего = ?(ПустоеЗначение(ПЗ.Материал)=1,ЗапрНЗ.Сумма,ЗапрНЗ.Кво);
			Если БазаВсего = 0 Тогда
				глКомментарий("Для распределяемых затрат база распределения нулевая!",0);
				глКомментарий("Подразделение: "+Печ(ПЗ.Подразделение)+", Вид деятельности: "+Печ(ПЗ.ВидДеятельности)+", Заказ: "+Печ(ПЗ.Заказ)+", Продукция: "+Печ(ПЗ.Продукция)+
				РазделительСтрок+", Статья калькуляции: "+Печ(ПЗ.ВидЗатрат.СтатьяКалькуляции)+", Вид затрат: "+Печ(ПЗ.ВидЗатрат)+?(ПустоеЗначение(ПЗ.Материал)=1,"",", Материал: "+ПЗ.Материал),1);
				глНеПроводить(Контекст);
				Возврат 0;
			КонецЕсли;
			БазаРаспр = 0; // сколько уже распределено, для борьбы с ошибками округления
			ВсегоКво = ПЗ.Количество;
			ВсегоСумма = ПЗ.Сумма;
			ВсегоСуммаДав = ПЗ.СуммаДав;
			РаспрКво = 0;
			РаспрСумма = 0;
			РаспрСуммаДав = 0;
			// списываем затраты с пустой аналитики
			ДвижениеПЗ(ПЗ,РаспрАналитика,ПустЗнач,-ВсегоКво,-ВсегоСумма, -ВсегоСуммаДав);
			// приходуем на определенную аналитику
			Пока ЗапрНЗ.Группировка(РаспрАналитика)=1 Цикл
				ТекАналитика = ЗапрНЗ.ПолучитьАтрибут(РаспрАналитика);
				// для нематериальных затрат базой является сумма, а для материальных - количество материалов
				База = ?(ПустоеЗначение(ПЗ.Материал)=1,ЗапрНЗ.Сумма,ЗапрНЗ.Кво);
				БазаРаспр = БазаРаспр + База;
				//рассчитываем суммы
				Если БазаРаспр = БазаВсего Тогда
					// если это последняя аналитика, по которой нужно распределить
					// берем остаток нераспределенного
					ТекКво 		= ВсегоКво - РаспрКво;
					ТекСумма 	= ВсегоСумма - РаспрСумма;
					ТекСуммаДав = ВсегоСуммаДав - РаспрСуммаДав;
				Иначе
					// рассчитываем пропорционально базе
					К 			= База/БазаВсего;
					ТекКво 		= Окр(ВсегоКво*К,3);
					ТекСумма 	= Окр(ВсегоСумма*К,2);
					ТекСуммаДав = Окр(ВсегоСуммаДав*К,2);
					// накапливаем, сколько уже распределено
					РаспрКво 	= РаспрКво + ТекКво;
					РаспрСумма	= РаспрСумма + ТекСумма;
					РаспрСуммаДав = РаспрСуммаДав + ТекСуммаДав;
				КонецЕсли;
				// приходуем на определенную аналитику
				ДвижениеПЗ(ПЗ,РаспрАналитика,ТекАналитика,ТекКво,ТекСумма,ТекСуммаДав);
				Если Этап = 1 Тогда
					// если происходит распределение между видами деятельности, то формируем проводки
					глПроводка(Контекст,"231","231",ТекСумма,"Перераспределение затрат",, ТекАналитика,ПЗ.Подразделение,,
					ПЗ.ВидДеятельности,ПЗ.Подразделение,,,,"РЗ",1);
					Если ТекСуммаДав <> 0 Тогда
						// если есть давльческая сумма, то формируем проводки по забалансовому счету ДВ
						глПроводка(Контекст,"ДВ","ДВ",ТекСумма+ТекСуммаДав,"Перераспределение затрат",, ТекАналитика,ПЗ.Подразделение,,
						ПЗ.ВидДеятельности,ПЗ.Подразделение,,,,"РЗ",1);
					КонецЕсли;
				ИначеЕсли Этап = 2 Тогда
					// если происходит распределение между заказами, 
					// то при добавлении затрат на давальческий заказ нужно дооценить счет ДВ
					Если глЭтоДавальческийЗаказ(ТекАналитика)=1 Тогда
						глПроводка(Контекст,"ДВ",,ТекСумма,"Перераспределение затрат",, ПЗ.ВидДеятельности,ПЗ.Подразделение,,
						,,,,,"РЗ",1);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			// в запросе по нормам не оказалось такой аналитики
			глКомментарий("Для распределяемых затрат не найдена база распределения!",0);
			глКомментарий("Подразделение: "+Печ(ПЗ.Подразделение)+", Вид деятельности: "+Печ(ПЗ.ВидДеятельности)+", Заказ: "+Печ(ПЗ.Заказ)+", Продукция: "+Печ(ПЗ.Продукция)+
			РазделительСтрок+", Статья калькуляции: "+Печ(ПЗ.ВидЗатрат.СтатьяКалькуляции)+", Вид затрат: "+Печ(ПЗ.ВидЗатрат)+?(ПустоеЗначение(ПЗ.Материал)=1,"",", Материал: "+ПЗ.Материал),1);
			глНеПроводить(Контекст);
			Возврат 0;
		КонецЕсли;
	КонецЦикла;
    Возврат 1;
КонецФункции //РаспределениеЗатрат

//===============================
Процедура ОбработкаПроведения()
	
	глКомментарий("Начало",2,Контекст);
	
	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;             
	Если РассчитатьШапку() = 0 Тогда
	    глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;
	Если ПоВидамДеятельности = 1 Тогда
		Если РаспределениеЗатрат(1) = 0 Тогда // Виды деятельности
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если ПоЗаказам = 1 Тогда
		Если РаспределениеЗатрат(2) = 0 Тогда // Заказы
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если ПоПродукции = 1 Тогда
		Если РаспределениеЗатрат(3) = 0 Тогда // Продукция
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	глЗаписатьПроводкиВоперацию(Контекст);
	Операция.Содержание = Примечание;
	Операция.Записать();
	
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
