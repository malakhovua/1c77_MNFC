Перем ТЗ_Фаршей;
Перем ТЗ_Вес_Кутера; 
Перем ТЗ_КэшФаршей;
Перем СтараяДата;              
Перем НачальнаяДатаДокумента;

Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

//======================================================================
Процедура ЗаполнитьСлужебныеТЗ()
	
	//--- тз Фаршей 
	ТЗ_Фаршей = СоздатьОбъект("ТаблицаЗначений");
	ТЗ_Фаршей.НоваяКолонка("Фарш","Справочник.ТМЦ");
	ТЗ_Фаршей.НоваяКолонка("Норма","Документ.НормыЗатрат");
	ТЗ_Фаршей.НоваяКолонка("Кво");
	
	//--- тз КэшФаршей 
	ТЗ_КэшФаршей = СоздатьОбъект("ТаблицаЗначений");
	ТЗ_КэшФаршей.НоваяКолонка("Фарш","Справочник.ТМЦ");
	ТЗ_КэшФаршей.НоваяКолонка("КвоФарша",		"Число",14);
	ТЗ_КэшФаршей.НоваяКолонка("КвоИзКутеров",	"Число",14);
		
	//--- Запрос и ТЗ по весу кутеров 
	ТЗ_Вес_Кутера = СоздатьОбъект("ТаблицаЗначений");
	ТЗ_Вес_Кутера.НоваяКолонка("ТМЦ","Справочник.ТМЦ");
	ТЗ_Вес_Кутера.НоваяКолонка("ВесКутера","Число",10);
	ТЗ_Вес_Кутера.НоваяКолонка("ВыходБезПотерь","Число",10);
	ТЗ_Вес_Кутера.НоваяКолонка("Норма","Документ.НормыЗатрат");
 	
	ВыборкаНорм = СоздатьОбъект("Документ.НормыЗатрат");
	ВыборкаНорм.ОбратныйПорядок();
	ВыборкаНорм.УстановитьФильтр(1,0);
	ВыборкаНорм.ВыбратьДокументы();
	Пока ВыборкаНорм.ПолучитьДокумент() = 1 Цикл
		
		Если ВыборкаНорм.фДляЗаказа <> 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ЭнСтрока 	= "";
		ЭнСтолбец 	= "";
		 
		Если ТЗ_Вес_Кутера.НайтиЗначение(ВыборкаНорм.Продукция,ЭнСтрока,ЭнСтолбец) <> 1 Тогда
			ТЗ_Вес_Кутера.НоваяСтрока();
			ТЗ_Вес_Кутера.ТМЦ 			= ВыборкаНорм.Продукция;
			ТЗ_Вес_Кутера.ВесКутера 	= ВыборкаНорм.КвоПродукции - (ВыборкаНорм.КвоПродукции * ВыборкаНорм.Продукция.ПроцПотерь.Получить(ДатаДок)/100);
			ТЗ_Вес_Кутера.ВыходБезПотерь= ВыборкаНорм.КвоПродукции;
			ТЗ_Вес_Кутера.Норма			= ВыборкаНорм.ТекущийДокумент();
		КонецЕсли;
		
		ЭнСтрока_ 	= "";
		ЭнСтолбец_ 	= "";
		 
		Если ТЗ_Фаршей.НайтиЗначение(ВыборкаНорм.Продукция,ЭнСтрока_,ЭнСтолбец_) <> 1 Тогда	
			ТЗ_Фаршей.НоваяСтрока();
			ТЗ_Фаршей.Фарш 	= ВыборкаНорм.Продукция;;
			ТЗ_Фаршей.Кво 	= ВыборкаНорм.КвоПродукции;
			ТЗ_Фаршей.Норма = ВыборкаНорм.ТекущийДокумент();		
		КонецЕсли;
				
	КонецЦикла;
	
	//ТЗ_Фаршей.ВыбратьСтроку();
		
КонецПроцедуры // ЗаполнитьСлужебныеТЗ

// ==============================================================
Процедура ПриНачалеВыбораЗначения(Идент, Флаг)
	Если (Идент = "Фарш") Тогда
		
		Флаг = 0;
		
		глВыборИзЗаказа = 2; //--- попадаем в фарш
				
		Справ=СоздатьОбъект("Справочник.ТМЦ");
		Справ.НайтиЭлемент(Фарш);
		Справ.ВыборГруппы(0);
		ФлВвода=Справ.Выбрать("Выберите фарш","ДляВыбора");
		Если ФлВвода=1 Тогда
			ТекФарш=Справ.ТекущийЭлемент();
		КонецЕсли;
		
		Если ПустоеЗначение(ТекФарш) <> 1 Тогда
			
			Фарш = ТекФарш;
			
	
			ТекСтр 	= "";
			ТекКол 	= "";
			Если ТЗ_Фаршей.НайтиЗначение(ТекФарш,ТекСтр,ТекКол) = 1 Тогда					
				ТЗ_Фаршей.ПолучитьСтрокуПоНомеру(ТекСтр);		
				Норма = ТЗ_Фаршей.Норма;
				ТекФарш = ТЗ_Фаршей.Фарш;
				ТекСтр 	= "";
				ТекКол 	= "";
				ТекКвоИзКутеров = 0;
				ТекКгФарша		= 0;
				Если ТЗ_КэшФаршей.НайтиЗначение(ТекФарш,ТекСтр,ТекСтр) = 1 Тогда
					ТЗ_КэшФаршей.ПолучитьСтрокуПоНомеру(ТекСтр);
					ТекКвоИзКутеров = ТЗ_КэшФаршей.КвоИзКутеров;
					ТекКгФарша      = ТЗ_КэшФаршей.КвоФарша;
				КонецЕсли;	
				
				Если ВвестиЧисло(ТекКвоИзКутеров,"Введите из КУТЕРОВ Фарша:",14,0) = 1 Тогда
					КвоФаршаИзКутеров = ТекКвоИзКутеров;		
				КонецЕсли;
				
				Если ТекКгФарша = 0 Тогда
					ТекКгФарша = ТекКвоИзКутеров / 100 * ТЗ_Фаршей.Кво;
				КонецЕсли;
				
				Если ВвестиЧисло(ТекКгФарша,"Введите число КГ Фарша:",14,0) = 1 Тогда
						
					ТекСтр 	= "";
					ТекКол 	= "";
					Если ТЗ_КэшФаршей.НайтиЗначение(ТекФарш,ТекСтр,ТекСтр) = 1 Тогда
						ТЗ_КэшФаршей.ПолучитьСтрокуПоНомеру(ТекСтр);
						ТЗ_КэшФаршей.КвоФарша = ТЗ_КэшФаршей.КвоФарша - ТекКгФарша;	
					Иначе
						ТЗ_КэшФаршей.НоваяСтрока();
						ТЗ_КэшФаршей.Фарш = ТекФарш;
						ТЗ_КэшФаршей.КвоИзКутеров = ТекКвоИзКутеров;
						ТЗ_КэшФаршей.КвоФарша = (ТекКвоИзКутеров / 100 * ТЗ_Фаршей.Кво )- ТекКгФарша;		
					КонецЕсли; 
					
					КвоФарша = ТекКгФарша;
					ПроцПотерь    			= Продукция.ПроцПотерь.Получить(ДатаДок);
					КвоКг					= КвоФарша * ((100-ПроцПотерь)/100);
					
				КонецЕсли;
					
			Иначе //--- создаем первую строку с фаршами
				
				Сообщить("Внимание !!! , нормы фарша " + Фарш + " не заданы !" , "!!!");
				
			КонецЕсли;	
				
		КонецЕсли;
		
	КонецЕсли;
	
	//--- очистка всех нижних фаршей
	ТекНомерСтроки = НомерСтроки;
	ТекФарш = Фарш;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если НомерСтроки > ТекНомерСтроки Тогда
			Если Фарш = ТекФарш Тогда
				КвоФарша = 0;
				КвоФаршаИзКутеров = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//======================================================================
Процедура ПоказатьСостояниеДляЗадания()
	
	Таб = СоздатьОбъект("Таблица");
	ИмяФайла = КаталогИБ() + СокрЛП("\zad_pr\") + СокрЛП(НомерДок) + "_" + 
	СтрЗаменить(Строка(ДатаДок), ".", "")+".mxl";
	Таб.Открыть(ИмяФайла);
	Таб.Показать();
	
КонецПроцедуры // ПоказатьСостояниеДляЗадания


//====================================================================== //--- УМК Сандомирский В.Ю. (03.06.15)
Процедура Печать()

	Таб = СоздатьОбъект("Таблица");
	СуффиксТаблицы = ?(Константа.ФормыНаУкраинском = Да, "_Укр", "");	
	Таб.ИсходнаяТаблица("НаПроизводство"); 
	Таб.ВывестиСекцию("Шапка|Основная");
	Если Итог("КвоФарша") > 0 Тогда
		Таб.ПрисоединитьСекцию("Шапка|Фарш");
	КонецЕсли;	
	Таб.ПовторятьПриПечатиСтроки(4, 4); 	
	СпрДвойник = СоздатьОбъект("Справочник.УМК_ДвойникГруппыТМЦ");
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл		
		Ном			 		= НомерСтроки;
		ПечПродукция		= Продукция;
		ПечКвоКутеров		= ?(КвоКутеров=0,"",КвоКутеров);
		ПечФарш				= Фарш;
		ПечФаршИзКутеров	= ?(КвоФаршаИзКутеров=0,"",КвоФаршаИзКутеров);
		ПечКвоФарша			= ?(КвоФарша=0,"",КвоФарша);
		ПечПримечаниеСтроки = ПримечаниеСтроки;	
		Если СпрДвойник.НайтиПоКоду(Продукция.Родитель.Код) = 1 Тогда
			ПечГруппаЗаказник = СокрЛП(СпрДвойник.Наименование);
		КонецЕсли;	
		Таб.ВывестиСекцию("Строка|Основная");
		Если Итог("КвоФарша") > 0 Тогда
			Таб.ПрисоединитьСекцию("Строка|Фарш");
		КонецЕсли;		
	КонецЦикла;	
	
	Если Итог("КвоФарша") > 0 Тогда
		Таб.ПараметрыСтраницы(2,,,5,5,5,5,4,,1); 
	Иначе
	    Таб.ПараметрыСтраницы(1,,,5,5,5,5,4,,1); 
	КонецЕсли;	
		
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: ЗАДАНИЕ на производство","");
		
КонецПроцедуры // 

//====================================================================== //--- УМК Сандомирский В.Ю. (03.06.15)
Процедура ПечатьВесовщикам()
	
	Если КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьСтрокуПоНомеру(1);
	Если ПустоеЗначение(Продукция) = 1 Тогда
		Возврат;
	КонецЕсли;
	
	Таб = СоздатьОбъект("Таблица");
	СуффиксТаблицы = ?(Константа.ФормыНаУкраинском = Да, "_Укр", "");	
	Таб.ИсходнаяТаблица("БланкВесовщикам"); 
	
	// --- ОпределяемВаренкаКапченка 
	ТекКодРодитель = Продукция.Родитель.Код;
	ВидПечатнойФормыДляЗаданияНаПроизводство = "";
	СпрДвойник = СоздатьОбъект("Справочник.УМК_ДвойникГруппыТМЦ");	
	Если СпрДвойник.НайтиПоКоду(ТекКодРодитель,0) = 1 Тогда
		ВидПечатнойФормыДляЗаданияНаПроизводство 	= СпрДвойник.ВидПечатнойФормыДляЗаданияНаПроизводство;
	Иначе
		Возврат;	
	КонецЕсли;

	Таб.ВывестиСекцию("Шапка|ЗаменаСырья");
	Если ВидПечатнойФормыДляЗаданияНаПроизводство = Перечисление.УМК_ФормыПечатиВесовщикам.Варенка Тогда // --- ММО	
		Таб.ПрисоединитьСекцию("Шапка|ММО");	
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Шапка|ОбщийПром"); //--- ОбщийПром
	Если ВидПечатнойФормыДляЗаданияНаПроизводство = Перечисление.УМК_ФормыПечатиВесовщикам.Варенка Тогда // --- ПромФарша	
		Таб.ПрисоединитьСекцию("Шапка|ПромФарша");	
	КонецЕсли;

	Таб.ПрисоединитьСекцию("Шапка|НаименованиеКолбас"); //--- НаименованиеКолбас
	Таб.ПрисоединитьСекцию("Шапка|КвоКутеров"); 		//--- КвоКутеров 
	Таб.ПрисоединитьСекцию("Шапка|Дата"); 		//--- Дата
	
	Таб.ПовторятьПриПечатиСтроки(1, 4); 
	
	СпрДвойник = СоздатьОбъект("Справочник.УМК_ДвойникГруппыТМЦ");

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		
		Ном			 		= НомерСтроки;
		ПечПродукция		= Продукция;
		ПечКвоКутеров		= ?(КвоКутеров=0,"",КвоКутеров);
		ПечФарш				= Фарш;
		ПечФаршИзКутеров	= ?(КвоФаршаИзКутеров=0,"",КвоФаршаИзКутеров);
		ПечКвоФарша			= ?(КвоФарша=0,"",КвоФарша);
		ПечПримечаниеСтроки = ПримечаниеСтроки;
		
		Если СпрДвойник.НайтиПоКоду(Продукция.Родитель.Код) = 1 Тогда
			ПечГруппаЗаказник = СокрЛП(СпрДвойник.Наименование);
		КонецЕсли;
		
		Таб.ВывестиСекцию("Строка|ЗаменаСырья");
		Если ВидПечатнойФормыДляЗаданияНаПроизводство = Перечисление.УМК_ФормыПечатиВесовщикам.Варенка Тогда // --- ММО	
			Таб.ПрисоединитьСекцию("Строка|ММО");	
		КонецЕсли;
		Таб.ПрисоединитьСекцию("Строка|ОбщийПром"); //--- ОбщийПром
		Если ВидПечатнойФормыДляЗаданияНаПроизводство = Перечисление.УМК_ФормыПечатиВесовщикам.Варенка Тогда // --- ПромФарша	
			Таб.ПрисоединитьСекцию("Строка|ПромФарша");	
		КонецЕсли;
	
		Таб.ПрисоединитьСекцию("Строка|НаименованиеКолбас"); //--- НаименованиеКолбас
		Таб.ПрисоединитьСекцию("Строка|КвоКутеров"); 		//--- КвоКутеров 
		Таб.ПрисоединитьСекцию("Строка|Дата"); 		//--- Дата
		
	КонецЦикла;	
	
	//Таб.ВывестиСекцию("Дно|Основная");	
	//Если Итог("КвоФарша") > 0 Тогда
	//	Таб.ПрисоединитьСекцию("Дно|Фарш");
	//КонецЕсли;
		

	Таб.ПараметрыСтраницы(2,,,5,5,5,5,4,,1); 
	
	//	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: ЗАДАНИЕ на производство","");
		
КонецПроцедуры // 

//======================================================================
Процедура ИзмНорма()
	
	//Если ПустоеЗначение(Норма) <> 1 Тогда
	//
	//	Фарш 				= Норма.Продукция;
	//	КвоФаршаИзКутеров 	= Норма.КвоПродукции;
	//	КвоКутеров			= 0;
	//
	//КонецЕсли;
	
КонецПроцедуры // ИзмНорма

//======================================================================
Процедура ИзмКвоКутеров()
	
	Если КвоКутеров > 0 Тогда
		
		ТекСтр 	= "";
		ТекКол 	= "";
		Если ТЗ_Вес_Кутера.НайтиЗначение(Продукция,ТекСтр,ТекКол) = 1 Тогда
			
			ТЗ_Вес_Кутера.ПолучитьСтрокуПоНомеру(ТекСтр);
			
			ВесКутера	= ТЗ_Вес_Кутера.ВесКутера;	
			КвоКг 		= КвоКутеров / 100 * ВесКутера;	
			
			КвоФарша	= 0;
			
		Иначе
			Сообщить("Внимание !!! , нормы продукции  " + Продукция + " не заданы !" , "!!!");
		КонецЕсли;
		
		
		
	КонецЕсли;
	
КонецПроцедуры // ИзмКвоКутеров()

//======================================================================
Процедура ПриОткрытии()
	
	
	ЗаполнитьСлужебныеТЗ();
	СтараяДата = ДатаДок;
	НачальнаяДатаДокумента = ДатаДок;
	
КонецПроцедуры // ПриОткрытии

//======================================================================
Функция УстДоступность() 
	
	Если ПустоеЗначение(Фарш) <> 1 Тогда //--- закрываем кутера
		Форма.КвоКутеров.Доступность(0);
	Иначе
		Форма.КвоКутеров.Доступность(1);
	КонецЕсли;
	
	Если КвоКутеров > 0 Тогда //--- закрываем всё про фарши
		Форма.Фарш.Доступность(0);
		//Форма.Норма.Доступность(0);
	Иначе
		Форма.Фарш.Доступность(1);
		//Форма.Норма.Доступность(1);		
	КонецЕсли;

	Возврат "";
КонецФункции

//====================================================================== //--- УМК Сандомирский В.Ю. (09.07.15)
Процедура ИзмПродукция()
	
	Если ПустоеЗначение(Продукция) <> 1 Тогда
		СпрДвойник = СоздатьОбъект("Справочник.УМК_ДвойникГруппыТМЦ");	
		Если СпрДвойник.НайтиПоКоду(Продукция.Родитель.Код,0) = 1 Тогда
			ДвойникГруппТМЦСтроки =  СпрДвойник.ТекущийЭлемент();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ИзмПродукция()

//====================================================================== //--- УМК Сандомирский В.Ю. (09.07.15)
Процедура ПриЗаписи()
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		ИзмПродукция();
	КонецЦикла;
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	ТЗ_ИменаЗаказник = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТЗ_ИменаЗаказник);
	ТЗ_ИменаЗаказник.Свернуть("ДвойникГруппТМЦСтроки","");
	ДвойникиГруппТМЦ = "";
	ТЗ_ИменаЗаказник.ВыбратьСтроки();
	Пока ТЗ_ИменаЗаказник.ПолучитьСтроку() = 1 Цикл
		ДвойникиГруппТМЦ = ДвойникиГруппТМЦ + СокрЛП(Строка(ТЗ_ИменаЗаказник.ДвойникГруппТМЦСтроки)) + ",";
	КонецЦикла;
		
КонецПроцедуры // ПриЗаписи

//====================================================================== //--- УМК Сандомирский В.Ю. (10.07.15)
Процедура Заполнить()
	
	УдалитьСтроки();
	ЗаданиеНаПроизводство = СоздатьОбъект("Документ.УМК_ЗаданиеНаПроизводство");
	ЗаданиеНаПроизводство.ВыбратьДокументы(ДатаДок,ДатаДок);
	Пока ЗаданиеНаПроизводство.ПолучитьДокумент() = 1 Цикл
		Если ЗаданиеНаПроизводство.ПометкаУдаления() = 1 Тогда
			Продолжить;			
		КонецЕсли;
		
		ЗаданиеНаПроизводство.ВыбратьСтроки();
		Пока ЗаданиеНаПроизводство.ПолучитьСтроку() = 1 Цикл
			НоваяСтрока();
			
			ДокЗаданиеНаПроизводство		= ЗаданиеНаПроизводство.ТекущийДокумент();
			ДатаВыпуска 					= ЗаданиеНаПроизводство.ДатаВыпуска;
			Продукция 						= ЗаданиеНаПроизводство.Продукция;
			Фарш							= ЗаданиеНаПроизводство.Фарш;
			КвоФаршаИзКутеров 				= ЗаданиеНаПроизводство.КвоФаршаИзКутеров;
			КвоФарша						= ЗаданиеНаПроизводство.КвоФарша;
			КвоКутеров						= ЗаданиеНаПроизводство.КвоКутеров;
			КвоКг							= ЗаданиеНаПроизводство.КвоКг;
			Норма							= ЗаданиеНаПроизводство.Норма;
			ПримечаниеСтроки				= ЗаданиеНаПроизводство.ПримечаниеСтроки;
			ДвойникГруппТМЦСтроки 			= ЗаданиеНаПроизводство.ДвойникГруппТМЦСтроки;
			КвоФаршаПродукция				= ЗаданиеНаПроизводство.КвоФаршаПродукция;
			ПроцентПотерьПродукция  		= ЗаданиеНаПроизводство.ПроцентПотерьПродукция;
			ПоловинаПроцентПотерьПродукция 	= ЗаданиеНаПроизводство.ПоловинаПроцентПотерьПродукция;
			ВесДляЭкспедиции				= ЗаданиеНаПроизводство.ВесДляЭкспедиции;
				
		КонецЦикла;
		
	КонецЦикла;
	
	
КонецПроцедуры // Заполнить

СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|Подчиненные");