Перем НачальнаяДатаДокумента;
Перем СтарыйКонтрагент;
Перем СтарВидНДС;
Перем СтарКатегорияЦен;

// ===============================
Процедура ПриОткрытии()
	глПроверкаДатыДок(Контекст,"Открытие");
	ПриЗаписиПерепроводить(1);

	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кОК.Доступность(0);
		Форма.кПровести.Доступность(0);
//		Форма.кДействия.Доступность(0);
		Форма.кПодбор.Доступность(0);
		Форма.кФирма.Доступность(0);
		Форма.кОснование.Доступность(0);
		Форма.КнопкаПоУмолчанию("кЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кОК");
	КонецЕсли; 
	
	НачальнаяДатаДокумента = ДатаДок;
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Дополнительный","Дополнительные");
	Форма.ИспользоватьСлой("Общий,Основной",2);
	
	глУстВидимостьЦен(Контекст);
	СтарыйКонтрагент = Контрагент;                             
	СтарВидНДС		 = ВидНДС;
	СтарКатегорияЦен = КатегорияЦен;
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
	    ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если Услуга = Форма.Параметр Тогда
			    АктивизироватьСтроку(НомерСтроки);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры                                                 

// ===============================
Процедура ИзмГлубина()
	Если Контрагент.ГлубинаВКалендарныхДнях	= 1 Тогда
		ДатаОплаты=ДатаДок + Глубина;
	Иначе
		ДатаОплаты=глБанковскаяДата(ДатаДок,Глубина);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура УстГлубина()
	Если ВидТорговли = Перечисление.ВидыТорговли.Кредит Тогда
		Глубина=Контрагент.Глубина.Получить(ДатаДок);
	Иначе
		Глубина=0;
	КонецЕсли;
	ИзмГлубина();
КонецПроцедуры
                           
// ===============================
Процедура ИзмДатаДок()
	ИзмГлубина();
КонецПроцедуры //

// ===============================
Процедура ИзмВидТорговли()
	Если ВидТорговли <> Перечисление.ВидыТорговли.Кредит Тогда
		Глубина = 0;
		ИзмГлубина();
	КонецЕсли;
КонецПроцедуры //

// ===============================
Процедура ИзмФирма()
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Фирма <> Фирма Тогда
		    Договор = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //

// ===============================
Процедура ИзмДоговор()
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Фирма <> Фирма Тогда
		    Договор = 0;
		КонецЕсли;
	КонецЕсли;
	Если (Договор.Выбран() = 1) и (Договор.Вид()="Договор") Тогда
		Если ПустоеЗначение(Договор.ВидТорговли) = 0 Тогда
			ВидТорговли = Договор.ВидТорговли;
		КонецЕсли;
	ИначеЕсли ПустоеЗначение(Контрагент.ВидТорговли) = 0 Тогда
		ВидТорговли = Контрагент.ВидТорговли;
	ИначеЕсли ПустоеЗначение(ВидТорговли) = 1 Тогда
		ВидТорговли = глВосстановитьЗначение(,"ОсновнойВидТорговли");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмСчет()
	Если ПустоеЗначение(Счет)=0 Тогда
		Если НЕ((Счет.ПринадлежитГруппе(СчетПоКоду("15"))=1) или (глЭтоСчетТЗР(Счет)=1) или (Счет = СчетПоКоду("39"))) Тогда
		    // проверяем дальше
		    Если (Сред(Счет.Код,1,1)="9") Тогда
		        Если ИспользоватьСчетаРасходов = Класс8 Тогда
		            Предупреждение("Нельзя использовать счета класса 9! Разрешенные классы счетов "+
					               "расходов устанавливаются в константе ""Использовать счета расходов"".");
					Счет = 0;
				КонецЕсли;
		    ИначеЕсли (Сред(Счет.Код,1,1) = "8") Тогда
		        Если ИспользоватьСчетаРасходов = Класс9 Тогда
		            Предупреждение("Нельзя использовать счета класса 8! Разрешенные классы счетов "+
					               "расходов устанавливаются в константе ""Использовать счета расходов"".");
					Счет = 0;
				КонецЕсли;
			Иначе
	            Предупреждение("В документе можно использовать только счета затрат, субсчета счета 15 и счета учета ТЗР.");
				Счет = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Для Инд = 1 По 3 Цикл
		НазначитьТип("Субконто"+Инд,Счет.ВидСубконто(Инд));
	КонецЦикла;
КонецПроцедуры //

// ===============================
Процедура ИзмСубконто3()
	Если Счет = СчетПоКоду("39") Тогда
		Возврат;
	КонецЕсли;
	
	Если Счет.ВидСубконто(3) = ВидыСубконто.ВидыЗатрат Тогда
	    Если Субконто3.Выбран()=1 Тогда
			Если ИспользоватьСчетаРасходов = Класс8 Тогда
				// не используется 9-й класс
			    Счет = Субконто3.Счет;
			КонецЕсли;
	    КонецЕсли;
	КонецЕсли;
КонецПроцедуры//

// ===============================
Процедура ИзмУслуга()
	Если ПустоеЗначение(Счет)=1 Тогда
		Если глЭтоСчетТЗР(Услуга.Счет)=1 Тогда
			Счет = Услуга.Счет;
			ИзмСчет();
		ИначеЕсли (ИспользоватьСчетаРасходов = Класс8) И (Услуга.Счет <> СчетПоКоду("39")) Тогда
			Счет = Услуга.ВидЗатрат.Счет;
			ИзмСчет();
			Субконто1 = Услуга.ВидДеятельности;
			Субконто3 = Услуга.ВидЗатрат;
		Иначе
			Счет = Услуга.Счет;
			ИзмСчет();
			Субконто1 = Услуга.ВидДеятельности;
			Субконто3 = Услуга.ВидЗатрат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ИзмУслуга

// ===============================
Процедура ИзмВидЗатратНДС()
	Если ИспользоватьСчетаРасходов = Класс8 Тогда
		Если ПустоеЗначение(ВидЗатратНДС)=0 Тогда
		    СчетЗатратНДС = ВидЗатратНДС.Счет;
		Иначе
			СчетЗатратНДС = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //

// ===============================
//Устанавливаем счет контрагента в зависимости от выбранной валюты
Процедура УстСчетКонтрагента()
	СчетКонтрагента = ?(Валюта=Гривня,СчетПоКоду("63.1"),СчетПоКоду("63.2"));
КонецПроцедуры                    

// ===============================
Процедура ИзмСчетЗатратНДС()
	Если глПроверитьСчетЗатрат(СчетЗатратНДС)=0 Тогда
		СчетЗатратНДС = 0;
	КонецЕсли;
КонецПроцедуры //

// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в докумнете
	ОткрытьФормуМодально("Обработка.ИнформацияОВалюте", Контекст);
	УстСчетКонтрагента();
КонецПроцедуры	

// ===============================
Процедура ВыборОснования()    
	// Процедура по кнопке редактирования основания в докумнете
	глРасшифровка = 0;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", Контекст);
	ИзмДоговор();
КонецПроцедуры	

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение)
	Форма.ИспользоватьСлой("Общий,"+Значение,2);
КонецПроцедуры

// ===============================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	// доступность в зависимости от слоя
	Форма.Заголовок(глЗаголовок(Контекст,"Услуги сторонних организаций"));
	Закл = Форма.Закладки.ПолучитьЗначение(Форма.Закладки.ТекущаяСтрока());

	Если Закл = "Дополнительный" Тогда
		// слой Дополнительный
		
		Если ИспользоватьСчетаРасходов = Класс8 Тогда
		    // не использыум десятый класс - нельзя выбирать счет
			Форма.СчетЗатратНДС.Доступность(0);
		КонецЕсли;
		Если ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда
			// для бартера запретим относить НДС на себестоимость
			// нет точной схемы проводок для этого случая
		    Форма.НДСнаЗатраты.Доступность(0);
		Иначе
			Форма.НДСнаЗатраты.Доступность(1);
		КонецЕсли;

		Форма.ВидНДС.Доступность(1-НДСНаЗатраты);
	    Форма.СчетЗатратНДС.Видимость(НДСНаЗатраты);
	    Форма.ВидЗатратНДС.Видимость(НДСНаЗатраты);
	    Форма.тСчетЗатратНДС.Видимость(НДСНаЗатраты);
	Иначе
		// слой основной
		фВидимостьКредит = ?(ВидТорговли = Перечисление.ВидыТорговли.Кредит,1,0);
		Форма.тДатаОплаты.Видимость(фВидимостьКредит);
		Форма.тГлубина.Видимость(фВидимостьКредит);
		Форма.Глубина.Видимость(фВидимостьКредит);
	КонецЕсли;
	Если (ВидТорговли = Перечисление.ВидыТорговли.Бартер) и (НДСнаЗатраты = 1) Тогда
		НДСнаЗатраты = 0;
	КонецЕсли;
	Форма.Фирма.Видимость(0);
	Возврат "";
КонецФункции

// ===============================
Процедура ИзмКонтрагент()
	Если (Контрагент.Выбран() = 0) или (СтарыйКонтрагент = Контрагент) Тогда
		Возврат;
	КонецЕсли;
	Если Договор.Выбран() = 1 Тогда
		Если Договор.Контрагент <> Контрагент Тогда
			Договор = 0;
		КонецЕсли;
	КонецЕсли;
	Если (глВосстановитьЗначение(,"ПодставлятьОсновнойДоговор") = Да) и
	(Фирма = Контрагент.БазДоговор.Фирма) Тогда
		Договор = Контрагент.БазДоговор;
	КонецЕсли;
	ИзмДоговор();
	// проверим документ-основание
	Если ПустоеЗначение(ДокументОснование) = 0 Тогда
		Если ДокументОснование.Контрагент <> Контрагент Тогда
			ДокументОснование = 0;
		КонецЕсли;	
	КонецЕсли;	
	Если ПустоеЗначение(Контрагент.КатегорияЦенПоставщика) = 0 Тогда
		КатегорияЦен = Контрагент.КатегорияЦенПоставщика;
	Иначе
		КатегорияЦен = глВосстановитьЗначение(,"ОсновнаяКатегорияЦен");
	КонецЕсли;
	УстГлубина();
	СтарыйКонтрагент = Контрагент
КонецПроцедуры

// ===============================
Процедура ЗаполнитьПоУмолчанию()
	// заполняем все то, что общее для ввода нового и для ввода на основании
	Если ПустоеЗначение(ВидНДС)=1 Тогда
		ВидНДС = глВосстановитьЗначение(,"БазНДС");
	КонецЕсли;
	СубконтоВалРасх = глВосстановитьЗначение(Контекст,"СубконтоВалРасх", Константа.БазВаловыйРасход);

	СчетЗатратНДСПоУмолч = СчетПоКоду("93");
	Если ИспользоватьСчетаРасходов = Класс8 Тогда
		СчетЗатратНДСПоУмолч = СчетПоКоду("85");
	КонецЕсли;
	СчетЗатратНДС = глВосстановитьЗначение(Контекст,"СчетЗатратНДС",СчетЗатратНДСПоУмолч);
	ВидЗатратНДС = глВосстановитьЗначение(Контекст,"ВидЗатратНДС");
	
	// установим валюту
	Валюта = Гривня;
	Дата_Курса= ДатаДок;
	Курс = глКурсДляВалюты(Валюта,Дата_Курса);
	УстСчетКонтрагента();
                                                             
	Если ПустоеЗначение(КатегорияЦен) = 1 Тогда
		КатегорияЦен = глВосстановитьЗначение(,"ОсновнаяКатегорияЦен");
	КонецЕсли;
	
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	глПроверкаДатыДок(Контекст,"Запись");
	Автор = глПользователь;
	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	глСохранитьЗначение(Контекст,"СубконтоВалРасх",СубконтоВалРасх);
	глСохранитьЗначение(Контекст,"СчетЗатратНДС",СчетЗатратНДС);
	глСохранитьЗначение(Контекст,"ВидЗатратНДС",ВидЗатратНДС);
	Если ПустоеЗначение(ДокументОснование)=1 Тогда
		ОтборСчетаКонтрагент = Контрагент;
	Иначе
		ОтборСчетаКонтрагент = 0;
	КонецЕсли;
КонецПроцедуры

// ===============================
// Назначение: Пересчет сумм в строке документа
//		
// Аргументы: НоваяЦена - новая цена для текущей строки документа
//		
Процедура ПересчетСтроки(НоваяЦена=0)
	Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаБезНДС Тогда
		Если ПустоеЗначение(НоваяЦена) = 0 Тогда                                  
			// определим ставку НДС
			Если ВидНДС = ОсновнаяСтавкаНДС Тогда
				СтавкаНДС = Услуга.СтавкаНДС;
			Иначе
				СтавкаНДС = ВидНДС;
			КонецЕсли;
			// устанавливаем цену без НДС
			ЦенаБезНДС = НоваяЦена*100/(глПроцентНДС(СтавкаНДС,ДатаДок)+100);
		КонецЕсли;
		глВыч_Суммы_Накл(Контекст,-1);
	Иначе                     
		Если ПустоеЗначение(НоваяЦена) = 0 Тогда
			ЦенаСНДС = НоваяЦена;
		КонецЕсли;
		глВыч_Суммы_Накл(Контекст,1);
	КонецЕсли;
КонецПроцедуры //ПересчетСтроки

// Назначение: пересчитывает суммы в строках документа при изменении реквизитов шапки
//		
// Аргументы: Реквизит - наименование измененного реквизита
//		
// ===============================
Процедура ИзмРеквизитШапки(Реквизит)
	// пересчитаем суммы НДС в строках табличной части
	Если (СтарВидНДС=ВидНДС) И (СтарКатегорияЦен=КатегорияЦен) Тогда
		// ничего не изменилось
	    Возврат;     
	Иначе
		СтарВидНДС		 = ВидНДС;
		СтарКатегорияЦен = КатегорияЦен;
	КонецЕсли;
	Если КоличествоСтрок() = 0 Тогда
	    Возврат;
	КонецЕсли;      
	текОтвет = Вопрос("В документе изменен реквизит """+Реквизит+""". Перезаполнить цены заново?","Да+Нет");
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если текОтвет = "Да" Тогда
			// изменим цены
			ЦенаТовара = глВернутьЦену(Услуга, КатегорияЦен);
			Если ПустоеЗначение(ЦенаТовара) = 0 Тогда
				// получим параметры цены
				ЦенаТовара.ИспользоватьДату(ДатаДок);
				ЦенаЦены   = ЦенаТовара.Цена;
				ВалютаЦены = ЦенаТовара.Валюта;
				ЕдЦены	   = ЦенаТовара.Единица;
				ЦенаЦены   = ЦенаЦены * Коэффициент / ЕдЦены.Коэффициент;			
				ЦенаЦены   = глПересчет(ЦенаЦены,ВалютаЦены,ДатаДок,Валюта,Курс,Дата_курса);
				ПересчетСтроки(ЦенаЦены);                                                   
			Иначе
				ПересчетСтроки();
			КонецЕсли;
		ИначеЕсли Реквизит = "ВидНДС" Тогда
			ПересчетСтроки();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 

// ===============================
Процедура ВводНового(Копируем)
	Если Копируем = 1 Тогда
		глУстановитьНомер(Контекст);
	    Возврат;
	КонецЕсли;
	глЗаполнитьШапку(Контекст);
	глУстановитьФирму(Контекст);
	Если ПустоеЗначение(Контрагент) = 1 Тогда
		Контрагент=глВосстановитьЗначение(,"ОсновнойПоставщик");
	КонецЕсли;
	ИзмКонтрагент();
	ЗаполнитьПоУмолчанию();
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры

// ===============================
// основание - счет входящий, договор, остатки взаиморасчетов
Процедура ВводНаОсновании(Док)
	Если (Док.Валюта <> Гривня) Тогда
		глКомментарий("Валюта документа-основания должна быть Гривня!",0,,"!!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Если Док.Вид()="СчетВходящий" Тогда
		Если Док.ЧтоПриходуем <> Перечисление.ЧтоПриходуем.ТМЦ Тогда
			Предупреждение("Выбранный счет должен содержать услуги в табличной части!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	глЗаполнитьШапкуНаОсн(Контекст,Док);
	ИзмКонтрагент();
	глУстановитьНомер(Контекст);
	ЗаполнитьПоУмолчанию();
	СтарыйКонтрагент = Контрагент;
	Если Док.Вид()="СчетВходящий" Тогда
		// табл. часть
	    Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку()=1 Цикл
			Если Док.ТМЦ.ВидТМЦ = Перечисление.ВидыТМЦ.Услуга Тогда
				НоваяСтрока();
				Услуга = Док.ТМЦ;
				ИзмУслуга();
				Ед = Док.Ед;
				Коэффициент = Док.Коэффициент;
				Кво = Док.Кво;
				Если Константа.ОсновнаяЦена = Перечисление.ВидыЦенВДокументах.ЦенаСНДС Тогда
					ЦенаСНДС = Док.ЦенаСНДС;
					глВыч_суммы_накл(Контекст,1);				
				Иначе	
					ЦенаБезНДС = Док.ЦенаБезНДС;
					глВыч_суммы_накл(Контекст,-1);				
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура Подбор()                          
	глПодбор(Контекст,"ТМЦ","ДляПодбора")
КонецПроцедуры
                                                                 
// ===============================
Процедура Взаиморасчеты()
	глПоказатьДолг(Контекст, Контрагент, "Закупка");
КонецПроцедуры

// ===============================
Процедура ОбработкаПодбора(Выб)
	Если ПустоеЗначение(Выб) = 0 Тогда
		глПриОбработкеПодбора(Выб,Контекст);
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			ИзмУслуга();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,ФлагСтандОбр)
	Если Рекв = "ВидНДС" Тогда
	    глВыбратьНДС(Контекст);
		ФлагСтандОбр = 0;
		ИзмРеквизитШапки("ВидНДС");
	КонецЕсли;
КонецПроцедуры

// ======================================
Процедура ОбработкаВыбораЗначения(Значение,Рекв,ФлагСтандОбр)
	Если Рекв = "Услуга" Тогда
		Если (Значение.ВидТМЦ<>Перечисление.ВидыТМЦ.Услуга) Тогда
		    Предупреждение("Можно выбирать только услугу!");
			ФлагСтандОбр = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

//====================================================================== УМК Сандомирский В.Ю. (*) //--- заголовок FormEx_ПланРаскраски
Функция Раскраска()
	ТекстПлана = "(BRUSH_S[" + Строка(100*255*100) + "])()()"; //--- добавляя колонку вначало документа до "раскрашек" часов добавить "()"
	Возврат  ТекстПлана;	
КонецФункции



// ===============================
// Инициализируем список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|Подчиненные");