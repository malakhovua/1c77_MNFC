Перем Касса;
Перем СчетНДС, СубконтоНДС1, СубконтоНДС2;
Перем ПересчКво, СуммаЗатрат, СуммаНДС;
Перем НДСВсего, ВзаиморасчетыВсего, ИтогСуммаСНДС, ИтогНДС; // для исправления ошибок при округлениях
                                                                               
// ===============================
Функция ПроверкаШапки()
    глВсеВыбрано = 1;
	глПроверкаДатыДок(Контекст,"Проведение");
    глВыбранЛи(Фирма,"Фирма");
    глВыбранЛи(Контрагент,"Поставщик");
	глВыбранЛи(Валюта,"Валюта");
    глВыбранЛи(СчетКонтрагента,"Счет контрагента");
	Если глВсеВыбрано = 1 Тогда
		Если глПроверитьСчетВзаиморасчетов(СчетКонтрагента)=0 Тогда
			глКомментарий("Выбран не корректный счет контрагента!",0,,"!");
			глВсеВыбрано = 0;
		ИначеЕсли (Валюта = Гривня) и (СчетКонтрагента.Валютный = 1) Тогда
			глКомментарий("Нельзя выбирать валютный счет, если валюта документа "+Валюта+" !",1,,"!");
			глВсеВыбрано = 0;       
		ИначеЕсли (Валюта <> Гривня) и (СчетКонтрагента.Валютный = 0) Тогда
			глКомментарий("Нельзя выбирать не валютный счет, если валюта документа "+Валюта+" !",1,,"!");
			глВсеВыбрано = 0;
		КонецЕсли;
	КонецЕсли;
    глВыбранЛи(ВидТорговли,"Вид торговли");
    глВыбранЛи(СубконтоВалРасх,"Субконто валовых расходов");
	глВыбранЛи(ВидНДС,"Вид НДС");
	Если НДСНаЗатраты = 1 Тогда
		Если ПустоеЗначение(СчетЗатратНДС)=0 Тогда
			Если глПроверитьСчетЗатрат(СчетЗатратНДС,2,0,"Счет затрат НДС")=0 Тогда
			    глВсеВыбрано = 0;
			КонецЕсли;
			глВыбранЛи(ВидЗатратНДС,"Вид затрат НДС");
		ИначеЕсли ПустоеЗначение(ВидЗатратНДС)=0 Тогда
			глВыбранЛи(СчетЗатратНДС,"Счет затрат НДС");
		КонецЕсли;
	КонецЕсли;         
	глПроверкаНДСВДокументе(Контекст, Итог("СуммаБезНДС"), Итог("СуммаСНДС"), Итог("НДС"));
	глВсеВыбрано = ?(глВсеВыбрано=0,0,глПроверкаДублейСтрок(Контекст));
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Процедура РассчитатьШапку()
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		Касса = глПолучитьКассу(Фирма,Гривня);
	КонецЕсли;
	
	// параметры для проводки по НДС
	СчетНДС = СчетПоКоду("6415");
	СубконтоНДС1 = ВидНДС;
	СубконтоНДС2 = 0;
	// налоговый кредит
	Если Константа.НДСпоВходящимНН = Да Тогда
		// проводку по налоговому кредиту сделаем в 
		// документе ЗаписьКнигиПриобретения
		СчетНДС = СчетПоКоду("6442");
		СубконтоНДС1 = Контрагент;
		СубконтоНДС2 = Договор;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ДвиженияВзаиморасчеты()
	// расчет итогов по взаиморасчетам
	ВремРег = СоздатьОбъект("Регистры");
	ФлагОтгрузки = 1; ЗнакОплаты = 1; ФлагВозврата = 0;
	глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,Фирма,?(ВидТорговли=Перечисление.ВидыТорговли.Бартер,-ЗнакОплаты,ЗнакОплаты),Контрагент,Договор,Валюта);
	тбСуммыПогашения = глПолучитьСуммыДляПогашения(Контекст,Фирма,Валюта);
	тбДолги = глПолучитьИтогиВзаиморасчетов(Контекст,ВремРег,Фирма,?(ВидТорговли=Перечисление.ВидыТорговли.Бартер,-ЗнакОплаты,ЗнакОплаты),Контрагент,Договор,Валюта);
	// проводим по регистрам взаиморасчетов
	глПровестиПоВзаиморасчетам(Контекст,ФлагОтгрузки,ЗнакОплаты,ФлагВозврата,Фирма,тбДолги,тбСуммыПогашения,Валюта,Контрагент,Договор,ВидТорговли,ДокументОснование);

	// формируем проводки по движениям регистров
	НДСВтороеСобытие = 0;   
	НДСВсего		   = 0;
	ВзаиморасчетыВсего = 0;	
	РегПоставщики=ВремРег.ВзаиморасчетыПоставщиков;	
	РегПокупатели=ВремРег.ВзаиморасчетыПокупателей;	
	// по регистру поставщиков
	Если (РегПоставщики.ВыбратьДвиженияДокумента(ТекущийДокумент()) = 0) 
	И (Итог("СуммаБезНДС") = 0) Тогда
		// по регистру взаиморасчетов нет движений. Данная ситуация может возникнуть если 
		// сумма по документу 0. В этом случае нужно сделать одну проводку по счету взаиморасчетов
		глПроводка(Контекст,,СчетКонтрагента,0,"Взаиморасчеты с поставщиком.",Итог("Кво"), ,,,
		Контрагент,Договор,, ,,"УС",1,"Взаиморасчеты",1);
	КонецЕсли;
	Пока РегПоставщики.ПолучитьДвижение() = 1 Цикл
		Если (Константа.ПроводкиПоКассеТолькоОрдерами = Нет) И (ВидТорговли = Перечисление.ВидыТорговли.Нал) 
				И (РегПоставщики.Приход = 1) Тогда
			// при оплате наличными формируются 2 движения, надо взять только одно. Возьмем расход, приход пропускаем
			Продолжить;
		КонецЕсли;
		
		Если Найти(АвансоваяОтгрузка+ПостОтгрузка+ПервоеСобытиеБартерПриход+ВтороеСобытиеБартерПриход,РегПоставщики.КодОперации) = 0 Тогда
			// для формирования проводок по взаиморасчетам нам нужны только эти коды операций
			Продолжить;
		КонецЕсли;
		// главная часть сложной проводки по взаиморасчетам
		глПроводка(Контекст,,СчетКонтрагента,РегПоставщики.Долг,"Взаиморасчеты с поставщиком.",, ,,,
			Контрагент,РегПоставщики.Договор,, ,,"УС",1,"Взаиморасчеты",1);
		ВзаиморасчетыВсего = ВзаиморасчетыВсего + РегПоставщики.Долг;
		// погашение аванса
		Если (глВыделятьЛиАвансыПоСчету(СчетКонтрагента) = 1) И (РегПоставщики.КодОперации = ПостОтгрузка) Тогда
			глПроводка(Контекст,СчетКонтрагента,"3711",РегПоставщики.Долг,"Прих: авансовый платеж",, Контрагент,Договор,,
			Контрагент,Договор,, ,,"УС",1);
		КонецЕсли;
		// проводки по НДС
        Если глДелатьПроводкиПоНалогам(РегПоставщики,"НДС") = 1 Тогда
			Если (ВидТорговли = Перечисление.ВидыТорговли.Нал) И (НДСнаЗатраты=0) Тогда
			    глПроводка(Контекст,СчетНДС,,РегПоставщики.НДС,"НДС",, СубконтоНДС1,СубконтоНДС2,,
		    	,,, ,,"УС",1,"Взаиморасчеты");
				НДСВсего = НДСВсего + РегПоставщики.НДС;
			ИначеЕсли ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда
	        	глПроводка(Контекст,СчетНДС,,РегПоставщики.НДС,"Бартер: НДС",, СубконтоНДС1,СубконтоНДС2,,
				,,, ,,"УС",1,"Взаиморасчеты");                                                                         
				НДСВсего = НДСВсего + РегПоставщики.НДС;
			Иначе
				Если НДСнаЗатраты = 0 Тогда
					Если РегПоставщики.КодОперации = АвансоваяОтгрузка Тогда
				    	глПроводка(Контекст,СчетНДС,,РегПоставщики.НДС,"НДС (первое событие)",, СубконтоНДС1,СубконтоНДС2,,
				    	,,, ,,"УС",1,"Взаиморасчеты");
					ИначеЕсли РегПоставщики.КодОперации = ПостОтгрузка Тогда
				    	глПроводка(Контекст,"6441",,РегПоставщики.НДС,"НДС (налоговый кредит)",, Контрагент,Договор,,
				    	,,, ,,"УС",1,"Взаиморасчеты");
					КонецЕсли;
				Иначе
					// возможно была предоплата, в которой указали сумму НДС.
					Если РегПоставщики.КодОперации = ПостОтгрузка Тогда          
						НДСВтороеСобытие = НДСВтороеСобытие + РегПоставщики.НДС; 
					КонецЕсли;	
				КонецЕсли;
				НДСВсего = НДСВсего + РегПоставщики.НДС;
			КонецЕсли;
		КонецЕсли;
		
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		//// проводки по ВД/ВР
        //Если (глДелатьПроводкиПоНалогам(РегПоставщики,"ВД/ВР") = 1) И (СубконтоВалРасх <> Константа.НиДоходНиРасход) Тогда
		//	Если ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда
		//    	глПроводка(Контекст,"ВР","ВР",РегПоставщики.СуммаСНДС_НУ-РегПоставщики.НДС,"Бартер: валовые доходы",, Контрагент,Договор,Константа.БартерВалДох,
		//    	Контрагент,Договор,Константа.БартерВалДох, ,,"УС",1);
		//	Иначе
		//		глПроводка(Контекст,"ВР","ВР",РегПоставщики.СуммаСНДС_НУ-РегПоставщики.НДС,"Вал. расходы (поставщик)",, Контрагент,Договор,СубконтоВалРасх,
		//		Контрагент,Договор,СубконтоВалРасх,,,"УС",1);
		//	КонецЕсли;
		//КонецЕсли;
		// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		// дополнительные проводки
		Если (ВидТорговли = Перечисление.ВидыТорговли.Нал) И (Константа.ПроводкиПоКассеТолькоОрдерами = Нет) Тогда
			// по кассе при оплате наличными
    		глПроводка(Контекст,СчетКонтрагента,"301",РегПоставщики.Долг,"Оплата наличными",, Контрагент,Договор,,
    		Касса,,, ,,"УС",1);
		ИначеЕсли (ВидТорговли = Перечисление.ВидыТорговли.Бартер) И (РегПоставщики.КодОперации = ПервоеСобытиеБартерПриход) Тогда
			// для бартерных операций первое событие показываем как аванс в счет встречной поставки
			Сч=?(глВыделятьЛиАвансыПоСчету(СчетКонтрагента)=1,"681","36") + ?(Валюта=Гривня,"1","2");
			глПроводка(Контекст,СчетКонтрагента,Сч,РегПоставщики.Долг,"Бартер: первое событие",, Контрагент,Договор,,
			Контрагент,Договор,, ,,"УС",1);
		КонецЕсли;
	КонецЦикла;
	// для бартера просмотрим другой регистр
	Если ВидТорговли = Перечисление.ВидыТорговли.Бартер Тогда
		РегПокупатели.ВыбратьДвиженияДокумента(ТекущийДокумент());
		Пока РегПокупатели.ПолучитьДвижение() = 1 Цикл
			Если Найти(ПервоеСобытиеБартерПриход+ВтороеСобытиеБартерПриход,РегПокупатели.КодОперации) = 0 Тогда
				// для формирования проводок по взаиморасчетам нам нужны только эти коды операций
				Продолжить;
			КонецЕсли;
			
			Если (РегПокупатели.КодОперации = ПервоеСобытиеБартерПриход) 
					И (глБартерНаОбщихОснованиях(РегПокупатели.Договор,РегПокупатели.Счет,РегПокупатели.КредДокумент) = 0) Тогда
				// проводки по НДС
				Если глДелатьПроводкиПоНалогам(РегПокупатели,"НДС") = 1 Тогда
    				глПроводка(Контекст,"6441",,РегПокупатели.НДС,"Бартер: НДС (кредит)",, Контрагент,Договор,,
        			,,, ,,"УС",1,"Взаиморасчеты");
					глПроводка(Контекст,"643","6415",РегПокупатели.НДС,"Бартер: НДС ",, Контрагент,Договор,,
					ВидНДС,,, ,,"УС",1); // налоговые обязательства возникаю ВСЕГДА по первому событию
					НДСВсего = НДСВсего + РегПокупатели.НДС;
				КонецЕсли;
				
				// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
				//// проводки по ВД/ВР
		        //Если (глДелатьПроводкиПоНалогам(РегПокупатели,"ВД/ВР") = 1) И (Константа.БартерВалДох <> Константа.НиДоходНиРасход) Тогда
			    //	глПроводка(Контекст,"ВД","ВД",РегПокупатели.СуммаСНДС_НУ-РегПокупатели.НДС,"Бартер: валовые доходы",, Контрагент,Договор,Константа.БартерВалДох,
			    //	Контрагент,Договор,Константа.БартерВалДох, ,,"УС",1);
				//КонецЕсли;
				// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
				
			ИначеЕсли РегПокупатели.КодОперации = ВтороеСобытиеБартерПриход Тогда
				// главная часть сложной проводки по взаиморасчетам
				глПроводка(Контекст,,СчетКонтрагента,РегПокупатели.Долг,"Взаиморасчеты с поставщиком.",, ,,,
				Контрагент,Договор,, ,,"УС",1,"Взаиморасчеты",1);
				ВзаиморасчетыВсего = ВзаиморасчетыВсего + РегПокупатели.Долг;
				// дополнительные проводки для бартера 
				Если глВыделятьЛиАвансыПоСчету(СчетКонтрагента)=1 Тогда
					// закрываем встречную поставку
					Сч = "371" + ?(Валюта=Гривня,"1","2");
					глПроводка(Контекст,СчетКонтрагента,Сч,РегПокупатели.Долг,"Бартер: второе событие",, Контрагент,Договор,,
					Контрагент,Договор,, ,,"УС",1);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если НДСнаЗатраты = 1 Тогда
	
		// --- УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		//Если СубконтоВалРасх <> Константа.НиДоходНиРасход Тогда
		//	// необходимо увеличить ВР на сумму НДС
		//	глПроводка(Контекст,"ВР","ВР",Итог("НДС"),"Вал. расх. увеличены на сумму НДС, не относящуюся к нал. кредиту",, Контрагент,Договор,СубконтоВалРасх,
		//	Контрагент,Договор,СубконтоВалРасх,,,"УС",1);
		//КонецЕсли;	
		// ... УМК Сандомирский В.Ю, (30.07.14) убираем проводки по ВЛ , ВД\ВР
		
		// если была предоплата ...
		Если НДСВтороеСобытие <> 0 Тогда
			// ... необходимо сторнировать НДС
	    	глПроводка(Контекст,СчетНДС,"6441",-НДСВтороеСобытие,"НДС: Сторно ранее учтенного налогового кредита",, СубконтоНДС1,СубконтоНДС2,,
	    	Контрагент,Договор,, ,,"УС");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Функция ПроверкаСтроки()
	глВсеВыбрано = 1;
    глВыбранЛи(Услуга,"Услуга",НомерСтроки);
	глВыбранЛи(Счет,"Счет",НомерСтроки);
	Если НЕ((Счет.ПринадлежитГруппе(СчетПоКоду("15"))=1) или (глЭтоСчетТЗР(Счет)=1)) Тогда
		Если глПроверитьСчетЗатрат(Счет,2,0,"Счет",НомерСтроки)=0 Тогда
		    глВсеВыбрано = 0;
		КонецЕсли;
	КонецЕсли;
	Если глВсеВыбрано = 1 Тогда
		Для Инд = 1 По Счет.КоличествоСубконто() Цикл  
			Если Метаданные.ВидСубконто(Счет.ВидСубконто(Инд).ПорядковыйНомер()).ПустоеСубконто=0 Тогда
				глВыбранЛи(ПолучитьАтрибут("Субконто"+Инд),"Субконто "+Инд,НомерСтроки);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если глВсеВыбрано = 1 Тогда
		глВсеВыбрано = ?(глПроверкаТовараВДокументе(Контекст,Услуга,НомерСтроки,1)=Да, глВсеВыбрано, 0);
	КонецЕсли;
    Возврат глВсеВыбрано;
КонецФункции

// ===============================
Процедура РассчитатьСтроку()
	ПересчКво = ?(глЭтоСчетТЗР(Счет)=1,0,Кво * Коэффициент);

	ПострСуммаСНДС	= глОкрКорр("Взаим", ?(ИтогСуммаСНДС = 0, 0, ВзаиморасчетыВсего * СуммаСНДС / ИтогСуммаСНДС),2);
	ПострНДС		= глОкрКорр("НДС", ?(ИтогНДС = 0, 0, НДСВсего * НДС / ИтогНДС),2);
	ПострСуммаБезНДС= ПострСуммаСНДС - ПострНДС;
	
	Если (НДСНаЗатраты = 1) и ((ПустоеЗначение(СчетЗатратНДС)=1)
	или (Счет.ПринадлежитГруппе(СчетПоКоду("15"))=1)  или (глЭтоСчетТЗР(Счет)=1)) Тогда
		СуммаЗатрат = ПострСуммаСНДС;
	Иначе
		СуммаЗатрат = ПострСуммаБезНДС;
	КонецЕсли;	                        
	СуммаНДС = ПострНДС;
КонецПроцедуры
                             
// ===============================
Процедура ДвиженияСтрока()	
	// отражаем приходование услуг в оборотах
	глПровестиПартию(Контекст, 1, 0, Фирма, Услуга, Счет, 0, Контрагент, 0, 0, 
		ПересчКво, СуммаЗатрат,0, ПокупкаУслуги, 1, СуммаЗатрат, 0);
	// формируем проводки
	Если (Счет.ПринадлежитГруппе(СчетПоКоду("15"))=1) или (глЭтоСчетТЗР(Счет)=1) Тогда
		// 15 счет или счет ТЗР
		глПроводка(Контекст,Счет,,СуммаЗатрат,"Приходование: услуга",ПересчКво, Субконто1,Субконто2,Субконто3,
		,,, ,,"УС",1,"Взаиморасчеты");
	Иначе
		Если (НДСНаЗатраты = 1) и (ПустоеЗначение(СчетЗатратНДС)=0) Тогда
			// права на налоговый кредит нет, относим НДС на затраты
			глПроводкаПоЗатратам(Контекст,СчетЗатратНДС,,СуммаНДС,"Приходование: НДС отнесен на затраты",,Субконто1,Субконто2,ВидЗатратНДС,
	    	,,, ,,"УС",1,"Взаиморасчеты");
		КонецЕсли;
		глПроводкаПоЗатратам(Контекст,Счет,,СуммаЗатрат,"Приходование: услуга",ПересчКво, Субконто1,Субконто2,Субконто3,
		,,, ,,"УС",1,"Взаиморасчеты");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ОбработкаПроведения()
	глКомментарий("Начало",2,Контекст);

	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
	    Возврат;
	КонецЕсли;

	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки()=0 Тогда
		    глНеПроводить(Контекст);
		    Возврат;
		КонецЕсли;
	КонецЦикла;
	
	РассчитатьШапку();
	ДвиженияВзаиморасчеты();
	
	глОчиститьКлючОкр("Взаим");
	глОчиститьКлючОкр("НДС");
	ИтогСуммаСНДС = Итог("СуммаСНДС");
	ИтогНДС		  = Итог("НДС");
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		РассчитатьСтроку();
		ДвиженияСтрока();
	КонецЦикла;
	глЗаписатьПроводкиВОперацию(Контекст);
	Операция.СуммаОперации = Итог("СуммаСНДС");
	Операция.Содержание = Примечание;
	Операция.Записать();
	глКомментарий("Окончание",2,Контекст);
КонецПроцедуры
