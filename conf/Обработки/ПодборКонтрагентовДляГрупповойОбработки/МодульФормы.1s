// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем ТекСтрокаВТаблицеМФ; // текущая строка в таблице значений  МФ 

Перем СписокКонтрагентов;	// список отфильтрованных контрагентов


// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
// Название: ПерерисовкаНазванийЗакладок
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
Функция ПерерисовкаНазванийЗакладок()      
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
КонецФункции // ПерерисовкаНазванийЗакладок	


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

// ===============================
// Название: ДоступностьЭлементов
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Устанавливает доступность элементов диалога
Процедура ДоступностьЭлементов(ВсеЭлементы=0)

	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбКонтрагент") Или (ЭлементДиалога="кХВыбКонтрагент") Тогда
		Форма.кХВыбКонтрагент.Доступность(ВыбКонтрагент.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбКатегорияЦенПоставщика") Или (ЭлементДиалога="кХВыбКатегорияЦенПоставщика") Тогда
		Форма.кХВыбКатегорияЦенПоставщика.Доступность(ВыбКатегорияЦенПоставщика.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбКатегорияЦенПокупателю") Или (ЭлементДиалога="кХВыбКатегорияЦенПокупателю") Тогда
		Форма.кХВыбКатегорияЦенПокупателю.Доступность(ВыбКатегорияЦенПокупателю.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбВалютаКредитаПокупателю") Или (ЭлементДиалога="кХВыбВалютаКредитаПокупателю") Тогда
		Доступность = ВыбВалютаКредитаПокупателю.Выбран();

		Форма.кХВыбВалютаКредитаПокупателю.	Доступность(Доступность); 
		Форма.ВклСКПокупателюОт.			Доступность(Доступность);
		Форма.ВклСКПокупателюДо.			Доступность(Доступность);

		ВклСКПокупателюОт = Мин(ВклСКПокупателюОт,Доступность);
		ВклСКПокупателюДо = Мин(ВклСКПокупателюДо,Доступность);
		ВклГКПокупателюОт = Мин(ВклСКПокупателюОт,Доступность);
		ВклГКПокупателюДо = Мин(ВклГКПокупателюДо,Доступность);

		Форма.СКПокупателюОт.Доступность(ВклСКПокупателюОт); 
		Форма.СКПокупателюДо.Доступность(ВклСКПокупателюДо);
		Форма.ГКПокупателюОт.Доступность(ВклГКПокупателюОт); 
		Форма.ГКПокупателюДо.Доступность(ВклГКПокупателюДо);
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклСКПокупателюОт") Тогда
		Форма.СКПокупателюОт.Доступность(ВклСКПокупателюОт); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклСКПокупателюДо") Тогда
		Форма.СКПокупателюДо.Доступность(ВклСКПокупателюДо); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклГКПокупателюОт") Тогда
		Форма.ГКПокупателюОт.Доступность(ВклГКПокупателюОт); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклГКПокупателюДо") Тогда
		Форма.ГКПокупателюДо.Доступность(ВклГКПокупателюДо); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбВалютаКредитаПоставщика") Или (ЭлементДиалога="кХВыбВалютаКредитаПоставщика") Тогда
		Доступность = ВыбВалютаКредитаПоставщика.Выбран();

		Форма.кХВыбВалютаКредитаПоставщика.	Доступность(Доступность); 
		Форма.ВклСКПоставщикаОт.			Доступность(Доступность);
		Форма.ВклСКПоставщикаДо.			Доступность(Доступность);

		ВклСКПоставщикаОт = Мин(ВклСКПоставщикаОт,Доступность);
		ВклСКПоставщикаДо = Мин(ВклСКПоставщикаДо,Доступность);
		ВклГКПоставщикаОт = Мин(ВклСКПоставщикаОт,Доступность);
		ВклГКПоставщикаДо = Мин(ВклГКПоставщикаДо,Доступность);

		Форма.СКПоставщикаОт.Доступность(ВклСКПоставщикаОт); 
		Форма.СКПоставщикаДо.Доступность(ВклСКПоставщикаДо);
		Форма.ГКПоставщикаОт.Доступность(ВклГКПоставщикаОт); 
		Форма.ГКПоставщикаДо.Доступность(ВклГКПоставщикаДо);
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклСКПоставщикаОт") Тогда
		Форма.СКПоставщикаОт.Доступность(ВклСКПоставщикаОт); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклСКПоставщикаДо") Тогда
		Форма.СКПоставщикаДо.Доступность(ВклСКПоставщикаДо); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклГКПоставщикаОт") Тогда
		Форма.ГКПоставщикаОт.Доступность(ВклГКПоставщикаОт); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВклГКПоставщикаДо") Тогда
		Форма.ГКПоставщикаДо.Доступность(ВклГКПоставщикаДо); 
	КонецЕсли;

КонецПроцедуры // ДоступностьЭлементов	

// ===============================
// Название: Выполнить
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопка "Выполнить",.
// Описание:
//   отбирает контрагентов, подлежащие групповой обработке и запускает 
//	 групповую обработку справочника номенклатуры
//
Процедура Выполнить()
    Перем Запрос;		// запрос
	Перем ТекстЗапроса;	// текст запроса
	                                 
	Перем ЧислоКатегорий, ЧислоКатегорийВСписке;
	Перем Контр;    
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были

	ТекстЗапроса = "                    
	   	|Период С '"+Строка(РабочаяДата())+"' По '"+Строка(РабочаяДата())+"';
		|Контрагент 				= Справочник.Контрагенты.ТекущийЭлемент;
		|Вид 						= Справочник.Контрагенты.ВидКонтрагента;                 

		|ВалютаКредитаПоставщика 	= Справочник.Контрагенты.ВалютаКредитаПоставщика;                  
		|ВалютаКредитаПокупателю 	= Справочник.Контрагенты.ВалютаКредита;                  

		|ГлубинаКредитаПоставщику 	= Справочник.Контрагенты.ГлубинаКредитаПоставщика;                  
		|ГлубинаКредитаПокупателю 	= Справочник.Контрагенты.Глубина;                  

		|СуммаКредитаПоставщика 	= Справочник.Контрагенты.СуммаКредитаПоставщика;                  
		|СуммаКредитаПокупатулю 	= Справочник.Контрагенты.СуммаКредита;                  

		|КатегорияЦенПокупателю 	= Справочник.Контрагенты.КатегорияЦен;                   
		|КатегорияЦенПоставщика 	= Справочник.Контрагенты.КатегорияЦенПоставщика;                  

		|Группировка Контрагент Без Групп;
		|";

	Если ВидКонтрагентов.ТекущаяСтрока() > 1 Тогда
		ТекПеречисление = ВидКонтрагентов.ПолучитьЗначение(ВидКонтрагентов.ТекущаяСтрока());
		ТекстЗапроса = ТекстЗапроса + "Условие (Вид=Перечисление.ВидыКонтрагентов."+ТекПеречисление.Идентификатор()+");";
	КонецЕсли;
    
	Если глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Контрагент", ВыбКонтрагент, "ВыбКонтрагент", ТекстЗапроса, "", "", "КатегорииКонтрагентов")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по контрагентам.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"КатегорияЦенПокупателю", ВыбКатегорияЦенПокупателю, "ВыбКатегорияЦенПокупателю", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по категориям цен покупателю.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"КатегорияЦенПоставщика", ВыбКатегорияЦенПоставщика, "ВыбКатегорияЦенПоставщика", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по категориям цен поставщика.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"ВалютаКредитаПокупателю", ВыбВалютаКредитаПокупателю, "ВыбВалютаКредитаПокупателю", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по валюте кредита покупателю.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"ВалютаКредитаПоставщика", ВыбВалютаКредитаПоставщика, "ВыбВалютаКредитаПоставщика", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по валюте кредита поставщика.");
		Возврат;
	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса + ?(ВклСКПокупателюОт = 0,"","Условие (СуммаКредитаПокупателю >="+Строка(СКПокупателюОт)+");");
	ТекстЗапроса = ТекстЗапроса + ?(ВклСКПокупателюДо = 0,"","Условие (СуммаКредитаПокупателю <="+Строка(СКПокупателюДо)+");");

	ТекстЗапроса = ТекстЗапроса + ?(ВклГКПокупателюОт = 0,"","Условие (ГлубинаКредитаПокупателю >="+Строка(ГКПокупателюОт)+");");
	ТекстЗапроса = ТекстЗапроса + ?(ВклГКПокупателюДо = 0,"","Условие (ГлубинаКредитаПокупателю <="+Строка(ГКПокупателюДо)+");");

	ТекстЗапроса = ТекстЗапроса + ?(ВклСКПоставщикаОт = 0,"","Условие (СуммаКредитаПоставщика >="+Строка(СКПоставщикаОт)+");");
	ТекстЗапроса = ТекстЗапроса + ?(ВклСКПоставщикаДо = 0,"","Условие (СуммаКредитаПоставщика <="+Строка(СКПоставщикаДо)+");");

	ТекстЗапроса = ТекстЗапроса + ?(ВклГКПоставщикаОт = 0,"","Условие (ГлубинаКредитаПоставщика >="+Строка(ГКПоставщикаОт)+");");
	ТекстЗапроса = ТекстЗапроса + ?(ВклГКПоставщикаДо = 0,"","Условие (ГлубинаКредитаПоставщика <="+Строка(ГКПоставщикаДо)+");");

    // выполняем запрос
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
		Предупреждение("Запрос по контрагентам не выполнился");
		Возврат;
	КонецЕсли;
        
	// выгружаем всех отобранных контрагентов в список
	СписокКонтрагентов = СоздатьОбъект("СписокЗначений");
	СписокКонтрагентов.УдалитьВсе();
	Запрос.ВНачалоВыборки();
	Пока Запрос.Группировка("Контрагент")=1 Цикл
		Если Запрос.Контрагент.Выбран()=1 Тогда
			СписокКонтрагентов.ДобавитьЗначение(Запрос.Контрагент.ТекущийЭлемент());
		КонецЕсли;
	КонецЦикла;
	
	Если СписокКонтрагентов.РазмерСписка()=0 Тогда
		Предупреждение("Список контрагентов пуст");
	Иначе
		ОткрытьФорму("Обработка.ГрупповаяОбработкаСправочникаКонтрагентов",СписокКонтрагентов);
	КонецЕсли;
КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение="")	// Предопределенная процедура
	// закладки
    Если Номер=1 Тогда
		Форма.ИспользоватьСлой("Общий,Основной");
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
    ИначеЕсли Номер=2 Тогда
		Форма.ИспользоватьСлой("Общий,МФ");
	КонецЕсли;      
	ПерерисовкаНазванийЗакладок();
	ДоступностьЭлементов(1);
КонецПроцедуры	// ПриВыбореЗакладки

// ===============================
Процедура ОбработкаПодбора(Значение)  // Предопределенная процедура
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
КонецПроцедуры  // ОбработкаПодбора

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	ПриВыбореЗакладки(1);
КонецПроцедуры	// ПриОткрытии

// ===============================
Процедура ВводНового()	// Предопределенная процедура
	// эта предопределенная процедура выполняется при восстановлении настройки
	ТекСтрокаВТаблицеМФ="";
	ПриВыбореЗакладки(1);
КонецПроцедуры	// ВводНового


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Отбор по значениям");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");

ТаблицаМФ.ВидимостьКолонки("Тип",				0);
ТаблицаМФ.ВидимостьКолонки("Вид",				0);
ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",	0);          
ТаблицаМФ.ВидимостьКолонки("ТипМФ",				0);
ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",		0);

ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

//                  			тип			вид				переменная  				название
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты",	"Контрагент",				"По контрагентам");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Валюты",		"ВалютаКредитаПокупателю",	"По валютам кредита покупателю");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Валюты",		"ВалютаКредитаПоставщика",	"По валютам кредита поставщика");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","КатегорииЦен",	"КатегорияЦенПокупателю",	"По категориям цен покупателю");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","КатегорииЦен",	"КатегорияЦенПоставщику",	"По категориям цен поставщику");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий","Контрагент",				"По категориям контрагентов");

ТекСтрокаВТаблицеМФ="";

// инициализация списков
ВидКонтрагентов.УдалитьВсе();
ВидКонтрагентов.ДобавитьЗначение("все контрагенты");

Для Инд=1 По Перечисление.ВидыКонтрагентов.КоличествоЗначений() Цикл
	ТекущееПеречисление = Перечисление.ВидыКонтрагентов.ЗначениеПоНомеру(Инд);
	ВидКонтрагентов.ДобавитьЗначение(ТекущееПеречисление);
КонецЦикла;

ВидКонтрагентов.ТекущаяСтрока(1);

