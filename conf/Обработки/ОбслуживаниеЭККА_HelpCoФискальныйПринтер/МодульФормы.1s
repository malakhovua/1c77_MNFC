Перем НазваниеДрайвера; 

// ===============================
Функция НалоговаяГруппа(СтавкаНДС)
	Если СтавкаНДС = ОсновнаяСтавкаНДС Тогда
		Возврат 1;
	ИначеЕсли СтавкаНДС = ЛьготнаяСтавкаНДС Тогда
		Возврат 2;
	ИначеЕсли СтавкаНДС = безНДС Тогда
		Возврат 3;
	КонецЕсли
КонецФункции                 

//===========================================            
Процедура ИнициализацияДрайвера()				      
	
КонецПроцедуры                

//===========================================            
Процедура Инициализация()			
	
	ЭККА = глРасшифровка.Получить("Касса");
	    
	Состояние("Инициализация драйвера ЭККА "+ЭККА);
	
	Попытка 
		
		Драйвер = CreateObject("EQLProc.EQLProcedure");
	
		Попытка 
			
			спПараметры = СоздатьОбъект("СписокЗначений");
			глПарсить(ЭККА.ПараметрыИнициализации,спПараметры,РазделительСтрок);
			
			НомерКассы = спПараметры.Получить("Номер кассы");
			Если ПустоеЗначение(НомерКассы)=1 Тогда
				НомерКассы = 1;
			КонецЕсли;
			
			Пользователь = спПараметры.Получить("Пользователь");
			Если ПустоеЗначение(Пользователь)=1 Тогда
				Пользователь = 1;
			КонецЕсли;
			
			Пароль = спПараметры.Получить("Пароль");
			Если ПустоеЗначение(Пароль)=1 Тогда
				Пароль = 0;
			КонецЕсли;
			
			Сервер = спПараметры.Получить("Сервер");
			
			//Инициализация ЭККА
			
			Драйвер.Number = НомерКассы;
			Драйвер.User = Пользователь;
			Драйвер.Password = Пароль;
			Если ПустоеЗначение(Пароль)=0 Тогда
				Драйвер.Location = Сервер;
			КонецЕсли;
			
			Драйвер.Connect = 1;
			
			
			глПереченьДрайверов.ДобавитьЗначение(Драйвер,НазваниеДрайвера+ЭККА.Код);
			глКомментарий("Подключен драйвер для ЭККА "+ЭККА,4);
			
			Попытка
				// начало смены
				Драйвер.SmenBegin();
			Исключение
				глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки(),0);
				глКомментарий("Кассовый аппарат не переведен в режим фискального принтера!",2);
			КонецПопытки;
			
		Исключение
			глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки(),0);
		КонецПопытки;
	Исключение
		глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки(),0);
		глКомментарий("Драйвер не установлен !",2);
	КонецПопытки;
	
	
КонецПроцедуры

//===========================================            
Процедура ПечатьЧека()
	
	Док = глРасшифровка.Получить("Документ"); // Чек
	
	Если ПустоеЗначение(Док)=1 Тогда
	    Возврат;
	КонецЕсли;
	
	Драйвер = глПереченьДрайверов.Получить(НазваниеДрайвера+Док.ЭККА.Код);
	
	Если ПустоеЗначение(Драйвер)=1 Тогда
		глКомментарий("Не найден драйвер ЭККА",1);
	    Возврат;
	КонецЕсли; 
	
	Попытка
		
		//Начало чека
		Драйвер.BegChk();
		
		Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку()=1 Цикл
			// печать строки
			НалоговаяГр = НалоговаяГруппа(Док.ТМЦ.СтавкаНДС);
			Драйвер.FullProd(Док.ТМЦ.Код, Док.ЦенаСНДС, Док.Кво, 1, 1, НалоговаяГр, Док.ТМЦ.Наименование);
		КонецЦикла;
		
		Если Док.Скидка > 0 Тогда
			// Скидка
			Драйвер.NacSkd(4,Док.Скидка,0);
		ИначеЕсли Док.Скидка < 0 Тогда
			// Наценка
			Драйвер.NacSkd(5,-Док.Скидка,0);
		КонецЕсли;
		
		// Полностью оплачен
		Драйвер.Oplata(0, 0, 0);
		
		// Конец чека
		Драйвер.EndChk();
		Док.ЧекПробит=1;
	Исключение
		глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки());
		Драйвер.VoidChk();
		Док.ЧекПробит=0;
	КонецПопытки;
	
КонецПроцедуры                  

//===========================================            
Процедура Отчет()
	
	Сообщить("Драйвер фискального принтера HelpCo не поддержвает загрузку отчета!");
	
КонецПроцедуры                  

//===========================================            
Процедура ВыгрузитьАртикулы()
	
	Сообщить("Драйвер фискального принтера HelpCo не поддержвает выгрузку артикулов!");
	
КонецПроцедуры


//===========================================            
Процедура ПриОткрытии()
	Параметр = глРасшифровка.Получить("Парам"); 
	Если Параметр = "Инициализация" тогда
		Инициализация();   
	ИначеЕсли Параметр = "ПодключитьДрайвер" тогда	
		ИнициализацияДрайвера();
	ИначеЕсли Параметр = "ВыгрузитьАртикулы" тогда	
		ВыгрузитьАртикулы();
	ИначеЕсли Параметр = "ПечатьЧека" тогда	
		ПечатьЧека();
	ИначеЕсли Параметр = "Отчет" тогда	
		Отчет();
	КонецЕсли;	
	СтатусВозврата(0);
КонецПроцедуры	

НазваниеДрайвера = "HelpCoФП";
