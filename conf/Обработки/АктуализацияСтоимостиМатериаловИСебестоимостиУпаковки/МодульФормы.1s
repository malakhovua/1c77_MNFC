
//*******************************************
Функция АктуальнаяЦенаИзПоступления(МатериалУпаковки, ДокументПН)
	
	Док = СоздатьОбъект("Документ");
	Док.ОбратныйПорядок(1);
	Док.УстановитьФильтр(1, 0);
	//Док.ВыбратьДокументы();
	Док.ВыбратьПоЗначению(, , "ТМЦ", МатериалУпаковки);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если Док.Вид() = "ПриходнаяНакладнаяЗапасы" Тогда
			Док.ВыбратьСтроки();
			Пока  Док.ПолучитьСтроку() = 1 Цикл
				Если Док.ТМЦ = МатериалУпаковки Тогда
					ДокументПН = Док.ТекущийДокумент();
					Возврат Окр(Док.ЦенаСНДС,2);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

//*******************************************
Процедура ВыполнитьЗапросПоМатериалам(ВыполнятьКорректировку = 0, ФормироватьОтчет = 1)
	Перем Запрос, ТекстЗапроса, Таб;
	
	ДатаОбтаботки = ?(ПустоеЗначение(Период)=1,ТекущаяДата(),Период);
	РВУОбъект = СоздатьОбъект("Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ");
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(ВыполнитьЗапросПоМатериалам)
	|ОбрабатыватьДокументы все;
	|ТекущийЭлемент = Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ.ТекущийЭлемент;
	|Владелец = Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ.Владелец;
	|Группировка Владелец без групп;
	|Группировка ТекущийЭлемент;
	|Условие(ТекущийЭлемент.ПометкаУдаления() = 0);" +
	?(ПустоеЗначение(Продукция) = 1,"","Условие(Владелец В Продукция);") + "
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовка к заполнению выходных форм данными запроса
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Отчет");
	// Заполнение полей "Заголовок"
	Таб.ВывестиСекцию("Заголовок");
	
	Если ФормироватьОтчет = 1 Тогда
		Состояние("Заполнение выходной таблицы...");
	Иначе
		Состояние("Выполняется корректировка...");
	КонецЕсли;
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0);
	Пока Запрос.Группировка(1) = 1 Цикл
		// Заполнение полей Владелец
		Номенклатура = Запрос.Владелец.ТекущийЭлемент();
		
		Если ФормироватьОтчет = 1 Тогда
			Таб.ВывестиСекцию("Владелец");
		КонецЕсли;
		
		Пока Запрос.Группировка(2) = 1 Цикл
			// Заполнение полей ТекущийЭлемент
			РВУ = Запрос.ТекущийЭлемент;
			ВидУпаковки = Запрос.ТекущийЭлемент.ВидУпаковки;
			
			//Материалы упаковки
			СписокМатериаловУпаковки = СоздатьОбъект("ТаблицаЗначений");
			глПолучитьТаблицуМатериаловДляФормы(СписокМатериаловУпаковки, "", 1);
			глЗаполнитьСписокМатериаловУпаковки(СписокМатериаловУпаковки, РВУ, "", ДатаОбтаботки, 1,,1,0);
			
			//Итоги
			ТекРасчСебестоимость = 0;
			АктуалСебестоимость = 0;
			РазницаСуммаИтог =0;
			
			Если ФормироватьОтчет = 1 Тогда
				Таб.ВывестиСекцию("ТекущийЭлемент");
			КонецЕсли;
			
			СписокМатериаловУпаковки.ВыбратьСтроки();
			
			Пока СписокМатериаловУпаковки.ПолучитьСтроку() = 1 Цикл
				
				ДокументПН = "<>";
				
				МатериалУпаковки = СписокМатериаловУпаковки.МатериалУпаковки; 
				ИзФормы = СписокМатериаловУпаковки.ИзФормы;
				НормаСписания = СписокМатериаловУпаковки.НормаСписания;
				НормаСписанияУп = СписокМатериаловУпаковки.НормаСписанияУп;
				Цена = СписокМатериаловУпаковки.Цена;
				Сумма = СписокМатериаловУпаковки.Сумма;
				
				//Справочник Нормы списания мат-в упак.
				НормаСписанияМатериалов = СписокМатериаловУпаковки.НормаСписанияМатериалов;
				
				//актуальные данные
				ЦенаАктуальная = АктуальнаяЦенаИзПоступления(МатериалУпаковки.ТекущийЭлемент(),ДокументПН);
				ЦенаАктуальная = ?(ЦенаАктуальная =0,Цена,ЦенаАктуальная); // берем текущую цену.
				СуммаАктуальная = Окр(ЦенаАктуальная*НормаСписания,2);
				
				
				//Текущая стоимость - актуальная по строке
				РазницаСумма = СуммаАктуальная - СписокМатериаловУпаковки.Сумма;
				
				//Итоги
				ТекРасчСебестоимость = ТекРасчСебестоимость + СписокМатериаловУпаковки.Сумма;
				АктуалСебестоимость = АктуалСебестоимость + СуммаАктуальная;
				РазницаСуммаИтог = РазницаСуммаИтог + РазницаСумма; 
				
				Если ФормироватьОтчет = 1 Тогда
					Таб.ВывестиСекцию("ТекущийЭлементМатериал");
				КонецЕсли;
				
				//Корректировка данных
				Если ВыполнятьКорректировку = 1 Тогда
					
					Если НормаСписанияМатериалов.Вид() <> "УМК_НормыСписанияМатериаловУпаковокГрупп" Тогда
						НормаСписанияМатериаловОбъект = СоздатьОбъект("Справочник." + НормаСписанияМатериалов.Вид());
						НормаСписанияМатериаловОбъект.НайтиЭлемент(НормаСписанияМатериалов);
						НормаСписанияМатериаловОбъект.Цена.Установить(ДатаОбтаботки, ЦенаАктуальная);
						Если НормаСписанияМатериалов.Вид() = "УМК_НормыСписанияМатериаловУпаковок" Тогда
							НормаСписанияМатериаловОбъект.Сумма.Установить(ДатаОбтаботки, ЦенаАктуальная*НормаСписанияМатериаловОбъект.НормаСписания.Получить(ДатаОбтаботки));
						КонецЕсли;
						НормаСписанияМатериаловОбъект.Записать();
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ТекСебестоимость = РВУ.Себестоимость.Получить(ДатаОбтаботки);
			
			Если ФормироватьОтчет = 1 Тогда
				Таб.ВывестиСекцию("Итоги");
			КонецЕсли;
			
			//Установим с/с для упаковки
			Если ВыполнятьКорректировку = 1 Тогда
				РВУОбъект.НайтиЭлемент(РВУ);
				РВУОбъект.Себестоимость.Установить(ДатаОбтаботки, АктуалСебестоимость);
				РВУОбъект.Записать();
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	// Вывод заполненной формы
	Если ФормироватьОтчет = 1 Тогда
		Таб.ТолькоПросмотр(1);
		Таб.Показать("Отчет", "");
	КонецЕсли;
	
КонецПроцедуры

//*******************************************
Процедура СформироватьОтчет()
	
	ВыполнитьЗапросПоМатериалам();
	
КонецПроцедуры  

Процедура ВыполнитьКорректировку()
	
	ВыполнитьЗапросПоМатериалам(1,0);
	ВыполнитьЗапросПоМатериалам();
	
КонецПроцедуры





