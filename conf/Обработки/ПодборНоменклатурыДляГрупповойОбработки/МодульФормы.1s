// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем ТекСтрокаВТаблицеМФ; // текущая строка в таблице значений  МФ 

Перем СписокНоменклатуры;	// список отфильтрованных позиций номенклатуры
Перем СпСтавкиНДС;			// список ставок НДС


// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
// Название: ПерерисовкаНазванийЗакладок
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
Функция ПерерисовкаНазванийЗакладок()      
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
КонецФункции // ПерерисовкаНазванийЗакладок	


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

// ===============================
// Название: ДоступностьЭлементов
// Параметры:
// НЕТ
// Возвращаемое значение:
// Вызывается из формул элементов диалога:
// Наименование,.
// Описание:
//  Устанавливает доступность элементов диалога
Процедура ДоступностьЭлементов(ВсеЭлементы=0)

	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбНоменклатура") Или (ЭлементДиалога="кХВыбНоменклатура") Тогда
		Форма.кХВыбНоменклатура.Доступность(ВыбНоменклатура.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбЕдИзмБазовая") Или (ЭлементДиалога="кХВыбЕдИзмБазовая") Тогда
		Форма.кХВыбЕдИзмБазовая.Доступность(ВыбЕдИзмБазовая.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбЕдИзмПоУмолчанию") Или (ЭлементДиалога="кХВыбЕдИзмПоУмолчанию") Тогда
		Форма.кХВыбЕдИзмПоУмолчанию.Доступность(ВыбЕдИзмПоУмолчанию.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбСтавкаНДС") Или (ЭлементДиалога="кХВыбСтавкаНДС") Тогда
		Форма.кХВыбСтавкаНДС.Доступность(ВыбСтавкаНДС.Выбран()); 
	КонецЕсли;
	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбСхемаРасчетаЗП") Или (ЭлементДиалога="кХВыбСхемаРасчетаЗП") Тогда
		Форма.кХВыбСхемаРасчетаЗП.Доступность(ВыбСхемаРасчетаЗП.Выбран()); 
	КонецЕсли;

КонецПроцедуры // ДоступностьЭлементов	

// ===============================
// Название: Выполнить
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопка "Выполнить",.
// Описание:
//   отбирает контрагентов, подлежащие групповой обработке и запускает 
//	 групповую обработку справочника номенклатуры
//
Процедура Выполнить()
    Перем Запрос;		// запрос
	Перем ТекстЗапроса;	// текст запроса
	                                 
	Перем ЧислоКатегорий, ЧислоКатегорийВСписке;
	Перем Контр;    
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были

	ТекстЗапроса = "                    
	   	|Период С ВыбДата По ВыбДата;
		|Номенклатура		= Справочник.ТМЦ.ТекущийЭлемент;
		|Вид 				= Справочник.ТМЦ.ВидТМЦ;                 
		|СтавкаНДС 			= Справочник.ТМЦ.СтавкаНДС; 
		|СхемаРасчетаЗП		= Справочник.ТМЦ.СхемаРасчетаЗП;
		|ЕдИзмБазовая		= Справочник.ТМЦ.БазоваяЕдиница; 
		|ЕдИзмПоУмолчанию	= Справочник.ТМЦ.ЕдиницаПоУмолчанию.Единица;
		|Группировка Номенклатура Без Групп;
		|";
    
	Если ВидНоменклатуры.ТекущаяСтрока() > 1 Тогда
		ТекПеречисление = ВидНоменклатуры.ПолучитьЗначение(ВидНоменклатуры.ТекущаяСтрока());
		ТекстЗапроса = ТекстЗапроса + "Условие (Вид=Перечисление.ВидыТМЦ."+ТекПеречисление.Идентификатор()+");";
	КонецЕсли;

	Если глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Номенклатура", ВыбНоменклатура, "ВыбНоменклатура", ТекстЗапроса, "", "", "КатегорииТоваров")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по номенклатурным позициям.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"ЕдИзмБазовая", ВыбЕдИзмБазовая, "ВыбЕдИзмБазовая", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по единицам измерения.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"ЕдИзмПоУмолчанию", ВыбЕдИзмПоУмолчанию, "ВыбЕдИзмПоУмолчанию", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по единицам измерения.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"СтавкаНДС", ВыбСтавкаНДС, "ВыбСтавкаНДС", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по ставкам НДС.");
		Возврат;
	ИначеЕсли глФильтрПоПеременнойЗапроса(ТаблицаМФ,"СхемаРасчетаЗП", ВыбСхемаРасчетаЗП, "ВыбСхемаРасчетаЗП", ТекстЗапроса, "", "")=0 Тогда
		Предупреждение("Возникли ошибки при наложении фильтра по схемам ЗП");
		Возврат;
	КонецЕсли;
	Если НГруппа.Выбран() = 1 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие (Номенклатура.НГруппа в НГруппа);";
	ИначеЕсли фПустаяНГ = 1 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие (Номенклатура.НГруппа = НГруппа);";		
	КонецЕсли;
	
	
	//// --- Технологически
	//Текст=СоздатьОбъект("Текст");
	//Текст.ДобавитьСтроку(ТекстЗапроса);
	//Текст.Показать("Текст запроса ЗапрСписание"); 
	//// ... Технологически
	
	
    // выполняем запрос
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
		Предупреждение("Запрос по ТМЦ не выполнился");
		Возврат;
	КонецЕсли;
        
	// выгружаем все отобранные товары в список
	СписокНоменклатуры = СоздатьОбъект("СписокЗначений");
	СписокНоменклатуры.УдалитьВсе();
	Запрос.ВНачалоВыборки();
	Пока Запрос.Группировка("Номенклатура")>0 Цикл
		Если Запрос.Номенклатура.Выбран()=1 Тогда
			СписокНоменклатуры.ДобавитьЗначение(Запрос.Номенклатура.ТекущийЭлемент());
		КонецЕсли;
	КонецЦикла;

	Если СписокНоменклатуры.РазмерСписка()=0 Тогда
		Предупреждение("Список номенклатуры пуст");
	Иначе
		ОткрытьФорму("Обработка.ГрупповаяОбработкаСправочникаНоменклатуры",СписокНоменклатуры);
	КонецЕсли;

КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "ВыбСтавкаНДС" Тогда
		СпСтавкиНДС.ВыбратьЗначение(ВыбСтавкаНДС, , , , 1);
		ДоступностьЭлементов();
		ФлагСтандОбр = 0;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение="")	// Предопределенная процедура
	// закладки
    Если Номер=1 Тогда
		Форма.ИспользоватьСлой("Общий,Основной");
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
    ИначеЕсли Номер=2 Тогда
		Форма.ИспользоватьСлой("Общий,МФ");
	КонецЕсли;      
	ПерерисовкаНазванийЗакладок();
	ДоступностьЭлементов(1);
КонецПроцедуры	// ПриВыбореЗакладки

// ===============================
Процедура ОбработкаПодбора(Значение)  // Предопределенная процедура
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
КонецПроцедуры  // ОбработкаПодбора

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	ПриВыбореЗакладки(1);
КонецПроцедуры	// ПриОткрытии

// ===============================
Процедура ВводНового()	// Предопределенная процедура
	// эта предопределенная процедура выполняется при восстановлении настройки
	ТекСтрокаВТаблицеМФ="";
	ПриВыбореЗакладки(1);
КонецПроцедуры	// ВводНового


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

СпСтавкиНДС 	= СоздатьОбъект("СписокЗначений");
СпСтавкиНДС.ДобавитьЗначение(безНДС);
СпСтавкиНДС.ДобавитьЗначение(ОсновнаяСтавкаНДС);
СпСтавкиНДС.ДобавитьЗначение(ЛьготнаяСтавкаНДС);

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Отбор по значениям");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");

ТаблицаМФ.ВидимостьКолонки("Тип",				0);
ТаблицаМФ.ВидимостьКолонки("Вид",				0);
ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",	0);          
ТаблицаМФ.ВидимостьКолонки("ТипМФ",				0);
ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",		0);

ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

//                  			тип				вид						переменная  		название
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник",	"ТМЦ",					"Номенклатура",		"По номенклатуре");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник",	"КлассификаторЕдИзм",	"ЕдИзмБазовая",		"По базовым ед. измер.");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник",	"КлассификаторЕдИзм",	"ЕдИзмПоУмолчанию",	"По ед. измер. по умолчанию");
глДобавитьВТаблицуМФ(ТаблицаМФ,"СписокЗначений",СпСтавкиНДС,			"СтавкаНДС",		"По ставкам НДС");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник",	"ВидыКатегорий",		"Номенклатура",		"По категориям номенклатуры");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Документ",		"СхемаРасчетаЗП",		"СхемаРасчетаЗП",	"По схемам расчёта з/п");

ТекСтрокаВТаблицеМФ="";

// инициализация списков
ВидНоменклатуры.УдалитьВсе();
ВидНоменклатуры.ДобавитьЗначение("Вся номенклатура");

Для Инд=1 По Перечисление.ВидыТМЦ.КоличествоЗначений() Цикл
	ТекущееПеречисление = Перечисление.ВидыТМЦ.ЗначениеПоНомеру(Инд);
	ВидНоменклатуры.ДобавитьЗначение(ТекущееПеречисление);
КонецЦикла;

ВидНоменклатуры.ТекущаяСтрока(1);
ВыбДата = РабочаяДата();
