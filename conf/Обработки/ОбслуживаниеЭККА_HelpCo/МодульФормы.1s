Перем НазваниеДрайвера; 

// ===============================
Функция НалоговаяГруппа(СтавкаНДС)
	Если СтавкаНДС = ОсновнаяСтавкаНДС Тогда
		Возврат 1;
	ИначеЕсли СтавкаНДС = ЛьготнаяСтавкаНДС Тогда
		Возврат 2;
	ИначеЕсли СтавкаНДС = безНДС Тогда
		Возврат 3;
	КонецЕсли
КонецФункции                 

//===========================================            
Процедура ИнициализацияДрайвера()				      
	
КонецПроцедуры                

//===========================================            
Процедура Инициализация()
	
	ЭККА = глРасшифровка.Получить("Касса");
	
	Если ПустоеЗначение(ЭККА)=1 Тогда
	    Возврат;
	КонецЕсли;
	
	Если (ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Регистрация) Тогда
		Попытка 
			
			Состояние("Инициализация драйвера ЭККА "+ЭККА);
			
			спПараметры = СоздатьОбъект("СписокЗначений");
			глПарсить(ЭККА.ПараметрыИнициализации,спПараметры,РазделительСтрок);
			
			НомерКассы = спПараметры.Получить("НомерКассы");
			Если ПустоеЗначение(НомерКассы)=1 Тогда
				НомерКассы = 1;
			КонецЕсли;
			
			Пользователь = спПараметры.Получить("Пользователь");
			Если ПустоеЗначение(Пользователь)=1 Тогда
				Пользователь = 1;
			КонецЕсли;
			
			Пароль = спПараметры.Получить("Пароль");
			Если ПустоеЗначение(Пароль)=1 Тогда
				Пароль = 0;
			КонецЕсли;
			
			Сервер = спПараметры.Получить("Сервер");
			
			Соединение = CreateObject("ADODB.Connection");
			стрСоединения = "Provider=EQL OLE DB Provider;Data Source="+НомерКассы+";User Id="+Пользователь+";Password="+Пароль+";";
			Если ПустоеЗначение(Сервер)=0 Тогда
				стрСоединения = стрСоединения + "Location="+Сервер+";";
			КонецЕсли;
			Соединение.Open(стрСоединения);
			
			Сервисы = CreateObject("ADODB.Recordset");
			Сервисы.Open("EQL_service", Соединение, -1, -1, 512);
			
			Proc = Сервисы.fields(1).value;
			Попытка
				// начало смены
				Proc.SmenBegin();
			Исключение
				глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки(),0);
				глКомментарий("Кассовый аппарат не переведен в режим фискального принтера!",2);
			КонецПопытки;

			
			глПереченьДрайверов.ДобавитьЗначение(Proc,НазваниеДрайвера+ЭККА.Код);
			глКомментарий("Подключен драйвер для ЭККА "+ЭККА,2);
		Исключение
			глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки(),0);            
			глКомментарий("Драйвер не установлен !",2);
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры

//===========================================            
Процедура ПечатьЧека()
	
	Док = глРасшифровка.Получить("Документ"); // Чек
	
	Если ПустоеЗначение(Док)=1 Тогда
	    Возврат;
	КонецЕсли;
	
	Драйвер = глПереченьДрайверов.Получить(НазваниеДрайвера+Док.ЭККА.Код);
	
	Если ПустоеЗначение(Драйвер)=1 Тогда
		глКомментарий("Не найден драйвер ЭККА",1);
	    Возврат;
	КонецЕсли;
	
	Попытка
		
		//Начало чека
		Драйвер.BegChk();
		
		Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку()=1 Цикл
			// печать строки
			Драйвер.Prod(Док.ТМЦ.Код, Док.ЦенаСНДС, Док.Кво, 1, 1);
		КонецЦикла;
		
		Если Док.СуммаСкидки > 0 Тогда
			// Скидка
			Драйвер.NacSkd(4,Док.Скидка,0);
		ИначеЕсли Док.СуммаСкидки < 0 Тогда
			// Наценка
			Драйвер.NacSkd(5,-Док.Скидка,0);
		КонецЕсли;
		
		// Полностью оплачен
		Драйвер.Oplata(0, 0, 0);
		
		// Конец чека
		Драйвер.EndChk();
		Док.ЧекПробит=1;
		
	Исключение
		глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки());
		Драйвер.VoidChk();
		Док.ЧекПробит=0;
	КонецПопытки;
	
КонецПроцедуры                  

//===========================================            
Процедура Отчет()
	                       
	
	Док = глРасшифровка.Получить("Документ"); // Отчет КА
	
	Если ПустоеЗначение(Док)=1 Тогда
	    Возврат;
	КонецЕсли;
	
	ЭККА = Док.ЭККА;
	
	Если ПустоеЗначение(ЭККА)=1 Тогда
		глКомментарий("При вызове драйвера ЭККА не указана касса",0);
	    Возврат;
	КонецЕсли;
	
	// читаем отчет ЭККА
	Состояние(" Загрузка отчета ЭККА "+ЭККА);
	
	спрТовары = СоздатьОбъект("Справочник.ТМЦ");
	
	спПараметры = СоздатьОбъект("СписокЗначений");
	глПарсить(ЭККА.ПараметрыИнициализации,спПараметры,РазделительСтрок);
	
	НомерКассы = спПараметры.Получить("НомерКассы");
	Если ПустоеЗначение(НомерКассы)=1 Тогда
		НомерКассы = 1;
	КонецЕсли;
	
	uc = CreateObject("Odc.UpdateCash");
	uc.update(НомерКассы);
	
	icr = uc.CurReports(НомерКассы);
	
	Goods = icr.Goods;
	Goods.Move(0, 1);
	
	Пока Goods.EOF <> -1 Цикл // -1 = True
		Код = Goods.GetID("Code");
		Количество = Goods.GetID("Amount");
		Сумма = Goods.GetID("Sum");
		
		// найдем товар
		Если спрТовары.НайтиПоКоду(Код)=1 Тогда
			Док.НоваяСтрока();
			Док.ТМЦ = спрТовары.ТекущийЭлемент();
			Док.Единица = спрТовары.ЕдиницаПоУмолчанию;
			Док.Коэффициент = 1;
			Док.Кво = Количество;
			Док.СуммаСНДС = Сумма;
		Иначе
			глКомментарий("Не найден товар с кодом "+Код,0);
		КонецЕсли;
		
		Goods.Move();
	КонецЦикла;
	
КонецПроцедуры                  

//===========================================            
Процедура ВыгрузитьАртикулы()
	
	ЭККА = глРасшифровка.Получить("ЭККА");
	СписокТоваров = глРасшифровка.Получить("СписокТоваров");
	
	Попытка
	
		спПараметры = СоздатьОбъект("СписокЗначений");
		глПарсить(ЭККА.ПараметрыИнициализации,спПараметры,РазделительСтрок);
		
		НомерКассы = спПараметры.Получить("НомерКассы");
		Если ПустоеЗначение(НомерКассы)=1 Тогда
			НомерКассы = 1;
		КонецЕсли;
		
		Пользователь = спПараметры.Получить("Пользователь");
		Если ПустоеЗначение(Пользователь)=1 Тогда
			Пользователь = 1;
		КонецЕсли;
		
		Пароль = спПараметры.Получить("Пароль");
		Если ПустоеЗначение(Пароль)=1 Тогда
			Пароль = 0;
		КонецЕсли;
		
		Сервер = спПараметры.Получить("Сервер");
		
		Соединение = CreateObject("ADODB.Connection");
		стрСоединения = "Provider=EQL OLE DB Provider;Data Source="+НомерКассы+";User Id="+Пользователь+";Password="+Пароль+";";
		Если ПустоеЗначение(Сервер)=0 Тогда
		    стрСоединения = стрСоединения + "Location="+Сервер+";";
		КонецЕсли;
		Соединение.Open(стрСоединения);
		
		ТаблицаТоваров = CreateObject("ADODB.Recordset");
		ТаблицаТоваров.Open("PLU", Соединение, -1, 4, 512);
		
		Для Инд=1 по СписокТоваров.РазмерСписка() Цикл
		
			Товар =СписокТоваров.ПолучитьЗначение(Инд);
			
			ТаблицаТоваров.Find("Code="+Товар.Код,0,1);
			
			Если ТаблицаТоваров.EOF = -1 Тогда
				// добавим новый товар
				ТаблицаТоваров.AddNew();
				ТаблицаТоваров.Fields("Code").Value = Товар.Код;
				глКомментарий("Добавили новый "+Товар,4);
			КонецЕсли;
			// заполним данные
			ТаблицаТоваров.Fields("Name").Value = СокрЛП(Товар.Наименование);
			Цена = глВернутьЦену(Товар,Константа.РозничнаяКатегорияЦен);
			Если ПустоеЗначение(Цена) = 1 Тогда
				// у товара нет розничной цены - выгружать не будем, сообщим пользователю
				глКомментарий("Не найдена розничная цена у товара "+Товар+". Товар не выгружен.",0);
				Продолжить;
			КонецЕсли;
			ЦенаЦены = Цена.Цена.Получить(РабочаяДата());
			ТаблицаТоваров.Fields("Cen").Value = ЦенаЦены;
			ТаблицаТоваров.Fields("Dep").Value = 1;
			ТаблицаТоваров.Fields("Grp").Value = 1;
			ТаблицаТоваров.Fields("Tax").Value = НалоговаяГруппа(Товар.СтавкаНДС.Получить(РабочаяДата()));
			ТаблицаТоваров.Fields("Kol").Value = 0;
			ТаблицаТоваров.Fields("Flg").Value = 1;
			глКомментарий("Изменены данные для товара "+Товар,4);
		КонецЦикла;
		ТаблицаТоваров.UpdateBatch();
		ТаблицаТоваров.Close();
		Соединение.Close();
	Исключение
		глКомментарий("Ошибка ЭККА :"+ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры


//===========================================            
Процедура ПриОткрытии()
	Параметр = глРасшифровка.Получить("Парам"); 
	Если Параметр = "Инициализация" тогда
		Инициализация();   
	ИначеЕсли Параметр = "ПодключитьДрайвер" тогда	
		ИнициализацияДрайвера();
	ИначеЕсли Параметр = "ВыгрузитьАртикулы" тогда	
		ВыгрузитьАртикулы();
	ИначеЕсли Параметр = "ПечатьЧека" тогда	
		ПечатьЧека();
	ИначеЕсли Параметр = "Отчет" тогда	
		Отчет();
	КонецЕсли;	
	СтатусВозврата(0);
КонецПроцедуры	

НазваниеДрайвера = "HelpCo";
