Перем ИсторияПереходов;
Перем ИндИстории;
Перем ПервыйЗапуск;
Перем ПоказПриЗапуске;
Перем ТекущаяСтраница;
Перем МенюСодержание;                       
Перем ЧислоРазделов;

// ===============================
Процедура ПоказатьСтраницу(ИмяСтраницы)
	Таблица.Очистить();
	Таблица.ИсходнаяТаблица("Основная");
	Таблица.ВывестиСекцию("Заголовок");
	Если ПустаяСтрока(ИмяСтраницы) = 0 Тогда
		Таблица.ИсходнаяТаблица(ИмяСтраницы);
		ТекущаяСтраница = ИмяСтраницы;		
	Иначе
		Таблица.ИсходнаяТаблица(ТекущаяСтраница);
	КонецЕсли;
	Если ТекущаяСтраница = "Содержание" Тогда
		Таблица.ВывестиСекцию("Страница<");
    	Если ПервыйЗапуск=1 Тогда
			Таблица.ВывестиСекцию("Текст");
		Иначе
			Таблица.ВывестиСекцию("Ссылка");
		КонецЕсли;
		Таблица.ВывестиСекцию("Страница>");
	Иначе
		Таблица.ВывестиСекцию("Страница");
	КонецЕсли;
	Таблица.Опции(0,0,1,0,,"Старт");
	Таблица.Защита(1);
	Таблица.ТолькоПросмотр();
	Таблица.Показать();
КонецПроцедуры

// ===============================
Функция СчетаПоРазделам(Раздел)
	СписокСчетов = СоздатьОбъект("СписокЗначений");
	Если Раздел = "НеоборотныеАктивы" Тогда
	    СписокСчетов.ДобавитьЗначение("10");
	    СписокСчетов.ДобавитьЗначение("11");
	    СписокСчетов.ДобавитьЗначение("12");
		СписокСчетов.ДобавитьЗначение("13");
	КонецЕсли;
	Возврат СписокСчетов;
КонецФункции

// ===============================
// Вызов отчета "Оборотно-сальдовая ведомость".
Процедура ОСВ(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомость");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());

	глТаблица = "";
	глФлагРасшифровки = 1;
	глОбновить = 0;
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомость");
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура ОСВпоСчету(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());

	Расшифровка.Установить("ОтборСубконто1", 1);
	Расшифровка.Установить("ПоГруппам1", 1);
	Расшифровка.Установить("ОтборСубконто2", 1);
	Расшифровка.Установить("ПоГруппам2", 1);
	Расшифровка.Установить("ДанныеПоСубсчетам", 1);
	глОбновить = 1;

	Если Вид = "Общая" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("10"));
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.НеоборотныеАктивы);
		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.МестаХранения);
		глОбновить = 0;
	ИначеЕсли Вид = "Банк" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("311"));
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.НашиДенежныеСчета);
	ИначеЕсли Вид = "Валюта" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("312"));
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.НашиДенежныеСчета);
	ИначеЕсли Вид = "Подотчет" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("3721"));
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Сотрудники);
	ИначеЕсли Вид = "Необоротные" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("10"));
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.НеоборотныеАктивы);
	КонецЕсли;

	глТаблица = "";
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 0;
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура АнализСчета(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "АнализСчета");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Если Вид = "Банк" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("311"));
	ИначеЕсли Вид = "Валюта" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("312"));
	ИначеЕсли Вид = "Касса" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("30"));
	КонецЕсли;
	глТаблица = "";
	глОбновить = 1;
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.АнализСчета");
	глОбновить = 0;
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура АнализСчетаПоДатам(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "АнализСчетаПоДатам");
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Если Вид = "Касса" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("30"));
    КонецЕсли;
	глТаблица = "";
	глОбновить = 0;
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.АнализСчетаПоДатам");
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура КарточкаСчета(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Если Вид = "Банк" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("311"));
	ИначеЕсли Вид = "Валюта" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("312"));
	ИначеЕсли Вид = "Касса" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("30"));
	ИначеЕсли Вид = "Подотчет" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("372"));
	КонецЕсли;
	глТаблица = "";
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 1;
	ОткрытьФорму("Отчет.КарточкаСчета");
	глОбновить = 0;
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура ЖурналОрдерПоСубконто(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ЖурналОрдер");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Расшифровка.Установить("ДанныеПоСубсчетам", 1);
	Расшифровка.Установить("ОстНачД", 1);
	Расшифровка.Установить("ОстНачК", 1);
	Расшифровка.Установить("ОбД", 1);
	Расшифровка.Установить("ОбК", 1);
	Расшифровка.Установить("КорОбД", 1);
	Расшифровка.Установить("КорОбК", 1);
	Расшифровка.Установить("ОстКонД", 1);
	Расшифровка.Установить("ОстКонК", 1);
	Расшифровка.Установить("ВидПериода", 2);
	
	глТаблица = "";
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 1;
	ОткрытьФорму("Отчет.ЖурналОрдерПоСубконто");
	глОбновить = 0;
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура АнализСчетаПоСубконто(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "АнализСчетаПоСубконто");
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Если Вид = "Общая" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("311"));
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.НашиДенежныеСчета);
		Расшифровка.Установить("ОтборСубконто1", 1 );
		Расшифровка.Установить("ПоГруппам1", 1 );
	ИначеЕсли Вид = "Зарплата" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("66"));
   		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Сотрудники);
		Расшифровка.Установить("ОтборСубконто1", 1 );
		Расшифровка.Установить("ПоГруппам1", 1 );
   		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.МесяцНачисленияЗП);
		Расшифровка.Установить("ОтборСубконто2", 1 );
		Расшифровка.Установить("ПоГруппам2", 1 );
	ИначеЕсли Вид = "НалоговыйУчет" Тогда
		Расшифровка.Установить("Счет", СчетПоКоду("6442"));
   		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Контрагенты);
		Расшифровка.Установить("ОтборСубконто1", 1 );
		Расшифровка.Установить("ПоГруппам1", 1 );
   		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.Договора);
		Расшифровка.Установить("ОтборСубконто2", 1 );
		Расшифровка.Установить("ПоГруппам2", 1 );
    КонецЕсли;
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 1;
	ОткрытьФорму("Отчет.АнализСчетаПоСубконто");
	глОбновить = 0;
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура АнализСубконто(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "АнализСубконто");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Расшифровка.Установить("ВидСубконто1", ВидыСубконто.ЗначениеПоИдентификатору(Вид));
	Расшифровка.Установить("ОтборСубконто1", 1);
	Расшифровка.Установить("ПоГруппам1", 1);
	Расшифровка.Установить("ДанныеПоСубсчетам", 1);
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 0;
	ОткрытьФорму("Отчет.АнализСубконто");
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура КарточкаСубконто(Вид)
	Перем Фрм;
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "КарточкаСубконто");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Расшифровка.Установить("ВидСубконто1", ВидыСубконто.ЗначениеПоИдентификатору(Вид));
	Расшифровка.Установить("ОтборСубконто1", 1);
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 2;
	ОткрытьФорму("Отчет.КарточкаСубконто",Фрм);
	Фрм.Активизировать("ВыбСубконто1");
	глФлагРасшифровки = 0;
	глОбновить = 0;
КонецПроцедуры

// ===============================
Процедура ОборотыМеждуСубконто(Вид)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ОборотыМеждуСубконто");
	Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
	Расшифровка.Установить("РазделительУчета", глБИ.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());

	Расшифровка.Установить("ОтборСубконто1", 1);
	Расшифровка.Установить("ОтборСубконто2", 3);
	Расшифровка.Установить("ОтборСубконто3", 3);
	Расшифровка.Установить("ОтборКорСубконто1", 1);
	Расшифровка.Установить("ОтборКорСубконто2", 3);
	Расшифровка.Установить("ОтборКорСубконто3", 3);

	глТаблица="";
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 1;
	ОткрытьФорму("Отчет.ОборотыМеждуСубконто");
	глОбновить = 0;
	глФлагРасшифровки = 0;
КонецПроцедуры

// ===============================
Процедура ОтчетПоПроводкам(Раздел)
	СписокСчетовРаздела = СчетаПоРазделам(Раздел);
	Фильтр = "";
	Для Инд = 1 По СписокСчетовРаздела.РазмерСписка() Цикл
		Фильтр = Фильтр+СписокСчетовРаздела.ПолучитьЗначение(Инд)+";";
	КонецЦикла;
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
	Расшифровка.Установить("Обновить", 0);
	Расшифровка.Установить("Дата1", НачалоПериодаБИ());
	Расшифровка.Установить("Дата2", КонецПериодаБИ());
	Расшифровка.Установить("Фильтр", Фильтр);
	Расшифровка.Установить("ИспФильтр",1);
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 0;
	ОткрытьФорму("Отчет.ОтчетПоПроводкам");
	глФлагРасшифровки = 0;
КонецПроцедуры
                                                    
// ===============================
Процедура ОбработкаЯчТаблицы(Зн)
	Команда=Лев(Зн,3);
	Параметр=Сред(Зн,5,СтрДлина(Зн)-4);    
	Если Команда = "Таб" Тогда
		ПоказатьСтраницу(Параметр);
		Если Параметр <> ?(ИсторияПереходов.РазмерСписка()=ИндИстории,"",ИсторияПереходов.ПолучитьЗначение(ИндИстории+1)) Тогда
			Если ИсторияПереходов.РазмерСписка() > ИндИстории Тогда
				ИсторияПереходов.УдалитьЗначение(ИндИстории+1, ИсторияПереходов.РазмерСписка()-ИндИстории);
			КонецЕсли;
			ИсторияПереходов.ДобавитьЗначение(Параметр);
		КонецЕсли;
		ИндИстории = ИндИстории+1;
	ИначеЕсли Команда = "Фрм" Тогда
		Инд = Найти(Параметр,".");
		ТипПараметра = Сред(Параметр,1,Инд-1);
		Если НРег(ТипПараметра) = "справочник" Тогда
			ВидСправочника = Сред(Параметр,Инд+1);
			Владелец = Метаданные.Справочник(ВидСправочника).Владелец;
			Если Владелец.Выбран() = 1 Тогда
			    // предложим выбрать владельца
				спрВлад = СоздатьОбъект("Справочник."+Владелец.Идентификатор);
				Если спрВлад.Выбрать("Выберите владельца",Метаданные.Справочник(Владелец.Идентификатор).ОсновнаяФормаДляВыбора) = 1 Тогда
					КонтФормы = "";
				    ОткрытьФорму(Параметр,КонтФормы);
					КонтФормы.ИспользоватьВладельца(спрВлад.ТекущийЭлемент());
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;			
		ОткрытьФорму(Параметр);
	ИначеЕсли Команда = "Мод" Тогда
		ОткрытьФормуМодально(Параметр);      
	ИначеЕсли Команда = "Дос" Тогда
		// ввод документа на основании
		Инд = Найти(Параметр,",");
		Если Инд = 0 Тогда
		    Возврат;
		КонецЕсли;
		НазвДокумента = Сред(Параметр,1,Инд-1);
		НазвОснования = Сред(Параметр,Инд+1);
		Если НазвОснования = "Быстрая продажа" Тогда
			ОткрытьФорму("Документ."+НазвДокумента,НазвОснования);                       
			Возврат;
		КонецЕсли;	
		Осн = СоздатьОбъект("Документ");
		Осн.ВидыДляВыбора(НазвОснования);
		Если Осн.Выбрать("Выберите документ, на основании которого будем вводить") = 0 Тогда
		    Возврат;
		КонецЕсли;
		ОткрытьФорму("Документ."+НазвДокумента,,Осн.ТекущийДокумент());
	ИначеЕсли Команда = "ТКС" Тогда
		Предупреждение(Параметр);
	ИначеЕсли Команда = "Наз" Тогда
		Если ИндИстории > 1 Тогда
			ИндИстории = ИндИстории-1;
			ПоказатьСтраницу(ИсторияПереходов.ПолучитьЗначение(ИндИстории));
		КонецЕсли;
	ИначеЕсли Команда = "Дал" Тогда
		Если ИндИстории <> 0 Тогда
			Если ИндИстории < ИсторияПереходов.РазмерСписка() Тогда
				ИндИстории = ИндИстории+1;
				ПоказатьСтраницу(ИсторияПереходов.ПолучитьЗначение(ИндИстории));
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Команда = "Реж" Тогда
		ПоказПриЗапуске = ?(ПоказПриЗапуске=1,-1,1);
		ПоказатьСтраницу("");
	ИначеЕсли Команда = "ОСВ" Тогда

		// Вызов стандартного отчета "Оборотно-сальдовая ведомость"
		ОСВ(Параметр);

	ИначеЕсли Команда = "ОСС" Тогда

		// Вызов стандартного отчета "Оборотно-сальдовая ведомость по счету"
		ОСВпоСчету(Параметр);

	ИначеЕсли Команда = "ЖОВ" Тогда

		// Вызов стандартного отчета "Журнал-ордер и Ведомость по счету"
		ЖурналОрдерПоСубконто(Параметр);

	ИначеЕсли Команда = "АСЧ" Тогда

		// Вызов стандартного отчета "Анализ счета"
		АнализСчета(Параметр);

	ИначеЕсли Команда = "АСД" Тогда

		// Вызов стандартного отчета "Анализ счета по датам"
		АнализСчетаПоДатам(Параметр);

	ИначеЕсли Команда = "АСС" Тогда

		// Вызов стандартного отчета "Анализ счета по субконто"
		АнализСчетаПоСубконто(Параметр);

	ИначеЕсли Команда = "КСЧ" Тогда

		// Вызов стандартного отчета "Карточка счета"
		КарточкаСчета(Параметр);

	ИначеЕсли Команда = "АСБ" Тогда

		// Вызов стандартного отчета "Анализ субконто"
		АнализСубконто(Параметр);

	ИначеЕсли Команда = "КСБ" Тогда

		// Вызов стандартного отчета "Карточка субконто"
		КарточкаСубконто(Параметр);

	ИначеЕсли Команда = "ОБС" Тогда

		// Вызов стандартного отчета "Карточка субконто"
		ОборотыМеждуСубконто(Параметр);

	ИначеЕсли Команда = "ОПП" Тогда

		// Вызов стандартного отчета "Отчет по проводкам"
		ОтчетПоПроводкам(Параметр);

	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ОбработкаЯчейкиТаблицы(Зн,Фл)          
	Перем СтрокаЗначения;   
	СтрокаЗначения = "";
	Команда = Лев(Зн,3);
	Параметр = Сред(Зн,5,СтрДлина(Зн)-4);    
	Если Лев(Зн,2)="Сп" Тогда
		// Вызов списка 
		ПунктМеню="";
		Меню = СоздатьОбъект("СписокЗначений");
		Если Лев(Зн,3)="СпС" Тогда
			// Вызов списка "Содержание"
			МенюСодержание.Выгрузить(Меню);
		ИначеЕсли Лев(Зн,3)="СпН" Тогда               
			Для Н=1 По ИндИстории-1 Цикл
				Значение = "Таб_"+ИсторияПереходов.ПолучитьЗначение(ИндИстории-Н);                      
				Если МенюСодержание.НайтиЗначение(Значение) <>0 Тогда
					МенюСодержание.ПолучитьЗначение(МенюСодержание.НайтиЗначение(Значение),СтрокаЗначения);
					Меню.ДобавитьЗначение(Значение,СтрокаЗначения);
				КонецЕсли;	
			КонецЦикла;
		ИначеЕсли Лев(Зн,3)="СпД" Тогда
			Для Н=ИндИстории+1 По ИсторияПереходов.РазмерСписка() Цикл
				Значение = "Таб_"+ИсторияПереходов.ПолучитьЗначение(Н);  
				Если МенюСодержание.НайтиЗначение(Значение) <>0 Тогда
					МенюСодержание.ПолучитьЗначение(МенюСодержание.НайтиЗначение(Значение),СтрокаЗначения);
					Меню.ДобавитьЗначение(Значение,СтрокаЗначения);
				КонецЕсли;	
			КонецЦикла;
			
		КонецЕсли;
		Если Меню.ВыбратьЗначение(ПунктМеню,,,,1) = 1 Тогда
			Если (Лев(Зн,3)="СпН") ИЛИ (Лев(Зн,3)="СпД" ) Тогда   
				ИндИстории = ИсторияПереходов.НайтиЗначение(Сред(ПунктМеню,5,СтрДлина(ПунктМеню)))-1;
			КонецЕсли;
			Если Лев(Зн,3)="СпС" Тогда
				Если ИндИстории<>0 Тогда
					Если Сред(ПунктМеню,5,СтрДлина(ПунктМеню)) <> ИсторияПереходов.ПолучитьЗначение(ИндИстории) Тогда
						ОбработкаЯчТаблицы(ПунктМеню);
					КонецЕсли;
				Иначе
					ОбработкаЯчТаблицы(ПунктМеню);
				КонецЕсли;
			Иначе
				ОбработкаЯчТаблицы(ПунктМеню);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ОбработкаЯчТаблицы(Зн);
	КонецЕсли;        
	Фл = 0;
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()                                                 
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Содержание");
	Секц = Таб.ПолучитьСекцию("Страница");
	МенюСодержание.ДобавитьЗначение("Таб_Содержание","Содержание");
	Для Н=1 По ЧислоРазделов Цикл
		Обл = Секц.Область("П"+Формат(Н,"Ч(0)3"));
		МенюСодержание.ДобавитьЗначение(Обл.Расшифровка(),Обл.Текст);
	КонецЦикла;
	
	ПервыйЗапуск = ВосстановитьЗначение("Первый запуск");
	ПоказПриЗапуске = ВосстановитьЗначение("ПоказПутеводителяПриЗапуске");
	Если ПустоеЗначение(ПервыйЗапуск) = 1 Тогда
		ПервыйЗапуск = 1;
	КонецЕсли;
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		СтартоваяСтраница = Форма.Параметр;
	Иначе
		СтартоваяСтраница = "Содержание";
	КонецЕсли;
	ПоказатьСтраницу(СтартоваяСтраница);
	ИсторияПереходов.ДобавитьЗначение(СтартоваяСтраница);
	ИндИстории = 1;
КонецПроцедуры

// ===============================
Процедура ПриЗакрытии()
	СохранитьЗначение("Первый запуск", 2);
	СохранитьЗначение("ПоказПутеводителяПриЗапуске",ПоказПриЗапуске);
КонецПроцедуры

ИндИстории = 0;
ИсторияПереходов = СоздатьОбъект("СписокЗначений");      
МенюСодержание = СоздатьОбъект("СписокЗначений");
ЧислоРазделов = 21;