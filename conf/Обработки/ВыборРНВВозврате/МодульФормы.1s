Перем ТипДок;
Перем ВыбДок;

Функция ПолучитьТБВозвраты(ДокРН, КонтролироватьВозврат)
	Спис = СоздатьОбъект("СписокЗначений");
	Спис.ДобавитьЗначение(0, "КвоВозврата");
	Спис.ДобавитьЗначение(0, "СуммаВозврата");		

	Если КонтролироватьВозврат = 1 Тогда
		Док = СоздатьОбъект("Документ");
		Док.УстановитьФильтр(1,0);
		Док.ВыбратьПодчиненныеДокументы(,, ДокРН);
		Пока Док.ПолучитьДокумент() = 1 Цикл
			Если (Док.Вид() = "ВозвратнаяНакладная") И (Док.ТекущийДокумент() <> ВыбДок) Тогда
				Док.ВыбратьСтроки();
				Пока Док.ПолучитьСтроку() = 1 Цикл
					Если (Док.ТМЦ = ВыбТМЦ) И (Док.ДокПродажи = ДокРН) Тогда
						Если ПустоеЗначение(ВыбВУП) = 0 Тогда
						    ВУП = Док.ВУП;
							Если ПустоеЗначение(ВУП) = 1 Тогда
							    ВУП = НетУп;
							КонецЕсли;
							Если ВУП <> ВыбВУП Тогда
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						
						Спис.Установить("КвоВозврата", Спис.Получить("КвоВозврата") + Док.Кво);
						Спис.Установить("СуммаВозврата", Спис.Получить("СуммаВозврата") + Док.СуммаСНДС);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Спис;
КонецФункции

Процедура ДобавитьСтроку(ДокРН, Кво, Сумма, КвоВ = 0, СуммаВ = 0, Цена = 0)
	ТЗ.НоваяСтрока();
	ТЗ.П = ?(ДокРН.ПометкаУдаления() = 1, 1, ?(ДокРН.Проведен() = 1,3,2));
	ТЗ.РН = ДокРН.ТекущийДокумент();
	ТЗ.Номер = ДокРН.НомерДок;
	ТЗ.Дата = ДокРН.ДатаДок;
	ТЗ.Сумма = Сумма;
	ТЗ.Кво = Кво;
	ТЗ.КвоВ = КвоВ;
	ТЗ.КвоДост = Кво - КвоВ;
	ТЗ.Цена = Цена;
КонецПроцедуры

//*******************************************
Процедура УстОтбор()
	ДокРН = СоздатьОбъект("Документ");
	ДокРН.УстановитьФильтр(1,0);
	Форма.ТЗ.Видимость(0);
	КонтролироватьВозврат = ВыбКонтрагент.КонтролироватьВозврат;
	Если ТипДок = "РасходнаяНакладная" Тогда
		Если ВыбКонтрагент.Выбран() = 0 Тогда
			Предупреждение("Нужно выбрать контрагента");
			Возврат;
		КонецЕсли;
		
		ТЗ.УдалитьСтроки();
		ДокРН.ВыбратьПоЗначению(НачДата, КонДата, "РНКонтрагентов", ВыбКонтрагент);
		Пока ДокРН.ПолучитьДокумент() = 1 Цикл
			Если ПустоеЗначение(ВыбДоговор) = 0 Тогда
			    Если ДокРН.Договор <> ВыбДоговор Тогда
					Продолжить;
			    КонецЕсли;
			КонецЕсли;
			Если ПустоеЗначение(ВыбТМЦ) = 1 Тогда
			    ДобавитьСтроку(ДокРН.ТекущийДокумент(), ДокРН.Итог("Кво"), ДокРН.Итог("СуммаСНДС"));
			Иначе				
				ДокРН.ВыбратьСтроки();
				Пока ДокРН.ПолучитьСтроку() = 1 Цикл
					Если ДокРН.ТМЦ = ВыбТМЦ Тогда
						Если ПустоеЗначение(ВыбВУП) = 0 Тогда
						    ВУП = ДокРН.ВидУпаковки;
							Если ПустоеЗначение(ВУП) = 1 Тогда
							    ВУП = НетУп;
							КонецЕсли;
							Если ВУП <> ВыбВУП Тогда
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						СписВозвраты = ПолучитьТБВозвраты(ДокРН.ТекущийДокумент(), КонтролироватьВозврат);
					    ДобавитьСтроку(ДокРН.ТекущийДокумент(),ДокРН.Кво, ДокРН.СуммаСНДС, СписВозвраты.Получить("КвоВозврата"), 
							СписВозвраты.Получить("СуммаВозврата"), ДокРН.ЦенаСНДС);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ТЗ.УдалитьСтроки();
		ДокРН.ВыбратьПоЗначению(НачДата, КонДата, "ТМЦ", ВыбТМЦ);
		Пока ДокРН.ПолучитьДокумент() = 1 Цикл
			ДокРН.ВыбратьСтроки();
			Пока ДокРН.ПолучитьСтроку() = 1 Цикл
				Если ДокРН.ТМЦ = ВыбТМЦ Тогда
				    ДобавитьСтроку(ДокРН.ТекущийДокумент(),ДокРН.Кво, ДокРН.СуммаСНДС, ,, ДокРН.ЦенаСНДС);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;	
	КонецЕсли;
	
	Форма.ТЗ.Видимость(1);
	Если ТЗ.КоличествоСтрок() > 0 Тогда
	    ТЗ.ТекущаяСтрока(ТЗ.КоличествоСтрок());
	КонецЕсли;	
КонецПроцедуры

Процедура Открыть()
	ТекСтр = ТЗ.ТекущаяСтрока();
	Если ТекСтр = 0 Тогда
		Возврат;
	Иначе
		ОткрытьФорму(ТЗ.ПолучитьЗначение(ТекСтр, "РН"));
	КонецЕсли;
КонецПроцедуры

Процедура Выбор()
	ТекСтр = ТЗ.ТекущаяСтрока();
	Если ТекСтр = 0 Тогда
		Возврат;
	Иначе
		Форма.Параметр = СоздатьОбъект("СписокЗначений");
		Форма.Параметр.Установить("РН", ТЗ.ПолучитьЗначение(ТекСтр, "РН"));
		Форма.Параметр.Установить("ВУП", ВыбВУП);
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
		ВыбКонтрагент = Форма.Параметр.Получить("Контрагент");
		ВыбДоговор = Форма.Параметр.Получить("Договор");
		ВыбТМЦ = Форма.Параметр.Получить("ТМЦ");
		ВыбДок = Форма.Параметр.Получить("ДокВозврата");
		ВыбВУП = Форма.Параметр.Получить("ВУП");
		Форма.ВыбКонтрагент.Доступность(0);
		Форма.ВыбТМЦ.Доступность(0);
		Если Форма.Параметр.Получить("ПН") = 1 Тогда
			ТипДок = "ПриходнаяНакладнаяЗапасы";
			Форма.ВыбКонтрагент.Видимость(0);
			Форма.тКонтрагент.Видимость(0);
			Форма.ВыбВУП.Видимость(0);
			Форма.тВУП.Видимость(0);
		Иначе
			ТипДок = "РасходнаяНакладная";
		КонецЕсли;
		ТЗ.ВидимостьКолонки("КвоВ", ВыбКонтрагент.КонтролироватьВозврат);
		ТЗ.ВидимостьКолонки("КвоДост", ВыбКонтрагент.КонтролироватьВозврат);		
		УстОтбор();
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьДату(Интервал, Конец = 0)
	Если ТипЗначенияСтр(Интервал) = "Дата" Тогда
		Возврат Интервал;
	Иначе
		Если Интервал = "День" Тогда
			Возврат ТекущаяДата();
		ИначеЕсли Интервал = "Месяц" Тогда
			Возврат ?(Конец = 0, НачМесяца(ТекущаяДата()), КонМесяца(ТекущаяДата()));
		ИначеЕсли Интервал = "Квартал" Тогда
			Возврат ?(Конец = 0, НачКвартала(ТекущаяДата()), КонКвартала(ТекущаяДата()));
		Иначе
			Возврат ?(Конец = 0, НачГода(ТекущаяДата()), КонГода(ТекущаяДата()));
		КонецЕсли;
	КонецЕсли;
КонецФункции

Процедура ПриНачалеВыбораЗначения(Идент, Флаг)
	Если Идент = "ВыбДоговор" Тогда
	    СписД = СоздатьОбъект("СписокЗначений");
		Док = СоздатьОбъект("Документ");
		Док.ВыбратьПоЗначению(,,"ДоговораКонтрагентов", ВыбКонтрагент);
		Пока Док.ПолучитьДокумент() = 1 Цикл
			Если Док.Вид() = "Договор" Тогда
			    Если Док.ПометкаУдаления() = 0 Тогда
			        СписД.ДобавитьЗначение(Док.ТекущийДокумент(), Док.НомерДок + " от " + Строка(Док.ДатаДок) + " / " + глАдресСтрокой(Док.Адрес));
			    КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если СписД.РазмерСписка() = 0 Тогда
		    Возврат;
		КонецЕсли;
		Флаг = 0;
		Если СписД.ВыбратьЗначение(ВыбДоговор,,,,2) = 1 Тогда
		    УстОтбор();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

ТЗ.НоваяКолонка("П", "Число", 1,, "", 2);
ТЗ.НоваяКолонка("РН", "Документ");
ТЗ.НоваяКолонка("Номер", "Строка",,,"Номер", 8);
ТЗ.НоваяКолонка("Дата", "Дата",,,"Дата", 7);
ТЗ.НоваяКолонка("Сумма", "Число", 12, 2,"Сумма", 7);
ТЗ.НоваяКолонка("Кво", "Число", 15, 3,"К-во прод.", 7);
ТЗ.НоваяКолонка("КвоВ", "Число", 15, 3,"К-во возвр.", 7);
ТЗ.НоваяКолонка("КвоДост", "Число", 15, 3,"К-во дост.", 7);
ТЗ.НоваяКолонка("Цена", "Число", 12, 2,"Цена", 7);
ТЗ.ВидимостьКолонки("РН", 0);
ТЗ.ВыводитьПиктограммы("П", 1);

НачДата = ПолучитьДату(НачалоСтандартногоИнтервала());
КонДата = ПолучитьДату(КонецСтандартногоИнтервала(), 1);
ВыбВУП = НетУп;