Перем КоррПров;
Перем Текст;

// ===============================
Функция ИмяФайлаПереноса()
	// если загрузка из внешней обработки, то ее имя должно совпадать
	// с именем файла коррекных проводок, а расширение - txt
	ФайлОбработки = РасположениеФайла();
	Если ПустоеЗначение(ФайлОбработки) = 0 Тогда
		ИмяФайлаПер = Лев(ФайлОбработки,СтрДлина(ФайлОбработки)-3)+"txt";
	ИначеЕсли ПустоеЗначение(ВосстановитьЗначение("ПереносКорректныхПроводок_ИмяФайла")) = 0 Тогда
		ИмяФайлаПер = ВосстановитьЗначение("ПереносКорректныхПроводок_ИмяФайла");
	Иначе
		ИмяФайлаПер = КаталогИБ()+"ExtForms\1sbcorr.txt";
	КонецЕсли;
	
	Возврат ИмяФайлаПер;
КонецФункции //ИмяФайлаПереноса

// ===============================
Процедура ПросмотрПроводок()
	Если ФС.СуществуетФайл(ИмяФайла) = 0 Тогда
	    Предупреждение("Неверное имя файла.
						|"+ИмяФайла);
		Возврат;
	КонецЕсли;
	
	СписокРеквизитов = СоздатьОбъект("СписокЗначений");
	Таб = СоздатьОбъект("Таблица");
    Таб.ВывестиСекцию("Шапка");
	
	Текст.Открыть(ИмяФайла);
	Для НомерСтроки = 1 По Текст.КоличествоСтрок() Цикл
		Стр = Текст.ПолучитьСтроку(НомерСтроки);
		СписокРеквизитов.ИзСтрокиСРазделителями(Стр);
		СчетДт = "";
		Если СписокРеквизитов.РазмерСписка() > 0 Тогда
			СчетДт = СписокРеквизитов.ПолучитьЗначение(1);
		КонецЕсли;
		СчетКт = "";
		Если СписокРеквизитов.РазмерСписка() > 1 Тогда
			СчетКт = СписокРеквизитов.ПолучитьЗначение(2);
		КонецЕсли;
		Комментарий = "";
		Если СписокРеквизитов.РазмерСписка() > 2 Тогда
			Комментарий = СписокРеквизитов.ПолучитьЗначение(3);
		КонецЕсли;

		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	Таб.ВывестиСекцию("Подвал");
	Таб.ОбластьПечати(1,2);
	Таб.Опции(0,0,4,0,"ОпцииПечатиКоррПров","ПараметрыОкнаКоррПров");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Список корректных проводок","");
КонецПроцедуры //ПросмотрПроводок

// ===============================
Процедура ОткрФайл()
	Если ФС.СуществуетФайл(ИмяФайла) = 0 Тогда
	    Предупреждение("Неверное имя файла.
						|"+ИмяФайла);
	Иначе
		Текст.Открыть(ИмяФайла);
		Текст.Показать("Файл переноса корректных проводок", ИмяФайла);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура Загрузить()
	Если ФС.СуществуетФайл(ИмяФайла) = 0 Тогда
		Если Форма.Параметр <> 1 Тогда
			Предупреждение("Неверное имя файла.
							|"+ИмяФайла);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	СохранитьЗначение("ПереносКорректныхПроводок_ИмяФайла", ИмяФайла);
	Если РежимЗагрузки = 2 Тогда
		КоррПров.ВыбратьКорректныеПроводки();
		Пока КоррПров.Выбрана() = 1 Цикл
			КоррПров.Удалить();
		КонецЦикла;
	КонецЕсли;
	СчетчикПроводок = 0;
	Состояние("Загрузка корректных проводок.");
	СписокРеквизитов = СоздатьОбъект("СписокЗначений");
	Текст.Открыть(ИмяФайла);
	Для НомерСтроки = 1 По Текст.КоличествоСтрок() Цикл
		Стр = Текст.ПолучитьСтроку(НомерСтроки);
		СписокРеквизитов.ИзСтрокиСРазделителями(Стр);
		СчетДт = "";
		Если СписокРеквизитов.РазмерСписка() > 0 Тогда
			СчетДт = СписокРеквизитов.ПолучитьЗначение(1);
		КонецЕсли;
		СчетКт = "";
		Если СписокРеквизитов.РазмерСписка() > 1 Тогда
			СчетКт = СписокРеквизитов.ПолучитьЗначение(2);
		КонецЕсли;
		Комментарий = "";
		Если СписокРеквизитов.РазмерСписка() > 2 Тогда
			Комментарий = СписокРеквизитов.ПолучитьЗначение(3);
		КонецЕсли;
		
		Если РежимЗагрузки = 1 Тогда
			ТакаяПроводкаЕсть = 0;
			КоррПров.ВыбратьКорректныеПроводкиПоСчету(СчетДт,0,ПлСчетов);
			Пока (КоррПров.ПолучитьКорректнуюПроводку() = 1) и (ТакаяПроводкаЕсть = 0) Цикл
				Если КоррПров.СчетКт.Код = СчетКт Тогда
					ТакаяПроводкаЕсть = 1;
				КонецЕсли;
			КонецЦикла;
			Если ТакаяПроводкаЕсть = 1 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		КоррПров.Новая();
		КоррПров.СчетДт = СчетПоКоду(СчетДт, ПлСчетов);
		КоррПров.СчетКт = СчетПоКоду(СчетКт, ПлСчетов);
		КоррПров.Комментарий = Комментарий;
		КоррПров.Записать();
		СчетчикПроводок = СчетчикПроводок + 1;
		Если (СчетчикПроводок/10) = Цел(СчетчикПроводок/10) Тогда
			Состояние("Загрузка корректных проводок. Загружено: "+СчетчикПроводок+" проводок.");
		КонецЕсли;
	КонецЦикла;
	Состояние("Загрузка корректных проводок. Загружено: "+СчетчикПроводок+" проводок.");
	Если Форма.Параметр <> 1 Тогда
		Предупреждение("Загружено "+СчетчикПроводок+" корректных проводок.");
	КонецЕсли;
КонецПроцедуры //Загрузить

// ===============================
Процедура Выгрузить()
	Если ФС.СуществуетФайл(ИмяФайла) = 1 Тогда
		Если Вопрос("Файл "+ИмяФайла+" существует!
					|Переписать?","Да+Нет") = "Нет" Тогда
			Возврат;
		Иначе
			Текст.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	СохранитьЗначение("ПереносКорректныхПроводок_ИмяФайла", ИмяФайла);
	СчетчикПроводок = 0;
	Состояние("Выгрузка корректных проводок.");
	СписокРеквизитов = СоздатьОбъект("СписокЗначений");
	КоррПров.ВыбратьКорректныеПроводки();
	Пока КоррПров.ПолучитьКорректнуюПроводку() = 1 Цикл
		СписокРеквизитов.УдалитьВсе();
		СписокРеквизитов.ДобавитьЗначение(КоррПров.СчетДт.Код);
		СписокРеквизитов.ДобавитьЗначение(КоррПров.СчетКт.Код);
		СписокРеквизитов.ДобавитьЗначение(СокрП(КоррПров.Комментарий));
        Стр = СписокРеквизитов.ВСтрокуСРазделителями();
		
		Текст.ДобавитьСтроку(Стр);
		СчетчикПроводок = СчетчикПроводок + 1;
		Если (СчетчикПроводок/10) = Цел(СчетчикПроводок/10) Тогда
			Состояние("Выгрузка корректных проводок. Выгружено: "+СчетчикПроводок+" проводок.");
		КонецЕсли;
	КонецЦикла;
	Состояние("Выгрузка корректных проводок. Выгружено: "+СчетчикПроводок+" проводок.");
	Текст.Записать(ИмяФайла);
	Предупреждение("Выгружено "+СчетчикПроводок+" корректных проводок.");
КонецПроцедуры //Выгрузить

// ===============================
Процедура ПриОткрытии(ФлагЧтенияНастройки) //предопределенная
	Попытка 
		КоррПров = СоздатьОбъект("КорректныеПроводки");
	Исключение
		Если Форма.Параметр <> 1 Тогда
			Предупреждение("Перенос корректных проводок работает с релизом
							|1С:Предприятия не ниже 7.70.006.");
		КонецЕсли;
		СтатусВозврата(0);
		Возврат;
	КонецПопытки;
		
	Текст = СоздатьОбъект("Текст");
	Текст.КодоваяСтраница(0);
	Если ПланыСчетов.КоличествоЗначений() > 1 Тогда
		Форма.ПлСчетов.Доступность(1);
	Иначе
		Форма.ПлСчетов.Доступность(0);
	КонецЕсли;

	Если Форма.Параметр = 1 Тогда
		//Первый запуск
		ПлСчетов = ВыбранныйПланСчетов();
		ИмяФайла = ИмяФайлаПереноса();
		Если КоррПров.ВыбратьКорректныеПроводки() = 1 Тогда
			//Только добавить новые
			РежимЗагрузки = 1;
		Иначе
			//Загрузить все без проверки
			РежимЗагрузки = 2;
		КонецЕсли;
		Текст = СоздатьОбъект("Текст");
		Текст.КодоваяСтраница(0);
		Загрузить();
		СтатусВозврата(0);
	
	ИначеЕсли ФлагЧтенияНастройки = 0 Тогда
		ПлСчетов = ВыбранныйПланСчетов();
		ИмяФайла = ИмяФайлаПереноса();
		РежимЗагрузки = 1;
	КонецЕсли;
КонецПроцедуры //ПриОткрытии

// ===============================
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр) //предопределенная
	Если ИдентЭлемДиалога = "ИмяФайла" Тогда
		Каталог = "";
		Если ФС.ВыбратьФайл(0, ИмяФайла, Каталог, "Выберите файл переноса", "Текстовые (*.txt) |*.txt|Все файлы (*.*) |*.*")=1 Тогда
			ИмяФайла = Каталог + ИмяФайла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ПриНачалеВыбораЗначения