Перем СписокРеквизитовИзВЭ;

//*******************************************
Процедура Сформировать()
	СписРекв = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По СписДоп.РазмерСписка() Цикл
		Если СписДоп.Пометка(Инд) = 1 Тогда
			СписРекв.ДобавитьЗначение(СписДоп.ПолучитьЗначение(Инд));
		КонецЕсли;
	КонецЦикла;
	
	Форма.Параметр = СписРекв;
	Форма.Закрыть();
КонецПроцедуры

Процедура ПриОткрытии()
	Если Форма.Параметр = "ИзЦен" Тогда
		Форма.кнЭкспорт.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Процедура Эксп()
	ТекстЗ = 
	 "//{{ЗАПРОС(Сформировать)
	|ТМЦ = Справочник.ТМЦ.ТекущийЭлемент;
	|Функция Счётчик = Счётчик();
	|Группировка ТМЦ упорядочить по ТМЦ.Наименование без групп;
	|Условие (ТМЦ в списТовар);
	|"//}}ЗАПРОС
	;
	
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
		Возврат;
	КонецЕсли;

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ДляЭкспорта");
	Таб.ВывестиСекцию("Шапка|Основная");
	Для Инд = 1 По СписДоп.РазмерСписка() Цикл
		Если СписДоп.Пометка(Инд) = 1 Тогда
			Таб.ПрисоединитьСекцию("Шапка|Доп");
		КонецЕсли;
	КонецЦикла;
	СпрВЭ = СоздатьОбъект("Справочник.Этикетки");
	
	НомерСтроки = 1;
	Пока Запрос.Группировка(1) = 1 Цикл
		Если Запрос.ТМЦ.ЭтоГруппа() = 0 Тогда
			ТМЦ = Запрос.ТМЦ;
			ТМЦС = СтрЗаменить(Строка(ТМЦ), Симв(13), "");			
			Таб.ВывестиСекцию("Строка|Основная");
			Для Инд = 1 По СписДоп.РазмерСписка() Цикл
				Если СписДоп.Пометка(Инд) = 1 Тогда
					ИмяР = СписДоп.ПолучитьЗначение(Инд);

					ЕстьРеквизит = 0;
					Если (Найти(СписокРеквизитовИзВЭ, ИмяР + ",") <> 0) и (ПустоеЗначение(ВидВесов) = 0) Тогда
						Если СпрВЭ.Владелец <> Запрос.ТМЦ Тогда
							СпрВЭ.ИспользоватьВладельца(Запрос.ТМЦ);						
							Если СпрВЭ.НайтиПоРеквизиту("ВидВесов", ВидВесов, 0) = 0 Тогда
								Сообщить("Указан вид весов для выгрузки, но для продукции: " + Строка(Запрос.ТМЦ) + " не найдена этикетка. Реквизиты этикетки будут взяты из продукции.");
							КонецЕсли;
						КонецЕсли;
						
						Если (СпрВЭ.Владелец = Запрос.ТМЦ) И (ПустоеЗначение(СпрВЭ.ТекущийЭлемент()) = 0) Тогда
							Зн = СпрВЭ.ПолучитьАтрибут(ИмяР);
							ЕстьРеквизит = 1;							
						КонецЕсли;
					КонецЕсли;

					Если ЕстьРеквизит = 0 Тогда
						Если Метаданные.Справочник("ТМЦ").Реквизит(ИмяР).Периодический = 1 Тогда
							Зн = ТМЦ.ПолучитьАтрибут(ИмяР).Получить(ТекущаяДата());
						Иначе
							Зн = ТМЦ.ПолучитьАтрибут(ИмяР);
						КонецЕсли;
						Если ТипЗначенияСтр(Зн) = "Перечисление" Тогда
							Зн = Зн.Идентификатор();			    
						КонецЕсли;
						Если ИмяР = "ПолнНаименование" Тогда
							Зн = СтрЗаменить(Зн, Симв(13), "");
						КонецЕсли;
					КонецЕсли;
					Таб.ПрисоединитьСекцию("Строка|Доп");
				КонецЕсли;
			КонецЦикла;
			
			НомерСтроки = НомерСтроки + 1;
		КонецЕсли;
	КонецЦикла;
	
	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать для эскпорта","");	
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Инд = 1;
Пока Метаданные.Справочник("ТМЦ").Реквизит(Инд).Выбран() = 1 Цикл
	ИмяРекв = Метаданные.Справочник("ТМЦ").Реквизит(Инд).Идентификатор;
	Синоним = Метаданные.Справочник("ТМЦ").Реквизит(Инд).Синоним;
	ТипЗнач = Метаданные.Справочник("ТМЦ").Реквизит(Инд).Тип;

	Если (ТипЗнач = "Строка") ИЛИ (ТипЗнач = "Число") ИЛИ (ТипЗнач = "Перечисление") ИЛИ (ТипЗнач = "Счет") Тогда
		списДоп.ДобавитьЗначение(ИмяРекв, Синоним);
	КонецЕсли;		
	Инд = Инд + 1;		
КонецЦикла;
списДоп.СортироватьПоПредставлению();

//--- УМК Сандомирский В.Ю. (14.07.14) в макет добавлена 6я колонка "Уд." 

//--- УМК Сандомирский В.Ю. (18.11.14) схема экспорта подчиненного справочника ТМЦ
Инд = 1;
Пока Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Выбран() = 1 Цикл
	ИмяРекв = Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Идентификатор;
	Синоним = Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Синоним;
	ТипЗнач = Метаданные.Справочник("УМК_РазрешенныеВидыУпаковкиТМЦ").Реквизит(Инд).Тип;

	Если (ТипЗнач = "Строка") ИЛИ (ТипЗнач = "Число") ИЛИ (ТипЗнач = "Перечисление") ИЛИ (ТипЗнач = "Счет") Тогда
		СписДопВУП.ДобавитьЗначение(ИмяРекв, Синоним);
	КонецЕсли;		
	Инд = Инд + 1;		
КонецЦикла;

СписДопВУП.СортироватьПоПредставлению();
СписокРеквизитовИзВЭ = "УсловияХранения,УсловияХраненияВП,УсловияХраненияВС,УсловияХраненияВЦ,УсловияХраненияГ,ОписаниеЭ,НаименованиеЭ,ВидЭ,";