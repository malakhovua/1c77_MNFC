//*********Предопределенные процедуры******* 
Процедура ПриОткрытии()
	фРезультат = 1;
КонецПроцедуры
//*******************************************

//*******************************************
Процедура УстановитьНорму()
	
	Перем Результат; //:ТаблицаЗначений
	
	рс = СоздатьОбъект("ODBCRecordset");
	
	ТекстЗапроса = "
	|SELECT УМК_НормыСписанияМатериаловУпаковок.ID [НормаСписанияМатериалаСсылка $Справочник.УМК_НормыСписанияМатериаловУпаковок]
	|	, УМК_РазрешенныеВидыУпаковкиТМЦ.ID [ВидУпаковкиСсылка $Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ]
	|   , $УМК_РазрешенныеВидыУпаковкиТМЦ.ВидУпаковки [ВидУпаковки $Справочник.ВидыУпаковки]
	|	, ТМЦ.ID [Номенклатура $Справочник.ТМЦ]
	|	, $ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковок.Материал(УМК_НормыСписанияМатериаловУпаковок.ID, :Период) [Материал $Справочник.ТМЦ]
	|   , $ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковок.НормаСписанияУп(УМК_НормыСписанияМатериаловУпаковок.ID, :Период) НормаСписанияУп
	|FROM $Справочник.ТМЦ AS ТМЦ
	|	INNER JOIN $Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ AS УМК_РазрешенныеВидыУпаковкиТМЦ ON ТМЦ.ID = УМК_РазрешенныеВидыУпаковкиТМЦ.PARENTEXT
	|	INNER JOIN $Справочник.УМК_НормыСписанияМатериаловУпаковок AS УМК_НормыСписанияМатериаловУпаковок ON УМК_РазрешенныеВидыУпаковкиТМЦ.ID = УМК_НормыСписанияМатериаловУпаковок.PARENTEXT
	|WHERE ($ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковок.Материал(УМК_НормыСписанияМатериаловУпаковок.ID, :Период) = :Материал)
	|	AND (УМК_РазрешенныеВидыУпаковкиТМЦ.ISMARK = 0)
	|" 
	+ ?(списПродукции.РазмерСписка()>0," and  ТМЦ.ID IN (SELECT Val FROM #списПродукции)", "");
	
	рс.УстановитьТекстовыйПараметр("Материал", Материал);
	рс.УстановитьТекстовыйПараметр("Период", Период);
	
	Если списПродукции.РазмерСписка() > 0 Тогда
		рс.УложитьСписокОбъектов(списПродукции, "#списПродукции", "ТМЦ");
	КонецЕсли;
	
	Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);
	Результат.НоваяКолонка("НормаСписанияУпНовая","Число",15,5);
	
	Результат.ВыбратьСтроки();
	
	Пока Результат.ПолучитьСтроку() = 1 Цикл
		
	  НормаСписания = СоздатьОбъект("Справочник.УМК_НормыСписанияМатериаловУпаковок");
	  Если НормаСписания.НайтиЭлемент(Результат.НормаСписанияМатериалаСсылка) = 0 Тогда
	  	Продолжить;
	  КонецЕсли;
	  
	  НормаСписания.НормаСписанияУп.Установить(Период, НовНормаСписанияУп);
	  НормаСписания.Записать();
      
	  Результат.НормаСписанияУпНовая = НормаСписания.НормаСписанияУп.Получить(Период);
	 
	КонецЦикла;

	Если фРезультат = 1 Тогда
		ПоказатьТЗ(Результат,"Результат");
	КонецЕсли;
	
КонецПроцедуры

//****************Работа со списком**********
Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	ДобавитьвСписок(Элт, списПродукции);
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры
//**************Работа со списком************


