// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Документ;			// обрабатываемый документ
Перем ВалютаДок;    

Перем ЕстьКолонкаЕдиница;		// флаг того, что в товарном составе присутствует колонка "Единица"

Перем ЗначениеПометки,ЗначениеНеПометки;

// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
// Название: ГрупповаяПометка
// Параметры: 
//   Режим
//		1 - пометить все
//		2 - снять пометку
//		3 - инвертировать пометку
//		4 - пометить товары из группы 
//		5 - пометить товары из категории
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопки "Пометить все","Снять пометку","Инвертировать пометку".
// Описание:
//    в зависимости от значения параметра выполняет пометку всех позиций, 
//    снимает пометку со всех позиций или инвертирует ее
//
Процедура ГрупповаяПометка(Режим)
	Перем НомерСтроки;
	Перем Область;
	Перем КоординатыЯчейки;
	Перем Пометка;                      
	Перем ГруппаПомечТоваров, КатегорияПомечТоваров, КатегорииТовара;
            
	Если Режим=4 Тогда				// пометить товары из группы
		ГруппаПомечТоваров = СоздатьОбъект("Справочник.ТМЦ");
		ГруппаПомечТоваров.ВыборГруппы(1);
		Если ГруппаПомечТоваров.Выбрать("Выбор группы помечаемой номенклатуры","Форма списка") = 0 Тогда
			Возврат;
		КонецЕсли;  

		Если ГруппаПомечТоваров.ЭтоГруппа() = 0 Тогда
			ГруппаПомечТоваров = ГруппаПомечТоваров.Родитель.ТекущийЭлемент();
		Иначе
			ГруппаПомечТоваров = ГруппаПомечТоваров.ТекущийЭлемент();
		КонецЕсли;

	ИначеЕсли Режим=5 Тогда			// пометить товары из категории
		КатегорияПомечТоваров 	= СоздатьОбъект("Справочник.ВидыКатегорий");
		КатегорииТовара 		= СоздатьОбъект("Справочник.КатегорииТоваров");
		Если КатегорияПомечТоваров.Выбрать("Выбор категории помечаемой номенклатуры","Форма списка") = 0 Тогда
			Возврат;
		КонецЕсли;  
	КонецЕсли;	
	                                          
	ТовСостав.ВыбратьСтроки();
	Пока ТовСостав.ПолучитьСтроку()>0 Цикл
		Если Режим = 1 Тогда
			ТовСостав.Пометка = ЗначениеПометки;

		ИначеЕсли Режим=2 Тогда
			ТовСостав.Пометка = ЗначениеНеПометки;

		ИначеЕсли Режим=3 Тогда
			ТовСостав.Пометка = 3 - ТовСостав.Пометка;

		ИначеЕсли Режим=4 Тогда 
			Если (ТовСостав.Товар.ПринадлежитГруппе(ГруппаПомечТоваров) = 1) Или (ГруппаПомечТоваров.Выбран() = 0) Тогда
				ТовСостав.Пометка = ЗначениеПометки;
			КонецЕсли;                           

		ИначеЕсли Режим=5 Тогда 
			КатегорииТовара.ИспользоватьВладельца(ТовСостав.Товар.ТекущийЭлемент());
			Если КатегорииТовара.НайтиПоРеквизиту("Категория",КатегорияПомечТоваров.ТекущийЭлемент(),0) = 1 Тогда
				ТовСостав.Пометка = ЗначениеПометки;
			КонецЕсли;                           
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


// ===============================
// Название: ПриДвойномЩелчкеНаТаблице
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   таблица ТовСостав
// Описание:
//   изменяет пометку
//
Процедура ПриДвойномЩелчкеНаТаблице()

	ТовСостав.Пометка = 3 - ТовСостав.Пометка;

КонецПроцедуры       // ПриДвойномЩелчкеНаТаблице
             

// ===============================
// Название: ДоступностьЭлементов
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//   флаги формы
// Описание:
//   в зависимости от значений флагов устанавливает доступность других элементов формы
//
Процедура ДоступностьЭлементов(ВсеЭлементы = 0)
	
	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы = 1) Или (ЭлементДиалога="УстановитьЦену") Тогда
		Форма.НеОбновлятьСуществующие.	Доступность(УстановитьЦену); 
		Форма.ТипЦен.					Доступность(УстановитьЦену);  
		НеОбновлятьСуществующие = Мин(НеОбновлятьСуществующие,УстановитьЦену);
	КонецЕсли;
	
	Если (ВсеЭлементы = 1) Или (ЭлементДиалога="УстановитьКатТоваров") Тогда
		Форма.КатегорияТоваров.Доступность(УстановитьКатТоваров);
	КонецЕсли;
	
	ТолькоПриИзмененииЦен = Мин(ТолькоПриИзмененииЦен,УстановитьЦену,УстановитьКатТоваров);
	Форма.ТолькоПриИзмененииЦен.Доступность(Мин(УстановитьЦену,УстановитьКатТоваров));
	
КонецПроцедуры       // ДоступностьЭлементов


// ===============================
// Название: ПриСменеТипаЦен
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
//  Поле "ТипЦен"
// Описание:
//   перерисовывает колонку тип цены
//
Процедура ПриСменеТипаЦен()         
	Перем Цена; 
	
	Если ТипЦен.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;	
	           
	Цена = СоздатьОбъект("Справочник.Цены");
	Цена.ИспользоватьДату(РабочаяДата());
	
	ТовСостав.ВыбратьСтроки();
		
	Пока ТовСостав.ПолучитьСтроку() = 1 Цикл   
		Если ТовСостав.Товар.Выбран() = 0 Тогда
			Продолжить;
		КонецЕсли;	

		Цена.ИспользоватьВладельца(ТовСостав.Товар.ТекущийЭлемент());
		
		Если Цена.НайтиПоРеквизиту("КатегорияЦены",ТипЦен.ТекущийЭлемент(),0) = 1 Тогда
			ОписаниеЦены = "";
			ОписаниеЦены = ОписаниеЦены + Строка(Цена.Цена)+" ";
			ОписаниеЦены = ОписаниеЦены + СокрЛП(Цена.Валюта.Кратко);
			ОписаниеЦены = ОписаниеЦены + "/"+СокрЛП(Цена.Единица.Наименование);
			Если Цена.ПометкаУдаления() = 1 Тогда
				ОписаниеЦены = ОписаниеЦены + " (уд.)";
			КонецЕсли;                            
			ТовСостав.ЦенаВСправ = ОписаниеЦены;
		Иначе     
			ТовСостав.ЦенаВСправ = "";
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры 	// ПриСменеТипаЦен

// ===============================
// Название: Выполнить
// Параметры:
// НЕТ
// Возвращаемое значение:
// НЕТ
// Вызывается из формул элементов диалога:
// Кнопка "Выполнить",.
// Описание:
//   выполняет групповое действие
//
Процедура Выполнить()
	Перем Цена;
	Перем КатегорияТов;
	    
	Если (УстановитьЦену = 0) И (УстановитьКатТоваров = 0) Тогда
		Возврат;
	КонецЕсли;	
	Если (УстановитьЦену <> 0) И (ТипЦен.Выбран() = 0) Тогда // пустое значение
		Предупреждение("Не выбран тип цен");
		Возврат;
	КонецЕсли;    
	Если (УстановитьКатТоваров <> 0) И (КатегорияТоваров.Выбран() = 0) Тогда // пустое значение
		Предупреждение("Не выбрана категория товаров");
		Возврат;
	КонецЕсли;    
	Цена = СоздатьОбъект("Справочник.Цены");
	Цена.ИспользоватьДату(РабочаяДата());
	КатегорияТов = СоздатьОбъект("Справочник.КатегорииТоваров");	
	ТовСостав.ВыбратьСтроки();
	НомерСтроки = 1;	
	Пока ТовСостав.ПолучитьСтроку() = 1 Цикл
		НомерСтроки = НомерСтроки + 1;				
		Если ТовСостав.Товар.Выбран() = 0 Тогда
			Продолжить;
		КонецЕсли;			
		Если ТовСостав.Пометка=ЗначениеНеПометки Тогда
			Продолжить;
		КонецЕсли;				
		глКомментарий("Выполняется обработка позиции "+СокрЛП(ТовСостав.Товар.Наименование)+
					?(ПустаяСтрока(ТовСостав.Товар.Артикул) = 1,"",", арт. "+СокрЛП(ТовСостав.Товар.Артикул))+
					?(ЕстьКолонкаЕдиница = 1,", "+СокрЛП(ТовСостав.Ед.Наименование),""),2);
	                        
		УстанавливатьЦенуУТовара = 0;		
		Если УстановитьЦену <> 0 Тогда
			УстанавливатьЦенуУТовара = 1;			
			Цена.ИспользоватьВладельца(ТовСостав.Товар.ТекущийЭлемент());			            
			Если Цена.НайтиПоРеквизиту("КатегорияЦены",ТипЦен.ТекущийЭлемент(),0) = 0 Тогда
				// новые цену установим на дату начала работы
				Цена.ИспользоватьДату(Константа.ДатаНачалаРаботы,1);			                           
				Цена.Новый();
				Цена.КатегорияЦены = ТипЦен.ТекущийЭлемент();
			Иначе
				Если НеОбновлятьСуществующие <> 0 Тогда
					УстанавливатьЦенуУТовара = 0;
				КонецЕсли;	
			КонецЕсли;
			                                  
			Если УстанавливатьЦенуУТовара = 1 Тогда
				Цена.Валюта	 = ВалютаДок.ТекущийЭлемент();
				Цена.Цена	 = ТовСостав.ЦенаСНДС;
				Цена.Единица = ?(ЕстьКолонкаЕдиница = 1,ТовСостав.Ед.ТекущийЭлемент(),
								глВернутьБазовуюЕдиницуТовара(ТовСостав.Товар.ТекущийЭлемент()));
				Если Цена.Единица.Выбран() = 0 Тогда
					Цена.Единица = глВернутьБазовуюЕдиницуТовара(ТовСостав.Товар.ТекущийЭлемент());
				КонецЕсли;	
				Цена.Записать();   
				// восстановим дату изменения цен
				Цена.ИспользоватьДату(РабочаяДата(),1);
			КонецЕсли;	
		КонецЕсли;	
							
		Если УстановитьКатТоваров <> 0 Тогда			
			Если (УстанавливатьЦенуУТовара = 1) Или (ТолькоПриИзмененииЦен = 0) Тогда				
				КатегорияТов.ИспользоватьВладельца(ТовСостав.Товар.ТекущийЭлемент());							
			    Если КатегорияТов.НайтиПоРеквизиту("Категория",КатегорияТоваров.ТекущийЭлемент(),0) = 0 Тогда
					КатегорияТов.Новый();
					КатегорияТов.Категория = КатегорияТоваров.ТекущийЭлемент();
					КатегорияТов.Записать();
				Иначе
					Если КатегорияТов.ПометкаУдаления() = 1 Тогда
						КатегорияТов.СнятьПометкуУдаления();  
						КатегорияТов.Записать();
					КонецЕсли;	
				КонецЕсли;      
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
	глКомментарий("Обработка завершена!",2);	
	Если (УстановитьЦену <> 0) Тогда
		ПриСменеТипаЦен();
	КонецЕсли;
    
КонецПроцедуры
    

// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
// Вызывается из 	ВозвратнаяНакладная, ВозвратРозница, 
//					ПриходнаяНакладнаяГТД, ПриходнаяНакладнаяЗапасы, ПриходнаяНакладнаяПрочие
//					РасходнаяНакладная, РасходнаяРозничная
//					Счет
Процедура ПриОткрытии()	// Предопределенная процедура
    Перем Товар;             
	Перем НомерКолонки;
	Перем ИдентификаторКолонки;
	Перем ВидДок;        
	
	Если ПустоеЗначение(Форма.Параметр) = 1 Тогда
		Предупреждение("Не указан документ. Данная обработка запускается автоматически из других процедур конфигурации.");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;	
	
	КатегорияТоваров.ВыборГруппы(0);
	
	Документ = Форма.Параметр;
	ВидДок = ВРег(Документ.Вид());              
	
	Документ.ВыгрузитьТабличнуюЧасть(ТовСостав);
	           
	ТовСостав.ВставитьКолонку("Пометка",1,"Число",4,0);
	ТовСостав.ВыводитьПиктограммы("Пометка",1);
	ТовСостав.ВидимостьКолонки("Пометка",1);
	
	ТовСостав.Фиксировать(0,2);
	
	Если глЕстьРеквизитМнЧ("Кво",Документ.Вид()) = Нет Тогда				
		Предупреждение("Документ не содержит товарный состав");	
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если глЕстьРеквизитМнЧ("ТМЦ",Документ.Вид()) = Да Тогда				
		
		ТовСостав.НоваяКолонка("Товар");
		ТовСостав.ВыбратьСтроки();
		Пока ТовСостав.ПолучитьСтроку() = 1 Цикл
			ТовСостав.Товар = ТовСостав.ТМЦ;
		КонецЦикла;

	ИначеЕсли глЕстьРеквизитМнЧ("Услуга",Документ.Вид()) = Да Тогда				

		ТовСостав.НоваяКолонка("Товар");
		ТовСостав.ВыбратьСтроки();
		Пока ТовСостав.ПолучитьСтроку() = 1 Цикл
			ТовСостав.Товар = ТовСостав.Услуга;
		КонецЦикла;

	Иначе	
		Предупреждение("Документ не содержит товарный состав");	
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Попытка         
		Если ТовСостав.ПолучитьПараметрыКолонки("ЦенаСНДС") = 0 Тогда		// нет колонки "цена"
			ЕстьКолонкаЦена = 0;
		Иначе   
			ЕстьКолонкаЦена = 1;                   
		КонецЕсли;	
	Исключение                      
		ЕстьКолонкаЦена = 0;
	КонецПопытки;                   
	
	// возможно это документ "ПереоценкаТМЦ"
	Если ЕстьКолонкаЦена = 0 Тогда
		Если (Документ.Вид() = "ПереоценкаТМЦ") Тогда
			// заменим колонку "НоваяЦена" на "ЦенаСНДС"
			ЕстьКолонкаЦена = 1;
			ТовСостав.НоваяКолонка("ЦенаСНДС");
			ТовСостав.ВыбратьСтроки();
			Пока ТовСостав.ПолучитьСтроку() = 1 Цикл
				ТовСостав.ЦенаСНДС = ТовСостав.НоваяЦена;	
			КонецЦикла;
			ТовСостав.УдалитьКолонку("НоваяЦена");
		КонецЕсли;
	ИначеЕсли Документ.Вид() = "ПереоценкаРозницы" Тогда
		ТовСостав.ВыбратьСтроки();
		Пока ТовСостав.ПолучитьСтроку() = 1 Цикл
			ТовСостав.ЦенаСНДС = ТовСостав.НоваяЦена;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьКолонкаЦена = 0 Тогда
		Предупреждение("Документ не содержит цен");	
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Попытка 
		Если ТовСостав.ПолучитьПараметрыКолонки("Ед") = 0 Тогда		// нет колонки "Единица"
			ЕстьКолонкаЕдиница = 0;
		Иначе
			ЕстьКолонкаЕдиница = 1;
		КонецЕсли;	
	Исключение                      
		ЕстьКолонкаЕдиница = 0;
	КонецПопытки;                   
	
	Попытка               
		// если ошибок не будет, то в документе есть поле валюта
		ВалютаДок = Документ.Валюта;
	Исключение                      
		ВалютаДок = Гривня;
	КонецПопытки;                   
	
	Попытка               
		// если ошибок не будет, то в документе есть категория цен
		ТипЦен = Документ.КатегорияЦен;
	Исключение                      
		ТипЦен = 0;
	КонецПопытки;  
	
	Если ВидДок = "ПЕРЕМЕЩЕНИЕ" Тогда
		ТипЦен = Константа.РозничнаяКатегорияЦен;
	КонецЕсли;
	
	ТовСостав.НоваяКолонка("ЦенаВСправ","Строка",30,,"Цена в справочнике");
	
	ПриСменеТипаЦен();
	// будем отслеживать только реальные изменения этих реквизитов
	Форма.ТипЦен.ВыполнятьФормулуТолькоПриИзменении(1); 
	
	СписокПоказываемыхКолонок = "\ТОВАР\ЕД\ЦЕНАСНДС\ЦЕНАВСПРАВ\";
	
	// прячем все колонки
	Для НомерКолонки = 2 По ТовСостав.КоличествоКолонок() Цикл
		
		ИдентификаторКолонки=ТовСостав.ПолучитьПараметрыКолонки(НомерКолонки);
		Если Найти(СписокПоказываемыхКолонок,"\"+ВРег(ИдентификаторКолонки)+"\") = 0 Тогда
			ТовСостав.ВидимостьКолонки(ИдентификаторКолонки,0);
		КонецЕсли;	
	КонецЦикла;	                 
	                  
	// помечаем все товары
	ГрупповаяПометка(1);        
	ДоступностьЭлементов(1);
	
КонецПроцедуры	// ПриОткрытии


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

ЗначениеПометки 	= 2; 
ЗначениеНеПометки 	= 1;
ВалютаДок = Гривня;
