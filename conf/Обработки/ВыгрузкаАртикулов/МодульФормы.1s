// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Процедура Сформировать(Режим)

	Если ВыбСклад.Выбран()=0 Тогда
		глКомментарий("Не выбран склад",1);
		Возврат;
	КонецЕсли;

	Если глСписокЭККА_OFFLine.РазмерСписка()=0 Тогда
		глКомментарий("На данном рабочем месте список подключенных ЭККА в режиме Off-Line пуст.",1);
		Возврат;
	КонецЕсли;

	СписокТоваров = СоздатьОбъект("СписокЗначений");
	Если Режим = "Склад" Тогда

		Запрос = СоздатьОбъект("Запрос");
		ТекстЗапроса =
			"//{{ЗАПРОС(Остатки)
			|Склад 			= Регистр.Остатки.МестоХранения;
			|Товар 			= Регистр.Остатки.ТМЦ;
			|ОстатокТовара 	= Регистр.Остатки.ОстатокТовара;
			|Группировка Товар упорядочить по Товар.Наименование без групп;
			|Функция КонКол = КонОст(ОстатокТовара);
			|Условие (Склад = ВыбСклад);
			|"//}}ЗАПРОС
			;
		Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
			Возврат;
		КонецЕсли;

		Если Запрос.КонКол=0 Тогда
			глКомментарий("На складе """+ВыбСклад+""" нет товаров. Загрузка не произведена.",1);
			Возврат;
		КонецЕсли;

		Пока Запрос.Группировка("Товар") = 1 Цикл
		    СписокТоваров.ДобавитьЗначение(Запрос.Товар);
		КонецЦикла;

	ИначеЕсли Режим="Прайс" Тогда 

		Прайс = СоздатьОбъект("Справочник.Прайс_лист");
		Прайс.ВыбратьЭлементы();
		Пока Прайс.ПолучитьЭлемент()>0 Цикл
			Если Прайс.ЭтоГруппа()=1 Тогда
				Продолжить;
			КонецЕсли;

			СписокТоваров.ДобавитьЗначение(Прайс.Товар);
		КонецЦикла;
	КонецЕсли;
	
	Если СписокТоваров.РазмерСписка()=0 Тогда
		Сообщить("Список товаров пуст. Загрузка не произведена.");
		Возврат;
	КонецЕсли;
	
	Для Инд = 1 По глСписокЭККА_OFFLine.РазмерСписка() Цикл

		Включена="";
		Касса = глСписокЭККА_OFFLine.ПолучитьЗначение(Инд,Включена);

		Если ПустоеЗначение(Касса)=1 Тогда
		    Продолжить;
		КонецЕсли;

		Если ПустоеЗначение(Включена)=1 Тогда
		    Продолжить;
		КонецЕсли;

		// вызов обработки обслуживания ЭККА    
		ИмяОбработки = "ОбслуживаниеЭККА_"+СокрЛП(Строка(Касса.Тип));
		Если ПустоеЗначение(СокрЛП(Строка(Касса.Тип)))=0 Тогда

			глРасшифровка = СоздатьОбъект("СписокЗначений");
			глРасшифровка.ДобавитьЗначение(Касса,				 "ЭККА");
			глРасшифровка.ДобавитьЗначение("ВыгрузитьАртикулы","Парам");
			глРасшифровка.ДобавитьЗначение(СписокТоваров,		 "СписокТоваров");
			глРасшифровка.ДобавитьЗначение(ВыбСклад,			 "ВыбСклад");
			глРасшифровка.ДобавитьЗначение(ИмяОбработки,		 "Отчет");
			глФлагРасшифровки = 1;
			ОткрытьФормуМодально("Обработка."+ИмяОбработки+"#");
			глФлагРасшифровки = 0;
			глРасшифровка = 0;
		КонецЕсли;

	КонецЦикла;
КонецПроцедуры


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()
	ВыбСклад=глВосстановитьЗначение(,"ОсновнойСклад");
КонецПроцедуры
