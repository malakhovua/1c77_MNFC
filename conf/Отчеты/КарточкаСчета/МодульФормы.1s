//-----------------------------------------------
Перем ОтборСубк1, ОтборСубк2, ОтборСубк3;
Перем Субконто1, Субконто2, Субконто3;
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция ЗначенияВСтроку(Значения)
	Если ТипЗначенияСтр(Значения) = "СписокЗначений" Тогда
		С = "";
		Для А=1 По Значения.РазмерСписка() Цикл
			Если А <> 1 Тогда
				С = С+" ";
			КонецЕсли;
			С = С+Значения.ПолучитьЗначение(А);
		КонецЦикла;
		Возврат С;
	Иначе
		Возврат Значения;
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура СформироватьОтчет()
	Если Счет.Выбран() = 0 Тогда
		Предупреждение("Не указан счет!");
		Возврат;
	КонецЕсли;
	
	Если (ПоВалюте = 1) и (ПустоеЗначение(Валюта) = 1) Тогда
		Предупреждение("Не выбрана валюта!");
		Возврат;
	КонецЕсли;

	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

	Если глПроверкаИнтервалаОтчета(Дата1,Дата2) = 0 Тогда
		Возврат;
	КонецЕсли;

	НФ = РазделительУчета;

	ОтборСубк1 = ?(ОтборСубконто1=1,2,3);
	ОтборСубк2 = ?(ОтборСубконто2=1,2,3);
	ОтборСубк3 = ?(ОтборСубконто3=1,2,3);

	Расшифровка = СоздатьОбъект("СписокЗначений");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Если ВыбВидСубконто1.Выбран() = 0 Тогда
		ОтборСубк1 = 3;
	КонецЕсли;

	Если ВыбВидСубконто2.Выбран() = 0 Тогда
		ОтборСубк2 = 3;
	КонецЕсли;

	Если ВыбВидСубконто3.Выбран() = 0 Тогда
		ОтборСубк3 = 3;
	КонецЕсли;    
	
	Заголовок = "Карточка счета: "+Счет;
	Если Константа.ФормыНаУкраинском = Да Тогда
    	Заголовок = "Картка рахунку :"+Счет;
	КонецЕсли;
	
    Заголовок1 = "";
    Если ОтборСубк1 <> 3 Тогда  // 3 - без учета субконто
    	Заголовок1 = Заголовок1+" : "+Строка(глУкр(ВыбВидСубконто1))+":"+ЗначенияВСтроку(Субконто1);
		Ит.ИспользоватьСубконто(ВыбВидСубконто1, Субконто1, ОтборСубк1);
	КонецЕсли;

    Если ОтборСубк2 <> 3 Тогда  // 3 - без учета субконто
    	Заголовок1 = Заголовок1+" : "+Строка(глУкр(ВыбВидСубконто2))+":"+ЗначенияВСтроку(Субконто2);
		Ит.ИспользоватьСубконто(ВыбВидСубконто2, Субконто2, ОтборСубк2);
	КонецЕсли;

    Если ОтборСубк3 <> 3 Тогда  // 3 - без учета субконто
    	Заголовок1 = Заголовок1+" : "+Строка(глУкр(ВыбВидСубконто3))+":"+ЗначенияВСтроку(Субконто3);
		Ит.ИспользоватьСубконто(ВыбВидСубконто3, Субконто3, ОтборСубк3);
	КонецЕсли;

	Если ПоВалюте = 1 Тогда
		Заголовок=Заголовок+" : "+Валюта;
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,, Валюта, 3, "Проводка") = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,,, 3, "Проводка") = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	 
	глУстПропись(Гривня);
	Если Константа.ФормыНаУкраинском = Да Тогда
		Т.ИсходнаяТаблица("Таблица_Укр");
	Иначе
		Т.ИсходнаяТаблица("Таблица");
	КонецЕсли;

	ОбДт = 0;
	ОбКт = 0;
	Расшифровка.Установить("Отчет", "КарточкаСчета");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Счет", Счет);
	Расшифровка.Установить("ВидСубконто1", ВыбВидСубконто1);
	Расшифровка.Установить("Субконто1", Субконто1);
	Расшифровка.Установить("ОтборСубконто1", ОтборСубк1);
	Расшифровка.Установить("ВидСубконто2", ВыбВидСубконто2);
	Расшифровка.Установить("Субконто2", Субконто2);
	Расшифровка.Установить("ОтборСубконто2", ОтборСубк2);
	Расшифровка.Установить("ВидСубконто3", ВыбВидСубконто3);
	Расшифровка.Установить("Субконто3", Субконто3);
	Расшифровка.Установить("ОтборСубконто3", ОтборСубк3);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Т.ВывестиСекцию("Секция_13");
	Т.ВывестиСекцию("Секция_1_1");
	Если Заголовок1 <> "" Тогда
		Т.ВывестиСекцию("Секция_1_2");
	КонецЕсли;
	Т.ВывестиСекцию("Секция_1_3");

	Расшифровка.УдалитьВсе();
	Если ((Счет.Количественный = 1) и ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2))) или (ПоВалюте = 1) Тогда
		Т.ВывестиСекцию("Секция_2_1");
	Иначе
		Т.ВывестиСекцию("Секция_2");
	КонецЕсли;

	Если ПоВалюте = 1 Тогда
		Если (Счет.Количественный = 1) и ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2)) Тогда
			Т.ВывестиСекцию("Секция_4_1");
		Иначе
			Т.ВывестиСекцию("Секция_4");
		КонецЕсли;
	КонецЕсли;

	Количественный = Счет.Количественный;
	КвоСубконто = Счет.КоличествоСубконто();
	Если Количественный = 1 Тогда
		Если (КвоСубконто = 0) Или ((КвоСубконто > 0)
		И ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2))) Тогда
		    Т.ВывестиСекцию("Секция_3");
		КонецЕсли;
	КонецЕсли;

	Ит.ВыбратьПериоды();
	Пока Ит.ПолучитьПериод() = 1 Цикл
		
		Ит.ВыбратьКорСчета();
		Пока Ит.ПолучитьКорСчет()=1 Цикл
		
			Дт = Ит.ВыбранаПоДт();
			Кт = Ит.ВыбранаПоКт();
			Опер = Ит.Операция;
			СД = Ит.СКД()-Ит.СКК();
	
			Расшифровка.Установить("Документ", Опер.Документ.ТекущийДокумент());
			Расшифровка.Установить("НомерПроводки", Опер.НомерПроводки());
			Расшифровка.Установить("НомерКорреспонденции", Опер.НомерКорреспонденции());
			
			Т.ВывестиСекцию("Секция_5");
			Если (Опер.ВалСумма <> 0) Или (ПоВалюте = 1) Тогда
				Если ПоВалюте = 1 Тогда
					СДВал = Ит.СКД(2)-Ит.СКК(2);
					Т.ВывестиСекцию("Секция_6");
				Иначе
					Т.ВывестиСекцию("Секция_6_1");
				КонецЕсли;
			КонецЕсли;
	
			Если Количественный = 1 Тогда
				Если (ОтборСубк1 <> 2) и (ОтборСубк2 <> 2) и (ОтборСубк3 <> 2) Тогда
					Т.ВывестиСекцию("Секция_7_1");
				Иначе
					СДКол = Ит.СКД(3)-Ит.СКК(3);
					Т.ВывестиСекцию("Секция_7");
				КонецЕсли;
			ИначеЕсли Опер.Количество <> 0 Тогда
				Т.ВывестиСекцию("Секция_7_1");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Т.ВывестиСекцию("Секция_8");
	Если ПоВалюте = 1 Тогда
		Т.ВывестиСекцию("Секция_14");
	КонецЕсли;
	Если Количественный = 1 Тогда
		Если (КвоСубконто = 0) Или ((КвоСубконто > 0)
		И ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2))) Тогда
		    Т.ВывестиСекцию("Секция_9");
		КонецЕсли;
	КонецЕсли;

	Т.ВывестиСекцию("Секция_10");
	Если ПоВалюте = 1 Тогда
		Т.ВывестиСекцию("Секция_15");
	КонецЕсли;
	Если Количественный = 1 Тогда
		Если (КвоСубконто = 0) Или ((КвоСубконто > 0)
		И ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2))) Тогда
		    Т.ВывестиСекцию("Секция_11");
		КонецЕсли;
	КонецЕсли;

	Т.ВывестиСекцию("Секция_12");
	Ит = 0;
	Т.ТолькоПросмотр(1);
	ФиксСтрок = 7;
	Если (Количественный = 1) и ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2)) Тогда
		ФиксСтрок = ФиксСтрок+1;
	КонецЕсли;
	Если ПоВалюте = 1 Тогда
		ФиксСтрок = ФиксСтрок+1;
	КонецЕсли;
	Если Заголовок1 <> "" Тогда
		ФиксСтрок = ФиксСтрок+1;
	КонецЕсли;

	Т.Опции(0, 0, ФиксСтрок, 2, "ОпцииПечатиКарточкаСчета", "КарточкаСчета");
	Т.ОбластьПечати(2);
	Т.Показать(Заголовок+Заголовок1+" ("+ПериодСтр(Дата1, Дата2)+?(ТипЗначения(РазделительУчета)=0, "", " "+РазделительУчета)+")", "");
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	Если Счет.КоличествоСубконто() > 0 Тогда
		НазначитьСчет(ВыбВидСубконто1, Счет, 1);
		Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);
		Доступность = 1;
	Иначе
		ВыбВидСубконто1 = "";
		ВыбСубконто1 = "";
		Доступность = 0;
	КонецЕсли;
	Форма.ВыбСубконто1.Доступность(Доступность);
	Форма.ОтборСубконто1.Доступность(Доступность);
	Форма.ВыбВидСубконто1.Доступность(Доступность);
	Форма.ОчиститьСубконто1.Доступность(Доступность);

	Если Счет.КоличествоСубконто() > 1 Тогда
		НазначитьСчет(ВыбВидСубконто2, Счет, 2);
		Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);
		Доступность = 1;
	Иначе
		ВыбВидСубконто2 = "";
		ВыбСубконто2 = "";
		Доступность = 0;
	КонецЕсли;
	Форма.ВыбСубконто2.Доступность(Доступность);
	Форма.ОтборСубконто2.Доступность(Доступность);
	Форма.ВыбВидСубконто2.Доступность(Доступность);
	Форма.ОчиститьСубконто2.Доступность(Доступность);

	Если Счет.КоличествоСубконто() > 2 Тогда
		НазначитьСчет(ВыбВидСубконто3, Счет, 3);
		Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);
		Доступность = 1;
	Иначе
		ВыбВидСубконто3 = "";
		ВыбСубконто3 = "";
		Доступность = 0;
	КонецЕсли;
	Форма.ВыбСубконто3.Доступность(Доступность);
	Форма.ОтборСубконто3.Доступность(Доступность);
	Форма.ВыбВидСубконто3.Доступность(Доступность);
	Форма.ОчиститьСубконто3.Доступность(Доступность);

	Если Счет.Валютный = 0 Тогда
	    ПоВалюте = 0;
	    Валюта = "";
	КонецЕсли;
	Форма.ПоВалюте.Доступность(Счет.Валютный);
	Форма.Валюта.Доступность(Счет.Валютный);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	Если Валюта.Выбран() = 1 Тогда
	    ПоВалюте = 1;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто1()
	Если ПустоеЗначение(ВыбСубконто1) = 0 Тогда
	    ОтборСубконто1 = 1;
	Иначе
	    ОтборСубконто1 = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто2()
	Если ПустоеЗначение(ВыбСубконто2) = 0 Тогда
	    ОтборСубконто2 = 1;
	Иначе
	    ОтборСубконто2 = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто3()
	Если ПустоеЗначение(ВыбСубконто3) = 0 Тогда
	    ОтборСубконто3 = 1;
	Иначе
	    ОтборСубконто3 = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОчиститьСубконто1()
	ВыбСубконто1 = "";
    ОтборСубконто1 = 0;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОчиститьСубконто2()
	ВыбСубконто2 = "";
    ОтборСубконто2 = 0;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОчиститьСубконто3()
	ВыбСубконто3 = "";
    ОтборСубконто3 = 0;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	ОтборСубконто1=0;
	ОтборСубконто2=0;
	ОтборСубконто3=0;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Дата1 = глРасшифровка.Получить("Дата1");
		Дата2 = глРасшифровка.Получить("Дата2");
		Счет = глРасшифровка.Получить("Счет");
	   	ПриВыбореСчета();
		ВыбВидСубконто1 = глРасшифровка.Получить("ВидСубконто1");
		Субконто1 = глРасшифровка.Получить("Субконто1");
		Если глРасшифровка.Получить("ОтборСубконто1") = 2 Тогда
			ОтборСубконто1 = 1;
		Иначе
			ОтборСубконто1 = 0;
		КонецЕсли;
		ВыбВидСубконто2 = глРасшифровка.Получить("ВидСубконто2");
		Субконто2 = глРасшифровка.Получить("Субконто2");
		Если глРасшифровка.Получить("ОтборСубконто2") = 2 Тогда
			ОтборСубконто2 = 1;
		Иначе
			ОтборСубконто2 = 0;
		КонецЕсли;
		ВыбВидСубконто3 = глРасшифровка.Получить("ВидСубконто3");
		Субконто3 = глРасшифровка.Получить("Субконто3");
		Если глРасшифровка.Получить("ОтборСубконто3") = 2 Тогда
			ОтборСубконто3 = 1;
		Иначе
			ОтборСубконто3 = 0;
		КонецЕсли;
		Валюта = глРасшифровка.Получить("Валюта");
		ПоВалюте = глРасшифровка.Получить("ПоВалюте");

		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			СформироватьОтчет();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;

		Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);
		ВыбСубконто1 = Субконто1;
		Если ТипЗначенияСтр(Субконто1) = "СписокЗначений" Тогда
			Если Субконто1.РазмерСписка() = 1 Тогда
				ВыбСубконто1 = Субконто1.ПолучитьЗначение(0);
			КонецЕсли;
		КонецЕсли;

		Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);
		ВыбСубконто2 = Субконто2;
		Если ТипЗначенияСтр(Субконто2) = "СписокЗначений" Тогда
			Если Субконто2.РазмерСписка() = 1 Тогда
				ВыбСубконто2 = Субконто2.ПолучитьЗначение(0);
			КонецЕсли;
		КонецЕсли;

		Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);
		ВыбСубконто3 = Субконто3;
		Если ТипЗначенияСтр(Субконто3) = "СписокЗначений" Тогда
			Если Субконто3.РазмерСписка() = 1 Тогда
				ВыбСубконто3 = Субконто3.ПолучитьЗначение(0);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Обновить = 0;
		РазделительУчета = глВосстановитьЗначение(,"БазФирма");
		ПоВсемРУ = 0;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		Если Счет.Выбран()=0 Тогда
		    ВосстановитьЗначение("ОтчРабСчет", Счет);
		КонецЕсли;
	КонецЕсли;
	ПриВыбореСчета();
	Форма.КнопкаПоУмолчанию("Сформировать");
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать()
    СохранитьЗначение("ОтчРабСчет",Счет);
	Субконто1 = ВыбСубконто1;
	Субконто2 = ВыбСубконто2;
	Субконто3 = ВыбСубконто3;
	СформироватьОтчет();
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ПриВыбореСчета();
КонецПроцедуры