//-----------------------------------------------
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура Сформировать()
	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

	Если глПроверкаИнтервалаОтчета(Дата1,Дата2) = 0 Тогда
		Возврат;
	КонецЕсли;
                      
	НФ = РазделительУчета;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ВключатьСубсчета(ДанныеПоСубсчетам, ДанныеПоСубсчетам);
    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
    Ит.ИспользоватьПланСчетов(ПланСчетов);
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Если ПоВалюте = 1 Тогда
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,, Валюта, 2) = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,,, 2) = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;

	глУстПропись(Гривня);
	Если Константа.ФормыНаУкраинском = Да Тогда
		Т.ИсходнаяТаблица("Таблица_Укр");
	Иначе
		Т.ИсходнаяТаблица("Таблица");
	КонецЕсли;

	Расшифровка.Установить("Отчет", "СводныеПроводки");
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Если ПоКредиту = 0 Тогда
		Расшифровка.Установить("ДтКт", 1);
	Иначе
		Расшифровка.Установить("ДтКт", 2);
	КонецЕсли;

   	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Т.ВывестиСекцию("Секция_6");
	Т.ВывестиСекцию("Секция_1");
	Расшифровка.Установить("Обновить");
   	Расшифровка.Установить("ДанныеПоСубсчетам");
	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Расшифровка.Установить("Счет", Ит.Счет);
		Ит.ВыбратьКорСчета();
		Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Если ?(ПоКредиту=0,Ит.КорДО(),Ит.КорКО()) <> 0 Тогда
				Расшифровка.Установить("КорСчет", Ит.КорСчет);
				Если ПоВалюте = 0 Тогда
					Т.ВывестиСекцию("Секция_2");
					Если (Ит.Счет.Валютный = 1) или (Ит.КорСчет.Валютный = 1) Тогда
						Расшифровка.Установить("ПоВалюте", 1);
						Ит.ВыбратьВалюты();
						Пока Ит.ПолучитьВалюту() = 1 Цикл
							Если (?(ПоКредиту=0,Ит.КорДО(),Ит.КорКО()) <> 0) или (?(ПоКредиту=0,Ит.КорДО(2),Ит.КорКО(2)) <> 0) Тогда
								Расшифровка.Установить("Валюта", Ит.Валюта);
								Т.ВывестиСекцию("Секция_3");
							КонецЕсли;
						КонецЦикла;
						Расшифровка.Установить("Валюта", Валюта);
						Расшифровка.Установить("ПоВалюте", ПоВалюте);
					КонецЕсли;
				Иначе
					Т.ВывестиСекцию("Секция_4");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Расшифровка.Установить("КорСчет");
	КонецЦикла;
	Т.ВывестиСекцию("Секция_5");
    Ит = 0;
    Т.ТолькоПросмотр(1);
	Т.Опции(0, 0, 5, 0, "ОпцииПечатиСводныеПроводки", "СводныеПроводки");
	Т.ОбластьПечати(2);
	Т.Показать("Сводные проводки"+?(ПоВалюте=1," "+Валюта, "")+" ("+ПериодСтр(Дата1, Дата2)+?(ТипЗначения(РазделительУчета)=0, "", " "+РазделительУчета)+")", "");
КонецПроцедуры

//-----------------------------------------------
Процедура ПривыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии(ИспНастройки)
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если ИспНастройки=0 Тогда
		ДанныеПоСубсчетам=1;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ВыбПланСчетов = 0;
		Если Обновить <> 0 Тогда
			ПланСчетов = глРасшифровка.Получить("ПланСчетов");
			РУ = глРасшифровка.Получить("РазделительУчета");
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
			Дата1 = глРасшифровка.Получить("Дата1");
			Дата2 = глРасшифровка.Получить("Дата2");
		   	ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
			Валюта = глРасшифровка.Получить("Валюта");
			ПоВалюте = глРасшифровка.Получить("ПоВалюте");
			ПоКредиту = глРасшифровка.Получить("ДтКт")-1;
			Т = глТаблица;
		Иначе
			ПланСчетов = ВыбранныйПланСчетов();
			РУ = глБИ.ИспользоватьРазделительУчета();
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РазделительУчета = глВосстановитьЗначение(,"БазФирма");
		ПоВсемРУ = 0;
	КонецЕсли;
	Форма.КнопкаПоУмолчанию("Сформировать");
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового() //Считывание существующей настройки
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
КонецПроцедуры
