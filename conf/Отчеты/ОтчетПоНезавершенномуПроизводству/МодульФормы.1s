Перем Таб;
Перем Обновить;
Перем Расшифровка;

Функция НайтиПодчиненноеСписание(Прод, Кстр, Заказ)
	
	Док = СоздатьОбъект("Документ");
	Док.ВыбратьПодчиненныеДокументы(Заказ.ДатаДок,, Заказ);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если (Док.Вид() = "СписаниеТМЦВПроизводство") и (Док.ПометкаУдаления() = 0) Тогда
			Если (Док.ПродукцияШ = Прод) и (Док.КодСтроки = КСтр) Тогда
				Возврат Док.ТекущийДокумент();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;		    
	Возврат "<>";
КонецФункции

Функция КолОжидаемое(Продукция, Заказ)
	
	КолОжидаемое = 0 ;
	
	Опер = СоздатьОбъект("Операция");
	Если Опер.НайтиОперацию(Заказ)=1 Тогда
		КвоПроводок = Опер.КоличествоПроводок();
		Для НомПроводки=1 по КвоПроводок Цикл
			Опер.ПолучитьПроводкуПоНомеру(НомПроводки);
			Если (Опер.Дебет.Субконто(1) = Заказ) И (Опер.Дебет.Субконто(2) = Продукция) И (Опер.Дебет.Счет.Код = "ОЖ") Тогда
				КолОжидаемое = КолОжидаемое + Опер.Количество;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат КолОжидаемое;
	
КонецФункции

Функция СтоимостьМатериаловВПроизводствеНаЕд(Субконто1, Субконто2, Списание)
	
	СтоимостьЕд = 0;
	Продукция = ?(фПродукция = 1,Субконто1,Субконто2);
	Заказ = ?(фПродукция=1,Субконто2,Субконто1);
	
	Заказ.ВыбратьСтроки();
	
	Пока Заказ.ПолучитьСтроку() = 1 Цикл
		Если Продукция = Заказ.Продукция Тогда
			Списание = НайтиПодчиненноеСписание(Продукция, Заказ.КодСтроки, Заказ);
			Если Списание <> "<>" Тогда
				КолОжидаемое = КолОжидаемое(Продукция, Заказ);
				СтоимостьЕд = ?(КолОжидаемое = 0, 0,   глПроизводственныеЗатратыПоДокументу(Продукция, Списание, 0)/КолОжидаемое);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтоимостьЕд;
	
КонецФункции

Процедура СоздатьРасшифровку()
	// все настройки помещаем в список
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", "ОтчетПоНезавершенномуПроизводству");
    
    Расшифровка.Установить("НаДату", НаДату);
	Расшифровка.Установить("фПродукция", фПродукция);
	
	Расшифровка.Установить("списТовар", списТовар);
	Расшифровка.Установить("списЗаказы", списЗаказы);	
	
КонецПроцедуры

Функция ПолучитьСписокТМЦ(пСписТовар)
	
	пСписокТМЦ = СоздатьОбъект("СписокЗначений");
	
	рс = СоздатьОбъект("ODBCRecordset");
	
	ТекстЗапроса = 
	"SELECT ТМЦ.ID [Ссылка $Справочник.ТМЦ]
	|FROM $Справочник.ТМЦ AS ТМЦ
	|WHERE (ТМЦ.ID IN (SELECT Val FROM #списТовар))";
	
	рс.УложитьСписокОбъектов(пСписТовар, "#списТовар","ТМЦ");
	
	Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);	
	
	Результат.Выгрузить(пСписокТМЦ);
	
	Возврат пСписокТМЦ;

КонецФункции
 
//*******************************************
Процедура Сформировать()
	СоздатьРасшифровку();
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ТипЗначенияСтр(Таб) = "Таблица" Тогда
	    Таб.Очистить();
	Иначе
		Таб = СоздатьОбъект("Таблица");
	КонецЕсли;
	Таб.ИсходнаяТаблица("Таблица");		
	Таб.ВывестиСекцию("Кнопки");
	
	Если фПродукция = 1 Тогда
		Если списТовар.РазмерСписка() > 0 Тогда
			Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ, списТовар, 2);
		Иначе
			Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,,1);		
		КонецЕсли;		
		Если списЗаказы.РазмерСписка() > 0 Тогда
			Ит.ИспользоватьСубконто(ВидыСубконто.Договора, списЗаказы, 2);
		Иначе
			Ит.ИспользоватьСубконто(ВидыСубконто.Договора,,1);
		КонецЕсли;
	Иначе		
		Если списЗаказы.РазмерСписка() > 0 Тогда
			Ит.ИспользоватьСубконто(ВидыСубконто.Договора, списЗаказы, 2);
		Иначе
			Ит.ИспользоватьСубконто(ВидыСубконто.Договора,,1);
		КонецЕсли;
		Если списТовар.РазмерСписка() > 0 Тогда
			Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ, ПолучитьСписокТМЦ(списТовар), 2,1);
		Иначе
			Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,,1);		
		КонецЕсли;	    
	КонецЕсли;
	Ит.Опции(1);
	Ит.ВыполнитьЗапрос(,НаДату, "ОЖ");
	
	СтрФ = "";
	ИтогоЗатраты = 0;
	глФильтры(СтрФ, списЗаказы, "Заказы ");
	глФильтры(СтрФ, списТовар, "Продукция ");
	Таб.ВывестиСекцию("Шапка");
	
	Ит.ВыбратьСубконто(1);
	Пока Ит.ПолучитьСубконто(1) = 1 Цикл
		
		Ит.ВыбратьСубконто(2);
		ТабИтогов = СоздатьОбъект("ТаблицаЗначений");
		ТабИтогов.НоваяКолонка("Субконто2");
		ТабИтогов.НоваяКолонка("Списание");
		ТабИтогов.НоваяКолонка("ОсталосьВыпустить");
		ТабИтогов.НоваяКолонка("МатЗатраты");
		
		Пока Ит.ПолучитьСубконто(2) = 1 Цикл
			Списание = "<>"; 
			
			МатЗатратыЕд = СтоимостьМатериаловВПроизводствеНаЕд(Ит.Субконто(1), Ит.Субконто(2), Списание);
			МатЗатраты = Окр(МатЗатратыЕд*Ит.СКД("К"),2);
			
			Если (МатЗатраты = 0) И (фНеВыводитьСтрокиБезЗатрат = 1) Тогда
				Продолжить;
			КонецЕсли;
			
			ИтогоЗатраты = ИтогоЗатраты + МатЗатраты;
			
			ТабИтогов.НоваяСтрока();
			ТабИтогов.Субконто2 = Ит.Субконто(2);
			ТабИтогов.Списание = Списание;
			ТабИтогов.ОсталосьВыпустить =Ит.СКД("К");
			ТабИтогов.МатЗатраты = МатЗатраты;
			
			
		КонецЦикла;
		
		//Группировка
		Если фНеВыводитьСтрокиБезЗатрат = 0 Тогда
			Таб.ВывестиСекцию("Заказ");
		ИначеЕсли (ТабИтогов.Итог("МатЗатраты") <> 0) И (фНеВыводитьСтрокиБезЗатрат = 1) Тогда
			Таб.ВывестиСекцию("Заказ");
		КонецЕсли;
		
		//Детальные записи
		ТабИтогов.ВыбратьСтроки();
		Пока ТабИтогов.ПолучитьСтроку() = 1 Цикл
			Таб.ВывестиСекцию("Товар");		
		КонецЦикла;
		
	КонецЦикла;
	
	ИтогоЗатраты = Окр(ИтогоЗатраты,2);
	
	Таб.ВывестиСекцию("Итоги");

	Таб.ОбластьПечати(2);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	
	Таб.Показать("Ожидаемый выпуск");
	
	Расшифровка.Установить("Обновить",0);	
	Если (Обновить = 2) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;	
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "Заказ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списЗаказы);
	ИначеЕсли Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		НаДату = глРасшифровка.Получить("НаДату");

		глРасшифровка.Получить("списТовар").Выгрузить(списТовар);
		глРасшифровка.Получить("списЗаказы").Выгрузить(списЗаказы);
		фПродукция = глРасшифровка.Получить("фПродукция");

		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
		НаДату = ТекущаяДата();
	КонецЕсли; 
КонецПроцедуры
