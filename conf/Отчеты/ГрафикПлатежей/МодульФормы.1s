// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
// используются для стандартного механизма кнопок "Обновить" и "Настройка"

Перем Язык;
Перем фВалюта;


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Функция РасполагаемыеСредства(Валюта = 0, ВГривнях = 0) 
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ПустоеЗначение(Валюта)=1 Тогда
		Ит.ВыполнитьЗапрос(Дата2,,"30;31",1,ВыбранныйПланСчетов(),ВыбФирма);
		Возврат Ит.СНД();
	КонецЕсли;

	Если Валюта = Гривня Тогда
		Ит.ВыполнитьЗапрос(Дата2,,"30.1;31.1;31.3",1,ВыбранныйПланСчетов(),ВыбФирма);
		Возврат Ит.СНД();
	Иначе
		Ит.ВыполнитьЗапрос(Дата2,,"30.2;31.2;31.4",1,ВыбранныйПланСчетов(),ВыбФирма);
		Ит.ВыбратьВалюты();
		Если Ит.ПолучитьВалюту(,Валюта)=1 Тогда
			Если ВГривнях = 1 Тогда
				Возврат Ит.СНД("С");     
			Иначе
				Возврат Ит.СНД("В");     
			КонецЕсли;
		Иначе
			Возврат 0;
		КонецЕсли;
	КонецЕсли;

КонецФункции

// ===============================
Процедура ДоступностьЭлементов(ВсеЭлементы=0)           
	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбФирма") Или (ЭлементДиалога="кХВыбФирма") Тогда
		Форма.кХВыбФирма.Доступность(ВыбФирма.Выбран()); 
	КонецЕсли;
	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбВалюта") Или (ЭлементДиалога="кХВыбВалюта") Тогда
		Форма.кХВыбВалюта.Доступность(ВыбВалюта.Выбран()); 
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура Сформировать(ЗакрытьЭкран=0)    
	Перем Запрос;					// запрос
	    
	//  Создание Таблицы для выходного отчета
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;

	ПечФорма = "Таблица";
	Язык = глЯзык(ПечФорма); 
	глУстПропись(Гривня,Язык);
    Таб.ИсходнаяТаблица(ПечФорма);
    	
	// все настройки помещаем в список
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", 				"ГрафикПлатежей");
    Расшифровка.Установить("Дата2", 				Дата2);
    Расшифровка.Установить("ВыбФирма", 				ВыбФирма);
    Расшифровка.Установить("ВыбВалюта", 			ВыбВалюта);
    
	Расшифровка.Установить("фУчГКПоставщиков", 		фУчГКПоставщиков); 
	Расшифровка.Установить("фУчГКПокупателям", 		фУчГКПокупателям); 
	Расшифровка.Установить("фДеталПоДокументам", 	фДеталПоДокументам); 
	
	фВалюта = 0;
	Если ВыбВалюта.Выбран()=1 Тогда
		Если ВыбВалюта <> Гривня Тогда
			фВалюта = 1;
		КонецЕсли;
	КонецЕсли;
	// формируем текст запроса
	Дата1 = Дата("01.01.1980");
	Если Дата2 >= ПолучитьДатуТА() Тогда
		ТекстЗапроса = "";
	Иначе 
		ТекстЗапроса = "Период с Дата1 по Дата2;";
	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса+"
		|Фирма		= Регистр.ВзаиморасчетыПокупателей.Фирма,		Регистр.ВзаиморасчетыПоставщиков.Фирма;
		|Контрагент	= Регистр.ВзаиморасчетыПокупателей.Контрагент,	Регистр.ВзаиморасчетыПоставщиков.Контрагент;
		|Валюта		= Регистр.ВзаиморасчетыПокупателей.Валюта,		Регистр.ВзаиморасчетыПоставщиков.Валюта;
		|КредДокум	= Регистр.ВзаиморасчетыПокупателей.КредДокумент,Регистр.ВзаиморасчетыПоставщиков.КредДокумент;

		|ДолгПокупОсн		= Регистр.ВзаиморасчетыПокупателей.ДолгОсн;
		|ДолгПостОсн		= Регистр.ВзаиморасчетыПоставщиков.ДолгОсн; 
		|Функция ДолгПокупателяОсн 	= КонОст(ДолгПокупОсн); 
		|Функция ДолгПоставщикаОсн 	= КонОст(ДолгПостОсн);";
		
	Если ВыбФирма.Выбран() = 1 Тогда
	    ТекстЗапроса = ТекстЗапроса+"Условие (Фирма=ВыбФирма);";
	КонецЕсли;        

	Если ВыбВалюта.Выбран() = 1 Тогда
	    ТекстЗапроса = ТекстЗапроса+"
			|ДолгПокуп			= Регистр.ВзаиморасчетыПокупателей.Долг;
			|ДолгПост			= Регистр.ВзаиморасчетыПоставщиков.Долг; 
			|Функция ДолгПокупателя 	= КонОст(ДолгПокуп); 
			|Функция ДолгПоставщика 	= КонОст(ДолгПост);
			|Условие (Валюта=ВыбВалюта);";
	КонецЕсли;        

	ТекстЗапроса = ТекстЗапроса+"
		|Группировка Контрагент;
		|Группировка КредДокум Упорядочить по КредДокум.ДатаДок,КредДокум.ВремяДок;"; 
    
	// выполняем запрос
	Запрос=СоздатьОбъект("Запрос");
    Если Запрос.Выполнить(ТекстЗапроса)=0 Тогда
		Предупреждение("Запрос по взаиморасчетам не выполнился!");
		Возврат;
	КонецЕсли;          
	
	ИтоговаяТабл = СоздатьОбъект("ТаблицаЗначений");
	ИтоговаяТабл.НоваяКолонка("Дата");  
	ИтоговаяТабл.НоваяКолонка("Контрагент");
	ИтоговаяТабл.НоваяКолонка("ДолгПоставщикаОсн");  
	ИтоговаяТабл.НоваяКолонка("ДолгПокупателяОсн"); 
	Если фВалюта = 1 Тогда
		ИтоговаяТабл.НоваяКолонка("ДолгПоставщика");  
		ИтоговаяТабл.НоваяКолонка("ДолгПокупателя"); 
	КонецЕсли;	
	Если фДеталПоДокументам = 1 Тогда
		ИтоговаяТабл.НоваяКолонка("КредДок");
	КонецЕсли;	
	
	ЧислоСтрок 	= 0;
	ЧислоДок 	= 0;
	Пока Запрос.Группировка("Контрагент") = 1 Цикл

		Контрагент = Запрос.Контрагент;
		Пока Запрос.Группировка("КредДокум") = 1 Цикл
			Док = Запрос.КредДокум;
			Если Док.Выбран()=0 Тогда
				Продолжить;
			КонецЕсли;	
			
			ЧислоДок = ЧислоДок + 1;
			Если ЧислоДок%10 = 0 Тогда
				Состояние("Обработано документов: "+Строка(ЧислоДок));
			КонецЕсли;	
			
			ДолгПокупателяОсн = Макс(0, Запрос.ДолгПокупателяОсн);
			ДолгПоставщикаОсн = Макс(0,-Запрос.ДолгПоставщикаОсн);
			Если фВалюта = 1 Тогда
				ДолгПокупателя = Макс(0, Запрос.ДолгПокупателя);
				ДолгПоставщика = Макс(0,-Запрос.ДолгПоставщика);
			КонецЕсли;

			Если (ДолгПокупателяОсн = 0) И (ДолгПоставщикаОсн = 0) Тогда // нет долгов или не отображаем отрицательные
				Продолжить;
			КонецЕсли;
			
            // есть долги, добавляем в таблицу
			ЧислоСтрок = ЧислоСтрок + 1;
			ИтоговаяТабл.НоваяСтрока();
			ИтоговаяТабл.ПолучитьСтрокуПоНомеру(ЧислоСтрок);

			ЕстьДатаОплаты=0;
			Если глЕстьРеквизитШапки("ДатаОплаты",Док.Вид()) = Да Тогда
				Если ПустоеЗначение(Док.ДатаОплаты) = 0 Тогда
					Если глЕстьРеквизитШапки("Контрагент",Док.Вид()) = Да Тогда
						Если Док.Контрагент = Контрагент Тогда
							ЕстьДатаОплаты = 1;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	               

			Если ЕстьДатаОплаты = 1 Тогда
				ДатаДолга = Док.ДатаОплаты;
			Иначе
				Если (фУчГКПокупателям = 1) И (ДолгПокупателяОсн <> 0) Тогда 
					ДатаДолга = Док.ДатаДок + Контрагент.Глубина.Получить(Док.ДатаДок);
				ИначеЕсли (фУчГКПоставщиков = 1) И (ДолгПоставщикаОсн <> 0) Тогда
					ДатаДолга = Док.ДатаДок + Контрагент.ГлубинаКредитаПоставщика.Получить(Док.ДатаДок);
				Иначе
					ДатаДолга = Док.ДатаДок;
				КонецЕсли;
			КонецЕсли;
			ИтоговаяТабл.Дата = Макс(ДатаДолга, Дата2);
			
			ИтоговаяТабл.Контрагент = Контрагент;
			ИтоговаяТабл.ДолгПокупателяОсн = ДолгПокупателяОсн;
			ИтоговаяТабл.ДолгПоставщикаОсн = ДолгПоставщикаОсн;
			Если фВалюта = 1 Тогда
				ИтоговаяТабл.ДолгПокупателя = ДолгПокупателя;
				ИтоговаяТабл.ДолгПоставщика = ДолгПоставщика;
			КонецЕсли;
			Если фДеталПоДокументам = 1 Тогда
				ИтоговаяТабл.КредДок = Док.ТекущийДокумент();
			КонецЕсли;	                                    
				
		КонецЦикла;	                
	КонецЦикла;	                
		
	КолонкиСвернуть = "ДолгПоставщикаОсн,ДолгПокупателяОсн"+?(фВалюта=0,"",",ДолгПоставщика,ДолгПокупателя");
	ИтоговаяТабл.   Свернуть("Дата,Контрагент"+?(фДеталПоДокументам = 1,",КредДок",""),КолонкиСвернуть);
	ИтоговаяТабл.Сортировать("Дата,Контрагент"+?(фДеталПоДокументам = 1,",КредДок",""));
	                              
	Если фДеталПоДокументам = 1 Тогда	// детализируем по документам
		// надо подготовить итоги по контрагентам
		ИтогиПоКонтрагентам = СоздатьОбъект("ТаблицаЗначений");
		ИтогиПоКонтрагентам.Загрузить(ИтоговаяТабл);
		ИтогиПоКонтрагентам.Свернуть("Дата,Контрагент",КолонкиСвернуть);
		ИтогиПоКонтрагентам.Сортировать("Дата,Контрагент","");
	КонецЕсли;	
	
	ИтогиПоДатам = СоздатьОбъект("ТаблицаЗначений");
	ИтогиПоДатам.Загрузить(?(фДеталПоДокументам = 1,ИтогиПоКонтрагентам,ИтоговаяТабл));
	ИтогиПоДатам.Свернуть("Дата",КолонкиСвернуть);
	ИтогиПоДатам.Сортировать("Дата","");
	
	// обнуляем счетчик числа строк, выведенных в таблицу отчета
	глЧислоСтрок=0;                                                          
	
	Таб.ВывестиСекцию("Кнопки");              
	    
	// выводим шапку отчета

	ОстатокРасполагаемойСуммыОсн 	= РасполагаемыеСредства(ВыбВалюта, 1);
	ОстатокРасполагаемойСуммы 		= РасполагаемыеСредства(ВыбВалюта);

	Заг = ?(Язык = "у","Починаючи з ","Начиная с ")+Дата2+". ";
	глЗаголовокФирма(ВыбФирма,Заг,Язык);
	Если ВыбВалюта.Выбран() = 1 Тогда
		Если ВыбВалюта = Гривня Тогда
			Если ОстатокРасполагаемойСуммыОсн = 0 Тогда
				Заг=Заг+?(Язык = "у","На "+ Дата2+" у гривнях не маємо коштів.","На "+ Дата2+" в гривнях нет денежных средств.");
			Иначе
				Заг=Заг+?(Язык = "у","На "+ Дата2+" у гривнях маємо коштів:","На "+ Дата2+" в гривнях имеются средства:");
				Заг = Заг + глФРМ(ОстатокРасполагаемойСуммыОсн,Гривня,1);
			КонецЕсли;
		Иначе
			Если ОстатокРасполагаемойСуммы = 0 Тогда
				Заг=Заг+?(Язык = "у","На "+ Дата2+" у валюті "+ВыбВалюта+" не маємо коштів.","На "+ Дата2+" в валюте "+ВыбВалюта+" нет денежных средств.");
			Иначе
				Заг=Заг+?(Язык = "у","На "+ Дата2+" у валюті "+ВыбВалюта+" маємо коштів:","На "+ Дата2+" в валюте "+ВыбВалюта+" имеются средства:");
				Заг = Заг + глФРМ(ОстатокРасполагаемойСуммы,ВыбВалюта,1);
				Если ОстатокРасполагаемойСуммыОсн <> 0 Тогда
					Заг = Заг + "("+глФРМ(ОстатокРасполагаемойСуммыОсн,Гривня,1)+")";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ОстатокРасполагаемойСуммыОсн = 0 Тогда
			Заг=Заг+?(Язык = "у","На "+ Дата2+" не маємо коштів.","На "+ Дата2+" нет денежных средств.");
		Иначе
			Заг=Заг+?(Язык = "у","На "+ Дата2+" маємо коштів:","На "+ Дата2+" имеются средства:");
			Заг = Заг + глФРМ(ОстатокРасполагаемойСуммыОсн,Гривня,1);
		КонецЕсли;
	КонецЕсли;
	Детал = "Дата / контрагент"+?(фДеталПоДокументам=0,"",?(Язык = "у"," / кредитний документ"," / кредитный документ"));

	Таб.ВывестиСекцию("Отчет");  
	Таб.ВывестиСекцию("Шапка"); 
	глОживить(Таб.ВысотаСекции("Отчет")+Таб.ВысотаСекции("Шапка"));
    
	ПоследняяДата = ПолучитьПустоеЗначение("Дата"); 
	ИтоговаяТабл.ВыбратьСтроки();
	Если фДеталПоДокументам = 1 Тогда	// детализируем по документам
		ИтогиПоКонтрагентам.ВыбратьСтроки();
	КонецЕсли;	                            
	ИтогиПоДатам.ВыбратьСтроки();
	
	Пока ИтоговаяТабл.ПолучитьСтроку()>0 Цикл
		Если ИтоговаяТабл.Дата<>ПоследняяДата Тогда
			ИтогиПоДатам.ПолучитьСтроку();
			ПоследняяДата = ИтоговаяТабл.Дата;
			ПоследнийКонтрагент = ПолучитьПустоеЗначение("Справочник.Контрагенты");
			Если ИтоговаяТабл.Дата=Дата2 Тогда
				ПечДата = ?(Язык = "у","Прострочена заборгованність","Просроченный долг");
			Иначе	
				ПечДата = ИтоговаяТабл.Дата;
			КонецЕсли;	            
			ПоступлениеОсн 					= ИтогиПоДатам.ДолгПокупателяОсн;
			ВыплатаОсн 						= ИтогиПоДатам.ДолгПоставщикаОсн;
			ОстатокРасполагаемойСуммыОсн	= ОстатокРасполагаемойСуммыОсн + ПоступлениеОсн - ВыплатаОсн;
			ПечПоступление 	= глФРМ(ПоступлениеОсн,					Гривня,1);
			ПечВыплата 		= глФРМ(ВыплатаОсн,						Гривня,1);
			ПечОстаток 		= глФРМ(ОстатокРасполагаемойСуммыОсн,	Гривня,1);
			Если фВалюта = 1 Тогда
				Поступление 				= ИтогиПоДатам.ДолгПокупателя;
				Выплата 					= ИтогиПоДатам.ДолгПоставщика;
				ОстатокРасполагаемойСуммы 	= ОстатокРасполагаемойСуммы + Поступление - Выплата;
				ПечПоступление 	= глФРМ(Поступление,				ВыбВалюта,1) + РазделительСтрок + ПечПоступление;
				ПечВыплата 		= глФРМ(Выплата,					ВыбВалюта,1) + РазделительСтрок + ПечВыплата;
				ПечОстаток 		= глФРМ(ОстатокРасполагаемойСуммы,	ВыбВалюта,1) + РазделительСтрок + ПечОстаток;
			КонецЕсли;
			Таб.ВывестиСекцию("Дата");
			глОживить(1);
		КонецЕсли;     

		Если ИтоговаяТабл.Контрагент<>ПоследнийКонтрагент Тогда
			ПоследнийКонтрагент = ИтоговаяТабл.Контрагент;
			ПечКонтрагент = ИтоговаяТабл.Контрагент;
			Если фДеталПоДокументам = 1 Тогда
				ИтогиПоКонтрагентам.ПолучитьСтроку();
				ПоступлениеОсн 	= ИтогиПоКонтрагентам.ДолгПокупателяОсн;
				ВыплатаОсн		= ИтогиПоКонтрагентам.ДолгПоставщикаОсн;
				Если фВалюта = 1 Тогда
					Поступление = ИтогиПоКонтрагентам.ДолгПокупателя;
					Выплата		= ИтогиПоКонтрагентам.ДолгПоставщика;
				КонецЕсли;
			Иначе    
				ПоступлениеОсн 	= ИтоговаяТабл.ДолгПокупателяОсн;
				ВыплатаОсн		= ИтоговаяТабл.ДолгПоставщикаОсн;
				Если фВалюта = 1 Тогда
					Поступление = ИтоговаяТабл.ДолгПокупателя;
					Выплата		= ИтоговаяТабл.ДолгПоставщика;
				КонецЕсли;
			КонецЕсли;	
			ПечПоступление 	= глФРМ(ПоступлениеОсн,	Гривня,1);
			ПечВыплата 		= глФРМ(ВыплатаОсн,		Гривня,1);
			Если фВалюта = 1 Тогда
				ПечПоступление 	= глФРМ(Поступление,ВыбВалюта,1) + РазделительСтрок + ПечПоступление;
				ПечВыплата 		= глФРМ(Выплата,	ВыбВалюта,1) + РазделительСтрок + ПечВыплата;
			КонецЕсли;
			Таб.ВывестиСекцию("Контрагент");
			глОживить(1);
		КонецЕсли;                                 

		Если фДеталПоДокументам = 1 Тогда
			ПоследнийКонтрагент = ИтоговаяТабл.Контрагент;
			ПечДокумент = глДокументВОтчете(ИтоговаяТабл.КредДок,"с номером","с датой",Язык);
			ПоступлениеОсн 	= ИтоговаяТабл.ДолгПокупателяОсн;
			ВыплатаОсн 		= ИтоговаяТабл.ДолгПоставщикаОсн;
			ПечПоступление 	= глФРМ(ПоступлениеОсн,	Гривня,1);
			ПечВыплата 		= глФРМ(ВыплатаОсн,		Гривня,1);
			Если фВалюта = 1 Тогда
				Поступление = ИтоговаяТабл.ДолгПокупателя;
				Выплата 	= ИтоговаяТабл.ДолгПоставщика;
				ПечПоступление 	= глФРМ(Поступление,ВыбВалюта,1) + РазделительСтрок + ПечПоступление;
				ПечВыплата 		= глФРМ(Выплата,	ВыбВалюта,1) + РазделительСтрок + ПечВыплата;
			КонецЕсли;
			Док = ИтоговаяТабл.КредДок;
			Таб.ВывестиСекцию("Документ");
			глОживить(1);
		КонецЕсли;	                                    
	КонецЦикла;	

	ФиксКвоСтрок = Таб.ВысотаСекции("Кнопки")+Таб.ВысотаСекции("Отчет")+Таб.ВысотаСекции("Шапка");
	ФиксКвоКолон = 3;
	Таб.Опции(0, 0, ФиксКвоСтрок, ФиксКвоКолон, "ПарамПечатиУпрГрафикПлатежей", "РазмОкнаУпрГрафикПлатежей");
	Таб.ОбластьПечати(Таб.ВысотаСекции("Кнопки")+1);

	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Таб.Показать("ПЕЧАТЬ: График платежей"+?(ТипЗначения(ВыбФирма)=0, "", " ("+ВыбФирма+")"),"");

	Если (Обновить = 2) Или (ЗакрытьЭкран=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры    	// Сформировать


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()	// Предопределенная процедура
	                           
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки отчета из списка
		Дата2 					= глРасшифровка.Получить("Дата2");
		ВыбФирма 				= глРасшифровка.Получить("ВыбФирма");
		ВыбВалюта				= глРасшифровка.Получить("ВыбВалюта");

		фУчГКПоставщиков 		= глРасшифровка.Получить("фУчГКПоставщиков");
		фУчГКПокупателям		= глРасшифровка.Получить("фУчГКПокупателям");
		фДеталПоДокументам 		= глРасшифровка.Получить("фДеталПоДокументам");

		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
    ДоступностьЭлементов(1);

КонецПроцедуры		// ПриОткрытии


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

Дата2 = РабочаяДата();

фУчГКПоставщиков = 1; 
фУчГКПокупателям = 1;
