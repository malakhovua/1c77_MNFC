Перем КодыОпераций;
Перем Исключить;
Перем Расшифровка;
Перем Обновить;
Перем Таб;
Функция ТаблицаДолгов() Далее
Процедура Сформировать() Далее

Процедура ПриОткрытии()
	
	Если глФлагРасшифровки = 1 Тогда 
		
		Обновить = глОбновить;
		
		ВыбНачПериода = глРасшифровка.Получить("ВыбНачПериода");
		ВыбКонПериода = глРасшифровка.Получить("ВыбКонПериода");
		глРасшифровка.Получить("списТМЦ").Выгрузить(списТМЦ);
		глРасшифровка.Получить("списТМЦИсключить").Выгрузить(списТМЦИсключить);
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;

	Форма.КнопкаПоУмолчанию("Сформировать");
	
КонецПроцедуры

///////////////////////////////////Расшифровка//////////////////////////////////////////////////////////////

Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	
	Возврат Расшифровка;
	
КонецФункции

///////////////////////////////////Работа со списками отбора и подбором///////////////////////////////////////

Процедура ДобавитьВСписок(Элт, Спис)
	
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	
	Если Элт.Вид() = "ТМЦ" Тогда
		Если Исключить = 1 Тогда
			ДобавитьвСписок(Элт, списТМЦИсключить);
		Иначе
			ДобавитьвСписок(Элт, списТМЦ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, Искл = 0)
	
	Исключить = Искл;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
	
КонецПроцедуры

//*******************************************
// Процедура генерации отчета Сформировать.
//
Процедура Сформировать()
	
	Перем Запрос, ТекстЗапроса, Долг;
	
	Если Обновить = 2 Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
	//Расшифровка
	Расшифровка = СоздатьОбъект("СписокЗначений");
	
	Расшифровка.Установить("ВыбНачПериода", ВыбНачПериода);
	Расшифровка.Установить("ВыбКонПериода", ВыбКонПериода);
	
	Расшифровка.Установить("списТМЦ", списТМЦ);
	Расшифровка.Установить("списТМЦИсключить", списТМЦИсключить);
		
	Расшифровка.Установить("Отчет", "Отчет_ПоЗакупкеСырья");
	Расшифровка.Установить("Обновить");
	
	// Подготовка к заполнению выходных форм данными запроса
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить=0) Тогда
		Таб = СоздатьОбъект("Таблица");
	Иначе
		Таб.Очистить();
	КонецЕсли;
		
	ДолгИтого = 0;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ВыбНачПериода по ВыбКонПериода;
	|ОбрабатыватьДокументы все;
	|Фирма = Регистр.Обороты.Фирма;
	|Поставщик = Регистр.Обороты.Поставщик;
	|ТМЦ = Регистр.Обороты.ТМЦ;
	|ПриходКво = Регистр.Обороты.ПриходКво;
	|ПриходСум = Регистр.Обороты.ПриходСум;
	|КодОперации = Регистр.Обороты.КодОперации;
	|Функция КоличествоЗакупки = Сумма(ПриходКво);
	|Функция СтоимостьЗакупки = Сумма(ПриходСум);
	|Группировка ТМЦ " + ?(фБезГрупп=1,"без групп","") + ";
	|Группировка Поставщик без групп;
	|Условие(КодОперации В КодыОпераций);
	|Условие(ПустоеЗначение(Поставщик) = 0);" +
	?(списТМЦ.РазмерСписка()=0,"", "
	|Условие(ТМЦ В СписТМЦ);") + 
	?(списТМЦИсключить.РазмерСписка()=0,"", "
	|Условие (НЕ(ТМЦ В СписТМЦисключить));") + "
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//Подготовим таблицу контрагентов для расчета итогового долга
	ТабКонтрагентов = СоздатьОбъект("ТаблицаЗначений");
	Запрос.Выгрузить(ТабКонтрагентов,,0);
	
	//Без повторяющихся позиций
	ТабКонтрагентов.Свернуть("Поставщик", "КоличествоЗакупки");
	
	ТабДолги = ТаблицаДолгов(); //Долги на конец периода
	
	//Подсчитаем долг итого по контрагентам из таблицы оброты
	ТабКонтрагентов.ВыбратьСтроки();
	
	Пока ТабКонтрагентов.ПолучитьСтроку() = 1 Цикл
		//Список-фильтр
		Фильтр = СоздатьОбъект("СписокЗначений");
		Фильтр.ДобавитьЗначение(Константа.БазФирма);
		Фильтр.ДобавитьЗначение(ТабКонтрагентов.Поставщик);
		
		ТабДолги.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
		ТабДолги.ВыбратьСтроки("Ключ_индекс");
		
		Пока ТабДолги.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
			ДолгИтого = ДолгИтого + ТабДолги.СуммаДолгОстаток; 
		КонецЦикла;
		
	КонецЦикла;
	
	// Подготовка к заполнению выходных форм данными запроса
	Таб.ИсходнаяТаблица("ТабОтчета");
	
	//Кнопки
	Таб.ВывестиСекцию("Кнопки");
	
	// Заполнение полей "Заголовок"
	Таб.ВывестиСекцию("Заголовок");
	Состояние("Заполнение выходной таблицы...");
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0);
	Пока Запрос.Группировка(1) = 1 Цикл
		Цена = 0;
		Уровень = Запрос.ТМЦ.Уровень;
		Значение = Запрос.ТМЦ;	
		ФорматЗначение = глСмещениеГруппыПоРеквизиту(Значение, Мин(3, фБезГрупп)-1, Уровень) + Значение;
		
		// Заполнение полей ТМЦ
		Если Запрос.ТМЦ.ЭтоГруппа = 1 Тогда
			Таб.ВывестиСекцию("ТМЦ_Группа");
		Иначе
			Цена = ?(Запрос.КоличествоЗакупки=0,0,Запрос.СтоимостьЗакупки/Запрос.КоличествоЗакупки);
			Таб.ВывестиСекцию("ТМЦ");
		КонецЕсли;
		
		Пока Запрос.Группировка(2) = 1 Цикл
			
			//Сумма долга п контрагенту
			//Список-фильтр
			Фильтр = СоздатьОбъект("СписокЗначений");
			Фильтр.ДобавитьЗначение(Запрос.Фирма);
			Фильтр.ДобавитьЗначение(Запрос.Поставщик);
			
			ТабДолги.УстановитьФильтр(Фильтр,Фильтр, "Ключ_индекс",0);
			ТабДолги.ВыбратьСтроки("Ключ_индекс");
			
			Пока ТабДолги.ПолучитьСтроку("Ключ_индекс") = 1 Цикл
				Долг = ТабДолги.СуммаДолгОстаток; 
			КонецЦикла;
			
			Цена = ?(Запрос.КоличествоЗакупки=0,0,Запрос.СтоимостьЗакупки/Запрос.КоличествоЗакупки);
			
			// Заполнение полей Поставщик
			Таб.ВывестиСекцию("Поставщик");
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Заполнение полей "Итого"
	Таб.ВывестиСекцию("Итого");
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Сформировать", "");
	
КонецПроцедуры  

//*******************************************
// Функция генерации запроса ТаблицаДолгов.
//
Функция ТаблицаДолгов()
	
	ТабДолги = СоздатьОбъект("ИндексированнаяТаблица");

	рс = СоздатьОбъект("ODBCRecordset");
	
	ТекстЗапроса = "
		|SELECT ВзаиморасчетыПоставщиковОстатки.Фирма [Фирма $Справочник.Фирмы]
		|	, ВзаиморасчетыПоставщиковОстатки.Контрагент [Контрагент $Справочник.Контрагенты]
		|   , ВзаиморасчетыПоставщиковОстатки.Собственник [Собственник $Справочник.Контрагенты]
		|	, Sum(ВзаиморасчетыПоставщиковОстатки.ДолгОснОстаток) СуммаДолгОстаток
		|FROM $РегистрОстатки.ВзаиморасчетыПоставщиков(:ВыбКонПериода~,,,
		|		(Фирма, Контрагент),
		|		ДолгОсн) AS ВзаиморасчетыПоставщиковОстатки
		|GROUP BY ВзаиморасчетыПоставщиковОстатки.Фирма
		|	, ВзаиморасчетыПоставщиковОстатки.Контрагент
		|";
	рс.УстановитьТекстовыйПараметр("ВыбКонПериода", ВыбКонПериода);
	рс.ВыполнитьИнструкцию(ТекстЗапроса, ТабДолги,1);
	ТабДолги.ДобавитьИндекс("Ключ_индекс","Фирма, Контрагент");
	
	Возврат ТабДолги;
	
КонецФункции



КодыОпераций = СоздатьОбъект("СписокЗначений");
КодыОпераций.ДобавитьЗначение("З");
КодыОпераций.ДобавитьЗначение("п");
