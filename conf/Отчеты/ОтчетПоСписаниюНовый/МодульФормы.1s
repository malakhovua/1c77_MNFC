//*******************************************
Процедура Сформировать()
	Таб = СоздатьОбъект("Таблица");
	СтрФ = "";
	глФильтры(СтрФ, списТовар, "ТМЦ");
	КонДата = Мин(КонДата, ПолучитьДатуТА());
	НачДата = Мин(НачДата, КонДата);
	ТекстЗ = 
	"//{{ЗАПРОС(Сформировать)
	|Период с НачДата по КонДата;
	|ТМЦ = Регистр.Остатки.ТМЦ;
	|МестоХранения = Регистр.Остатки.МестоХранения;
	|ТекДок = Регистр.Остатки.ТекущийДокумент;
	|ОстатокТовара = Регистр.Остатки.ОстатокТовара;
	|Функция ОТР = Расход(ОстатокТовара);
	|Группировка МестоХранения упорядочить по МестоХранения.Наименование без групп;
	|Группировка ТМЦ упорядочить по ТМЦ.Наименование без групп;" + 
	?(фПоДок = 1, "
	|Группировка Документ;", "") + "
	|Условие (ТМЦ в списТовар);
	|Условие (ТекДок.Вид() = ""СписаниеТМЦ"");
	|"//}}ЗАПРОС
	;
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Товар", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("ТоварС", "Справочник.ТМЦ");
	ТЗ.НоваяКолонка("Документ", "Документ");
	ТЗ.НоваяКолонка("Ключ", "Число");
	ТЗ.НоваяКолонка("Итог", "Число");
	СтрС = "Итог";

	СписСкладов = СоздатьОбъект("СписокЗначений");
	
	Запр = СоздатьОбъект("Запрос");
	Если Запр.Выполнить(ТекстЗ) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока Запр.Группировка(1) = 1 Цикл
		СписСкладов.ДобавитьЗначение(Запр.МестоХранения);
		К = Строка(СписСкладов.РазмерСписка());
		ТЗ.НоваяКолонка("К"+К, "Число");
		СтрС = СтрС + ",К"+К;
		Пока Запр.Группировка(2) = 1 Цикл
			ТЗ.НоваяСтрока();
			ТЗ.Товар = Запр.ТМЦ;
			ТЗ.ТоварС = Запр.ТМЦ.Наименование;
			ТЗ.Итог = Запр.ОТР;
			ТЗ.Ключ = 1;
			ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, "К"+К, Запр.ОТР);
			ТЗ.Ключ = 1;
			Пока Запр.Группировка(3) = 1 Цикл				
				ТЗ.НоваяСтрока();
				ТЗ.Товар = Запр.ТМЦ;
				ТЗ.ТоварС = Запр.ТМЦ.Наименование;
				ТЗ.Итог = Запр.ОТР;
				ТЗ.Документ = Запр.Документ;
				ТЗ.Ключ = 2;
				ТЗ.УстановитьЗначение(ТЗ.НомерСтроки, "К"+К, Запр.ОТР);
			КонецЦикла;					
		КонецЦикла;		
	КонецЦикла;
	
	ТЗ.Свернуть("Товар,ТоварС,Документ,Ключ", СтрС);
	ТЗ.Сортировать("ТоварС,Товар*,Документ,Ключ");
	
	Таб.ВывестиСекцию("Шапка|Основная");
	Для Инд = 1 По СписСкладов.РазмерСписка() Цикл
		Таб.ПрисоединитьСекцию("Шапка|Склад");
	КонецЦикла;
	Таб.ПрисоединитьСекцию("Шапка|Ит");
	
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		ИС = ?(ТЗ.Ключ = 1, "Строка", "Док");
		Таб.ВывестиСекцию(ИС + "|Основная");
		Для Инд = 1 По СписСкладов.РазмерСписка() Цикл
			Таб.ПрисоединитьСекцию(ИС + "|Склад");
		КонецЦикла;
		Таб.ПрисоединитьСекцию(ИС + "|Ит");
	КонецЦикла;
	
	ТЗ.Свернуть("Ключ", СтрС);
	Попытка
		ТЗ.ПолучитьСтрокуПоНомеру(1);
		Таб.ВывестиСекцию("Итого|Основная");
		Для Инд = 1 По СписСкладов.РазмерСписка() Цикл
			Таб.ПрисоединитьСекцию("Итого|Склад");
		КонецЦикла;
		Таб.ПрисоединитьСекцию("Итого|Ит");			
	Исключение КонецПопытки;
	
	Таб.ПараметрыСтраницы(2);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчёт по списанию");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры
