Перем Расшифровка;
Перем Обновить;
Перем мТаблица;
Перем мТаблОтборов;

//******************************************************************************
//	
Функция ДвижениеКурсора(пФлОбновления=0)
	Перем Список,Зн,Стр;
	
	Если (Форма.АктивныйЭлемент()="лСпЗначенийОтборов") и (пФлОбновления=0) Тогда
		Возврат "";
	КонецЕсли;
	лСпЗначенийОтборов.УдалитьВсе();
	Если лСпОтборов.ТекущаяСтрока()>0 Тогда
		лТекВидОтбора=лСпОтборов.ПолучитьЗначение(лСпОтборов.ТекущаяСтрока());
		мТаблОтборов.ВыбратьСтроки();
		Пока мТаблОтборов.ПолучитьСтроку()=1 Цикл
		    Если мТаблОтборов.ВидОтбора=лТекВидОтбора Тогда
		    	лСпЗначенийОтборов.ДобавитьЗначение(мТаблОтборов.Отбор);
		    КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции // ДвижениеКурсора

//******************************************************************************
//	
Процедура фДобавитьЗначениеОтбора()
	
	Если лСпОтборов.ТекущаяСтрока()>0 Тогда
		лТекВидОтбора=лСпОтборов.ПолучитьЗначение(лСпОтборов.ТекущаяСтрока());
		лВыбЗн="";
		Если лТекВидОтбора="КорСчета" Тогда
			Если ВвестиЗначение(лВыбЗн,"Выберите кор. счет","Счет")=0 Тогда Возврат; КонецЕсли;
		Иначе //Значит субконто
			лТипЗн=лТекВидОтбора.ТипСубконто();
			Если ВвестиЗначение(лВыбЗн,"Выберите значение отбора",лТипЗн)=0 Тогда Возврат; КонецЕсли;
		КонецЕсли;
		Если ПустоеЗначение(лВыбЗн)=0 Тогда
			мТаблОтборов.НоваяСтрока();
			мТаблОтборов.ВидОтбора=лТекВидОтбора;
			мТаблОтборов.Отбор=лВыбЗн;
			мТаблОтборов.Свернуть("ВидОтбора,Отбор","");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // фДобавитьЗначениеОтбора

//******************************************************************************
//	
Процедура фУдалитьЗначениеОтбора(пФлВсе=0)

	Если лСпОтборов.ТекущаяСтрока()>0 Тогда
		лТекВидОтбора=лСпОтборов.ПолучитьЗначение(лСпОтборов.ТекущаяСтрока());
		Если пФлВсе=1 Тогда
			Для Ы=-мТаблОтборов.КоличествоСтрок() По -1 Цикл
				мТаблОтборов.ПолучитьСтрокуПоНомеру(-Ы);
			    Если мТаблОтборов.ВидОтбора=лТекВидОтбора Тогда
					мТаблОтборов.УдалитьСтроку();
			    КонецЕсли;
			КонецЦикла;
		ИначеЕсли лСпЗначенийОтборов.ТекущаяСтрока()>0 Тогда
			лТекЗн=лСпЗначенийОтборов.ПолучитьЗначение(лСпЗначенийОтборов.ТекущаяСтрока());
			мТаблОтборов.ВыбратьСтроки();
			Пока мТаблОтборов.ПолучитьСтроку()=1 Цикл
			    Если (мТаблОтборов.ВидОтбора=лТекВидОтбора) и (мТаблОтборов.Отбор=лТекЗн) Тогда
			    	мТаблОтборов.УдалитьСтроку();
					Прервать;
			    КонецЕсли;
			КонецЦикла;
			ДвижениеКурсора(1);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // фУдалитьЗначениеОтбора

//******************************************************************************
//	
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//*******************************************
Процедура Сформировать(ФлагЗакрытияФормы = 0)

	лСтолбец=фСтолбцы.ПолучитьЗначение(фСтолбцы.ТекущаяСтрока());
	лСтрока=фСтроки.ПолучитьЗначение(фСтроки.ТекущаяСтрока());
	Если ПустоеЗначение(фСчет)=1 Тогда
		Сообщить("Укажите счет отчета"); СтатусВозврата(0);
	КонецЕсли;
	Если ПустоеЗначение(лСтолбец)+ПустоеЗначение(лСтрока)=2 Тогда
		Сообщить("Не указан не один параметр вывода", "!"); СтатусВозврата(0);
	КонецЕсли;
	Если ПустоеЗначение(НачДата)+ПустоеЗначение(КонДата)>0 Тогда
		Сообщить("Не указан период формирования отчета", "!"); СтатусВозврата(0);
	КонецЕсли;
	Если СтатусВозврата()=0 Тогда Возврат; КонецЕсли;
	
	лНачДата=Мин(НачДата,КонДата);
	лКонДата=Макс(НачДата,КонДата);
	Если лНачДата>КонецРассчитанногоПериодаБИ() Тогда
		Сообщить("За указанный период нет данных", "!");
		Возврат;
	КонецЕсли;
	лКонДата=Мин(лКонДата,КонецРассчитанногоПериодаБИ());
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", РасположениеФайла());
	Расшифровка.Установить("НачДата", НачДата);
	Расшифровка.Установить("КонДата", КонДата);
	Расшифровка.Установить("фСчет", фСчет);
	Расшифровка.Установить("фДК", фДК);
	лПериодичность=фПериодичность.ПолучитьЗначение(фПериодичность.ТекущаяСтрока());
	Расшифровка.Установить("фСтолбцы", лСтолбец);
	Расшифровка.Установить("фСтроки", лСтрока);
	Расшифровка.Установить("фПериодичность", лПериодичность);
	Расшифровка.Установить("мТаблОтборов", мТаблОтборов);
	лОперация=СоздатьОбъект("Операция");

	Если (ТипЗначенияСтр(мТаблица) <> "Таблица") или (Обновить = 0) Тогда
		мТаблица = СоздатьОбъект("Таблица");
	Иначе
	 	мТаблица.Очистить();
	КонецЕсли;
	мТаблица.ИсходнаяТаблица("Таблица");
	мТаблица.ВывестиСекцию("Кнопки");
	
	//Обработаем отборы
	лТаблОтборов=СоздатьОбъект("ТаблицаЗначений");
	мТаблОтборов.Выгрузить(лТаблОтборов);
	лТаблОтборов.НоваяКолонка("Фл","Число",1,0);
	лТаблОтборов.Сортировать("ВидОтбора");
	лСтрокаОтбора="";
	лСпКорСчетов=СоздатьОбъект("СписокЗначений");
	лСпОтборовСтроки=СоздатьОбъект("СписокЗначений");
	лСпОтборовСтолбца=СоздатьОбъект("СписокЗначений");
	Если лТаблОтборов.КоличествоСтрок()>0 Тогда
		лСтрокаОтбора="Отборы:";
		лТекВидОбора="";
		лТаблОтборов.ВыбратьСтроки();
		Пока лТаблОтборов.ПолучитьСтроку()=1 Цикл
		    Если лТекВидОбора<>лТаблОтборов.ВидОтбора Тогда
		    	лТекВидОбора=лТаблОтборов.ВидОтбора;
				лСтрокаОтбора=лСтрокаОтбора+"
				|"+СокрЛП(?(лТекВидОбора="КорСчета","Кор. счета",лТекВидОбора))+": ";
			КонецЕсли;
			лСтрокаОтбора=лСтрокаОтбора+СокрЛП(лТаблОтборов.Отбор)+",";
			Если лТекВидОбора="КорСчета" Тогда
				лСпКорСчетов.ДобавитьЗначение(лТаблОтборов.Отбор);
				лТаблОтборов.Фл=1;
			КонецЕсли;
			Если лТаблОтборов.ВидОтбора=лСтрока Тогда
				лСпОтборовСтроки.ДобавитьЗначение(лТаблОтборов.Отбор);
				лТаблОтборов.Фл=1;
			КонецЕсли;
			Если лТаблОтборов.ВидОтбора=лСтолбец Тогда
				лСпОтборовСтолбца.ДобавитьЗначение(лТаблОтборов.Отбор);
				лТаблОтборов.Фл=1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	лБИ=СоздатьОбъект("БухгалтерскиеИтоги");
	лБИ.ВключатьСубсчета(0,1);
	лПериод="Период";лРазвКорСчет=0;лКолвоСубконто=0;
	Если лСтрока="Период" Тогда
		лПериод=лПериодичность;
	ИначеЕсли лСтрока="КорСчета" Тогда
		лРазвКорСчет=2;
	ИначеЕсли лСтрока<>"" Тогда //значит разворачивание по субконто
		Если лСпОтборовСтроки.РазмерСписка()>0 Тогда
			лБИ.ИспользоватьСубконто(лСтрока,лСпОтборовСтроки,2);
		Иначе
			лБИ.ИспользоватьСубконто(лСтрока,,1);
		КонецЕсли;
    	лКолвоСубконто=1;
	КонецЕсли;
	
	Если лСтолбец="Период" Тогда
		лПериод=лПериодичность;
	ИначеЕсли лСтолбец="КорСчета" Тогда
		лРазвКорСчет=1;
	ИначеЕсли лСтолбец<>"" Тогда //значит разворачивание по субконто
		Если лСпОтборовСтолбца.РазмерСписка()>0 Тогда
			лБИ.ИспользоватьСубконто(лСтолбец,лСпОтборовСтолбца,2);
		Иначе
			лБИ.ИспользоватьСубконто(лСтолбец);
		КонецЕсли;
		лКолвоСубконто=лКолвоСубконто+1;
	КонецЕсли;
	
	лТаблОтборов.Сортировать("Фл");
	Для А=-лТаблОтборов.КоличествоСтрок() По -1 Цикл
		лТаблОтборов.ПолучитьСтрокуПоНомеру(-А);
		Если лТаблОтборов.Фл=1 Тогда
			лТаблОтборов.УдалитьСтроку();
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	лТаблОтборов.УдалитьКолонку("Фл");
	Если лТаблОтборов.КоличествоСтрок()>0 Тогда //значит остались фильтры по видам субконто
		лТаблОтборов.Сортировать("ВидОтбора");
		лТекВид="";
		лСпСубконто=СоздатьОбъект("СписокЗначений");
		лСпСубконто1=СоздатьОбъект("СписокЗначений");
		лСпСубконто2=СоздатьОбъект("СписокЗначений");
		лСпСубконто3=СоздатьОбъект("СписокЗначений");
		лСпСубконто4=СоздатьОбъект("СписокЗначений");
		лСпСубконто5=СоздатьОбъект("СписокЗначений");
		лТекСп=1;
		лТаблОтборов.ВыбратьСтроки();
		Пока лТаблОтборов.ПолучитьСтроку()=1 Цикл
		    Если лТекВид<>лТаблОтборов.ВидОтбора Тогда
		    	Если лСпСубконто.РазмерСписка()>0 Тогда
					Если лТекСп=1 Тогда
						лСпСубконто.Выгрузить(лСпСубконто1);
						лБИ.ИспользоватьСубконто(лТекВид,лСпСубконто1,2);
					ИначеЕсли лТекСп=2 Тогда
						лСпСубконто.Выгрузить(лСпСубконто2);
						лБИ.ИспользоватьСубконто(лТекВид,лСпСубконто2,2);
					ИначеЕсли лТекСп=3 Тогда
						лСпСубконто.Выгрузить(лСпСубконто3);
						лБИ.ИспользоватьСубконто(лТекВид,лСпСубконто3,2);
					ИначеЕсли лТекСп=4 Тогда
						лСпСубконто.Выгрузить(лСпСубконто4);
						лБИ.ИспользоватьСубконто(лТекВид,лСпСубконто4,2);
					ИначеЕсли лТекСп=5 Тогда
						лСпСубконто.Выгрузить(лСпСубконто5);
						лБИ.ИспользоватьСубконто(лТекВид,лСпСубконто5,2);
					КонецЕсли;
					лТекСп=лТекСп+1;
				КонецЕсли;
				лСпСубконто.УдалитьВсе();
				лТекВид=лТаблОтборов.ВидОтбора;
			КонецЕсли;
			лСпСубконто.ДобавитьЗначение(лТаблОтборов.Отбор);
		КонецЦикла;
		Если лСпСубконто.РазмерСписка()>0 Тогда
			лБИ.ИспользоватьСубконто(лТекВид,лСпСубконто,2);
		КонецЕсли;
	КонецЕсли;
	
	Если лБИ.ВыполнитьЗапрос(лНачДата,лКонДата,фСчет,лСпКорСчетов,,3,лПериод)=1 Тогда

		лТаблСтрок=СоздатьОбъект("ТаблицаЗначений");
		лТаблСтрок.НоваяКолонка("Стр");
		лТаблСтрок.НоваяКолонка("Сумма","Число",15,2);
		
		Если лСтрока="Период" Тогда
			лБИ.ВыбратьПериоды();
			Пока лБИ.ПолучитьПериод()=1 Цикл
				лТаблСтрок.НоваяСтрока();
				Если лПериод="Операция" Тогда
					лТаблСтрок.Стр=лБИ.Операция.Документ.ТекущийДокумент();
				Иначе
					лТаблСтрок.Стр=лБИ.НачДата;
				КонецЕсли;
				лТаблСтрок.Сумма=?(фДК=1,лБИ.КО(),лБИ.ДО());
			КонецЦикла;
		ИначеЕсли лСтрока="КорСчета" Тогда
			лБИ.ВыбратьКорСчета();
			Пока лБИ.ПолучитьКорСчет()=1 Цикл
				Если лБИ.КорСчет.ЭтоГруппа()=1 Тогда Продолжить; КонецЕсли;
				лСумма=?(фДК=1,лБИ.КорКО(),лБИ.КорДО());
				Если лСумма=0 Тогда	Продолжить;	КонецЕсли;
				лТаблСтрок.НоваяСтрока();
				лТаблСтрок.Стр=лБИ.КорСчет;
				лТаблСтрок.Сумма=?(фДК=1,лБИ.КорКО(),лБИ.КорДО());
			КонецЦикла;
		ИначеЕсли лСтрока<>"" Тогда
			лБИ.ВыбратьСубконто(1);
			Пока лБИ.ПолучитьСубконто(1)=1 Цикл
				лСумма=?(фДК=1,лБИ.КО(),лБИ.ДО());
				Если лСумма=0 Тогда Продолжить; КонецЕсли;
				лТаблСтрок.НоваяСтрока();
				лТаблСтрок.Стр=лБИ.Субконто(1);
				лТаблСтрок.Сумма=лСумма;
			КонецЦикла;
		КонецЕсли;
		
		лТаблКолонок=СоздатьОбъект("ТаблицаЗначений");
		лТаблКолонок.НоваяКолонка("Столбец");
		лТаблКолонок.НоваяКолонка("Сумма","Число",15,2);
		
		Если лСтолбец="Период" Тогда
			лБИ.ВыбратьПериоды();
			Пока лБИ.ПолучитьПериод()=1 Цикл
				лТаблКолонок.НоваяСтрока();
				Если лПериод="Операция" Тогда
					лТаблКолонок.Столбец=лБИ.Операция.Документ.ТекущийДокумент();
				Иначе
					лТаблКолонок.Столбец=лБИ.НачДата;
				КонецЕсли;
				лТаблКолонок.Сумма=?(фДК=1,лБИ.КО(),лБИ.ДО());
			КонецЦикла;
		ИначеЕсли лСтолбец="КорСчета" Тогда
			лБИ.ВыбратьКорСчета();
			Пока лБИ.ПолучитьКорСчет()=1 Цикл
				Если лБИ.КорСчет.ЭтоГруппа()=1 Тогда Продолжить; КонецЕсли;
				лСумма=?(фДК=1,лБИ.КорКО(),лБИ.КорДО());
				Если лСумма=0 Тогда Продолжить; КонецЕсли;
				лТаблКолонок.НоваяСтрока();
				лТаблКолонок.Столбец=лБИ.КорСчет;
				лТаблКолонок.Сумма=?(фДК=1,лБИ.КорКО(),лБИ.КорДО());
			КонецЦикла;
		ИначеЕсли лСтолбец<>"" Тогда
			лБИ.ВыбратьСубконто(лКолвоСубконто);
			Пока лБИ.ПолучитьСубконто(лКолвоСубконто)=1 Цикл
				лСумма=?(фДК=1,лБИ.КО(),лБИ.ДО());
				Если лСумма=0 Тогда Продолжить; КонецЕсли;
				лТаблКолонок.НоваяСтрока();
				лТаблКолонок.Столбец=лБИ.Субконто(лКолвоСубконто);
				лТаблКолонок.Сумма=лСумма;
			КонецЦикла;
		КонецЕсли;
		
		мТаблица.ВывестиСекцию("Заголовок|ТаблШапка");
		лТаблКолонок.ВыбратьСтроки();
		Пока лТаблКолонок.ПолучитьСтроку()=1 Цикл
			лЗн=лТаблКолонок.Столбец;
			мТаблица.ПрисоединитьСекцию("Заголовок|ТаблКолонка");
		КонецЦикла;
		мТаблица.ПрисоединитьСекцию("Заголовок|ТаблИтог");
		
		лТаблСтрок.ВыбратьСтроки();
		Пока лТаблСтрок.ПолучитьСтроку()=1 Цикл
			лЗн=лТаблСтрок.Стр;
			мТаблица.ВывестиСекцию("ТаблСтрока|ТаблШапка");
			Если лСтрока="Период" Тогда
				Если лПериод="Операция" Тогда
					лБИ.ВыбратьПериоды();
					Пока лБИ.ПолучитьПериод()=1 Цикл
						Если лБИ.Операция.Документ.ТекущийДокумент()=лЗн Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
				Иначе
					лБИ.ПолучитьПериод(,лЗн);
				КонецЕсли;
			ИначеЕсли лСтрока="КорСчета" Тогда
				лБИ.ПолучитьКорСчет(,лЗн);
			ИначеЕсли лСтрока<>"" Тогда
				лБИ.ПолучитьСубконто(1,,лЗн);
			КонецЕсли;
			лТаблКолонок.ВыбратьСтроки();
			Пока лТаблКолонок.ПолучитьСтроку()=1 Цикл
				лЗн1=лТаблКолонок.Столбец;
				Если лСтолбец="Период" Тогда
					Если лПериод="Операция" Тогда
						лЕсть=0;
						лБИ.ВыбратьПериоды();
						Пока лБИ.ПолучитьПериод()=1 Цикл
							Если лБИ.Операция.Документ.ТекущийДокумент()=лЗн1 Тогда
								лЕсть=1;
								Прервать;
							КонецЕсли;
						КонецЦикла;
					Иначе
						лЕсть=лБИ.ПолучитьПериод(,лЗн1);
					КонецЕсли;
				ИначеЕсли лСтолбец="КорСчета" Тогда
					лЕсть=лБИ.ПолучитьКорСчет(,лЗн1);
				ИначеЕсли лСтолбец<>"" Тогда
					лЕсть=лБИ.ПолучитьСубконто(лКолвоСубконто,,лЗн1);
				КонецЕсли;
				Если лЕсть=0 Тогда
					лСумма=0;
				ИначеЕсли (лСтолбец="КорСчета") или (лСтрока="КорСчета") Тогда
					лСумма=?(фДК=1,лБИ.КорКО(),лБИ.КорДО());
				Иначе
					лСумма=?(фДК=1,лБИ.КО(),лБИ.ДО());
				КонецЕсли;
				мТаблица.ПрисоединитьСекцию("ТаблСтрока|ТаблКолонка");
			КонецЦикла;
			лСумма=лТаблСтрок.Сумма;
			мТаблица.ПрисоединитьСекцию("ТаблСтрока|ТаблИтог");
		КонецЦикла;
		
		мТаблица.ВывестиСекцию("Итог|ТаблШапка");
		лТаблКолонок.ВыбратьСтроки();
		Пока лТаблКолонок.ПолучитьСтроку()=1 Цикл
			лСумма=лТаблКолонок.Сумма;
			мТаблица.ПрисоединитьСекцию("Итог|ТаблКолонка");
		КонецЦикла;
		Если лСтрока<>"" Тогда
			лСумма=лТаблСтрок.Итог("Сумма");
		Иначе
			лСумма=лТаблКолонок.Итог("Сумма");
		КонецЕсли;
		мТаблица.ПрисоединитьСекцию("Итог|ТаблИтог");

	КонецЕсли;

	мТаблица.ТолькоПросмотр(1);
	мТаблица.ПараметрыСтраницы(1,,,5,5,5,5,,,,1);
	мТаблица.Опции(0,0,6,1);
	мТаблица.Показать("Анализ "+фСчет);
	
	Если (ФлагЗакрытияФормы = 1) или (Обновить = 2) Тогда
		Форма.Закрыть();
	КонецЕсли;

КонецПроцедуры

//******************************************************************************
//	
Процедура ЗаполнитьСписок(пСписок)
	
	пСписок.УдалитьВсе();
	пСписок.ДобавитьЗначение("");
	пСписок.ДобавитьЗначение("Период","Периоды");
	пСписок.ДобавитьЗначение("КорСчета","Кор. счета");
	Для ъ=1 По фСчет.КоличествоСубконто() Цикл
		пСписок.ДобавитьЗначение(фСчет.ВидСубконто(ъ));
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСписок

//******************************************************************************
//	
Процедура фПриВыбореСтолбца()
	
	лТекЗн=фСтолбцы.ПолучитьЗначение(фСтолбцы.ТекущаяСтрока());
	лТекЗнСтр=фСтроки.ПолучитьЗначение(фСтроки.ТекущаяСтрока());
	ЗаполнитьСписок(фСтроки);
	Если ПустоеЗначение(лТекЗн)=0 Тогда
		фСтроки.УдалитьЗначение(фСтроки.НайтиЗначение(лТекЗн));
	КонецЕсли;
	фСтроки.ТекущаяСтрока(фСтроки.НайтиЗначение(лТекЗнСтр));
	
	Форма.фТекстПериодичность.Видимость(0);
	Форма.фПериодичность.Видимость(0);
	Если (лТекЗн="Период") или (лТекЗнСтр="Период") Тогда
		Форма.фТекстПериодичность.Видимость(1);
		Форма.фПериодичность.Видимость(1);
	КонецЕсли;
	
КонецПроцедуры // фПриВыбореСтолбца

//******************************************************************************
//	
Процедура фПриВыбореСтроки()
	
	лТекЗн=фСтроки.ПолучитьЗначение(фСтроки.ТекущаяСтрока());
	лТекЗнСтл=фСтолбцы.ПолучитьЗначение(фСтолбцы.ТекущаяСтрока());
	ЗаполнитьСписок(фСтолбцы);
	Если ПустоеЗначение(лТекЗн)=0 Тогда
		фСтолбцы.УдалитьЗначение(фСтолбцы.НайтиЗначение(лТекЗн));
	КонецЕсли;
	фСтолбцы.ТекущаяСтрока(фСтолбцы.НайтиЗначение(лТекЗнСтл));
	
	Форма.фТекстПериодичность.Видимость(0);
	Форма.фПериодичность.Видимость(0);
	Если (лТекЗн="Период") или (лТекЗнСтл="Период") Тогда
		Форма.фТекстПериодичность.Видимость(1);
		Форма.фПериодичность.Видимость(1);
	КонецЕсли;
	
КонецПроцедуры // фПриВыбореСтроки

//******************************************************************************
//	
Процедура фПриВыбореСчета()
	
	фСтолбцы.УдалитьВсе();
	фСтроки.УдалитьВсе();
	Если ПустоеЗначение(фСчет)=1 Тогда
		Форма.фСтолбцы.Доступность(0);
		Форма.фСтроки.Доступность(0);
		Возврат;
	Иначе
		Форма.фСтолбцы.Доступность(1);
		Форма.фСтроки.Доступность(1);
	КонецЕсли;
	
	ЗаполнитьСписок(фСтолбцы);
	фСтолбцы.Выгрузить(фСтроки);
	фСтолбцы.ТекущаяСтрока(1);
	фСтроки.ТекущаяСтрока(1);
	
	Форма.фТекстПериодичность.Видимость(0);
	Форма.фПериодичность.Видимость(0);
	
	мТаблОтборов.УдалитьСтроки();
	лСпОтборов.УдалитьВсе();
	лСпОтборов.ДобавитьЗначение("КорСчета","Кор. счета");
	Для ъ=1 По фСчет.КоличествоСубконто() Цикл
		лСпОтборов.ДобавитьЗначение(фСчет.ВидСубконто(ъ));
	КонецЦикла;
	
КонецПроцедуры // фПриВыбореСчета

//******************************************************************************
//	
Процедура ПриВыбореЗакладки(пНомЗакладки)
	
	Форма.ИспользоватьСлой("Кнопки",2);
	Если пНомЗакладки=1 Тогда
		Форма.ИспользоватьСлой("Основной",1);
	ИначеЕсли пНомЗакладки=2 Тогда
		Форма.ИспользоватьСлой("Отборы",1);
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореЗакладки

//******************************************************************************
//	
Процедура ПриОткрытии()
	
	Обновить = 0;
	Если глФлагРасшифровки = 1 Тогда
		Обновить = Число(глРасшифровка.Получить("Обновить"));
		НачДата = глРасшифровка.Получить("НачДата");
		КонДата = глРасшифровка.Получить("КонДата");
		фСчет = глРасшифровка.Получить("фСчет");
		фДК = глРасшифровка.Получить("фДК");
		фПриВыбореСчета();
		фСтолбцы.ТекущаяСтрока(фСтроки.НайтиЗначение(глРасшифровка.Получить("фСтолбцы")));
		фПриВыбореСтолбца();
		фСтроки.ТекущаяСтрока(фСтроки.НайтиЗначение(глРасшифровка.Получить("фСтроки")));
		фПриВыбореСтроки();
		фПериодичность.ТекущаяСтрока(фПериодичность.НайтиЗначение(глРасшифровка.Получить("фПериодичность")));
		
		//лТаблВр=СоздатьОбъект("ТаблицаЗначений");
		лТаблВр=глРасшифровка.Получить("мТаблОтборов");
		Если ПустоеЗначение(лТаблВр)=0 Тогда
			лТаблВр.Выгрузить(мТаблОтборов);
		КонецЕсли;
		лТаблВр="";
		
		Если Обновить <> 0 Тогда
			мТаблица = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		фПриВыбореСчета();
	КонецЕсли;	
	Если фДК=0 Тогда
		фДК=1;
	КонецЕсли;
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Настройки");
	Форма.Закладки.ДобавитьЗначение("Отборы");
	ПриВыбореЗакладки(1);
	
КонецПроцедуры // ПриОткрытии

//******************************************************************************
//
фПериодичность.УдалитьВсе();
фПериодичность.ДобавитьЗначение("Период", "Период в целом");
фПериодичность.ДобавитьЗначение("Операция", "Документ");
фПериодичность.ДобавитьЗначение("День", "День");
фПериодичность.ДобавитьЗначение("Неделя", "Неделя");
фПериодичность.ДобавитьЗначение("Декада", "Декада");
фПериодичность.ДобавитьЗначение("Месяц", "Месяц");
фПериодичность.ДобавитьЗначение("Квартал", "Квартал");
фПериодичность.ДобавитьЗначение("Год", "Год");

мТаблОтборов=СоздатьОбъект("ТаблицаЗначений");
мТаблОтборов.НоваяКолонка("ВидОтбора");
мТаблОтборов.НоваяКолонка("ТипЗначения");
мТаблОтборов.НоваяКолонка("Отбор");