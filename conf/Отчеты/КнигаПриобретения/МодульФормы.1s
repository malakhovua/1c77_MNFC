// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================                                     
Процедура Сформировать()
	
	Если глПроверкаИнтервалаОтчета(Дата1,Дата2,1) = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	ТекстЗапр = "//{{ЗАПРОС(Запр)
	|Период с Дата1 по Дата2;
	|ОбрабатыватьДокументы Проведенные;
	|ЗКП = Документ.ЗаписьКнигиПриобретения.ТекущийДокумент;
	|Фрм = Документ.ЗаписьКнигиПриобретения.Фирма;
	|СуммаБезНДС = Документ.ЗаписьКнигиПриобретения.СуммаБезНДС;	
	|НДС = Документ.ЗаписьКнигиПриобретения.НДС;                 
	|СуммаСНДС = Документ.ЗаписьКнигиПриобретения.СуммаСНДС;		
	|Условие (Фрм=Фирма);
	|Группировка ЗКП упорядочить по ЗКП.ДатаДок;
	|"//}}ЗАПРОС
    ;
    
	Запр = СоздатьОбъект("Запрос");
	Если Запр.Выполнить(ТекстЗапр) = 0 Тогда
		Предупреждение("Не выполнился запрос по документам!");
	    Возврат;
	КонецЕсли;                                  
	                      
	глУстПропись(Гривня, "у"); 
	Ист6  = 0;	Ист7  = 0;	Ист8  = 0;	Ист9  = 0;
	Ист10 = 0;	Ист11 = 0;	Ист12 = 0;	Ист13 = 0;
	Ист14 = 0;	Ист15 = 0;	Ист16 = 0;	Ист17 = 0;
	Ист18 = 0;	Ист19 = 0;	Ист20 = 0;	Ист21 = 0;
	Ист22 = 0;	Ист23 = 0;	Ист24 = 0;	Ист25 = 0;
	
	Фирма.ИспользоватьДату(Дата2);
	Таб = СоздатьОбъект("Таблица");
	Таб.ИспользоватьФормат("Ч012.2");
	Таб.ВывестиСекцию("Шапка");
	Ном = 1;
	
	ЗКП = СоздатьОбъект("Документ.ЗаписьКнигиПриобретения");
	Пока Запр.Группировка("ЗКП")=1 Цикл
		Если ЗКП.НайтиДокумент(Запр.ЗКП) = 0 Тогда
		    // странно, нет такого документа
			Продолжить;
		КонецЕсли;
		
		ВидНДС = ЗКП.ВидНДС;                     
		
		Ст1 = Ном;
		Ст2 = ЗКП.ДатаДок;
		Ст3 = ЗКП.НомерДокумента;
		Ст4 = ЗКП.Контрагент.ПолнНаименование;
		Ст5 = "" + ЗКП.Контрагент.ИНН;
		Если Число(Ст5) = 0 Тогда                
			Ст5 = "Х     ";
		КонецЕсли;
		Ст6  = 0;	Ст7  = 0; 	Ст8  = 0; 	Ст9  = 0; 	Ст10 = 0;
		Ст11 = 0;	Ст12 = 0;	Ст13 = 0;	Ст14 = 0;	Ст15 = 0;
		Ст16 = 0;	Ст17 = 0;	Ст18 = 0;	Ст19 = 0;	Ст20 = 0;
		Ст21 = 0;	Ст22 = 0;	Ст23 = 0;	Ст24 = 0;	Ст25 = 0;
		Ст26 = "";	Ст27 = "";	Ст28 = "";	Ст29 = "";	Ст30 = ""; 
		Ст31 = "";
        Если ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.ПогашениеНалоговогоВекселя Тогда
			// погашаем вексель
            Ст6 = Запр.НДС;
        КонецЕсли;
        Если ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.ИмпортПоВекселю Тогда
			// импортируем по векселю
            Ст7 = Запр.СуммаБезНДС;
            Ст8 = Запр.НДС;
        КонецЕсли;
        Если ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РаботыОтНерезидента Тогда
			// работы от нерезидента
            Ст9 = Запр.СуммаБезНДС;
            Ст10 = Запр.НДС;
		КонецЕсли;
		Если ЗКП.НалоговыйКредит = 1 Тогда
			// имеем право на налоговый кредит
			Если ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.ПокупкаВУкраине Тогда
				// обыкновенная покупка в Украине
            	Ст13 = Запр.СуммаБезНДС;
				Ст11 = Ст11 + Запр.СуммаБезНДС;
				Ст12 = Ст12 + Запр.НДС;
				Ст17 = Запр.НДС;
			ИначеЕсли (ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.Импорт) или
			(ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РаботыОтНерезидентаПрошлогоПериода) Тогда
				// импорт, НДС заплатили на таможне (в прошлом периоде получили работы)
    	        Ст14 = Запр.СуммаБезНДС;
				Ст11 = Ст11 + Запр.СуммаБезНДС;
				Ст12 = Ст12 + Запр.НДС;
				Ст17 = Запр.НДС;
			ИначеЕсли ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.ВексельПрошлогоПериода Тогда
				// в прошлом периоде погасили вексель, в этом имеем право на нал. кредит
    	        Ст15 = Запр.СуммаБезНДС;
				Ст11 = Ст11 + Запр.СуммаБезНДС;
				Ст12 = Ст12 + Запр.НДС;
				Ст17 = Запр.НДС;
			ИначеЕсли ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РасчетКорректировки Тогда
				// расчет корректировки (приложение 2 к налоговой накладной)
            	Ст16 = Запр.СуммаБезНДС;
				Ст11 = Ст11 + Запр.СуммаБезНДС;
				Ст12 = Ст12 + Запр.НДС;
				Ст17 = Запр.НДС;
			КонецЕсли;
		Иначе
			// нет налогового кредита
			Если ЗКП.ВаловыеРасходы = 1 Тогда
			    // относятся на валовые расходы или подлежат амортизации
				Если ЗКП.Импорт = 0 Тогда
				    // на территории Украины
					Если (ЗКП.ДляОблагаемыхОпераций = 0) и ((ВидНДС.Код = "НДС0") или (ВидНДС.Код = "НДС20")) Тогда
					    // купили с НДС для необлагаемых операций - нет нал кредита
					    Ст18 = Запр.СуммаСНДС;
						Ст11 = Ст11 + Запр.СуммаБезНДС;
						Ст12 = Ст12 + Запр.НДС;
					ИначеЕсли (ЗКП.ДляОблагаемыхОпераций = 1) и (ВидНДС.Код = "БезНДС") Тогда
						// без НДС - нет налогового кредита, для облагаемых операций
						Ст19 = Запр.СуммаБезНДС;
						Ст11 = Ст11 + Запр.СуммаБезНДС;
						Ст12 = Ст12 + Запр.НДС;
					ИначеЕсли (ЗКП.ДляОблагаемыхОпераций = 0) и (ВидНДС.Код = "БезНДС") Тогда
						// без НДС - нет налогового кредита, для необлагаемых операций
						Ст20 = Запр.СуммаБезНДС;
						Ст11 = Ст11 + Запр.СуммаБезНДС;
						Ст12 = Ст12 + Запр.НДС;
					КонецЕсли;
				Иначе
					// импорт
					Если ((ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.Импорт) или (ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РаботыОтНерезидентаПрошлогоПериода)) 
					и ((ВидНДС.Код = "НДС0") или (ВидНДС.Код = "НДС20")) Тогда
					    // НДС заплатили на таможне (в прошлом периоде получили работы от нерезидента)
						Ст21 = Запр.СуммаСНДС;
						Ст11 = Ст11 + Запр.СуммаБезНДС;
						Ст12 = Ст12 + Запр.НДС;
					ИначеЕсли (ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.ИмпортПоВекселю) и ((ВидНДС.Код = "НДС0") или (ВидНДС.Код = "НДС20")) Тогда
						// оформили вексель (одновременно с отражением в графах 7,8)
						Ст22 = Запр.СуммаСНДС;
						Ст11 = Ст11 + Запр.СуммаБезНДС;
						Ст12 = Ст12 + Запр.НДС;
					ИначеЕсли ВидНДС.Код = "БезНДС" Тогда
						// освобождение от НДС
						Ст23 = Запр.СуммаСНДС;
						Ст11 = Ст11 + Запр.СуммаСНДС;
						Ст12 = Ст12 + Запр.НДС;
					КонецЕсли;
				КонецЕсли;
			Иначе
				// не относятся на валовые расходы, не подлежат амортизации
				Если (ЗКП.Импорт = 1) или (ЗКП.ВидОперации = Перечисление.ВидыОперацийКнигиПриобретения.РаботыОтНерезидентаПрошлогоПериода) Тогда
				    // импорт
					Ст25 = Запр.СуммаСНДС; 
					Ст11 = Ст11 + Запр.СуммаБезНДС;
					Ст12 = Ст12 + Запр.НДС;
				Иначе
					// на территории Украины
					Ст24 = Запр.СуммаСНДС;
					Ст11 = Ст11 + Запр.СуммаБезНДС;
					Ст12 = Ст12 + Запр.НДС;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если Число(Ст12) = 0 Тогда
		    Ст12 = "Х     ";
		КонецЕсли;
		Ст26 = Запр.ЗКП.ФормаОплаты;
		Если Запр.ЗКП.Импорт = 1 Тогда
			Ст28 = Запр.ЗКП.ДатаОплаты;
		Иначе
			Ст27 = Запр.ЗКП.ДатаОплаты;
		КонецЕсли;
		Ст29 = Запр.ЗКП.ДатаВыдачиВекселя;
		Ст30 = Запр.ЗКП.ДатаПогашенияВекселя;
		Ст31 = Запр.ЗКП.ФактическаяДатаПогашенияВекселя;
		
		Ист6 = Ист6 + Ст6;
		Ист7 = Ист7 + ст7;
		Ист8 = Ист8 + ст8;		
		Ист9 = Ист9 + ст9;
		Ист10 = Ист10 + ст10;
		Ист11 = Ист11 + Ст11;
		Ист12 = Ист12 + Ст12;
		Ист13 = Ист13 + ст13;		
		Ист14 = Ист14 + ст14;
		Ист15 = Ист15 + ст15;
		Ист16 = Ист16 + ст16;
		Ист17 = Ист17 + Ст17;
		Ист18 = Ист18 + ст18;
		Ист19 = Ист19 + ст19;
		Ист20 = Ист20 + ст20;
		Ист21 = Ист21 + ст21;		
		Ист22 = Ист22 + ст22;
		Ист23 = Ист23 + ст23;		
		Ист24 = Ист24 + ст24;
		Ист25 = Ист25 + ст25;		
		
		Таб.ВывестиСекцию("Строка");                                           
		Ном = Ном+1;
	КонецЦикла;
    Таб.ВывестиСекцию("Дно");              
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);	
	Таб.Опции(0,0,,,"ПарамПечатиУпрКнигаПриобретения","РазмОкнаУпрКнигаПриобретения");
	Таб.ПараметрыСтраницы(2);
	Таб.Показать("ПЕЧАТЬ: Книга приобретения ("+ПериодСтр(Дата1,Дата2)+", "+Фирма+")","");
КонецПроцедуры
              

// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Фирма = глВосстановитьЗначение(,"БазФирма");
	Если Метаданные.РазделительУчета.Выбран() = 0 Тогда
		Форма.Фирма.Видимость(0);
		Форма.тФирма.Видимость(0);
	КонецЕсли;
	Дата1 = РабочаяДата();
	Дата2 = РабочаяДата();
КонецПроцедуры
