//*******************************************
Процедура Сформировать()
	ТЗ = СоздатьОбъект("ТаблицаЗначений");	
	ТЗ.НоваяКолонка("Контрагент", "Справочник.Контрагенты");
	ТЗ.НоваяКолонка("РН", "Документ.РасходнаяНакладная");
	ТЗ.НоваяКолонка("СуммаСкидочного", "Число", 15, 2);
	ТЗ.НоваяКолонка("СуммаНеСкидочного", "Число", 15, 2);
	
	Док = СоздатьОбъект("Документ.РасходнаяНакладная");
	Док.УстановитьФильтр(1, 0);
	Док.ВыбратьДокументы(НачДата, КонДата);
	Пока Док.ПолучитьДокумент() = 1 Цикл
		Если ПустоеЗначение(Док.УстановкаСкидокТМЦ) = 0 Тогда
			СуммаСк = 0; СуммаБск = 0;
			Док.ВыбратьСтроки();
			Пока Док.ПолучитьСтроку() = 1 Цикл
				Скидка = Док.ЦенаБезСкидки - Док.ЦенаСНДС;
				Скидка = ?(Скидка < 0, -Скидка, Скидка);
				СуммаСк = СуммаСк + ?(Скидка > 0.02, Док.ЦенаБезСкидки * Док.Кво * Док.Коэффициент, 0); // могут быть ошибки округления
				СуммаБск = СуммаБск + Док.ЦенаБезСкидки * Док.Кво * Док.Коэффициент; 
			КонецЦикла;
			
			Если (СуммаБск <> 0) И (СуммаСк <> 0) Тогда // учитываем только РН, где есть хоть что-то со скидкой
				Если (СуммаСк / СуммаБск * 100 > Док.УстановкаСкидокТМЦ.Отношение) ИЛИ (фТолькоПревышение = 0) Тогда
					ТЗ.НоваяСтрока();
					ТЗ.Контрагент = Док.Контрагент;
					ТЗ.СуммаСкидочного = СуммаСк;
					ТЗ.СуммаНеСкидочного = СуммаБск;
					
					Если фРН = 1 Тогда
						ТЗ.НоваяСтрока();
						ТЗ.Контрагент = Док.Контрагент;
						ТЗ.РН = Док.ТекущийДокумент();
						ТЗ.СуммаСкидочного = СуммаСк;
						ТЗ.СуммаНеСкидочного = СуммаБск;						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Шапка");
	
	ТЗ.Свернуть("Контрагент,РН", "СуммаСкидочного,СуммаНеСкидочного");
	ТЗ.Сортировать("Контрагент,РН");
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию(?(ПустоеЗначение(ТЗ.РН) = 1, "Контрагент", "РН"));
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отношение скидка / общая суммма");
КонецПроцедуры
