Перем СписК;
Перем Таб;
Перем Обновить;
Перем Расшифровка;

//*******************************************
Функция ПолучитьЗапрос()
	ТекстЗ =
	"//{{ЗАПРОС(Сформировать)
	|Период с КонДата по КонДата;
	|Контрагент = Справочник.Контрагенты.ТекущийЭлемент;
	|СхемаРасчетаБ = Справочник.Контрагенты.СхемаРасчетаБонуса;
	|Функция Сч = Счётчик();
	|Условие(Контрагент в " + ?(фТолькоСОтгр = 1, "СписК", "списКонтрагенты") + ");
	|Условие(СхемаРасчетаБ" + ?(фНЕ = 1, "<>" , "=") + " Схема);
	|Группировка Контрагент Упорядочить по Контрагент.Наименование;
	|"//}}ЗАПРОС
	;

	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат Запрос;
КонецФункции

Процедура ОбновитьРодителя(ТЗВР, Род, КвоР)
	Если ПустоеЗначение(Род) = 1 Тогда
		Возврат;
	КонецЕсли;
	Стр = 0;
	Если ТЗВР.НайтиЗначение(Род, Стр, "Контр") = 1 Тогда
		ТЗВР.УстановитьЗначение(Стр, "КвоВР", ТЗВР.ПолучитьЗначение(Стр, "КвоВР") + КвоР);
		ОбновитьРодителя(ТЗВР, ТЗВР.ПолучитьЗначение(Стр, "Род"), КвоР);
	КонецЕсли;
КонецПроцедуры

Процедура ПоВидамРабот()
	
	Если НачДата > КонДата Тогда //--- УМК Сандомирский В.Ю, (03.09.2014)
		Сообщить("Дата начала больше даты конца","!");
		Возврат;
	КонецЕсли;
	
	КДата = Мин(КонДата, ПолучитьДатуТА());
	НДата = Мин(НачДата, КДата);
	
	//--- УМК Сандомирский В.Ю, (26.05.2014)
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ПоСхемамНБ");	
	Расшифровка.Установить("НачДата", 		НачДата);
    Расшифровка.Установить("Схема", 		Схема);
	Расшифровка.Установить("фТолькоСОтгр", 	фТолькоСОтгр);
    Расшифровка.Установить("фНЕ", 			фНЕ);
	Расшифровка.Установить("фСПустой",		фСПустой);
	Расшифровка.Установить("фПримечание",	фПримечание);	
	Расшифровка.Установить("списКонтрагенты",списКонтрагенты);	
	Расшифровка.Установить("Группировки",	Группировки);	
	

	ЗапрПок = СоздатьОбъект("Запрос");
	ТекстЗ = "
	|Период с НДата по КДата;
	|Контрагент = Регистр.ВзаиморасчетыПокупателей.Контрагент;
	|Долг = Регистр.ВзаиморасчетыПокупателей.Долг;
	|Функция ДолгПриход = Приход(Долг);
	|Группировка Контрагент упорядочить по Контрагент.Наименование без групп;
	|Условие(Контрагент в списКонтрагенты);
	|"//}}ЗАПРОС
	;
	Если ЗапрПок.Выполнить(ТекстЗ) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СписК = СоздатьОбъект("СписокЗначений");
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ЗапрПок.Выгрузить(ТЗ);
	ТЗ.Выгрузить(СписК, , , "Контрагент");	

	Запрос = ПолучитьЗапрос();	
	Если Запрос = 0 Тогда
		Возврат;
	КонецЕсли;

	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;
	
	Таб.ВывестиСекцию("Кнопки|Основная");
	
	
	Если Схема.Выбран() = 1 Тогда
		Заг = "Контрагенты, в которой схема расч. бонуса " + ?(фНЕ = 1, " не", "") + " соответсвтует " + Схема;
	Иначе
		Заг = "Контрагенты, в которой схема расч. бонуса " + ?(фНЕ = 1, " не", "") + " пустая";
	КонецЕсли;
	СписВБ = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По Группировки.РазмерСписка() Цикл
		Если Группировки.Пометка(Инд) = 1 Тогда
			СписВБ.ДобавитьЗначение(Группировки.ПолучитьЗначение(Инд));
		КонецЕсли;		
	КонецЦикла;
	Если СписВБ.РазмерСписка() = 0 Тогда
		Группировки.Выгрузить(СписВБ);
	КонецЕсли;
	
	глФильтры(Заг, списКонтрагенты, "Контрагенты");
	Если (СписВБ.РазмерСписка() <> Группировки.РазмерСписка()) Тогда
		ЕстьФильтрПоВБ = 1;
		глФильтры(Заг, СписВБ, "Виды базы");
	Иначе
		ЕстьФильтрПоВБ = 0;
	КонецЕсли;
	
	
	ТЗВР = СоздатьОбъект("ТаблицаЗначений");
	СписВР = СоздатьОбъект("СписокЗначений");
	ТЗВР.НоваяКолонка("Контр", "Справочник.Контрагенты");
	ТЗВР.НоваяКолонка("Род", "Справочник.Контрагенты");
	ТЗВР.НоваяКолонка("Схема", "Документ.СхемаРасчетаБонуса");
	ТЗВР.НоваяКолонка("КвоВР", "Число");
	
	ТЗВБ = СоздатьОбъект("ТаблицаЗначений");
	ТЗВБ.НоваяКолонка("ВидБазы", "Перечисление.БазыДляРасчетаБонуса");
	ТЗВБ.НоваяКолонка("ВидК", "Строка");
	ТЗВБ.НоваяКолонка("Получатель", "Справочник");
	ТЗВБ.НоваяКолонка("КатЦен", "Справочник.КатегорииЦен");
	ТЗВБ.НоваяКолонка("Ключ", "Строка");
	ТЗВБ.НоваяКолонка("ИмяКол", "Строка");

	Пока Запрос.Группировка(1) = 1 Цикл
		ТЗВР.НоваяСтрока();
		ТЗВР.Контр = Запрос.Контрагент;
		ТЗВР.Род = ТЗВР.Контр.Родитель;
		// нужно удалить продукцию, по которой нет ни одного ВР, а также её родителей
		
		Если Запрос.ЭтоГруппа("Контрагент") = 0 Тогда
			ТЗВР.Схема = Запрос.СхемаРасчетаБ;
			ДокСх = Запрос.СхемаРасчетаБ;
			Если ПустоеЗначение(ДокСх) = 0 Тогда
			    ДокСх.ВыбратьСтроки();
				Пока ДокСх.ПолучитьСтроку() = 1 Цикл
					Если ЕстьФильтрПоВБ = 1 Тогда						
						Если СписВБ.НайтиЗначение(ДокСх.База) = 0 Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					Ключ = ЗначениеВСтрокуВнутр(ДокСх.Объект) + "//" + ДокСх.База.Идентификатор() + "//" + ЗначениеВСтрокуВнутр(ДокСх.КатЦен);
					Стр = 0;
					Поз = ТЗВБ.НайтиЗначение(Ключ, Стр, "Ключ");
					Если Поз = 0 Тогда
						ИмКол = "К" + Строка(ТЗВБ.КоличествоСтрок()+1);
						ТЗВБ.НоваяСтрока();
						ТЗВБ.ВидК = ДокСх.Объект.Вид();
						ТЗВБ.ВидБазы = ДокСх.База;
						ТЗВБ.Получатель = ДокСх.Объект;
						ТЗВБ.КатЦен = ДокСх.КатЦен;
						ТЗВБ.Ключ = Ключ;
						ТЗВБ.ИмяКол = ИмКол;
						ТЗВР.НоваяКолонка(ИмКол, "Число", 13, 5);
					Иначе
						ИмКол = ТЗВБ.ПолучитьЗначение(Стр, "ИмяКол");
					КонецЕсли;
					ТЗВР.УстановитьЗначение(ТЗВр.НомерСтроки, ИмКол, ДокСх.Зн);
					ТЗВР.КвоВР=ТЗВР.КвоВР+1;
				КонецЦикла;
			КонецЕсли;
			
		    Если ТЗВР.КвоВР <> 0 Тогда
		        ОбновитьРодителя(ТЗВР, ТЗВР.Род, ТЗВР.КвоВР);
		    КонецЕсли;
		КонецЕсли;		
	КонецЦикла;	
	
	Таб.ИсходнаяТаблица("Кросс");
	ТЗВБ.Сортировать("ВидК,Получатель,ВидБазы");
	
	СтарВидК = "";
	Таб.ВывестиСекцию("Шапка|Основная");
	ШТабл = Таб.ШиринаТаблицы() + 1;
	Если фПримечание = 1 Тогда
		Таб.ПрисоединитьСекцию("Шапка|Примечание");
	КонецЕсли;
	ТЗВБ.ВыбратьСтроки();
	Пока ТЗВБ.ПолучитьСтроку() = 1 Цикл	
		Таб.ПрисоединитьСекцию("Шапка|ВР");
		Если СтарВидК <> ТЗВБ.ВидК Тогда
			СтарВидК = ТЗВБ.ВидК;
			Если ТЗВБ.ВидК = "Сотрудники" Тогда
				Таб.Область(4, Таб.ШиринаТаблицы(), Таб.ВысотаТаблицы(), Таб.ШиринаТаблицы()).РамкаСлева(5);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Таб.Опции(0,0, Таб.ВысотаТаблицы(), 2);
	
	ТЗВР.ВыбратьСтроки();
	Пока ТЗВР.ПолучитьСтроку() = 1 Цикл
		Если (ТЗВР.КвоВР = 0) И (фСПустой = 0) Тогда
			Продолжить;
		КонецЕсли;
		Если ТЗВР.Контр.ЭтоГруппа() = 0 Тогда			
			ДО = 0;
			ЗапрПок.ВНачалоВыборки();
			Если ЗапрПок.Получить(ТЗВР.Контр) = 1 Тогда
				ДО = ЗапрПок.ДолгПриход;
			КонецЕсли;
			Таб.ВывестиСекцию("Строка|Основная");
			Если фПримечание = 1 Тогда
				Таб.ПрисоединитьСекцию("Строка|Примечание");
			КонецЕсли;
			
			СтарВидК = "";
			ТЗВБ.ВыбратьСтроки();
			Пока ТЗВБ.ПолучитьСтроку() = 1 Цикл		
				Таб.ПрисоединитьСекцию("Строка|ВР");
				Если СтарВидК <> ТЗВБ.ВидК Тогда
					СтарВидК = ТЗВБ.ВидК;
					Если ТЗВБ.ВидК = "Сотрудники" Тогда
						Таб.Область(Таб.ВысотаТаблицы(), ТЗВБ.НомерСтроки + ШТабл, Таб.ВысотаТаблицы(), ТЗВБ.НомерСтроки + ШТабл).РамкаСлева(5);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Таб.ВывестиСекцию("Группа|Основная");
			Если фПримечание = 1 Тогда
				Таб.ПрисоединитьСекцию("Группа|Примечание");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Таб.ПовторятьПриПечатиСтроки(4,6);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ПараметрыСтраницы(2);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("По видам работ");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "Контрагенты" Тогда
		ДобавитьвСписок(Элт, списКонтрагенты);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры



//====================================================================== //--- УМК Сандомирский В.Ю, (26.05.2014)
//======================================================================
Процедура ПриОткрытии()
	

	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		глФлагРасшифровки = 0;
		
		// восстанавливаем настройки из списка
		НачДата 		= глРасшифровка.Получить("НачДата");
		КонДата			= глРасшифровка.Получить("КонДата");
		Схема 			= глРасшифровка.Получить("Схема");
		фТолькоСОтгр	= глРасшифровка.Получить("фТолькоСОтгр");
		фНЕ				= глРасшифровка.Получить("фНЕ");
		фСПустой		= глРасшифровка.Получить("фСПустой");
		фПримечание		= глРасшифровка.Получить("фПримечание");
		глРасшифровка.Получить("списКонтрагенты").Выгрузить(списКонтрагенты);
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			ПоВидамРабот();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;            

КонецПроцедуры //

НачДата = НачМесяца(ТекущаяДата());
КонДата = ТекущаяДата();
Дл = Метаданные.Справочник("Контрагенты").ДлинаНаименования;
Для Инд = 1 По Перечисление.БазыДляРасчетаБонуса.КоличествоЗначений() Цикл
	Группировки.ДобавитьЗначение(Перечисление.БазыДляРасчетаБонуса.ЗначениеПоНомеру(Инд));
	Группировки.Пометка(Инд, 1);
КонецЦикла;
фНЕ = 1;
фПримечание = 1;