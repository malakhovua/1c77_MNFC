//-----------------------------------------------
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура Сформировать()
	Если глПроверкаИнтервалаОтчета(Дата1,Дата2) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НФ = РазделительУчета;

	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Т) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;

	глУстПропись(Гривня);
	Если Константа.ФормыНаУкраинском = Да Тогда
		Т.ИсходнаяТаблица("Таблица_Укр");
	Иначе
		Т.ИсходнаяТаблица("Таблица");
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Ит = СоздатьОбъект("БухгалтерскиеИтоги");

    // запрос за период  фильтровать по валюте и считать только обороты
    Ит.ВключатьСубсчета(ДанныеПоСубсчетам, ДанныеПоСубсчетам);
    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
    Ит.ИспользоватьПланСчетов(ПланСчетов);
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Если ПоВалюте = 1 Тогда
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,, Валюта, 2) = 0 Тогда
			Возврат;
		КонецЕсли;	
	Иначе
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,,, 2) = 0 Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	Расшифровка.Установить("Отчет", "Шахматка");
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
   	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	
	Т.ВывестиСекцию("Секция_11");
	Т.ВывестиСекцию("Секция_1");     
   	Расшифровка.Установить("ДанныеПоСубсчетам");
	Расшифровка.Установить("Обновить");
	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");

    // вывести шапку таблицы
	Т.ВывестиСекцию("Секция_2|Секция_5");
	Расшифровка.Установить("ДтКт", 2);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Ит.ВыбратьКорСчета(0, 1);
   	Пока Ит.ПолучитьКорСчет() = 1 Цикл
		Расшифровка.Установить("КорСчет", Ит.КорСчет);
		Т.ПрисоединитьСекцию("Секция_2|Секция_6");
   		Если Ит.КорСчет.Валютный = 1 Тогда
   			Ит.ВыбратьВалюты(-1, 1, 1);
   			Пока Ит.ПолучитьВалюту(1) = 1 Цикл
				Расшифровка.Установить("Валюта", Ит.Валюта);
				Расшифровка.Установить("ПоВалюте", 1);
   				Т.ПрисоединитьСекцию("Секция_2|Секция_7");
   			КонецЦикла;
			Расшифровка.Установить("Валюта", Валюта);
			Расшифровка.Установить("ПоВалюте", ПоВалюте);
   		КонецЕсли;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", ПоВалюте);
   	КонецЦикла;
	Т.ПрисоединитьСекцию("Секция_2|Секция_8");
   	   
   	// перебор счетов только с дебетовыми оборотами
	Ит.ВыбратьСчета(0, 1);
    Пока Ит.ПолучитьСчет() = 1 Цикл
		Расшифровка.Установить("ДтКт", 1);
		Расшифровка.Установить("Счет", Ит.Счет);

		// показать обороты с кор счетами
	   	Т.ВывестиСекцию("Секция_3|Секция_5");
		Ит.ВыбратьКорСчета(1, 1);
	   	Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Расшифровка.Установить("КорСчет", Ит.КорСчет);
			Если Ит.КорДО() = 0 Тогда
	   			Т.ПрисоединитьСекцию("Секция_10|Секция_6");
	   		Иначе
	   			Т.ПрисоединитьСекцию("Секция_3|Секция_6");
	   		КонецЕсли;
	   		Если Ит.КорСчет.Валютный = 1 Тогда
	   			Ит.ВыбратьВалюты(-1, 1, 1);
	   			Пока Ит.ПолучитьВалюту(1) = 1 Цикл
					Расшифровка.Установить("Валюта", Ит.Валюта);
					Расшифровка.Установить("ПоВалюте", 1);
					Если Ит.КорДО(2) = 0 Тогда
			   			Т.ПрисоединитьСекцию("Секция_10|Секция_7");
			   		Иначе
		   				Т.ПрисоединитьСекцию("Секция_3|Секция_7");
		   			КонецЕсли;
	   			КонецЦикла;
				Расшифровка.Установить("Валюта", Валюта);
				Расшифровка.Установить("ПоВалюте", ПоВалюте);
	   		КонецЕсли;
	   	КонецЦикла;
		Расшифровка.Установить("КорСчет");
		Т.ПрисоединитьСекцию("Секция_3|Секция_8");
                                         
        // по валютам дебета
		Ит.ВыбратьВалюты(0, 2);
		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Расшифровка.Установить("Валюта", Ит.Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
		   	Т.ВывестиСекцию("Секция_3_1|Секция_5");
			Ит.ВыбратьКорСчета(1, 1, 1);
		   	Пока Ит.ПолучитьКорСчет(1) = 1 Цикл
				Расшифровка.Установить("КорСчет", Ит.КорСчет);
				Если Ит.КорДО(2) = 0 Тогда
		   			Т.ПрисоединитьСекцию("Секция_10_1|Секция_6");
		   		Иначе
			   		Т.ПрисоединитьСекцию("Секция_3_1|Секция_6");
			   	КонецЕсли;
		   		Если Ит.КорСчет.Валютный = 1 Тогда
		   			Ит.ВыбратьВалюты(-1, 1, 2);
		   			Пока Ит.ПолучитьВалюту(2) = 1 Цикл
						Если Ит.КорДО(2) = 0 Тогда
				   			Т.ПрисоединитьСекцию("Секция_10_1|Секция_7");
				   		Иначе
			   				Т.ПрисоединитьСекцию("Секция_3_1|Секция_7");
					   	КонецЕсли;
		   			КонецЦикла;
		   		КонецЕсли;
		   	КонецЦикла;
			Расшифровка.Установить("КорСчет");
			Т.ПрисоединитьСекцию("Секция_3_1|Секция_8");
		КонецЦикла;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", ПоВалюте);
    КонецЦикла;
	    
	// итоговая строка таблицы    
   	Т.ВывестиСекцию("Секция_4|Секция_5");
	Расшифровка.Установить("ДтКт", 2);
	Ит.ВыбратьКорСчета(0, 1);
   	Пока Ит.ПолучитьКорСчет() = 1 Цикл
		Расшифровка.Установить("КорСчет", Ит.КорСчет);
		Т.ПрисоединитьСекцию("Секция_4|Секция_6");
   		Если Ит.КорСчет.Валютный = 1 Тогда
   			Ит.ВыбратьВалюты(-1, 1, 1);
   			Пока Ит.ПолучитьВалюту(1) = 1 Цикл
				Расшифровка.Установить("Валюта", Ит.Валюта);
				Расшифровка.Установить("ПоВалюте", 1);
   				Т.ПрисоединитьСекцию("Секция_4|Секция_7");
   			КонецЦикла;
			Расшифровка.Установить("Валюта", Валюта);
			Расшифровка.Установить("ПоВалюте", ПоВалюте);
   		КонецЕсли;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", ПоВалюте);
   	КонецЦикла;
	Т.ПрисоединитьСекцию("Секция_4|Секция_8");
	
    Ит = 0;
    Т.ТолькоПросмотр(1);
	Т.Опции(0, 0, 5, 1, "ОпцииПечатиШахматка", "Шахматка");
	Т.ОбластьПечати(2);
    Т.Показать("Шахматка ("+ПериодСтр(Дата1, Дата2)+?(ТипЗначения(РазделительУчета)=0, "", " "+РазделительУчета)+")", "");
КонецПроцедуры            

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Форма.КнопкаПоУмолчанию("ОК");
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;
	Если глФлагРасшифровки = 1 Тогда
		ВыбПланСчетов = 0;
		Обновить = глОбновить;
		Если Обновить <> 0 Тогда
			ПланСчетов = глРасшифровка.Получить("ПланСчетов");
			РУ = глРасшифровка.Получить("РазделительУчета");
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
			Дата1 = глРасшифровка.Получить("Дата1");
			Дата2 = глРасшифровка.Получить("Дата2");
			ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
			Валюта = глРасшифровка.Получить("Валюта");
			ПоВалюте = глРасшифровка.Получить("ПоВалюте");
			Т = глТаблица;
		Иначе
			ПланСчетов = ВыбранныйПланСчетов();
			РУ = глБИ.ИспользоватьРазделительУчета();
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РазделительУчета = глВосстановитьЗначение(,"БазФирма");
		ПоВсемРУ = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового() //Считывание существующей настройки
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ДанныеПоСубсчетам = 1;
КонецПроцедуры             
