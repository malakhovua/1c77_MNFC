//-----------------------------------------------
Перем Расшифровка;
Перем Т;
Перем Обновить;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция РасшифровкаДтКт(ДтКт)
	Расшифровка.Установить("ДтКт", ДтКт);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура КорСчета(Т, Ит, СекцияВалюты)
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Расшифровка.Установить("ДтКт");
	Если СекцияВалюты = 1 Тогда
		Т.ВывестиСекцию("Секция_2_1");
	КонецЕсли;
	Т.ВывестиСекцию("Секция_2_2");

	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
   	Ит.ВыбратьКорСчета();
	Пока Ит.ПолучитьКорСчет() = 1 Цикл
		Расшифровка.Установить("КорСчет", Ит.КорСчет);
		Т.ВывестиСекцию("Секция_3");
	КонецЦикла;
	Расшифровка.Установить("КорСчет");
	Т.ВывестиСекцию("Секция_4");
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Расшифровка.Установить("ДтКт");
	Т.ВывестиСекцию("Секция_5");
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать(Ручн=0)
	Если Счет.Выбран() = 0 Тогда
		Предупреждение("Не указан счет!");
		Возврат;
	КонецЕсли;

	Если глПроверкаИнтервалаОтчета(Дата1,Дата2) = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Ручн=1 Тогда
	    СохранитьЗначение("ОтчРабСчет",Счет);
	КонецЕсли;

	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

	НФ = РазделительУчета;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ПоВсемРУ = 0 Тогда
    	Ит.ИспользоватьРазделительУчета(РазделительУчета);
	КонецЕсли;
    Ит.ВключатьСубсчета(0, ДанныеПоСубсчетам);
  	Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,,, 3) = 0 Тогда
  		Возврат;
  	КонецЕсли;
	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	 
	глУстПропись(Гривня);
	Если Константа.ФормыНаУкраинском = Да Тогда
		Т.ИсходнаяТаблица("Таблица_Укр");
	Иначе
		Т.ИсходнаяТаблица("Таблица");
	КонецЕсли;
	
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Счет", Счет);
   	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
   	Расшифровка.Установить("ДанныеПоВалютам", ДанныеПоВалютам);
   	Расшифровка.Установить("Отчет", "АнализСчета");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Т.ВывестиСекцию("Секция_6");
	Т.ВывестиСекцию("Секция_1");
   	Расшифровка.Установить("ДанныеПоСубсчетам");
   	Расшифровка.Установить("ДанныеПоВалютам");
   	Расшифровка.Установить("Отчет");
	Расшифровка.Установить("Обновить");
	КорСчета(Т, Ит, 0);
    Если ДанныеПоВалютам = 1 Тогда;
		Ит.ВыбратьВалюты();
		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Расшифровка.Установить("Валюта", Ит.Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
			КорСчета(Т, Ит, 1);
	    КонецЦикла;
		Расшифровка.Установить("Валюта");
		Расшифровка.Установить("ПоВалюте");
	КонецЕсли;
	Ит = 0;
	Т.ТолькоПросмотр(1);
    Т.Опции(0, 0, 6, 0, "ОпцииПечатиАнализСчет", "АнализСчет");
	Т.ОбластьПечати(2);
    Т.Показать("Анализ счета: "+Счет+" ("+ПериодСтр(Дата1, Дата2)+?(ТипЗначения(РазделительУчета)=0, "", " "+РазделительУчета)+")", "");
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	Если Счет.Валютный = 0 Тогда
	    ДанныеПоВалютам = 0;
	КонецЕсли;
	Форма.ДанныеПоВалютам.Доступность(Счет.Валютный);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
	   	Дата1 = глРасшифровка.Получить("Дата1");
	   	Дата2 = глРасшифровка.Получить("Дата2");
	   	Счет = глРасшифровка.Получить("Счет");
		ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
		ДанныеПоВалютам = глРасшифровка.Получить("ДанныеПоВалютам");
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		РУ = глБИ.ИспользоватьРазделительУчета();
		РазделительУчета = глВосстановитьЗначение(,"БазФирма");
		ПоВсемРУ = 0;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		Если Счет.Выбран()=0 Тогда
		    ВосстановитьЗначение("ОтчРабСчет",Счет);
		КонецЕсли;
		ПриВыбореСчета();
	КонецЕсли;
	Форма.КнопкаПоУмолчанию("Сформировать");
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ПриВыбореСчета();
КонецПроцедуры