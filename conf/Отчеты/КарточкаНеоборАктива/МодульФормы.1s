// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем Подразделение,МестоХранения;
Перем фТиповаяФорма;
Перем Таб;
Перем Обновить;
Перем Расшифровка; 

// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

// ===============================
Процедура ДоступностьЭлементов(ВсеЭлементы=0)           
	
	ЭлементДиалога = Форма.АктивныйЭлемент();

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбФирма") Или (ЭлементДиалога="кХВыбФирма") Тогда
		Форма.кХВыбФирма.Доступность(ВыбФирма.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбПодразделение") Или (ЭлементДиалога="кХВыбПодразделение") Тогда
		Форма.кХВыбПодразделение.Доступность(ВыбПодразделение.Выбран()); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбМестоХранения") Или (ЭлементДиалога="кХВыбМестоХранения") Тогда
		Форма.кХВыбМестоХранения.Доступность(ВыбМестоХранения.Выбран()); 
	КонецЕсли;

	Если (ВыбПодразделение.Выбран() = 1) Или (ВыбМестоХранения.Выбран() = 1) Тогда
		ВыбНеоборотныйАктив = 0;
		Форма.ВыбНеоборотныйАктив.	Доступность(0); 
		Форма.тВыбНеоборотныйАктив.	Доступность(0); 
		Форма.кХВыбНеоборотныйАктив.Доступность(0); 
	Иначе
		Форма.ВыбНеоборотныйАктив.	Доступность(1); 
		Форма.тВыбНеоборотныйАктив.	Доступность(1); 
	КонецЕсли;

	Если (ВсеЭлементы=1) Или (ЭлементДиалога="ВыбНеоборотныйАктив") Или (ЭлементДиалога="кХВыбНеоборотныйАктив") Тогда
		Если ВыбНеоборотныйАктив.Выбран() = 1 Тогда
			ВыбПодразделение = 0;
			ВыбМестоХранения = 0;
			Форма.кХВыбНеоборотныйАктив.Доступность(1); 
			Форма.ВыбПодразделение.		Доступность(0); 
			Форма.ВыбМестоХранения.		Доступность(0); 
			Форма.тВыбПодразделение.	Доступность(0); 
			Форма.тВыбМестоХранения.	Доступность(0); 
		Иначе
			Форма.кХВыбНеоборотныйАктив.Доступность(0); 
			Форма.ВыбПодразделение.		Доступность(1); 
			Форма.ВыбМестоХранения.		Доступность(1); 
			Форма.тВыбПодразделение.	Доступность(1); 
			Форма.тВыбМестоХранения.	Доступность(1); 
		КонецЕсли;
		Форма.кХВыбПодразделение.Доступность(0); 
		Форма.кХВыбМестоХранения.Доступность(0); 
	КонецЕсли;

КонецПроцедуры  	// ДоступностьЭлементов

// ===============================
Процедура ПечатьИсторииРеквизитов(НеоборАктив)
Перем Пер;
    МДНеоборотныеАктивы = Метаданные.Справочник("НеоборотныеАктивы");
	Таб.ВывестиСекцию("ШапкаРеквизитов");
	НачВысотаТаблицы = Таб.ВысотаТаблицы();

	ПредыдущаяДата 		= ПолучитьПустоеЗначение("Дата");
	КвоРеквизитовДаты 	= 0;
	Пер = СоздатьОбъект("Периодический");
	Пер.ИспользоватьОбъект(,НеоборАктив);
	Пер.ВыбратьЗначения();
	Пока Пер.ПолучитьЗначение()=1 Цикл
		Если ПредыдущаяДата <> Пер.ДатаЗнач Тогда
			Если ПустоеЗначение(ПредыдущаяДата) = 0 Тогда
				Таб.Область(Таб.ВысотаТаблицы() - КвоРеквизитовДаты + 1, 2, Таб.ВысотаТаблицы(), 5).Объединить();
			КонецЕсли;           
			ПредыдущаяДата = Пер.ДатаЗнач;
			КвоРеквизитовДаты = 0;
		КонецЕсли;
		КвоРеквизитовДаты = КвоРеквизитовДаты + 1;
		ПечДатаЗнач = Пер.ДатаЗнач;
		ПечРеквизит = МДНеоборотныеАктивы.Реквизит(Пер.ТекущийРеквизит()).Представление();
		ПечЗначение = Пер.Значение;
		Таб.ВывестиСекцию("СтрокаРеквизитов");
	КонецЦикла;
	Таб.Область(Таб.ВысотаТаблицы() - КвоРеквизитовДаты + 1, 2, Таб.ВысотаТаблицы(), 5).Объединить();

КонецПроцедуры //ПечатьИсторииРеквизитов

// ===============================
Процедура ПечатьПеремещений(НеоборАктив)
Перем Ит3;

	ТЗПеремещения = СоздатьОбъект("ТаблицаЗначений");
	ТЗПеремещения.НоваяКолонка("Дата");
	ТЗПеремещения.НоваяКолонка("Документ");
	ТЗПеремещения.НоваяКолонка("Подразделение");
	ТЗПеремещения.НоваяКолонка("МестоХранения");

	Ит3 = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ВыбФирма.Выбран()=1 Тогда
		Ит3.ИспользоватьРазделительУчета(ВыбФирма);
	КонецЕсли;
	Ит3.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,НеоборАктив,2,0);
	Ит3.ИспользоватьСубконто(ВидыСубконто.Подразделения,,,0);
	Ит3.ИспользоватьСубконто(ВидыСубконто.МестаХранения,,,0);

	Ит3.Опции(0,0);
	Ит3.ВключатьСубсчета(0);
	Если Ит3.ВыполнитьЗапрос(Дата("01.01.1980"),ДатаОтчета,"10;11;12",,,,"Операция") = 0 Тогда
		Предупреждение ("Запрос по бухгалтерским итогам по Необоротному активу не выполнен!");
		Возврат;
	КонецЕсли;

	Ит3.ВыбратьСубконто(1);
	Если Ит3.ПолучитьСубконто(1)=1 Тогда

		Ит3.ВыбратьПериоды();
		Пока Ит3.ПолучитьПериод()=1 Цикл
			ТЗПеремещения.НоваяСтрока();
			ТЗПеремещения.Дата			= Ит3.Операция.ДатаОперации;
			ТЗПеремещения.Документ		= Ит3.Операция.ТекущийДокумент();
			Ит3.ВыбратьСубконто(2);
			Если Ит3.ПолучитьСубконто(2)=1 Тогда
				ТЗПеремещения.Подразделение	= Ит3.Субконто(2);
			КонецЕсли;

			Ит3.ВыбратьСубконто(3);
			Если Ит3.ПолучитьСубконто(3)=1 Тогда
				ТЗПеремещения.МестоХранения = Ит3.Субконто(3);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
				
	Таб.ВывестиСекцию("ШапкаПеремещения");

	ПодразделениеПеремещения = ПолучитьПустоеЗначение("Справочник.Подразделения"); 
	МестоХраненияПеремещения = ПолучитьПустоеЗначение("Справочник.МестаХранения");

	ТЗПеремещения.ВыбратьСтроки();
	Пока ТЗПеремещения.ПолучитьСтроку()=1 Цикл
		Если (ПодразделениеПеремещения = ТЗПеремещения.Подразделение) 
				И (МестоХраненияПеремещения = ТЗПеремещения.МестоХранения) Тогда
			Продолжить;
		КонецЕсли;
		ДатаПеремещения				= ТЗПеремещения.Дата;
		ДокументПеремещения			= ТЗПеремещения.Документ;
		ПодразделениеПеремещения	= ТЗПеремещения.Подразделение;
   			МестоХраненияПеремещения	= ТЗПеремещения.МестоХранения;
		Таб.ВывестиСекцию("СтрокаПеремещения");
	КонецЦикла;

КонецПроцедуры //ПечатьИсторииРеквизитов

// ===============================
Процедура ПечатьКарточки(НеоборАктив)
Перем Ит2;
	
	Наименование 		= НеоборАктив.ПолнНаименование;
	ДатаВвода 			= НеоборАктив.ДатаВвода;
	НормаАмортизации	= НеоборАктив.НормаАмортизации;
    ИнвентарныйНомер	= НеоборАктив.ИнвенНомер;
	СчетУчета 			= НеоборАктив.Счет.			Получить(ДатаОтчета);
	СчетЗатрат			= НеоборАктив.СчетЗатрат.	Получить(ДатаОтчета);
	КодГдеХранится 		= ?(Подразделение.Выбран()=1,Подразделение.Код,"");

	ПервСтоимость 		= 0;
	ПервСтоимостьЗаписи	= 0;
	Износ		 		= 0;
	ДатаЗаписи 			= "";
	НомерЗаписи 		= "";
	ДатаУбытия 			= "";
	НомерУбытия			= "";

	ТЗМодернизации = СоздатьОбъект("ТаблицаЗначений");
	ТЗМодернизации.НоваяКолонка("Дата");
	ТЗМодернизации.НоваяКолонка("Документ");
	ТЗМодернизации.НоваяКолонка("ИнвентарныйНомер");
	ТЗМодернизации.НоваяКолонка("Сумма");

	Ит2 = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ВыбФирма.Выбран()=1 Тогда
		Ит2.ИспользоватьРазделительУчета(ВыбФирма);
	КонецЕсли;
	Ит2.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,НеоборАктив,2,0);

	Ит2.Опции(0,0);
	Ит2.ВключатьСубсчета(0);
	Если Ит2.ВыполнитьЗапрос(Дата("01.01.1980"),ДатаОтчета,,,,,"Операция","С") = 0 Тогда
		Предупреждение ("Запрос по бухгалтерским итогам по Необоротному активу не выполнен!");
		Возврат;
	КонецЕсли;

	Ит2.ВыбратьСубконто(1);
	Если Ит2.ПолучитьСубконто(1)=1 Тогда

		Ит2.ВыбратьСчета();
		Пока Ит2.ПолучитьСчет()=1 Цикл
			КодСчета = Лев(Строка(Ит2.Счет.Код),2);
			Если (КодСчета < "10") Или (КодСчета > "12") Тогда
				Если КодСчета = "13" Тогда
					Износ = Ит2.СКК();
				КонецЕсли;
				Продолжить;
			КонецЕсли;

			ПервСтоимость = Ит2.СКД();
			
			Ит2.ВыбратьПериоды();
			Пока Ит2.ПолучитьПериод()=1 Цикл
				Док	= Ит2.Операция.Документ;
				Если Док.Выбран()=1 Тогда
					ДокВид = ВРег(Док.Вид());
					Если (ДокВид = "ВВОДВЭКСПЛУАТАЦИЮ") Или (ДокВид = "ВВОДОСТАТКОВНЕОБОРАКТИВОВ") Тогда
						Если ПустоеЗначение(ДатаЗаписи) = 0 Тогда
							Сообщить(Шаблон("Для Необоротного актива [НеоборАктив] обнаружен повторный документ [Док]"));
						КонецЕсли;
						ДатаЗаписи 	= Док.ДатаДок;
						НомерЗаписи = Док.НомерДок;
						ПервСтоимостьЗаписи	= Ит2.ДО()
					ИначеЕсли ДокВид = "МОДЕРНИЗАЦИЯНЕОБОРАКТИВОВ" Тогда
						ТЗМодернизации.НоваяСтрока();
						ТЗМодернизации.Дата 			= Док.ДатаДок;
						ТЗМодернизации.Документ			= Док.ТекущийДокумент();
						ТЗМодернизации.ИнвентарныйНомер = ИнвентарныйНомер;
						ТЗМодернизации.Сумма = Ит2.ДО();
					ИначеЕсли ДокВид = "ЛИКВИДАЦИЯНЕОБОРАКТИВОВ" Тогда
						Если ПустоеЗначение(ДатаУбытия) = 0 Тогда
							Сообщить(Шаблон("Для Необоротного актива [НеоборАктив] обнаружен повторный документ [Док]"));
						КонецЕсли;
						ДатаУбытия 	= Док.ДатаДок;
						НомерУбытия	= Док.НомерДок;
					КонецЕсли;
				Иначе
					ТЗМодернизации.НоваяСтрока();
					ТЗМодернизации.Дата 			= Ит2.Операция.ДатаОперации;
					ТЗМодернизации.Документ			= Ит2.Операция.ТекущийДокумент();
					ТЗМодернизации.ИнвентарныйНомер = ИнвентарныйНомер;
					ТЗМодернизации.Сумма = Ит2.ДО();
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если (ПустоеЗначение(ДатаЗаписи) = 1) И (ПустоеЗначение(ДатаВвода)=0) Тогда
		ДатаЗаписи = ДатаВвода;
	КонецЕсли;

	Если (ПустоеЗначение(ПервСтоимостьЗаписи) = 1) И (ПустоеЗначение(ПервСтоимость)=0) Тогда
		ПервСтоимостьЗаписи	= ПервСтоимость;
	КонецЕсли;

	Таб.ВывестиСекцию("ШапкаТитульная");
	Если фТиповаяФорма = 1 Тогда
		Таб.НоваяСтраница();
	Иначе
		МетодРасчетаИзноса = НеоборАктив.МетодРасчетаИзноса.Получить(ДатаОтчета);
		ПечСрокИспользования = "Срок полезного использования = " + НеоборАктив.СрокИспользования.Получить(ДатаОтчета) + " мес.";
		ПечЛиквСтоимость = "Ликв. стоимость = " + Формат(НеоборАктив.ЛиквидационнаяСтоимость.Получить(ДатаОтчета),"Ч17.2.'") + " грн.";
		ПечНормаАмортизации = "Норма амотртизации = " + НеоборАктив.НормаАмортизации+ " %";
		ПечРасчОбъемПрозводства = "Расч. объем производства = " + Формат(НеоборАктив.РасчетныйОбъемПроизводства.Получить(ДатаОтчета),"Ч11.3.'");
		ПечТекОбъемПрозводства = "Тек. объем производства = " + Формат(НеоборАктив.ТекущийОбъемПроизводства.Получить(ДатаОтчета),"Ч11.3.'");

		Если МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПрямолинейноеСписание Тогда
			ПараметрыРасчетаИзноса = ПечСрокИспользования
									+ РазделительСтрок + ПечЛиквСтоимость;
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.УменьшениеОстатка Тогда
			ПараметрыРасчетаИзноса = ПечСрокИспользования 
									+ РазделительСтрок + ПечЛиквСтоимость
									+ РазделительСтрок + ПечНормаАмортизации;
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.УскоренноеУменьшениеОстатка Тогда
			ПараметрыРасчетаИзноса = ПечСрокИспользования 
									+ РазделительСтрок + ПечЛиквСтоимость
									+ РазделительСтрок + ПечНормаАмортизации;
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.СуммЕдиниц Тогда
			ПараметрыРасчетаИзноса = ПечЛиквСтоимость 
									+ РазделительСтрок + ПечРасчОбъемПрозводства
									+ РазделительСтрок + ПечТекОбъемПрозводства;
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Кумулятивный Тогда
			ПараметрыРасчетаИзноса = ПечСрокИспользования 
									+ РазделительСтрок + ПечЛиквСтоимость;
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйМесяц Тогда
			ПараметрыРасчетаИзноса = "Норма амотртизации = 100 %";
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.ПервыйИПоследнийМесяц Тогда
			ПараметрыРасчетаИзноса = "Норма амотртизации = 50 %";
		ИначеЕсли МетодРасчетаИзноса = Перечисление.МетодыРасчетаИзноса.Налоговый Тогда
			Если НеоборАктив.ВидНеоборотногоАктива = НМА Тогда
				ПараметрыРасчетаИзноса = ПечСрокИспользования; 
			Иначе
				ПараметрыРасчетаИзноса = "Норма амортизации определяется законодательством";
			КонецЕсли;
		КонецЕсли;                       
		Таб.ВывестиСекцию("МетодРасчетаИзноса");
	КонецЕсли;

	Таб.ВывестиСекцию("ШапкаМодернизации");

	КвоМодернизаций = ТЗМодернизации.КоличествоСтрок();
	Для Инд = КвоМодернизаций+1 По 5 Цикл
		ТЗМодернизации.НоваяСтрока();
	КонецЦикла;
	
	ТЗМодернизации.ВыбратьСтроки();
	Пока ТЗМодернизации.ПолучитьСтроку()=1 Цикл
		ДатаМодернизации  				= ТЗМодернизации.Дата;
		ДокументМодернизации			= ТЗМодернизации.Документ;
		ИнвентарныйНомерМодернизации 	= ТЗМодернизации.ИнвентарныйНомер;
		СуммаМодернизации				= ТЗМодернизации.Сумма;
		Таб.ВывестиСекцию("СтрокаМодернизации");
	КонецЦикла;

	Если фТиповаяФорма <> 1 Тогда
		ПечатьИсторииРеквизитов(НеоборАктив);
		ПечатьПеремещений(НеоборАктив);
	КонецЕсли;
	Таб.ВывестиСекцию("Подвал");
	Таб.НоваяСтраница();

КонецПроцедуры

// ======================================
Процедура ЗаполнитьПоБухгалтерии()
Перем Ит;
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ВыбФирма.Выбран()=1 Тогда
		Ит.ИспользоватьРазделительУчета(ВыбФирма);
	КонецЕсли;

	Если ВыбПодразделение.Выбран() = 1 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения,ВыбПодразделение,1,0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.Подразделения,,,0);
	КонецЕсли;

	Если ВыбМестоХранения.Выбран() = 1 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,ВыбМестоХранения,2,0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения,,,0);
	КонецЕсли;

	Если ВыбНеоборотныйАктив.Выбран() = 1 Тогда
		Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,ВыбНеоборотныйАктив,2,0);
	Иначе
		Ит.ИспользоватьСубконто(ВидыСубконто.НеоборотныеАктивы,,,0);
	КонецЕсли;

	Ит.Опции(0,0);
	Если Ит.ВыполнитьЗапрос(ДатаОтчета,,"10;11;12") = 0 Тогда
		Предупреждение ("Запрос по бухгалтерским итогам (первоначальная стоимость НА) не выполнен!");
		Возврат;
	КонецЕсли;

	Ит.ВыбратьСубконто(3);
	Пока Ит.ПолучитьСубконто(3)=1 Цикл
		Подразделение = 0;
		Ит.ВыбратьСубконто(1);
		Пока Ит.ПолучитьСубконто(1)=1 Цикл
			Если ПустоеЗначение(Подразделение)=0 Тогда
				Сообщить("Необоротный актив "+Ит.Субконто(3)+" по бухгалтерским итогам числится на нескольких подразделениях:
							|""" + Подразделение + """ и """ + Ит.Субконто(1)+"""!");
			Иначе
				Подразделение = Ит.Субконто(1);
			КонецЕсли;
		КонецЦикла;
		МестоХранения = 0;
		Ит.ВыбратьСубконто(2);
		Пока Ит.ПолучитьСубконто(2)=1 Цикл
			Если ПустоеЗначение(МестоХранения)=0 Тогда
				Сообщить("Необоротный актив "+Ит.Субконто(3)+" по бухгалтерским итогам числится на нескольких местах хранения:
							|""" + МестоХранения + """ и """ + Ит.Субконто(2)+"""!");
			Иначе
				МестоХранения = Ит.Субконто(2);
			КонецЕсли;
		КонецЦикла;
		ПечатьКарточки(Ит.Субконто(3));
	КонецЦикла;

КонецПроцедуры 

// ===============================
Процедура Сформировать(ЗакрытьЭкран=0)
	Если (ВыбНеоборотныйАктив.Выбран()=0) И (ВыбПодразделение.Выбран()=0) И (ВыбМестоХранения.Выбран()=0) Тогда
		Если Вопрос("Строить по всем необоротным активам?", "Да+Нет")="Нет" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
    Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Объект", 				"КарточкаНеоборАктива");
	Расшифровка.Установить("ДатаОтчета", 			ДатаОтчета);
    Расшифровка.Установить("ВыбФирма", 				ВыбФирма);
    Расшифровка.Установить("ВыбПодразделение", 		ВыбПодразделение);
    Расшифровка.Установить("ВыбМестоХранения", 		ВыбМестоХранения);
    Расшифровка.Установить("ВыбНеоборотныйАктив", 	ВыбНеоборотныйАктив);
	Расшифровка.Установить("спФормПечати", 			спФормПечати.ТекущаяСтрока());

	//  Создание Таблицы для выходного отчета
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;
	
	фТиповаяФорма = спФормПечати.ТекущаяСтрока();
	Если фТиповаяФорма = 1 Тогда
		Таб.ИсходнаяТаблица("Типовая");
	Иначе
		Таб.ИсходнаяТаблица("Произвольная");
	КонецЕсли;
	Таб.ВывестиСекцию("Кнопки");

	ЗаполнитьПоБухгалтерии();	

	ФиксКвоСтрок = Таб.ВысотаСекции("Кнопки");
	ФиксКвоКолон = 0;
	Таб.Опции(0,0,ФиксКвоСтрок,ФиксКвоКолон, "ПарамПечатиУпрКарточкаКлиента", "РазмОкнаУпрКарточкаКлиента");
	Таб.ОбластьПечати(Таб.ВысотаСекции("Кнопки")+1);

	// Вывод заполненной формы
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("ПЕЧАТЬ: Карточка необоротного актива","");
	
	Если (Обновить = 2) Или (ЗакрытьЭкран=1) Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры

// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии()

	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		ДатаОтчета	 			= глРасшифровка.Получить("ДатаОтчета");
		ВыбФирма	 			= глРасшифровка.Получить("ВыбФирма");
		ВыбПодразделение	 	= глРасшифровка.Получить("ВыбПодразделение");
		ВыбМестоХранения	 	= глРасшифровка.Получить("ВыбМестоХранения");
		ВыбНеоборотныйАктив	 	= глРасшифровка.Получить("ВыбНеоборотныйАктив");
		спФормПечати.ТекущаяСтрока(глРасшифровка.Получить("спФормПечати"));
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Если ТипЗначенияСтр(Форма.Параметр)="Справочник" Тогда
			ВыбНеоборотныйАктив = Форма.Параметр;
			ДатаОтчета = РабочаяДата();
			спФормПечати.ТекущаяСтрока(1); // типовая
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		Обновить = 0;
	КонецЕсли;

	ДоступностьЭлементов(1);

КонецПроцедуры  


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

ДатаОтчета = РабочаяДата();

спФормПечати.УдалитьВсе();
спФормПечати.ДобавитьЗначение("Стандартная",	"Типовая форма ОЗ-6");
спФормПечати.ДобавитьЗначение("Произвольная",	"Произвольная форма");
спФормПечати.ТекущаяСтрока(1);

