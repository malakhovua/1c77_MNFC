Перем гТекРегистр,гСписокИсхПараметров;
Перем гРежим, гСпрМД, гДокМД, СтрМД, ВыбВидСубконто, СтрТип;
//*******************************************
Функция ПолучитьТип(СтрТип,Длина,Точность)
	Если ВыбАттр.ТекущаяСтрока()=0 Тогда
		Возврат "";
	КонецЕсли;
	СтрМД=ВыбАттр.ПолучитьЗначение(ВыбАттр.ТекущаяСтрока());
	Если гРежим="Регистр" Тогда
		МДАттр=гТекРегистр.Измерение(СтрМД);
		Если МДАттр.Выбран()=0 Тогда
			МДАттр=гТекРегистр.Реквизит(СтрМД);
		КонецЕсли;
		Если (МДАттр.Выбран()=0)И(СтрМД="Текущий документ") Тогда
			СтрТип="Документ";
			Тип=СтрТип;
			Длина=0;
			Точность=0;
		Иначе
			СтрТип=МДАттр.Тип;
			Тип=МДАттр.Тип;
			Вид=МДАттр.Вид;
			Если Вид<>"" Тогда
				СтрТип=СтрТип+"."+МДАттр.Вид;
			КонецЕсли;
			Длина=МДАттр.Длина;
			Точность=МДАттр.Точность;
		КонецЕсли;
    ИначеЕсли гРежим="Справочник" Тогда
		МДАттр=гСпрМД.Реквизит(СтрМД);
		Если МДАттр.Выбран()=1 Тогда
			СтрТип=МДАттр.Тип;
			Тип	= МДАттр.Тип;
			Вид = МДАттр.Вид;
			Если Вид <> "" Тогда
				СтрТип=СтрТип+"."+МДАттр.Вид;
			КонецЕсли;
			Длина=МДАттр.Длина;
			Точность=МДАттр.Точность;
		ИначеЕсли СтрМД = "Наименование" Тогда
			СтрТип = "Строка";
			Тип = "Строка";
			Длина = гСпрМД.ДлинаНаименования;
			Точность = 0;		
		Иначе
			Спр = СоздатьОбъект("Справочник." + СтрМД);
			Если СтрМД=гСпрМД.Идентификатор Тогда
				МДАттр=гСпрМД;
				ФлГруппа=1;
			ИначеЕсли МДАттр.Выбран()=0 Тогда
				МДАттр=гСпрМД.Владелец;
			КонецЕсли;
			СтрТип="Справочник"+"."+Спр.Вид();
			Тип=СтрТип;
			Длина=0;
			Точность=0;
		КонецЕсли;
	ИначеЕсли гРежим="Документ" Тогда
		МДАттр=Метаданные.ОбщийРеквизитДокумента(СтрМД);
		Если МДАттр.Выбран()=0 Тогда
			МДАттр=гДокМД.РеквизитШапки(СтрМД);
		КонецЕсли;
		Если МДАттр.Выбран()=0 Тогда
			МДАттр=гДокМД.РеквизитТабличнойЧасти(СтрМД);
		КонецЕсли;
		СтрТип = МДАттр.Тип;
		Вид	   = МДАттр.Вид;
		Если Вид <> "" Тогда
			СтрТип=СтрТип+"."+МДАттр.Вид;
		КонецЕсли;
		Тип=МДАттр.Тип;
		Длина=МДАттр.Длина;
		Точность=МДАттр.Точность;
	ИначеЕсли гРежим="Операция" Тогда
		Длина 	 = 0;
		Точность = 0;
		Если СтрМД = "Документ" Тогда
			СтрТип = "Строка";
		ИначеЕсли СтрМД = "СуммаОперации" Тогда
			СтрТип 	 = "Число";
			Длина 	 = Метаданные.ДлинаСуммыОперация;
			Точность = Метаданные.ТочностьСуммыОперации;		
		ИначеЕсли СтрМД = "Содержание" Тогда
			СтрТип = "Строка";
			Длина  = Метаданные.ДлинаСодержанияОперации;
		ИначеЕсли (СтрМД = "Дебет.Счет") Или (СтрМД = "Кредит.Счет") Тогда
			СтрТип = "Счет";
		ИначеЕсли (СтрМД = "СубконтоДт") Или (СтрМД = "СубконтоКт") Тогда
			СтрТип = "Справочник";
		ИначеЕсли СтрМД = "Валюта" Тогда
			СтрТип = "Справочник." + СокрЛП(Метаданные.Валюта);
		ИначеЕсли СтрМД = "Сумма" Тогда
			СтрТип	 = "Число";
			Длина 	 = Метаданные.ДлинаСуммыПроводки;
			Точность = Метаданные.ТочностьСуммыПроводки;			
		ИначеЕсли СтрМД = "Количество" Тогда
			СтрТип	 = "Число";
			Длина	 = Метаданные.ДлинаКоличестваПроводки;
			Точность = Метаданные.ТочностьКоличестваПроводки;			
		ИначеЕсли СтрМД = "ВалСумма" Тогда
			СтрТип	 = "Число";
			Длина	 = Метаданные.ДлинаВалютнойСуммыПроводки;
			Точность = Метаданные.ТочностьВалютнойСуммыПроводки;			
		ИначеЕсли СтрМД = "ПланСчетов" Тогда			
			СтрТип = "ПланСчетов";
		Иначе 
			МДАттр = Метаданные.ОбщийРеквизитДокумента(СтрМД);
			Если МДАттр.Выбран()=0 Тогда
				МДАттр = Метаданные.РеквизитОперации(СтрМД);
			КонецЕсли;
			Если МДАттр.Выбран()=0 Тогда
				МДАттр = Метаданные.РеквизитПроводки(СтрМД);
			КонецЕсли;
			СтрТип = МДАттр.Тип;
			СтрВид = МДАттр.Вид;
			Если СтрВид <> "" Тогда
				СтрТип = СтрТип + "." + СтрВид;
			КонецЕсли;
			Длина = МДАттр.Длина;
			Точность = МДАттр.Точность;						
		КонецЕсли;
		Тип = СтрТип;
	ИначеЕсли гРежим="Графа" Тогда
		СтрТип   = гСписокИсхПараметров.Получить("ТипГрафы");
		Длина	 = гСписокИсхПараметров.Получить("Длина");
		Точность = гСписокИсхПараметров.Получить("Точность");
		Если СокрЛП(СтрТип) = "Выражение" Тогда СтрТип = "Число" КонецЕсли;
		Тип = СтрТип;
 	КонецЕсли;
	Возврат Тип;
КонецФункции

Процедура ПриВыбореУсловия()
	Перем Стр,Длина,Точность;
	СтрТип=ПолучитьТип(Стр,Длина,Точность);
	Если СтрТип = "Неопределенный" Тогда
		СтрТип = ТипЗначенияСтр(ВыбФильтр);
	КонецЕсли;
	ВыбЗначение=ВыбУсловие.ПолучитьЗначение(ВыбУсловие.ТекущаяСтрока());
	Если ((Найти(ВыбЗначение,"<")<>0)ИЛИ(Найти(ВыбЗначение,">")<>0))И(ВыбЗначение<>"<>") Тогда
		Если (СтрТип<>"Число")И(СтрТип<>"Строка")И(СтрТип<>"Дата") Тогда
			Предупреждение("Сравнения на больше-меньше допустимы только над значениями совпадающих базовых типов (число, строка, дата)");
			ВыбУсловие.ТекущаяСтрока(1);
		КонецЕсли;
	ИначеЕсли ВыбЗначение="В" Тогда
		Если (СтрЧислоВхождений(СтрТип,"Справочник") = 0) И
			 (СтрЧислоВхождений(СтрТип,"Счет") = 0) Тогда
			Предупреждение("Условие вхождения можно задавать только для справочников и счетов!");
			ВыбУсловие.ТекущаяСтрока(1);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриВыбАттр()
	Перем Стр, Длина, Точность;
	ВыбФильтр 	 = ПолучитьПустоеЗначение();
	ВыбФильтр=0;
	ФлГруппа=0;
	ВыбУсловие.ТекущаяСтрока(1);
	Если ВыбАттр.ТекущаяСтрока()=0 Тогда
		Возврат;
	КонецЕсли;
	СтрМД=ВыбАттр.ПолучитьЗначение(ВыбАттр.ТекущаяСтрока());
	Если ПустаяСтрока(СтрМД)=1 Тогда
		Форма.ВыбФильтр.НазначитьТип("");
		Возврат;
	КонецЕсли;
	ПолучитьТип(Стр,Длина,Точность);
	Если Длина>0 Тогда
		Форма.ВыбФильтр.НазначитьТип(Стр,Длина,Точность);
	Иначе
		Форма.ВыбФильтр.НазначитьТип(Стр);
	КонецЕсли;
	Если (Стр = "Строка") И (СокрЛП(ВыбФильтр) = "0") Тогда 
		ВыбФильтр = "";					
	КонецЕсли;
КонецПроцедуры

Процедура ПриНачалеВыбораЗначения(ЭлемДиалога)	
	Перем Стр, Длина, Точность, ВыбЗначение;	
	Если СокрЛП(ЭлемДиалога) <> "ВыбФильтр" Тогда Возврат КонецЕсли;
	ВыбЗначение = ПолучитьПустоеЗначение();
	Если ВыбАттр.ТекущаяСтрока()=0 Тогда
		Возврат;
	КонецЕсли;
	СтрМД=ВыбАттр.ПолучитьЗначение(ВыбАттр.ТекущаяСтрока());
	Если ПустаяСтрока(СтрМД)=1 Тогда
		Форма.ВыбФильтр.НазначитьТип("");
		Возврат;
	КонецЕсли;
	ПолучитьТип(Стр,Длина,Точность);
		
	Если Стр = "Неопределенный" Тогда
		сзТипов = СоздатьОбъект("СписокЗначений");
		сзТипов.ДобавитьЗначение("ВидСубконто", "Вид субконто");
		сзТипов.ДобавитьЗначение("Справочник");
		сзТипов.ДобавитьЗначение("Документ");
		сзТипов.ДобавитьЗначение("Перечисление");
		сзТипов.ДобавитьЗначение("ПланСчетов", "План cчетов");
		сзТипов.ДобавитьЗначение("Счет");
		сзТипов.ДобавитьЗначение("Число");
		сзТипов.ДобавитьЗначение("Строка");
		сзТипов.ДобавитьЗначение("Дата");            
		ТипЗначен = "";
		ВидЗначен = "";
		Длина = 0;
		Точность = 0;
		Поз = ПолучитьПустоеЗначение();
		Если сзТипов.ВыбратьЗначение(ТипЗначен, "Выберите тип реквизита", Поз, , 0) = 0 Тогда
			Возврат;
		КонецЕсли;
		Если ТипЗначен = "Число" Тогда
			Если ВвестиЗначение(Длина,"Введите длину числа", "Число", 2, 0) = 0 Тогда
				Возврат;
			КонецЕсли;
			Если ВвестиЗначение(Точность,"Введите точность числа", "Число", 1, 0) = 0 Тогда
				Возврат;
			КонецЕсли;
		ИначеЕсли ТипЗначен = "Строка" Тогда
			Длина = 22;
		ИначеЕсли ТипЗначен = "Перечисление" Тогда
			сзВидовПеречислений	= СоздатьОбъект("СписокЗначений");
			Для Сч = 1 По Метаданные.Перечисление() Цикл
				Иден = Метаданные.Перечисление(Сч).Идентификатор;
				Пред = Метаданные.Перечисление(Сч).Представление();
				сзВидовПеречислений.ДобавитьЗначение(Иден, Пред);
			КонецЦикла;
			Поз = ПолучитьПустоеЗначение();
			Если сзВидовПеречислений.ВыбратьЗначение(ВидЗначен, "Выберите вид перечисления", Поз, , 0) = 0 Тогда
				Возврат;
			КонецЕсли;
		ИначеЕсли ТипЗначен = "ВидСубконто" Тогда
			ВыбВидСубконто = ПолучитьПустоеЗначение(); 
			Если ВвестиВидСубконто(ВыбВидСубконто,"Выберите вид субконто") = 0 Тогда
				Возврат;
			КонецЕсли;		
			ТипЗначен = Метаданные.ВидСубконто(ВыбВидСубконто.Идентификатор()).Тип;
			ВидЗначен = Метаданные.ВидСубконто(ВыбВидСубконто.Идентификатор()).Вид;			
		КонецЕсли;
		Стр = ТипЗначен + ?(ВидЗначен = "","",".") + ВидЗначен;
		СтрТип = СокрЛП(ТипЗначен);
	КонецЕсли;
	
	Если гРежим = "Операция" Тогда    	
		Если СтрМД = "Документ" Тогда			
			сзВидовБухДокументов = СоздатьОбъект("СписокЗначений");
			Для Сч = 1 По Метаданные.Документ() Цикл				
				ДокМД = Метаданные.Документ(Сч); 
				Если СокрЛП(ДокМД.БухгалтерскийУчет) = "1" Тогда 
					Иден = ДокМД.Идентификатор;
					Пред = ДокМД.Представление();				
					сзВидовБухДокументов.ДобавитьЗначение(Иден, Пред);							
				КонецЕсли; 
			КонецЦикла;
			Поз 	  = ПолучитьПустоеЗначение();
			ВидЗначен = ПолучитьПустоеЗначение();			
			Если сзВидовБухДокументов.ВыбратьЗначение(ВидЗначен, "Выберите вид документа операции", Поз, , 0) = 0 Тогда
				Возврат;
			КонецЕсли;                    
			Форма.ВыбФильтр.НазначитьТип("Строка");
			ВыбФильтр = ВидЗначен;
			Возврат;
		ИначеЕсли (СтрМД = "СубконтоДт") Или (СтрМД = "СубконтоКт") Тогда						
			ВыбВидСубконто = ПолучитьПустоеЗначение(); 
			Если ВвестиВидСубконто(ВыбВидСубконто,"Выберите вид субконто") = 0 Тогда
				Возврат;
			КонецЕсли;		
			ТипЗначен = Метаданные.ВидСубконто(ВыбВидСубконто.Идентификатор()).Тип;
			ВидЗначен = Метаданные.ВидСубконто(ВыбВидСубконто.Идентификатор()).Вид;
			Стр = ТипЗначен + ?(ВидЗначен = "","",".") + ВидЗначен;			
		КонецЕсли;		
	КонецЕсли;
	
	Если (Стр = "ПланСчетов") Тогда
		ПланЗначение = ПланыСчетов.ЗначениеПоНомеру(1);
		Форма.ВыбФильтр.УстановитьТип(ПланЗначение);
		Возврат;
	Иначе
		Форма.ВыбФильтр.НазначитьТип(Стр,Длина,Точность);	
	КонецЕсли;
КонецПроцедуры

Процедура Выбрать()
	Перем СтрПредставления;
	ТекСтр=ВыбАттр.ТекущаяСтрока();
	Если ТекСтр=0 Тогда
		Предупреждение("Внимание!Не выбрано измерение регистра!");
		Возврат;
	КонецЕсли;
	ТекИзмерение=ВыбАттр.ПолучитьЗначение(ТекСтр,СтрПредставления);
	Если ПустоеЗначение(ТекИзмерение)=1 Тогда
		Предупреждение("Внимание!Не выбрано измерение регистра!");
		Возврат;
	КонецЕсли;
	Если (СтрПредставления="Родитель")ИЛИ(СтрПредставления="Владелец") Тогда
		ТекИзмерение=СтрПредставления;
	КонецЕсли;
	ТекСтр=ВыбУсловие.ТекущаяСтрока();
	Если ТекСтр=0 Тогда
		Предупреждение("Внимание!Не выбрано условие!");
		Возврат;
	КонецЕсли;
	ТекУсловие=ВыбУсловие.ПолучитьЗначение(ТекСтр);
	Если ПустоеЗначение(ТекИзмерение)=1 Тогда
		Предупреждение("Внимание!Не выбрано условие!");
		Возврат;
	КонецЕсли;
	гСписокИсхПараметров.УдалитьВсе();
	гСписокИсхПараметров.ДобавитьЗначение(ТекИзмерение,"Атрибут");
	гСписокИсхПараметров.ДобавитьЗначение(СтрПредставления,"Представление");
	гСписокИсхПараметров.ДобавитьЗначение(ТекУсловие,"Условие");
	гСписокИсхПараметров.ДобавитьЗначение(ВыбФильтр,"Значение");
	гСписокИсхПараметров.ДобавитьЗначение(ФлИспользовать,"ВклУсловие");
	Если (СтрМД = "СубконтоДт") Или (СтрМД = "СубконтоКт") Тогда
		гСписокИсхПараметров.ДобавитьЗначение(ВыбВидСубконто.Идентификатор(), "ВидСубконто");
	Иначе
		гСписокИсхПараметров.ДобавитьЗначение(ПолучитьПустоеЗначение("ВидСубконто"), "ВидСубконто");
	КонецЕсли; 

    Форма.Параметр=гСписокИсхПараметров;
	Форма.Закрыть();
КонецПроцедуры

гСписокИсхПараметров=Форма.Параметр;
Если ПустоеЗначение(гСписокИсхПараметров)=0 Тогда
	гРежим			= гСписокИсхПараметров.Получить("Режим");
	ИсхАтр			= гСписокИсхПараметров.Получить("Атрибут");
	ИсхПред			= гСписокИсхПараметров.Получить("Представление");
	ИсхУсл			= СокрЛП(гСписокИсхПараметров.Получить("Условие"));
	ИсхЗнач			= гСписокИсхПараметров.Получить("Значение");
	ФлИспользовать 	= гСписокИсхПараметров.Получить("ВклУсловие");
	Если ПустоеЗначение(ИсхАтр)=1 Тогда
		ФлИспользовать=1;
	КонецЕсли;
	ВыбАттр.УдалитьВсе();

	Если гРежим="Регистр" Тогда
		гТекРегистр=гСписокИсхПараметров.Получить("Объект");
		Для Инд=1 По гТекРегистр.Измерение() Цикл
			ВыбАттр.ДобавитьЗначение(гТекРегистр.Измерение(Инд).Идентификатор,гТекРегистр.Измерение(Инд).Представление());
		КонецЦикла;
		Для Инд=1 По гТекРегистр.Реквизит() Цикл
			ВыбАттр.ДобавитьЗначение(гТекРегистр.Реквизит(Инд).Идентификатор,гТекРегистр.Реквизит(Инд).Представление());
		КонецЦикла;
		ВыбАттр.ДобавитьЗначение("Текущий документ","Текущий документ");
	ИначеЕсли гРежим="Справочник" Тогда
		гСпрМД=гСписокИсхПараметров.Получить("Объект");
		//владелец
		Если СокрЛП(гСпрМД.Владелец)<>"Метаданные" Тогда
			ВыбАттр.ДобавитьЗначение(гСпрМД.Владелец.Идентификатор, "Владелец");
		КонецЕсли;
		//родитель
		Если гСпрМД.КоличествоУровней > 1 Тогда
			ВыбАттр.ДобавитьЗначение(гСпрМД.Идентификатор, "Родитель");
		КонецЕсли;
		//Наименование
		ВыбАттр.ДобавитьЗначение("Наименование", "Наименование");		
		Для Инд = 1 По гСпрМД.Реквизит() Цикл
			ВыбАттр.ДобавитьЗначение(гСпрМД.Реквизит(Инд).Идентификатор, гСпрМД.Реквизит(Инд).Представление());
		КонецЦикла;
	ИначеЕсли гРежим="Документ" Тогда
		гДокМД=гСписокИсхПараметров.Получить("Объект");
		//общие реквизиты
		Для Инд = 1 По Метаданные.ОбщийРеквизитДокумента() Цикл
			Рекв=Метаданные.ОбщийРеквизитДокумента(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор,
					Рекв.Представление());
			КонецЕсли;
		КонецЦикла;
		// Реквизиты шапки...
		Для Инд = 1 По гДокМД.РеквизитШапки() Цикл
			Рекв=гДокМД.РеквизитШапки(Инд);
			ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор, Рекв.Представление());
		КонецЦикла;
		//табличная часть
		Для Инд = 1 По гДокМД.РеквизитТабличнойЧасти() Цикл
			Рекв=гДокМД.РеквизитТабличнойЧасти(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор, 
					Рекв.Представление());
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли гРежим="Операция" Тогда
		гСпрМД="Операция";
		ВыбАттр.ДобавитьЗначение("Документ", "Документ операции");		
		ВыбАттр.ДобавитьЗначение("СуммаОперации", "Сумма операции");
		ВыбАттр.ДобавитьЗначение("Содержание", "Содержание операции");		
		//реквизиты операций
		Для Инд = 1 По Метаданные.РеквизитОперации() Цикл
			Рекв=Метаданные.РеквизитОперации(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор, Рекв.Представление());
			КонецЕсли;
		КонецЦикла;				
		ВыбАттр.ДобавитьЗначение("Дебет.Счет", "Счет дебета проводки");
		ВыбАттр.ДобавитьЗначение("Кредит.Счет", "Счет кредита проводки");		
		ВыбАттр.ДобавитьЗначение("СубконтоДт", "Субконто дебета проводки");
		ВыбАттр.ДобавитьЗначение("СубконтоКт", "Субконто кредита проводки");		
		ВыбАттр.ДобавитьЗначение("Количество", "Количество проводки");		
		ВыбАттр.ДобавитьЗначение("Сумма", "Сумма проводки");
		ВыбАттр.ДобавитьЗначение("Валюта", "Валюта проводки");		
		ВыбАттр.ДобавитьЗначение("ВалСумма", "Сумма в валюте");
		Если ПланыСчетов.КоличествоЗначений() > 1 Тогда 
			ВыбАттр.ДобавитьЗначение("ПланСчетов", "План счетов");				
		КонецЕсли;		
		//реквизиты проводок
		Для Инд = 1 По Метаданные.РеквизитПроводки() Цикл
			Рекв=Метаданные.РеквизитПроводки(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор, Рекв.Представление());
			КонецЕсли;
		КонецЦикла;				
		//общие реквизиты документов
		Для Инд = 1 По Метаданные.ОбщийРеквизитДокумента() Цикл
			Рекв=Метаданные.ОбщийРеквизитДокумента(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор, Рекв.Представление());
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли гРежим="Графа" Тогда
		ВыбАттр.ДобавитьЗначение(ИсхАтр, ИсхПред);		
	КонецЕсли;

	ТекСтр=ВыбАттр.НайтиЗначение(ИсхАтр);
	Если (ТекСтр=0)И(ВыбАттр.РазмерСписка()>0) Тогда
		ТекСтр=1;
	КонецЕсли;
	ВыбАттр.ТекущаяСтрока(ТекСтр);

	ПриВыбАттр(); 
	            
	Стр = "";
	ПолучитьТип(Стр, 0, 0);
	Если Стр = "Неопределенный" Тогда
		Форма.ВыбФильтр.НазначитьТип(ТипЗначенияСтр(ИсхЗнач));
	КонецЕсли;          
	
	ВыбФильтр=ИсхЗнач;
  	ТекФильтр=ВыбФильтр;
	ВыбУсловие.ДобавитьЗначение("=");
	ВыбУсловие.ДобавитьЗначение(">");
	ВыбУсловие.ДобавитьЗначение("<");
	ВыбУсловие.ДобавитьЗначение("<=");
	ВыбУсловие.ДобавитьЗначение(">=");
	ВыбУсловие.ДобавитьЗначение("<>");
	ВыбУсловие.ДобавитьЗначение("В");

	ТекСтр=ВыбУсловие.НайтиЗначение(ИсхУсл);
	Если (ВыбУсловие.НайтиЗначение(ИсхУсл)=0) Тогда
		ТекСтр=1;
	КонецЕсли;
	ВыбУсловие.ТекущаяСтрока(ТекСтр);
КонецЕсли;
Форма.Параметр=ПолучитьПустоеЗначение();
