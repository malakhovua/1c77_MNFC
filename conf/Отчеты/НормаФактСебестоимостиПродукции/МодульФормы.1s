// ===============================
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
// ===============================

Перем ТекСтрокаВТаблицеМФ; // текущая строка в таблице значений  МФ 

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;
Перем Обновить;
Перем Расшифровка;
                        
// переменные предназначены для механизма универсальных группировок отчета
Перем СписокГруппировок;
Перем КоличествоГруппировок;
Перем НомерГруппировкиПоТМЦ;

// переменные предназначены для запроса по периодам
Перем Запрос,ЗапросСПериодами,ТаблицаПериодов,Периодичность;
Перем Язык, УкрГруппировки;

Перем Дата1, Дата2;

Перем ВыпускПродукции, ВыпускПродукцииЗаПериод;

Перем ГруппировкиПоВидамЗатрат;

// ===============================
// "СЛУЖЕБНЫЕ" ПРОЦЕДУРЫ И ФУНКЦИИ
// ===============================

// ===============================
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

// ===============================
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      

	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

// ===============================
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Основной,Подвал");
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()   

// ===============================
// УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  ТекстЗапроса - переданный по ссылке текст запроса
// 			   ТекстЗагол   - переданный по ссылке текст заголовка
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Дополняет строку запроса и заголовка в соответствии с выбранными группировками.
//       
Процедура УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
	                       
	СписокГруппировок = СоздатьОбъект("СписокЗначений");
	НомерГруппировкиПоТМЦ = 9999;
	
	Для Сч=1 По Группировки.РазмерСписка() Цикл
		
		Если Группировки.Пометка(Сч)=1 Тогда
			ПредставлениеГрупп=""; УкрПредставлениеГрупп="";
			ТекстГрупп		= Группировки.ПолучитьЗначение(Сч,ПредставлениеГрупп);
			УкрГруппировки.ПолучитьЗначение(УкрГруппировки.НайтиЗначение(ТекстГрупп),УкрПредставлениеГрупп);
			
            ТекстБезУпорядочивания = "";
			ТекстБезГрупп = "";
			Если  ((ТекстГрупп = "Подразделение") Или (ТекстГрупп = "Продукция") Или (ТекстГрупп = "Материал"))
				И (фБезГрупп = 1) Тогда
					ТекстБезГрупп = " без групп";
			ИначеЕсли (ТекстГрупп = "Заказ" ) И (глОптимизацияОтчетов = 1) Тогда
				ТекстБезУпорядочивания = " без упорядочивания";
			КонецЕсли;
			ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка "+ТекстГрупп+ТекстБезУпорядочивания+ТекстБезГрупп+";";
			ТекстЗагол	= ТекстЗагол + " / "+?(Язык="у",УкрПредставлениеГрупп,ПредставлениеГрупп);
			СписокГруппировок.ДобавитьЗначение(ТекстГрупп,ПредставлениеГрупп);
			Если  (ТекстГрупп = "Продукция") Тогда
			    НомерГруппировкиПоТМЦ = СписокГруппировок.РазмерСписка();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	Если ПустаяСтрока(ТекстЗагол)=0 Тогда // удаляем ведущий разделитель " / "
		ТекстЗагол = Сред(ТекстЗагол,4)
	КонецЕсли;
КонецПроцедуры // УстановитьГруппировкиЗапроса()
    
// ===============================
Функция ПериодСтрока()
	Зн = СписокПериодов.ПолучитьЗначение(СписокПериодов.ТекущаяСтрока());
    Если Зн = "Общий" Тогда
    	Возврат 0;
	Иначе
		Возврат Зн;
    КонецЕсли;
КонецФункции //

// ===============================
Процедура ПредопределенныеНастройки()
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("ПоПродукции", 			"Анализ по продукции");
	Меню.ДобавитьЗначение("ПоПродукцииСтатьям", 	"Анализ продукции по статьям калькуляции");
	Меню.ДобавитьЗначение("ПоПродукцииЗаказам", 	"Анализ продукции по заказам");
	Меню.ДобавитьЗначение("ПоПодразделениям", 		"Анализ по подразделениям");
	Меню.ДобавитьЗначение("ПоПодразделениямСтатьям","Анализ подразделений по статьям калькуляции");
	Меню.ДобавитьЗначение("ПоСтатьям", 				"Анализ по статьям калькуляции");
	Меню.ДобавитьЗначение("ПоВидамДеятельности", 	"Анализ по видам деятельности");
	Значение = "";
	Если Меню.ВыбратьЗначение(Значение, , , , 1) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Группировки.УдалитьВсе();
	Если Значение = "ПоПродукции" Тогда
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,0);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 1;
	ИначеЕсли Значение = "ПоПродукцииСтатьям" Тогда
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,1);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 1;
	ИначеЕсли Значение = "ПоПродукцииЗаказам" Тогда
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,1);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 1;
	ИначеЕсли Значение = "ПоПодразделениям" Тогда
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,0);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 0;
	ИначеЕсли Значение = "ПоПодразделениямСтатьям" Тогда
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,1);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 0;
	ИначеЕсли Значение = "ПоСтатьям" Тогда
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,0);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 0;
	ИначеЕсли Значение = "ПоВидамДеятельности" Тогда
		Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
		Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
		Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
		Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
		Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
		Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
		Группировки.ДобавитьЗначение("Материал",		"Материал");
		
		Группировки.Пометка(1,1);
		Группировки.Пометка(2,0);
		Группировки.Пометка(3,0);
		Группировки.Пометка(4,0);
		Группировки.Пометка(5,0);
		Группировки.Пометка(6,0);
		Группировки.Пометка(7,0);
		
		фСебестоимостьЗаЕдиницу = 0;
	КонецЕсли;
КонецПроцедуры // КнопкаНастройкиГруппировок

// ===============================
// ПечатьСтроки(Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка)
//
// Параметры:
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//	ЭтоГруппа - признак группы справочника (Число 1/0)
//	НазваниеГруппировки - название группировки (Строка)
//
// Возвращаемое значение:
//  1 - строку нужно выводить, 0 - не нужно
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса.
//                                                
Функция ПечатьСтроки(Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка = "",ЭтоГруппа=0, НазваниеГруппировки="")
Перем Секция;
	
	НормСумма = Запрос.НормСумма;
	ФактСумма = Запрос.ФактСумма;
	Если (НормСумма = 0) и (ФактСумма = 0)  Тогда
	    Возврат 0; // в выборке нет данных по суммам, пропускаем строку
	КонецЕсли;
	
	Наим = ПечТекстСтроки;
	Значение = ТекРасшифровка;
	Секция = Таб.ПолучитьСекцию(НазваниеСекции+"|Наименование");
	Область = Секция.Область("R1C1:R1C7");
	Область.Подчеркнутый(ЭтоГруппа);
	Таб.ВывестиСекцию(Секция);
	
	ПечНорма = глФРМ(НормСумма,Гривня,0);
	ПечФакт  = глФРМ(ФактСумма,Гривня,0);
	Если (НазваниеГруппировки = "Материал") и (фКоличествоМатериалов = 1) Тогда
		// для материалов выведем количество
		Материал = Запрос.ПолучитьАтрибут(НазваниеГруппировки);
		Если ПустоеЗначение(Материал) = 0 Тогда
			ПечНорма = ПечНорма + "
					|К="+глФРМКоличествоВОтчете(Запрос.НормКво,Материал);
			ПечФакт = ПечФакт + "
					|К="+глФРМКоличествоВОтчете(Запрос.ФактКво,Материал);
		КонецЕсли;
	КонецЕсли;
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Себестоимость");
	
	Если фСебестоимостьЗаЕдиницу = 1 Тогда
		Если ВыпускПродукции = 0 Тогда
			ПечНормаЗаЕдиницу = "-";
			ПечФактЗаЕдиницу  = "-";
		Иначе
			ПечНормаЗаЕдиницу = глФРМ(НормСумма/ВыпускПродукции,Гривня,0);
			ПечФактЗаЕдиницу  = глФРМ(ФактСумма/ВыпускПродукции,Гривня,0);
			Если (НазваниеГруппировки = "Материал") и (фКоличествоМатериалов = 1) Тогда
				// для материалов выведем количество
				Если ПустоеЗначение(Материал) = 0 Тогда
					ПечНормаЗаЕдиницу = ПечНормаЗаЕдиницу + "
							|К="+глФРМКоличествоВОтчете(Запрос.НормКво/ВыпускПродукции,Материал);
					ПечФактЗаЕдиницу = ПечФактЗаЕдиницу + "
							|К="+глФРМКоличествоВОтчете(Запрос.ФактКво/ВыпускПродукции,Материал);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|СебестоимостьЗаЕдиницу");
	КонецЕсли;                            
	
	Если фОтклоненияОтНормы = 1 Тогда
		ПечСуммаОтклонения = глФРМ(ФактСумма - НормСумма,Гривня,0);
		ПечПроцентОтклонения = глФРМ(?(НормСумма = 0,0,(ФактСумма - НормСумма)/НормСумма*100),Гривня,0);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Отклонения");
	КонецЕсли;
	Возврат 1
КонецФункции //ПечатьСтроки()

// ===============================
// ПечатьПериода(Ном,НазваниеСекции)
//
// Параметры:
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	НазваниеГруппировки - название группировки (Строка)
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит присоединение к печатнуй форме одного периода.
//                                                
Процедура ПечатьПериода(Ном,НазваниеСекции,НазваниеГруппировки="")
Перем ПечНормаЗаЕдиницу, ПечФактЗаЕдиницу;
    
	НормСумма = ЗапросСПериодами.НормСумма;
	ФактСумма = ЗапросСПериодами.ФактСумма;
	
	ПечНорма = глФРМ(НормСумма,Гривня,0);
	ПечФакт  = глФРМ(ФактСумма,Гривня,0);
	Если (НазваниеГруппировки = "Материал") и (фКоличествоМатериалов = 1) Тогда
		// для материалов выведем количество
		Материал = ЗапросСПериодами.ПолучитьАтрибут(НазваниеГруппировки);
		Если ПустоеЗначение(Материал) = 0 Тогда
			ПечНорма = ПечНорма + "
					|К="+глФРМКоличествоВОтчете(ЗапросСПериодами.НормКво,Материал);
			ПечФакт = ПечФакт + "
					|К="+глФРМКоличествоВОтчете(ЗапросСПериодами.ФактКво,Материал);
		КонецЕсли;
	КонецЕсли;
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Себестоимость");
	
	Если фСебестоимостьЗаЕдиницу = 1 Тогда
		Если ВыпускПродукцииЗаПериод = 0 Тогда
			ПечНормаЗаЕдиницу = "-";
			ПечФактЗаЕдиницу  = "-";
		Иначе
			ПечНормаЗаЕдиницу = глФРМ(НормСумма/ВыпускПродукцииЗаПериод,Гривня,0);
			ПечФактЗаЕдиницу  = глФРМ(ФактСумма/ВыпускПродукцииЗаПериод,Гривня,0);
			Если (НазваниеГруппировки = "Материал") и (фКоличествоМатериалов = 1) Тогда
				// для материалов выведем количество
				Если ПустоеЗначение(Материал) = 0 Тогда
					ПечНормаЗаЕдиницу = ПечНормаЗаЕдиницу + "
							|К="+глФРМКоличествоВОтчете(ЗапросСПериодами.НормКво/ВыпускПродукцииЗаПериод,Материал);
					ПечФактЗаЕдиницу = ПечФактЗаЕдиницу + "
							|К="+глФРМКоличествоВОтчете(ЗапросСПериодами.ФактКво/ВыпускПродукцииЗаПериод,Материал);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|СебестоимостьЗаЕдиницу");
	КонецЕсли;     
	
	Если фОтклоненияОтНормы = 1 Тогда
		ПечСуммаОтклонения = глФРМ(ФактСумма - НормСумма,Гривня,0);
		ПечПроцентОтклонения = глФРМ(?(НормСумма = 0,0,(ФактСумма - НормСумма)/НормСумма*100),Гривня,0);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Отклонения");
	КонецЕсли;
	
КонецПроцедуры //ПечатьПериода()

// ===============================
// ВывестиГруппировки(Ном)
//
// Параметры:
//	Ном - номер выводимой группировки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Выводит в печатную форму одну группировку запроса. Если
//	Есть нижележащие группировки, они выводятся также с использованием рекурсивного
//	вызова этой же процедуры.
//
Процедура ВывестиГруппировки(Ном=1)
	Если Ном <= КоличествоГруппировок Тогда
		НазваниеГруппировки = СписокГруппировок.ПолучитьЗначение(Ном);
		Пока Запрос.Группировка(Ном) = 1 Цикл
			НазваниеСекции="Группировка"+Ном;
			Значение = Запрос.ПолучитьАтрибут(НазваниеГруппировки);
			
			Если (НазваниеГруппировки <> "Документ") и (НазваниеГруппировки <> "Заказ") Тогда
				ПечТекстСтроки = глСмещениеГруппы(Значение,фБезГрупп) + Значение;
			Иначе
				ПечТекстСтроки = ""+глДокументВОтчете(Значение,"с номером","с датой",Язык);
			КонецЕсли;
			
			Расшифровка.Установить("Выб"+НазваниеГруппировки, Значение);
			ПечТекстСтроки = ?(ПустоеЗначение(ПечТекстСтроки)=1,глПредставлениеПустогоЗначения(НазваниеГруппировки,Язык),ПечТекстСтроки);
			
			Если фСебестоимостьЗаЕдиницу = 1 Тогда
				// сделаем так, чтобы количество выпущенной продукции было доступно и в нижних группировках
				// по которым разреза в ВыпускПродукции нет
				Если (НазваниеГруппировки = "Материал") или  (НазваниеГруппировки = "СтатьяКалькуляции") или (НазваниеГруппировки = "Материал") Тогда
				    // выпущенное кол-во определилось на вышестоящих группировках, переопределять не надо
				Иначе
					// в этом разрезе выпуск хранится
					ВыпускПродукции = Запрос.Выпуск;
				КонецЕсли;
			КонецЕсли;
			
			Если ПечатьСтроки(Ном,НазваниеСекции,ПечТекстСтроки,Расшифровка,Запрос.ЭтоГруппа(НазваниеГруппировки),НазваниеГруппировки) = 1 Тогда
				Если (Периодичность <> 0) Тогда
					// выведем теперь все периоды, с разворотом вправо
					ТаблицаПериодов.ВыбратьСтроки();
					Пока ТаблицаПериодов.ПолучитьСтроку() = 1 Цикл
						глПозиционироватьЗапросПоПериодам(Запрос,ЗапросСПериодами,СписокГруппировок,КоличествоГруппировок,Ном,ТаблицаПериодов.РеалНачалоПериода);

						Если фСебестоимостьЗаЕдиницу = 1 Тогда
							// сделаем так, чтобы количество выпущенной продукции было доступно и в нижних группировках
							// по которым разреза в ВыпускПродукции нет
							Если (НазваниеГруппировки = "Материал") или  (НазваниеГруппировки = "СтатьяКалькуляции") или (НазваниеГруппировки = "Материал") Тогда
							    // выпущенное кол-во определилось на вышестоящих группировках, переопределять не надо
							Иначе
								// в этом разрезе выпуск хранится
								ВыпускПродукцииЗаПериод = ЗапросСПериодами.Выпуск;
							КонецЕсли;
						КонецЕсли;
						
						ПечатьПериода(Ном,НазваниеСекции,НазваниеГруппировки);
					КонецЦикла;
				КонецЕсли;
				
				глОживить(1);
			КонецЕсли;
			

			// если есть более детальная группировка - выведем ее
			Если КоличествоГруппировок > Ном Тогда
				ВывестиГруппировки(Ном+1);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВывестиГруппировки()

// ===============================
// Назначение:
//		
// Аргументы:
//		
Процедура ВывестиШапкуПериода(Период)
	Таб.ПрисоединитьСекцию("Шапка|Себестоимость");
	Если фСебестоимостьЗаЕдиницу = 1 Тогда
		Таб.ПрисоединитьСекцию("Шапка|СебестоимостьЗаЕдиницу");
	КонецЕсли;
	
	Если фОтклоненияОтНормы = 1 Тогда
		Таб.ПрисоединитьСекцию("Шапка|Отклонения");
	КонецЕсли;
КонецПроцедуры //


// ===============================
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// ===============================

//============================
Процедура ИзмПериод(Знач Пер, Знач Мес)
	Если Пер = 1 Тогда
		Дата1 = НачМесяца(Мин(ДобавитьМесяц(Дата1,Мес),Дата2));
	Иначе
		Дата2 = КонМесяца(Макс(ДобавитьМесяц(Дата2,Мес),Дата1));
	КонецЕсли;
КонецПроцедуры

// ===============================
Функция УстДоступность()
	фДат = ?(Дата2=КонМесяца(Дата1),0,1);
	Форма.кПериодМес1.Доступность(фДат);
	Форма.кПериодГод1.Доступность(фДат);
	Форма.кПериодМес2.Доступность(фДат);
	Форма.кПериодГод2.Доступность(фДат);
	ПериодС = ПериодСтр(Дата1,КонМесяца(Дата1));
	ПериодПо = ПериодСтр(НачМесяца(Дата2),Дата2);
	
	Форма.кХФирма.Доступность(1-ПустоеЗначение(ВыбФирма));
	Форма.кХВидДеятельности.Доступность(1-ПустоеЗначение(ВыбВидДеятельности));
	Форма.кХПодразделение.Доступность(1-ПустоеЗначение(ВыбПодразделение));
	Форма.кХПродукция.Доступность(1-ПустоеЗначение(ВыбПродукция));
	Форма.кХЗаказ.Доступность(1-ПустоеЗначение(ВыбЗаказ));
	Форма.кХСтатьяКалькуляции.Доступность(1-ПустоеЗначение(ВыбСтатьяКалькуляции));
	Форма.кХМатериал.Доступность(1-ПустоеЗначение(ВыбМатериал));
КонецФункции //УстДоступность

// ===============================
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог	
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем ТекстЗапроса;
	
	глПроверкаДаты(Дата1,Дата2);
	
	ПечФорма = "Таблица";
	Язык = глЯзык(ПечФорма); 
	глУстПропись(Гривня,Язык);
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;
	Таб.ИсходнаяТаблица(ПечФорма);
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	
	// все настройки помещаем в список
	Расшифровка.Установить("Объект", "НормаФактСебестоимостиПродукции");
	Расшифровка.Установить("Дата1", 		Дата1);
    Расшифровка.Установить("Дата2", 		Дата2);
	Расшифровка.Установить("ВыбФирма",		ВыбФирма);
	
	Расшифровка.Установить("ВыбВидДеятельности",	ВыбВидДеятельности);
	Расшифровка.Установить("ВыбПодразделение",		ВыбПодразделение);
	Расшифровка.Установить("ВыбПродукция",			ВыбПродукция);
	Расшифровка.Установить("ВыбЗаказ",				ВыбЗаказ);
	Расшифровка.Установить("ВыбСтатьяКалькуляции",	ВыбСтатьяКалькуляции);
	Расшифровка.Установить("ВыбМатериал",	ВыбМатериал);
	Расшифровка.Установить("Группировки",	Группировки);	
	Расшифровка.Установить("фБезГрупп",		фБезГрупп);
	Расшифровка.Установить("фСебестоимостьЗаЕдиницу",фСебестоимостьЗаЕдиницу);
	Расшифровка.Установить("фКоличествоМатериалов",фКоличествоМатериалов);
	Расшифровка.Установить("фОтклоненияОтНормы",фОтклоненияОтНормы);
	Расшифровка.Установить("СписокПериодов",СписокПериодов.ТекущаяСтрока());
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были

	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ);
	КонецЕсли;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ЗапросСПериодами = СоздатьОбъект("Запрос");
	               
	Если Дата2 >= ПолучитьДатуТА() Тогда
		ТекстЗапроса = "
			|Период с Дата1;";
	Иначе 
		ТекстЗапроса = "
			|Период с Дата1 по Дата2;";
	КонецЕсли;
	
	Если фСебестоимостьЗаЕдиницу = 1 Тогда
	    // запрос с учетом количества выпущенной продукции
		ТекстЗапроса = ТекстЗапроса + "
		|Фирма 			= Регистр.ПроизводственныеЗатраты.Фирма,Регистр.НормативныеЗатраты.Фирма,Регистр.ВыпускПродукции.Фирма;
		|ВидДеятельности= Регистр.ПроизводственныеЗатраты.ВидДеятельности,Регистр.НормативныеЗатраты.ВидДеятельности,Регистр.ВыпускПродукции.ВидДеятельности;
		|Подразделение	= Регистр.ПроизводственныеЗатраты.Подразделение,Регистр.НормативныеЗатраты.Подразделение,Регистр.ВыпускПродукции.Подразделение;
		|Продукция		= Регистр.ПроизводственныеЗатраты.Продукция,Регистр.НормативныеЗатраты.Продукция,Регистр.ВыпускПродукции.Продукция;
		|Заказ			= Регистр.ПроизводственныеЗатраты.Заказ,Регистр.НормативныеЗатраты.Заказ,Регистр.ВыпускПродукции.Заказ;
		|СтатьяКалькуляции = Регистр.ПроизводственныеЗатраты.ВидЗатрат.СтатьяКалькуляции,Регистр.НормативныеЗатраты.СтатьяКалькуляции;
		|Материал 		= Регистр.ПроизводственныеЗатраты.Материал,Регистр.НормативныеЗатраты.Материал;
		|ТипЗатрат 		= Регистр.НормативныеЗатраты.ТипЗатрат;
		|Норм			= Регистр.НормативныеЗатраты.Сумма;
		|Факт			= Регистр.ПроизводственныеЗатраты.Сумма;
		|Кво			= Регистр.ВыпускПродукции.Количество;";
		Если фКоличествоМатериалов = 1 Тогда
		    ТекстЗапроса = ТекстЗапроса + "
			|НормКоличество	= Регистр.НормативныеЗатраты.Количество;
			|ФактКоличество	= Регистр.ПроизводственныеЗатраты.Количество;
			|Функция НормКво	= Сумма(НормКоличество);
			|Функция ФактКво	= Расход(ФактКоличество);";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + "
		|Функция НормСумма	= Сумма(Норм);
		|Функция ФактСумма	= Расход(Факт);
		|Функция Выпуск		= Сумма(Кво);
		|";
	Иначе
		// без учета количества 
		ТекстЗапроса = ТекстЗапроса + "
		|Фирма 			= Регистр.ПроизводственныеЗатраты.Фирма,Регистр.НормативныеЗатраты.Фирма;
		|ВидДеятельности= Регистр.ПроизводственныеЗатраты.ВидДеятельности,Регистр.НормативныеЗатраты.ВидДеятельности;
		|Подразделение	= Регистр.ПроизводственныеЗатраты.Подразделение,Регистр.НормативныеЗатраты.Подразделение;
		|Продукция		= Регистр.ПроизводственныеЗатраты.Продукция,Регистр.НормативныеЗатраты.Продукция;
		|Заказ			= Регистр.ПроизводственныеЗатраты.Заказ,Регистр.НормативныеЗатраты.Заказ;
		|СтатьяКалькуляции = Регистр.ПроизводственныеЗатраты.ВидЗатрат.СтатьяКалькуляции,Регистр.НормативныеЗатраты.СтатьяКалькуляции;
		|Материал 		= Регистр.ПроизводственныеЗатраты.Материал,Регистр.НормативныеЗатраты.Материал;
		|ТипЗатрат 		= Регистр.НормативныеЗатраты.ТипЗатрат;
		|Норм			= Регистр.НормативныеЗатраты.Сумма;
		|Факт			= Регистр.ПроизводственныеЗатраты.Сумма;";
		Если фКоличествоМатериалов = 1 Тогда
		    ТекстЗапроса = ТекстЗапроса + "
			|НормКоличество	= Регистр.НормативныеЗатраты.Количество;
			|ФактКоличество	= Регистр.ПроизводственныеЗатраты.Количество;
			|Функция НормКво	= Сумма(НормКоличество);
			|Функция ФактКво	= Расход(ФактКоличество);";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + "
		|Функция НормСумма	= Сумма(Норм);
		|Функция ФактСумма	= Расход(Факт);
		|";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "Условие (ТипЗатрат = НормыНаВыпуск);
	|";
	
	Загол="";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Фирма",				ВыбФирма,				"ВыбФирма",				ТекстЗапроса,Загол,Язык);
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"ВидДеятельности",	ВыбВидДеятельности,		"ВыбВидДеятельности",	ТекстЗапроса,Загол,Язык);
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Подразделение",		ВыбПодразделение,		"ВыбПодразделение",		ТекстЗапроса,Загол,Язык);
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Продукция",			ВыбПродукция,			"ВыбПродукция",			ТекстЗапроса,Загол,Язык,"КатегорииТоваров");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Заказ",				ВыбЗаказ,				"ВыбЗаказ",				ТекстЗапроса,Загол,Язык);
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"СтатьяКалькуляции",	ВыбСтатьяКалькуляции,	"ВыбСтатьяКалькуляции",	ТекстЗапроса,Загол,Язык);
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ,"Материал",			ВыбМатериал,			"ВыбМатериал",			ТекстЗапроса,Загол,Язык,"КатегорииТоваров");
	Если глГруппыДоступаПродукции.РазмерСписка() > 0 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие (Продукция В глГруппыДоступаПродукции);";
	КонецЕсли;	
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Периодичность = ПериодСтрока();
	ТаблицаПериодов = СоздатьОбъект("ТаблицаЗначений");
	Если Периодичность<>0 Тогда
	    ТекстЗапросаПоПериодам = ТекстЗапроса+ "Группировка " + Периодичность + ";";
	КонецЕсли;

	ПечЗаголовокСтолбца = "";
	ТекстГруппировок = "";
	
	УстановитьГруппировкиЗапроса(ТекстГруппировок, ПечЗаголовокСтолбца);
	КоличествоГруппировок = СписокГруппировок.РазмерСписка();
	
	Если КоличествоГруппировок > 7 Тогда
		Предупреждение("Нельзя сделать больше 7 группировок!",60);
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ТекстГруппировок;
	Если Периодичность<>0 Тогда
 		ТекстЗапросаПоПериодам = ТекстЗапросаПоПериодам + ТекстГруппировок;		
		Если ЗапросСПериодами.Выполнить(ТекстЗапросаПоПериодам)=0 тогда
			Предупреждение("Запрос по периодам не выполнился!!!");
			Возврат;
		КонецЕсли;
 		глПолучитьТаблицуПериодов(ТаблицаПериодов,ЗапросСПериодами,Периодичность);
		ЗапросСПериодами.ВНачалоВыборки();
	КонецЕсли;
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;  
	
	глЧислоСтрок = 0;

	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Заголовок");
	Таб.ВывестиСекцию("Шапка|Наименование");
	
	// в  дальнейшем строки расшифровываем через отчет ФактическаяСебестоимостьПродукции
	Расшифровка.Установить("Объект", "ФактическаяСебестоимостьПродукции");
	Расшифровка.Установить("Обновить",0);
	Расшифровка.Установить("ТаблицаМФ", 0);
	Расшифровка.Установить("ТипСумм",3); // движения по расходу
	Расшифровка.Установить("фТипСуммНачОст",0);
	Расшифровка.Установить("фТипСуммПриход",0);
	Расшифровка.Установить("фТипСуммРасход",1);
	Расшифровка.Установить("фТипСуммКонОст",0);
	Расшифровка.Установить("Группировки",ГруппировкиПоВидамЗатрат);
	
	// выводим шапку для итогов по периодам
	ВывестиШапкуПериода(?(Язык="у", "Всього за період", "Всего за период"));
	Если Периодичность<>0 Тогда
		// выводим шапку для каждого периода
	    ТаблицаПериодов.ВыбратьСтроки();
		Пока ТаблицаПериодов.ПолучитьСтроку()=1 Цикл
			ВывестиШапкуПериода(ПериодСтр(ТаблицаПериодов.НачалоПериода,ТаблицаПериодов.КонецПериода));
		КонецЦикла;
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Шапка|Рамка");
	
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаСекции("Шапка")+Таб.ВысотаСекции("Кнопки")+Таб.ВысотаСекции("Заголовок"), Таб.ШиринаСекции("Наименование"), "НормаФактСебестоимостиПродукции", "НормаФактСебестоимостиПродукции");
	                         
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА          
	ВыпускПродукции = 0;
	ВыпускПродукцииЗаПериод = 0;
	ВывестиГруппировки();
	
	ПечатьСтроки(0,"Итого",?(Язык="у", "Всього:", "Всего:"));
	Если (Периодичность <> 0) Тогда
		// выведем теперь все периоды, с разворотом вправо
		ТаблицаПериодов.ВыбратьСтроки();
		Пока ТаблицаПериодов.ПолучитьСтроку() = 1 Цикл
			глПозиционироватьЗапросПоПериодам(Запрос,ЗапросСПериодами,СписокГруппировок,КоличествоГруппировок,0,ТаблицаПериодов.РеалНачалоПериода);
			ПечатьПериода(0,"Итого");
		КонецЦикла;
	КонецЕсли;

	Таб.ОбластьПечати(2);
	
	// Вывод заполненной формы
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);

	Таб.Показать("Норма/факт себестоимости продукции", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;  
	глУстПропись(Гривня,"Р");
КонецПроцедуры // Сформировать()


// ===============================
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// ===============================

// ===============================
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	Если ФлагВосстановленияНастройки = 0 Тогда     
		фБезГрупп 	   = 0;
	КонецЕсли;
	Дата1 = глВосстановитьЗначение(,"ОсновнаяДатаНачалаОтчета");
	Если ПустоеЗначение(Дата1) = 1 Тогда
		Дата1 = НачМесяца(Дата2);
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		Дата1 		= глРасшифровка.Получить("Дата1");
		Дата2		= глРасшифровка.Получить("Дата2");
		ВыбФирма	= глРасшифровка.Получить("ВыбФирма");
		
		ВыбВидДеятельности = глРасшифровка.Получить("ВыбВидДеятельности");
		ВыбПодразделение = глРасшифровка.Получить("ВыбПодразделение");
		ВыбПродукция = глРасшифровка.Получить("ВыбПродукция");
		ВыбЗаказ = глРасшифровка.Получить("ВыбЗаказ");
		ВыбСтатьяКалькуляции = глРасшифровка.Получить("ВыбСтатьяКалькуляции");
		ВыбМатериал = глРасшифровка.Получить("ВыбМатериал");
		
		фСебестоимостьЗаЕдиницу	= глРасшифровка.Получить("фСебестоимостьЗаЕдиницу");
		фКоличествоМатериалов	= глРасшифровка.Получить("фКоличествоМатериалов");
		фОтклоненияОтНормы      = глРасшифровка.Получить("фОтклоненияОтНормы");
		
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);
		
		фБезГрупп		= глРасшифровка.Получить("фБезГрупп");
		
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		СписокПериодов.ТекущаяСтрока(глРасшифровка.Получить("СписокПериодов"));
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;            
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

// ===============================
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ТекСтрокаВТаблицеМФ="";
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()   

// ===============================
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблицеМФ,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
// ===============================
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора


// ===============================
// ТЕЛО МОДУЛЯ
// ===============================

Дата2 = КонМесяца(ПолучитьДатуТА());

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

СписокПериодов.ДобавитьЗначение("Общий","Общие итоги");
СписокПериодов.ДобавитьЗначение("Месяц","Месяц");  
СписокПериодов.ДобавитьЗначение("Квартал","Квартал");
СписокПериодов.ДобавитьЗначение("Год","Год");

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

//                  			тип			вид				переменная  	название
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы",			"Фирма",			"По фирмам");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Подразделения",	"Подразделение",	"По подразделениям");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыДеятельности",	"ВидДеятельности","По видам деятельности");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Документ",	"Заказ",			"Заказ",			"По заказам");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ТМЦ",				"Продукция",		"По продукции");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",	"Продукция",		"По категориям продукции");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СтатьиКалькуляции","СтатьяКалькуляции","По статьям калькуляции");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ТМЦ",				"Материал",			"По материалам");
глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ВидыКатегорий",	"Материал",			"По категориям материалов");

ТекСтрокаВТаблицеМФ="";

Группировки.ДобавитьЗначение("Фирма", 			"Фирма");
Группировки.ДобавитьЗначение("Подразделение",	"Подразделение");
Группировки.ДобавитьЗначение("ВидДеятельности", "Вид деятельности");           
Группировки.ДобавитьЗначение("Заказ", 			"Заказ");
Группировки.ДобавитьЗначение("Продукция", 		"Продукция");
Группировки.ДобавитьЗначение("СтатьяКалькуляции","Статья калькуляции");
Группировки.ДобавитьЗначение("Материал",		"Материал");

Группировки.Пометка(5,1);
Группировки.Пометка(6,1);

ГруппировкиПоВидамЗатрат = СоздатьОбъект("СписокЗначений"); // для расшифровки отчета по движениям
ГруппировкиПоВидамЗатрат.ДобавитьЗначение("ВидЗатрат",		"Вид затрат");
ГруппировкиПоВидамЗатрат.Пометка(1,1);

// список украинских названий группировок для построения шапки отчета
УкрГруппировки = СоздатьОбъект("СписокЗначений");
УкрГруппировки.ДобавитьЗначение("Фирма", 			"Фірма");
УкрГруппировки.ДобавитьЗначение("Подразделение",	"Підрозділ");
УкрГруппировки.ДобавитьЗначение("ВидДеятельности", "Вид діяльності");           
УкрГруппировки.ДобавитьЗначение("Заказ", 			"Замовлення");
УкрГруппировки.ДобавитьЗначение("Продукция", 		"Продукція");
УкрГруппировки.ДобавитьЗначение("СтатьяКалькуляции","Стаття калькуляції");
УкрГруппировки.ДобавитьЗначение("Материал",			"Матеріал");