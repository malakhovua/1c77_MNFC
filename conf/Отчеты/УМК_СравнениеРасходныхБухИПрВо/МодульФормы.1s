Перем V7;

//======================================================================
Процедура ОбработатьДокумент(Док, ТЗРН, РеквКво, РеквСумма, Знак, ДокРН = "")
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл			
		Если (ДокРН <> "") Тогда
			Если Док.ДокПродажи <> ДокРН Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		ТЗРН.НоваяСтрока();		
		ТЗРН.ТМЦ = Док.ТМЦ;
		Если (ДокРН <> "") Тогда
			ТЗРН.РН = Док.ДокПродажи;
			ТЗРН.НомерДок = Док.ДокПродажи.НомерДок;
			ТЗРН.ДатаДок = Док.ДокПродажи.ДатаДок;			
		Иначе
			ТЗРН.РН = Док.ТекущийДокумент();
			ТЗРН.НомерДок = Док.НомерДок;
			ТЗРН.ДатаДок = Док.ДатаДок;
		КонецЕсли;
		
		Если Док.Вид() = "РасходнаяНакладная" Тогда
			ТЗРН.ВУП = ?(ПустоеЗначение(Док.ВидУпаковки) = 1, Перечисление.ВидыУпаковки.Нет, Док.ВидУпаковки.ТипУпаковки);
		Иначе
			ТЗРН.ВУП = ?(ПустоеЗначение(Док.ВУП) = 1, Перечисление.ВидыУпаковки.Нет, Док.ВУП.ТипУпаковки);
		КонецЕсли;

		ТЗРН.УстановитьЗначение(ТЗРН.НомерСтроки, РеквКво, Док.Кво * Знак);
		ТЗРН.УстановитьЗначение(ТЗРН.НомерСтроки, РеквСумма, Док.СуммаСНДС * Знак);
	КонецЦикла;
КонецПроцедуры

Процедура ВывестиТаблицу(ТЗРН)
	ТЗРН.НоваяКолонка("РазницаКво", "Число", 15, 3);
	ТЗРН.НоваяКолонка("РазницаСумма", "Число", 15, 2);
	
	Таб = СоздатьОбъект("Таблица");
	
	Таб.ВывестиСекцию("Шапка");	
	СтарНомерДок = "";
	
	ТЗРН.ВыбратьСтроки();
	Пока ТЗРН.ПолучитьСтроку() = 1 Цикл
		ТЗРН.РазницаКво = ?((ТЗРН.КвоПродано <> 0) ИЛИ (ТЗРН.КвоПроданоБ <> 0), ТЗРН.КвоПродано - ТЗРН.КвоПроданоБ, ТЗРН.КвоВозвращено - ТЗРН.КвоВозвращеноБ);
		ТЗРН.РазницаСумма = ?((ТЗРН.КвоПродано <> 0) ИЛИ (ТЗРН.КвоПроданоБ <> 0), ТЗРН.СуммаПродано - ТЗРН.СуммаПроданоБ, ТЗРН.СуммаВозвращено - ТЗРН.СуммаВозвращеноБ);
		Если (ТЗРН.РазницаКво = 0) И (ТЗРН.РазницаСумма = 0) И (фРНРасх = 1) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТЗРН.НомерДок <> СтарНомерДок Тогда
			СтарНомерДок = ТЗРН.НомерДок;
			Таб.ВывестиСекцию("Строка");
		КонецЕсли;
		
		Таб.ВывестиСекцию("ТМЦ");		
	КонецЦикла;
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);	
	Таб.Показать("Отчёт о расхождения в расходных накладных");
КонецПроцедуры // ВывестиТаблицу()

//*******************************************
Процедура Сформировать()
	Если ВыбКонтрагент.Выбран() = 0 Тогда
		Предупреждение("Необходимо выбрать контрагента");
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ВыбКонтрагент.ЕДРПОУ) = 1 Тогда
		Предупреждение("У контрагента должен быть указан код ЕГРПОУ");
		Возврат;
	КонецЕсли;
	
	Если (ПустоеЗначение(НачДата) = 1) ИЛИ (ПустоеЗначение(КонДата) = 1) Тогда
		Предупреждение("Нужно задать начальную и конечную даты");
		Возврат;
	КонецЕсли;
	Если V7 = 0 Тогда
		Предупреждение("Нужно инициализировать подключение к бух. базе");
		Возврат;
	КонецЕсли;
	
	ТЗРН = СоздатьОбъект("ТаблицаЗначений");
	ТЗРН.НоваяКолонка("РН", "Документ");
	ТЗРН.НоваяКолонка("НомерДок", "Строка");
	ТЗРН.НоваяКолонка("ДатаДок", "Дата");
	ТЗРН.НоваяКолонка("ТМЦ");
	ТЗРН.НоваяКолонка("ВУП", "Перечисление.ВидыУпаковки");
	ТЗРН.НоваяКолонка("КвоПродано", "Число", 15, 3);
	ТЗРН.НоваяКолонка("СуммаПродано", "Число", 15, 2);
	ТЗРН.НоваяКолонка("КвоВозвращено", "Число", 15, 3);
	ТЗРН.НоваяКолонка("СуммаВозвращено", "Число", 15, 2);
	ТЗРН.НоваяКолонка("КвоПроданоБ", "Число", 15, 3);
	ТЗРН.НоваяКолонка("СуммаПроданоБ", "Число", 15, 2);
	ТЗРН.НоваяКолонка("КвоВозвращеноБ", "Число", 15, 3);
	ТЗРН.НоваяКолонка("СуммаВозвращеноБ", "Число", 15, 2);
	Префикс = СокрЛП(Константа.БазФирма.Префикс);
	
	ДокРН = СоздатьОбъект("Документ");
	ДокВН = СоздатьОбъект("Документ");
	ДокРНБ = V7.CreateObject("Документ");
	СпрТМЦ = СоздатьОбъект("Справочник.ТМЦ");
	СпрКТут = СоздатьОбъект("Справочник.Контрагенты");
	СпрКТут.ВыбратьЭлементыПоРеквизиту("ЕДРПОУ", ВыбКонтрагент.ЕДРПОУ, 0, 0);
	Пока СпрКТут.ПолучитьЭлемент() = 1 Цикл
		ДокРН.УстановитьФильтр(1, 0);
		ДокРН.ВыбратьПоЗначению(НачДата, КонДата, "Контрагент", СпрКТут.ТекущийЭлемент());
		Пока ДокРН.ПолучитьДокумент() = 1 Цикл
			Если ДокРН.Вид() = "РасходнаяНакладная" Тогда
				ОбработатьДокумент(ДокРН.ТекущийДокумент(), ТЗРН, "КвоПродано", "СуммаПродано", 1);
				
				ДокВН.УстановитьФильтр(1, 0);
				ДокВН.ВыбратьПодчиненныеДокументы(,, ДокРН.ТекущийДокумент());
				Пока ДокВН.ПолучитьДокумент() = 1 Цикл
					Если (ДокВН.Вид() = "ВозвратнаяНакладная") Тогда
						Если (ДокВН.Ф1 = 0) Тогда
							ОбработатьДокумент(ДокВН.ТекущийДокумент(), ТЗРН, "КвоПродано", "СуммаПродано", -1, ДокРН.ТекущийДокумент());
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ДокРН.Вид() = "ВозвратнаяНакладная" Тогда 
				Если ДокРН.Ф1 = 1 Тогда
					ОбработатьДокумент(ДокРН.ТекущийДокумент(), ТЗРН, "КвоВозвращено", "СуммаВозвращено", 1);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;		
	КонецЦикла;
	
		
	СпрК = V7.CreateObject("Справочник.Контрагенты");
	Если СпрК.НайтиПоРеквизиту("ЕДРПОУ", ВыбКонтрагент.ЕДРПОУ, 1) = 1 Тогда
		ДокРНБ.УстановитьФильтр(1, 0);
		ДокРНБ.ВыбратьПоЗначению(НачДата, КонДата, "Контрагент", СпрК.ТекущийЭлемент());
			
		Пока ДокРНБ.ПолучитьДокумент() = 1 Цикл
			Если (ДокРНБ.Вид() = "РасходнаяНакладная") ИЛИ (ДокРНБ.Вид() = "ВозвратнаяНакладная") Тогда
				Если ДокРН.НайтиПоНомеру(Префикс + "-" + СокрЛП(ДокРНБ.НомерФ2), ДокРНБ.ДатаДок, ДокРНБ.Вид()) = 1 Тогда
					ТекДок = ДокРН.ТекущийДокумент();
					НомерДок = ТекДок.НомерДок;
					ДатаДок = ТекДок.ДатаДок;
				Иначе
					ТекДок = ПолучитьПустоеЗначение("Документ.РасходнаяНакладная");
					НомерДок = ДокРНБ.НомерДок;
					ДатаДок = ДокРНБ.ДатаДок;					
				КонецЕсли;
				
				ДокРНБ.ВыбратьСтроки();
				Пока ДокРНБ.ПолучитьСтроку() = 1 Цикл
					ТЗРН.НоваяСтрока();
					ТЗРН.РН = ТекДок;
					ТЗРН.НомерДок = НомерДок;					
					ТЗРН.ДатаДок = ДатаДок;
					
					Если (ПустоеЗначение(ДокРНБ.ТМЦ.КодС) = 0) И (ДокРНБ.ТМЦ.ЕстьКодС.Получить(ДокРНБ.ДатаДок) = 1) Тогда
						Если СпрТМЦ.НайтиПоКоду(ДокРНБ.ТМЦ.КодС) = 1 Тогда
							ТЗРН.ТМЦ = СпрТМЦ.ТекущийЭлемент();
						КонецЕсли;
					КонецЕсли;
						
					Если ПустоеЗначение(ТЗРН.ТМЦ) = 1 Тогда
						ТЗРН.ТМЦ = ДокРНБ.ТМЦ.Наименование;
					КонецЕсли;
					ТЗРН.ВУП = Перечисление.ВидыУпаковки.ЗначениеПоИдентификатору(ДокРНБ.ВУП.Идентификатор());
					Если ПустоеЗначение(ТЗРН.ВУП) = 1 Тогда
						ТЗРН.ВУП = Перечисление.ВидыУпаковки.Нет;
					КонецЕсли;
					ТЗРН.УстановитьЗначение(ТЗРН.НомерСтроки, ?(ДокРНБ.Вид() = "РасходнаяНакладная", "КвоПроданоБ", "КвоВозвращеноБ"), ДокРНБ.Кво);
					ТЗРН.УстановитьЗначение(ТЗРН.НомерСтроки, ?(ДокРНБ.Вид() = "РасходнаяНакладная", "СуммаПроданоБ", "СуммаВозвращеноБ"), ДокРНБ.СуммаСНДС);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ТЗРН.Свернуть("РН,ТМЦ, ВУП, НомерДок, ДатаДок", "КвоПродано, КвоВозвращено, СуммаПродано, СуммаВозвращено, КвоПроданоБ, СуммаПроданоБ, КвоВозвращеноБ, СуммаВозвращеноБ");
	ТЗРН.Сортировать("НомерДок,ДатаДок,РН-,ТМЦ");
	
	ВывестиТаблицу(ТЗРН);
КонецПроцедуры

//-----------------------------------------------------------------
Процедура ИнитOLE()
	V7=СоздатьОбъект("V77.Application");              
	й=V7.Initialize(V7.RMTrade,"/d "+СокрЛП(ПутьКБазе), "No_Splash_Show"); //+" /nДмитрий"
	Если й=0 Тогда
		Предупреждение("Ошибка инициализации !!!");
		V7=0;
	КонецЕсли;
КонецПроцедуры

//*******************************************
Процедура ПриЗакрытии()
	СохранитьЗначение("ПутьКЧБазе", ПутьКБазе);
	Если V7 <> 0 Тогда
		V7.ExecuteBatch("ЗавершитьРаботуСистемы()");
		V7=0;
	КонецЕсли;
КонецПроцедуры

Процедура ВыбратьКаталог()
	ФС.ВыбратьКаталог(ПутьКБазе, "Выберите путь к базе");
КонецПроцедуры

КонДата = НачМесяца(РабочаяДата())-1;
ДатаФОст = НачМесяца(КонДата);
ПутьКБазе = ВосстановитьЗначение("ПутьКЧБазе");
V7 = 0;
фТМЦ = 1;