Перем Разделитель;

Процедура Расшифровать(Номенклатура, СчетКор)
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Расшифровка");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ, Номенклатура);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения, МестоХранения);
	Ит.ИспользоватьРазделительУчета(Разделитель);
	Ит.ВыполнитьЗапрос(ВыбНачПериода, ВыбКонПериода, ВыбСчет, СчетКор,, 3, "Проводка", "СК");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0,Таб.ВысотаТаблицы(),0);
	Ит.ВыбратьПериоды();
	Пока Ит.ПолучитьПериод() = 1 Цикл
		Дт = Ит.ВыбранаПоДт();
		Кт = Ит.ВыбранаПоКт();
		Опер = Ит.Операция;
		Сд = Ит.СКД() - Ит.СКК();
		Таб.ВывестиСекцию("Строка");
		Если (Опер.Дебет.Счет.Валютный = 1) Или (Опер.Кредит.Счет.Валютный = 1) Тогда
			СдВал = Ит.СКД(2) - Ит.СКК(2);
			Таб.ВывестиСекцию("Валюта");
		КонецЕсли;
		Если (Опер.Дебет.Счет.Количественный = 1) Или (Опер.Кредит.Счет.Количественный = 1) Тогда
			СдКол = Ит.СКД(3) - Ит.СКК(3);
			Таб.ВывестиСекцию("Количество");
		КонецЕсли;
	КонецЦикла;
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Расшифровка","");
КонецПроцедуры

Процедура ОбработкаЯчейкиТаблицы(ТекЗначение,ТекФлагСтандОбраб,ТекТаблица,ТекАдрес)
	
	Если ТипЗначения(ТекЗначение) = 2 Тогда
      Номенклатура = СоздатьОбъект("Справочник.ТМЦ");
	  Если Номенклатура.НайтиПоКоду(Лев(ТекЗначение,5)) = 1 Тогда
	  	Расшифровать(Номенклатура.ТекущийЭлемент(), СчетПоКоду(Прав(ТекЗначение,3)));
	  КонецЕсли;
	 Иначе
	  	ТекФлагСтандОбраб = 1;
	КонецЕсли;

КонецПроцедуры

Функция ВыполнитьЗапрос(СчетБУ)
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(Разделитель);
	Ит.ИспользоватьСубконто(ВидыСубконто.ТМЦ,, 1);
	Ит.ИспользоватьСубконто(ВидыСубконто.МестаХранения, МестоХранения);
	Ит.ВыполнитьЗапрос(ВыбНачПериода, ВыбКонПериода, ВыбСчет, СчетБУ,, 2,, "СК");
	
	
	Возврат Ит;

КонецФункции

Процедура Сформировать()
	
	Результат = СоздатьОбъект("ТаблицаЗначений");
	Результат.НоваяКолонка("Номенклатура", "ТМЦ");
	//Суммы
	Результат.НоваяКолонка("ОборотДТ_947", "Число");
	Результат.НоваяКолонка("ОборотКТ_947", "Число");
	Результат.НоваяКолонка("ОборотДТ_719", "Число");
	Результат.НоваяКолонка("ОборотКТ_719", "Число");
	//количество
	Результат.НоваяКолонка("ОборотДТ_947_к", "Число");
	Результат.НоваяКолонка("ОборотКТ_947_к", "Число");
	Результат.НоваяКолонка("ОборотДТ_719_к", "Число");
	Результат.НоваяКолонка("ОборотКТ_719_к", "Число");
	
	//суммы
	ИтОборотДТ_947 = 0;
	ИтОборотКТ_947 = 0;
	ИтОборотДТ_719 = 0;
	ИтОборотКТ_719 = 0;
	ИтОборотРазницаКТ_ДТ = 0;
	ИтОборотРазницаДТ_КТ = 0;
	
	//количество
	ИтОборотДТ_947_к = 0;
	ИтОборотКТ_947_к = 0;
	ИтОборотДТ_719_к = 0;
	ИтОборотКТ_719_к = 0;
	ИтОборотРазницаКТ_ДТ_к = 0;
	ИтОборотРазницаДТ_КТ_к = 0;
	
	
	//Доходы
	Ит  = ВыполнитьЗапрос("719");
	Ит.ВыбратьСубконто(ВидыСубконто.МестаХранения);
	Пока Ит.ПолучитьСубконто(ВидыСубконто.МестаХранения) = 1 Цикл
		Ит.ВыбратьСубконто(ВидыСубконто.ТМЦ);
		Пока Ит.ПолучитьСубконто(ВидыСубконто.ТМЦ) = 1 Цикл
			
			Результат.НоваяСтрока();
			Результат.Номенклатура = Ит.Субконто(ВидыСубконто.ТМЦ);
			//сумма
			Результат.ОборотДТ_719 = Ит.ДО();
			Результат.ОборотКТ_719 = Ит.КО();
			
			ИтОборотДТ_719 = ИтОборотДТ_719 + Ит.ДО();
			ИтОборотКТ_719 = ИтОборотКТ_719 + Ит.КО();
			
			//количество
			Результат.ОборотДТ_719_к = Ит.ДО(3);
			Результат.ОборотКТ_719_к = Ит.КО(3);
			
			ИтОборотДТ_719_к = ИтОборотДТ_719_к + Ит.ДО(3);
			ИтОборотКТ_719_к = ИтОборотКТ_719_к + Ит.КО(3);
			
			
		КонецЦикла;// по ТМЦ
	КонецЦикла;// по складам
	

	//Затраты
	Ит  = ВыполнитьЗапрос("947");
	Ит.ВыбратьСубконто(ВидыСубконто.МестаХранения);
	Пока Ит.ПолучитьСубконто(ВидыСубконто.МестаХранения) = 1 Цикл
		Ит.ВыбратьСубконто(ВидыСубконто.ТМЦ);
		Пока Ит.ПолучитьСубконто(ВидыСубконто.ТМЦ) = 1 Цикл
			
			Результат.НоваяСтрока();
			Результат.Номенклатура = Ит.Субконто(ВидыСубконто.ТМЦ);
			//сумма
			Результат.ОборотДТ_947 = Ит.ДО();
			Результат.ОборотКТ_947 = Ит.КО();
			
			ИтОборотДТ_947 = ИтОборотДТ_947 + Ит.ДО();
			ИтОборотКТ_947 = ИтОборотКТ_947 + Ит.КО();
			
			//количество
			Результат.ОборотДТ_947_к = Ит.ДО(3);
			Результат.ОборотКТ_947_к = Ит.КО(3);
			
			ИтОборотДТ_947_к = ИтОборотДТ_947_к + Ит.ДО(3);
			ИтОборотКТ_947_к = ИтОборотКТ_947_к + Ит.КО(3);
			
		КонецЦикла;// по ТМЦ
	КонецЦикла;// по складам
	
	Результат.Свернуть("Номенклатура","ОборотДТ_947, ОборотКТ_947, ОборотДТ_719, ОборотКТ_719,   
	|ОборотДТ_947_к, ОборотКТ_947_к, ОборотДТ_719_к, ОборотКТ_719_к");
	
	// вывод результата
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Заголовок");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0,Таб.ВысотаТаблицы(),0);
	
	Результат.ВыбратьСтроки();
	
	Пока Результат.ПолучитьСтроку() = 1  Цикл
		
		// сумма
		ОборотРазницаДТ_КТ = Результат.ОборотДТ_719 - Результат.ОборотКТ_947;
		ОборотРазницаКТ_ДТ = Результат.ОборотКТ_719 - Результат.ОборотДТ_947;
		
		ИтОборотРазницаДТ_КТ = ИтОборотРазницаДТ_КТ + ОборотРазницаДТ_КТ;
		ИтОборотРазницаКТ_ДТ = ИтОборотРазницаКТ_ДТ + ОборотРазницаКТ_ДТ;
		
		// количество
		ОборотРазницаДТ_КТ_к = Результат.ОборотДТ_719_к - Результат.ОборотКТ_947_к;
		ОборотРазницаКТ_ДТ_к = Результат.ОборотКТ_719_к - Результат.ОборотДТ_947_к;
		
		ИтОборотРазницаДТ_КТ_к = ИтОборотРазницаДТ_КТ_к + ОборотРазницаДТ_КТ_к;
		ИтОборотРазницаКТ_ДТ_к = ИтОборотРазницаКТ_ДТ_к + ОборотРазницаКТ_ДТ_к;
		
		Таб.ВывестиСекцию("Строка");
		
	КонецЦикла;
	
	Таб.ВывестиСекцию("Итоги");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Таблица","");
	
КонецПроцедуры

Процедура ПриОткрытии()
	Разделитель = Константа.БазФирма.ТекущийЭлемент();
КонецПроцедуры


