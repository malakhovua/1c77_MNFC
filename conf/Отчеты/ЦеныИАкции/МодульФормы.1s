Перем СписЗначений, СписСтолбцов;
Перем ИмяСписка;
Перем ВыбТЦ;
Перем фСтолбец1;
Перем СтрокаДата;
Перем Таб;
Перем Обновить;
Перем Расшифровка;
Перем ТекстКонтрагент;
Перем МаксСтолбцов;
Перем ТекСхема;
Перем МинПериодСкидки, МаксПериодСкидки;
Перем ЕстьПроблемы;

//======================================================================
Процедура ПрисоединитьБлоки(ТЗТМЦ, Секция)
	Таб.ВывестиСекцию(Секция + "|Основная");
	НДата = НачДата;
	Индекс = 0;
	НШрина = 4;	
	
	Пока НДата <= КонДата Цикл		
		Индекс = Индекс + 1;
		Умножитель = 0;
		
		Для Инд = 1 По МаксСтолбцов Цикл
			Если Шаблон("[фСтолбец" + Инд + "]") = "1" Тогда
				Если Секция = "Шапка" Тогда
					Умножитель = Умножитель + 1;
					ИмяСтолбца = "";
					СписЗначений.ПолучитьЗначение(Инд, ИмяСтолбца);
				Иначе
					Расш = "";
					Зн = ТЗТМЦ.ПолучитьЗначение(ТЗТМЦ.НомерСтроки, "З"+Инд).ПолучитьЗначение(Индекс, Расш);
				КонецЕсли;
				
				Обл = Таб.ПолучитьСекцию(Секция + "|Столбец");				

				Если (Секция <> "Шапка") И (Инд = 3) Тогда					
					Если (ПустоеЗначение(Зн) = 0) И (Зн <> ТекСхема) Тогда
						Обл.Область(1,1,1,1).ЦветФона(255, 147, 147);
					КонецЕсли;
				ИначеЕсли (Секция = "Строка") И (Инд = 6) Тогда
					Если Расш = "Дубль" Тогда
						Обл.Область(1,1,1,1).ЦветФона(255, 242, 0);
					КонецЕсли;
				КонецЕсли;				
				
				Таб.ПрисоединитьСекцию(Обл);				
			КонецЕсли;
		КонецЦикла;
		
		
		Если Секция = "Шапка" Тогда
			Таб.Область(СтрокаДата, НШрина, СтрокаДата, НШрина + Умножитель - 1).Объединить();
		КонецЕсли;
		НШрина = НШрина + Умножитель;
		
		НДата = НДата + 1;		
	КонецЦикла;	
КонецПроцедуры // гл

//======================================================================
Процедура ПолучитьИсториюЗначенийБазовойЦены(ТЗТМЦ, Объект)
	СтарКЦ = ТЗТМЦ.З1.ПолучитьЗначение(1);
	
	НДата = НачДата;
	Инд = 1;
	Пока НДата <= КонДата Цикл
		КЦ = ТЗТМЦ.З1.ПолучитьЗначение(Инд);
		Если СтарКЦ <>  КЦ Тогда
			Объект = глВернутьЦену(ТЗТМЦ.ТМЦ, КЦ);
			СтарКЦ = КЦ;
		КонецЕсли;
		Если ПустоеЗначение(Объект) = 0 Тогда
			ЗначениеДобавления = Объект.Цена.Получить(НДата);
		Иначе
			ЗначениеДобавления = "";
		КонецЕсли;
		ТЗТМЦ.З2.ДобавитьЗначение(ЗначениеДобавления);
		
		НДата = НДата + 1;
		Инд = Инд + 1;
	КонецЦикла;
КонецПроцедуры // гл

//======================================================================
Процедура ПолучитьИсториюРеквизитов(ТЗТМЦ, Инд)
	Пер = СоздатьОбъект("Периодический");
	
	ИмяРеквизита = СписЗначений.ПолучитьЗначение(Инд);
	
	Если Инд = 1 Тогда
		Объект = ТЗТМЦ.ТМЦ;
	ИначеЕсли Инд = 2 Тогда
		Объект = глВернутьЦену(ТЗТМЦ.ТМЦ, ТЗТМЦ.З1.ПолучитьЗначение(1));
	Иначе
		Объект = глВернутьЦену(ТЗТМЦ.ТМЦ, ВыбТЦ);
	КонецЕсли;

	Если (Инд = 2) И (ТЗТМЦ.БылоИзменениеБазовойКЦ = 1) Тогда
		// в целях упрощения алгоритма, проверим было ли изменение базовой категории цены для ТМЦ в выбранном периоде. Если было, 
		//просто пробежимся по датам, если не было, то получаем в этой же процедуре
		ПолучитьИсториюЗначенийБазовойЦены(ТЗТМЦ, Объект);
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(Объект) = 0 Тогда
		ПредЗначение = Объект.ПолучитьАтрибут(ИмяРеквизита).Получить(НачДата);
	Иначе
		ПредЗначение = "";
	КонецЕсли;
	
	ТЗТМЦ.ПолучитьЗначение(ТЗТМЦ.НомерСтроки, "З"+Инд).ДобавитьЗначение(ПредЗначение);
	ПоследняяДата = НачДата + 1;
	
	Если ПустоеЗначение(Объект) = 0 Тогда
		Пер.ИспользоватьОбъект(ИмяРеквизита, Объект);	
		Пер.ВыбратьЗначения(ПоследняяДата, КонДата);
		Пока Пер.ПолучитьЗначение() = 1 Цикл
			ТекДата = ПоследняяДата;
			Пока ТекДата < Пер.ДатаЗнач Цикл
				ТЗТМЦ.ПолучитьЗначение(ТЗТМЦ.НомерСтроки, "З"+Инд).ДобавитьЗначение(ПредЗначение);
				ТекДата = ТекДата + 1;
			КонецЦикла;
			ПредЗначение = Пер.Значение;
			ПоследняяДата = Пер.ДатаЗнач;
			Если Инд = 1 Тогда
				ТЗТМЦ.БылоИзменениеБазовойКЦ = 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Пока ПоследняяДата <= КонДата Цикл
		ТЗТМЦ.ПолучитьЗначение(ТЗТМЦ.НомерСтроки, "З"+Инд).ДобавитьЗначение(ПредЗначение);
		ПоследняяДата = ПоследняяДата + 1;
	КонецЦикла;	
КонецПроцедуры // ПолучитьИсториюРеквизитов(ТЗТМЦ, Инд)

Функция ПолучитьТаблицуАкций(списКонтрагентов)
	ТЗА = СоздатьОбъект("ТаблицаЗначений");
	ТЗА.НоваяКолонка("ТМЦ");
	ТЗА.НоваяКолонка("ДатаНачала", "Дата");
	ТЗА.НоваяКолонка("ДатаОкончания", "Дата");
	ТЗА.НоваяКолонка("Скидка", "Число", 15, 2);
	ТЗА.НоваяКолонка("СпособПредоставления", "Перечисление.СпособыПредоставленияСкидки");
	ТЗА.НоваяКолонка("ОкруглятьДо", "Число", 10);
	ТЗА.НоваяКолонка("КатегорияЦен", "Справочник.КатегорииЦен");
	ТЗА.НоваяКолонка("УСН", "Документ.УстановкаСкидокТМЦ");
	
	ТЗКА = СоздатьОбъект("ТаблицаЗначений");
	ТЗКА.НоваяКолонка("Контрагент", "Справочник.Контрагенты");
	ТЗКА.НоваяКолонка("ТЗА", "ТаблицаЗначений");
	Для ИндК = 1 По списКонтрагентов.РазмерСписка() Цикл
		ТекК = списКонтрагентов.ПолучитьЗначение(ИндК);
		ТЗКА.НоваяСтрока();
		ТЗКА.Контрагент = ТекК;
		ТЗКА.ТЗА = СоздатьОбъект("ТаблицаЗначений");
		ТЗКА.ТЗА.Загрузить(ТЗА);
	КонецЦикла;	
	
	ДокПС = СоздатьОбъект("Документ.ПолучателиСкидки");
	ДокПС.УстановитьФильтр(1, 0);
	ДокПС.ВыбратьДокументы();
	Пока ДокПС.ПолучитьДокумент() = 1 Цикл
		ТЗА.УдалитьСтроки();
		ТекСписКонтрагентов = СоздатьОбъект("СписокЗначений");
		
		УС = ДокПС.УстановкаСкидокТМЦ;
		НужноДобавить = 0;
		Если ПустоеЗначение(УС) = 0 Тогда
			Если (УС.ДатаДок > КонДата) ИЛИ ((ПустоеЗначение(УС.ДатаПо) = 0) И (УС.ДатаПо < НачДата)) Тогда
				Продолжить;
			КонецЕсли;
		
			
			ДокПС.ВыбратьСтроки();
			Пока ДокПС.ПолучитьСтроку() = 1 Цикл
				Если (ДокПС.Контрагент.ЭтоГруппа() = 0) И (списКонтрагентов.НайтиЗначение(ДокПС.Контрагент) <> 0) Тогда
					Если ТекСписКонтрагентов.НайтиЗначение(ДокПС.Контрагент) = 0 Тогда
						ТекСписКонтрагентов.ДобавитьЗначение(ДокПС.Контрагент);
					КонецЕсли;
				ИначеЕсли (ДокПС.Контрагент.ЭтоГруппа() = 1) Тогда
					Для ИндК = 1 По списКонтрагентов.РазмерСписка() Цикл
						Если списКонтрагентов.ПолучитьЗначение(ИндК).ПринадлежитГруппе(ДокПС.Контрагент) = 1 Тогда
							Если ТекСписКонтрагентов.НайтиЗначение(списКонтрагентов.ПолучитьЗначение(ИндК)) = 0 Тогда
								ТекСписКонтрагентов.ДобавитьЗначение(списКонтрагентов.ПолучитьЗначение(ИндК));
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			Если ТекСписКонтрагентов.РазмерСписка() > 0 Тогда
				УС.ВыбратьСтроки();
				Пока УС.ПолучитьСтроку() = 1 Цикл
					Если списТовар.РазмерСписка() > 0 Тогда
						Если списТовар.Принадлежит(УС.ТМЦ) = 0 Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					ТЗА.НоваяСтрока();
					ТЗА.ТМЦ = УС.ТМЦ;
					ТЗА.ДатаНачала = УС.ДатаДок;
					ТЗА.ДатаОкончания = ?(ПустоеЗначение(УС.ДатаПо) = 1, КонДата, УС.ДатаПо);
					ТЗА.Скидка = УС.Скидка;
					ТЗА.СпособПредоставления = УС.СпособПредоставления;
					ТЗА.ОкруглятьДо = УС.ОкруглятьДо;
					ТЗА.КатегорияЦен = УС.КатегорияЦен;
					ТЗА.УСН = УС;
				КонецЦикла;
				
				Если ТЗА.КоличествоСтрок() > 0 Тогда
					Для ИндК = 1 По ТекСписКонтрагентов.РазмерСписка() Цикл
						СтрКА = 0;					
						ТекК = ТекСписКонтрагентов.ПолучитьЗначение(ИндК);
						ТЗКА.НайтиЗначение(ТекК, СтрКА, "Контрагент");
						ТЗКА.ПолучитьСтрокуПоНомеру(СтрКА);
						ТЗА.ВыбратьСтроки();
						Пока ТЗА.ПолучитьСтроку() = 1 Цикл
							ТЗКА.ТЗА.НоваяСтрока();
							Для ИндТЗА = 1 По ТЗА.КоличествоКолонок() Цикл								
								ТЗКА.ТЗА.УстановитьЗначение(ТЗКА.ТЗА.НомерСтроки, ИндТЗА, ТЗА.ПолучитьЗначение(ТЗА.НомерСтроки, ИндТЗА));
							КонецЦикла;						
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;			
			КонецЕсли;			
		КонецЕсли;		
	КонецЦикла;
	
	ТЗКА.ВыбратьСтроки();
	Пока ТЗКА.ПолучитьСтроку() = 1 Цикл
		ТЗКА.ТЗА.Сортировать("ТМЦ*, ДатаНачала");
	КонецЦикла;
	
	Возврат ТЗКА;
КонецФункции

//======================================================================
Процедура ЗаполнитьПустыеЗначенияВСписок(Спис)
	НДата = НачДата;
	Пока НДата <= КонДата Цикл
		Спис.ДобавитьЗначение("", "");
		
		НДата = НДата + 1;
	КонецЦикла;		
КонецПроцедуры // 

//======================================================================
Функция ПолучитьСпособПредоставленияСкидкиСтрокой(СпособПредоставления, ЗначениеСкидки, КатегорияЦен)
	Если СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.Процент Тогда
		Возврат Строка(ЗначениеСкидки) +" %";
	ИначеЕсли СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.Сумма Тогда
		Возврат Строка(ЗначениеСкидки) +" грн.";
	ИначеЕсли СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.ФиксЦена Тогда
		Возврат "Ф. " + Строка(ЗначениеСкидки) +" грн.";
	ИначеЕсли СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.ЦенаПоКатегории Тогда
		Возврат "К. " + Строка(КатегорияЦен);
	КонецЕсли;
	
	Возврат "";
КонецФункции // гл

Процедура УстановитьЗначениеВЯчейкуТЗ(Спис, Позиция, Значение, ТМЦ, ДатаЗн, Устанавливать)
	Уже = "";
	Спис.ПолучитьЗначение(Позиция, Уже);
	УстЗначение = ?(Уже = "Есть", "Дубль", "Есть");
	Спис.УстановитьЗначение(Позиция, Значение, УстЗначение);
	Если (УстЗначение = "Дубль") И (Устанавливать = 1) Тогда
		ЕстьПроблемы.ДобавитьЗначение(Строка(ТМЦ) + " - " + Строка(ДатаЗн));
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ПолучитьАкции(ТЗТМЦ, ТЗА, СписЦен)
	Стр = 0;
	ЗаполнитьПустыеЗначенияВСписок(ТЗТМЦ.З5);
	ЗаполнитьПустыеЗначенияВСписок(ТЗТМЦ.З6);
	ЗаполнитьПустыеЗначенияВСписок(ТЗТМЦ.З7);
	
	Если ТЗА.НайтиЗначение(ТЗТМЦ.ТМЦ, Стр, "ТМЦ") = 1 Тогда
		ТЗА.ПолучитьСтрокуПоНомеру(Стр);
		Пока ТЗА.ТМЦ = ТЗТМЦ.ТМЦ Цикл
			НДата = Макс(НачДата, ТЗА.ДатаНачала);
			Пока НДата <= Мин(ТЗА.ДатаОкончания, КонДата) Цикл
				Если фСтолбец5 = 1 Тогда
					УстановитьЗначениеВЯчейкуТЗ(ТЗТМЦ.З5, НДата - НачДата + 1, ПолучитьСпособПредоставленияСкидкиСтрокой(ТЗА.СпособПредоставления, ТЗА.Скидка, ТЗА.КатегорияЦен), ТЗТМЦ.ТМЦ, НДата, 0);
				КонецЕсли;				
				
				Если фСтолбец6 = 1 Тогда
					ЦенаСоСкидкой = 0;
					ЦенаИсх = Число(СписЦен.ПолучитьЗначение(НДата - НачДата + 1));
					Если (ЦенаИсх <> 0) ИЛИ ((ПустоеЗначение(ТЗА.КатегорияЦен) = 0) И (ТЗА.СпособПредоставления = Перечисление.СпособыПредоставленияСкидки.ЦенаПоКатегории)) Тогда
						ЦенаСоСкидкой = Окр(глРассчитатьЦенуСоСкидкой(НДата, ЦенаИсх, ТЗА.ТМЦ, ТЗА.КатегорияЦен, ТЗА.СпособПредоставления, ТЗА.Скидка, ТЗА.ОкруглятьДо, УстановкаСкидокТМЦ), 2); 
						//Окр(ЦенаИсх - ?(ТЗА.СкидкаСуммой = Да, ТЗА.Скидка, ЦенаИсх * ТЗА.Скидка / 100), 2);
					КонецЕсли;
					
					УстановитьЗначениеВЯчейкуТЗ(ТЗТМЦ.З6, НДата - НачДата + 1, ЦенаСоСкидкой, ТЗТМЦ.ТМЦ, НДата, 1);
				КонецЕсли;
				УстановитьЗначениеВЯчейкуТЗ(ТЗТМЦ.З7, НДата - НачДата + 1, ТЗА.УСН, ТЗТМЦ.ТМЦ, НДата, 0);
				
				НДата = НДата + 1;
			КонецЦикла;
			
			Стр = Стр + 1;
			Если Стр > ТЗА.КоличествоСтрок() Тогда
				Прервать;
			Иначе
				ТЗА.ПолучитьСтрокуПоНомеру(Стр);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ПолучитьАкции(ТЗТМЦ, Инд)

//======================================================================
Процедура ДобавитьКонтрагентавТК(ТК, ВыбТЦ)
	ТК.НоваяСтрока();
	ТК.СписКонтрагентов = СоздатьОбъект("СписокЗначений");
	ТК.ТипЦен = ВыбТЦ;
	ТК.ТЗА = СоздатьОбъект("ТаблицаЗначений");
КонецПроцедуры //

//======================================================================
Функция СравнитьТаблицы(Т1, Т2)
	Если Т1.КоличествоСтрок() <> Т2.КоличествоСтрок() Тогда
		Возврат 0;
	КонецЕсли;
	
	Т1.ВыбратьСтроки();
	Пока Т1.ПолучитьСтроку() = 1 Цикл
		Для Инд = 1 По Т1.КоличествоКолонок() Цикл
			Если Т1.ПолучитьЗначение(Т1.НомерСтроки, Инд) <> Т2.ПолучитьЗначение(Т1.НомерСтроки, Инд) Тогда
				Возврат 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат 1;
КонецФункции // СравнитьТаблицы

//======================================================================
Процедура ДобавитьСтрокуТЗТМЦ(ТЗТМЦ, СпрТМЦ)
	ТЗТМЦ.НоваяСтрока();
	ТЗТМЦ.ТМЦ = СпрТМЦ.ТекущийЭлемент();
	Если СпрТМЦ.ЭтоГруппа() = 0 Тогда
		Если (фСтолбец1 = 1) ИЛИ (фСтолбец2 = 1) Тогда
			// у ТМЦ можем меняться базовая категория цен, поэтому придётся всегда получать историю базовой категории
			ТЗТМЦ.З1 = СоздатьОбъект("СписокЗначений");
			// получим значения за период
			ПолучитьИсториюРеквизитов(ТЗТМЦ, 1);
			Если фСтолбец2 = 1 Тогда
				ТЗТМЦ.З2 = СоздатьОбъект("СписокЗначений");
				ПолучитьИсториюРеквизитов(ТЗТМЦ, 2);
			КонецЕсли;				
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры // 

// используется сложный алогритм выведения блоков
// если выбранный контрагент - это группа, то строится таблица акций для каждого контрагента, также определяется тип цен для каждого контрагента
// для каждого сочетания Тип цен, отличающаяся таблица акций будет выведен отдельный блок

//*******************************************
Процедура Сформировать()
	ЕстьПроблемы.УдалитьВсе();
	Если списКонтрагенты.РазмерСписка() = 0 Тогда
		Предупреждение("Выберите контрагентов");
		Возврат;
	КонецЕсли;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ЦеныИАкции");
	Расшифровка.Установить("НачДата", НачДата);	
	Расшифровка.Установить("КонДата", КонДата);	
	Расшифровка.Установить("фТемпСтолбец1", фТемпСтолбец1);	
	Для Инд = 2 По МаксСтолбцов Цикл
		Расшифровка.Установить("фСтолбец" + Инд, Число(Шаблон("[фСтолбец" + Инд + "]")));	
	КонецЦикла;
	Расшифровка.Установить("Обновить", 2);
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;	
	Расшифровка.Установить("списКонтрагенты", списКонтрагенты);	
	Расшифровка.Установить("СписТовар", СписТовар);
	Расшифровка.Установить("УстановкаСкидокТМЦ", УстановкаСкидокТМЦ);
	Таб.ИсходнаяТаблица("Таблица");	

	// заполним значение столбца 1, потому что после формирования отчёта пользователь может решить его не выводить
	фСтолбец1 = фТемпСтолбец1;	

	// нужно попстроить таблицу и список ТМЦ. Это мы сделаем один раз
	ТЗТМЦ = СоздатьОбъект("ТаблицаЗначений");
	ТЗТМЦ.НоваяКолонка("ТМЦ");
	ТЗТМЦ.НоваяКолонка("БылоИзменениеБазовойКЦ", "Число", 1);
	Для Инд = 1 По 7 Цикл
		ТЗТМЦ.НоваяКолонка("З" + Инд, "СписокЗначений");
	КонецЦикла;

	// список контрагентов для контрагента и группы заполняетс по разному
	списКонтрагентов = СоздатьОбъект("СписокЗначений");
	Для Инд = 1 По списКонтрагенты.РазмерСписка() Цикл
		Контр = списКонтрагенты.ПолучитьЗначение(Инд);
		Если Контр.ЭтоГруппа() = 1 Тогда
			СпрК = СоздатьОбъект("Справочник.Контрагенты");
			СпрК.ИспользоватьРодителя(Контр);
			СпрК.ВыбратьЭлементы();
			Пока СпрК.ПолучитьЭлемент() = 1 Цикл
				Если (СпрК.ПометкаУдаления() = 0) И (СпрК.ЭтоГруппа() = 0) Тогда
					списКонтрагентов.ДобавитьЗначение(СпрК.ТекущийЭлемент());
				КонецЕсли;
			КонецЦикла;
		Иначе
			списКонтрагентов.ДобавитьЗначение(Контр);
		КонецЕсли;
	КонецЦикла;
	
	// получим таблицу сочетаний акций и контрагентов
	Если (фСтолбец5 = 1) ИЛИ (фСтолбец6 = 1) Тогда
		ТЗКА = ПолучитьТаблицуАкций(списКонтрагентов);
	КонецЕсли;
	
	// 	теперь создаздим таблицу, где каждой строчке будут соответствовать контрагенты, у которых тип цен и таблица акций - одинаковые
	ТК = СоздатьОбъект("ТаблицаЗначений");
	ТК.НоваяКолонка("СписКонтрагентов", "СписокЗначений");
	ТК.НоваяКолонка("ТипЦен", "Справочник.КатегорииЦен");
	ТК.НоваяКолонка("ТЗА", "ТаблицаЗначений");
	Для Инд = 1 По списКонтрагентов.РазмерСписка() Цикл
		Контрагент = списКонтрагентов.ПолучитьЗначение(Инд);
		ВыбТЦ = ?(ПустоеЗначение(ВидЦенКонтрагента)=1,Контрагент.КатегорияЦен.Получить(НачДата),ВидЦенКонтрагента);
		Стр = 0;
		Если ТК.НайтиЗначение(ВыбТЦ, Стр, "ТипЦен") = 0 Тогда
			ДобавитьКонтрагентавТК(ТК, ВыбТЦ);
		Иначе
			ТК.ПолучитьСтрокуПоНомеру(Стр);
		КонецЕсли;
		
		Если фСтолбец5 + фСтолбец6 > 0 Тогда
			ТЗКА.ПолучитьСтрокуПоНомеру(Инд);
			Если Стр = 0 Тогда
				ТК.ТЗА.Загрузить(ТЗКА.ТЗА);
			Иначе
				НужноДобавитьНовую = 1;
				Пока ТК.ТипЦен = ВыбТЦ Цикл
					Если СравнитьТаблицы(ТК.ТЗА, ТЗКА.ТЗА) = 1 Тогда
						НужноДобавитьНовую = 0;
						Прервать;
					КонецЕсли;
					
					Стр = Стр + 1;
					Если Стр > ТК.КоличествоСтрок() Тогда
						Прервать;
					Иначе
						ТК.ПолучитьСтрокуПоНомеру(Стр);
					КонецЕсли;
				КонецЦикла;
				
				Если НужноДобавитьНовую = 1 Тогда
					ДобавитьКонтрагентавТК(ТК, ВыбТЦ);
					ТК.ТЗА.Загрузить(ТЗКА.ТЗА);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ТК.СписКонтрагентов.ДобавитьЗначение(Контрагент);

		ТК.Сортировать("ТипЦен");		
	КонецЦикла;
	//ТК.ВыбратьСтроку();
	//Возврат;
	
	// создадим ТЗТМЦ. Также сразу запишем историю изменения базовой категории и базовую цену, если это нужно.
	СпрТМЦ = СоздатьОбъект("Справочник.ТМЦ");
	Если списТовар.РазмерСписка() = 0 Тогда		
		СпрТМЦ.ВыбратьЭлементы(1);
		Пока СпрТМЦ.ПолучитьЭлемент() = 1 Цикл
			ДобавитьСтрокуТЗТМЦ(ТЗТМЦ, СпрТМЦ);	
		КонецЦикла;			
	Иначе
		Для Инд = 1 По списТовар.РазмерСписка() Цикл
			ТМЦ = списТовар.ПолучитьЗначение(Инд);			
			Если ТМЦ.ЭтоГруппа() = 1 Тогда
				СпрТМЦ.НайтиЭлемент(ТМЦ);
				ДобавитьСтрокуТЗТМЦ(ТЗТМЦ, СпрТМЦ);	
				
				СпрТМЦ.ИспользоватьРодителя(ТМЦ);
				СпрТМЦ.ВыбратьЭлементы(1);
				Пока СпрТМЦ.ПолучитьЭлемент() = 1 Цикл
					ДобавитьСтрокуТЗТМЦ(ТЗТМЦ, СпрТМЦ);	
				КонецЦикла;
			Иначе
				СпрТМЦ.НайтиЭлемент(ТМЦ);
				ДобавитьСтрокуТЗТМЦ(ТЗТМЦ, СпрТМЦ);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Отв = "Нет";
	Если (фСтолбец1 = 0) И (ТЗТМЦ.Итог("БылоИзменениеБазовойКЦ") <> 0) Тогда
		Отв = Вопрос("В периоде было изменение базовой категории цены для ТМЦ. Хотите добавить этот столбец в таблицу?", "Да+Нет");
		Если Отв = "Да" Тогда
			фСтолбец1 = 1;
		КонецЕсли;
	ИначеЕсли (фСтолбец1 = 1) И (ТЗТМЦ.Итог("БылоИзменениеБазовойКЦ") = 0) Тогда 
		Отв = Вопрос("В периоде не было изменения базовой категории цены для ТМЦ. Хотите добавить убрать этот столбец из таблицы?", "Да+Нет");
		Если Отв = "Да" Тогда
			фСтолбец1 = 0;
		КонецЕсли;		
	КонецЕсли;
	
	ЕстьПроблемыТекст = ?(ЕстьПроблемы.РазмерСписка() = 0, "", "Есть проблемы");
	Таб.ВывестиСекцию("Заголовок|Основная");	
	
	ПредТЦ = "";
	ТК.ВыбратьСтроки();
	Пока ТК.ПолучитьСтроку() = 1 Цикл
		СтрокаДата = Таб.ВысотаТаблицы() + 3;
		ВыбТЦ = ТК.ТипЦен;
		ТекСхема = ТК.ТипЦен.БазоваяСхемаЦенообразования;
		ТЗТМЦ.ВыбратьСтроки();
		Пока ТЗТМЦ.ПолучитьСтроку() = 1 Цикл
			Если СпрТМЦ.ЭтоГруппа() = 0 Тогда
				Если ПредТЦ <> ТК.ТипЦен Тогда
					Если (фСтолбец3 = 1) Тогда
						ТЗТМЦ.З3 = СоздатьОбъект("СписокЗначений");
						ПолучитьИсториюРеквизитов(ТЗТМЦ, 3);
					КонецЕсли;
					Если (фСтолбец4 = 1) Тогда
						ТЗТМЦ.З4 = СоздатьОбъект("СписокЗначений");
						ПолучитьИсториюРеквизитов(ТЗТМЦ, 4);
					КонецЕсли;					
					
					Если (фСтолбец6 <> 0) И (фСтолбец4 = 0) Тогда
						ТЗТМЦ.З4 = СоздатьОбъект("СписокЗначений");
						ПолучитьИсториюРеквизитов(ТЗТМЦ, 4);
					КонецЕсли;
				КонецЕсли;
				
				Если фСтолбец5 + фСтолбец6 <> 0 Тогда
					ТЗТМЦ.З5 = СоздатьОбъект("СписокЗначений");
					ТЗТМЦ.З6 = СоздатьОбъект("СписокЗначений");
					ТЗТМЦ.З7 = СоздатьОбъект("СписокЗначений");
					ПолучитьАкции(ТЗТМЦ, ТК.ТЗА, ТЗТМЦ.З4);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ПредТЦ = ТК.ТипЦен;
		
		ТекстКонтрагент = "";
		Для ИндК = 1 По ТК.СписКонтрагентов.РазмерСписка() Цикл
			ТекстКонтрагент = ТекстКонтрагент + ?(ТекстКонтрагент = "", "", ", ") + ТК.СписКонтрагентов.ПолучитьЗначение(ИндК);
		КонецЦикла;
		
		ПрисоединитьБлоки(ТЗТМЦ, "Шапка");
		Если ТК.НомерСтроки = 1 Тогда
			Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 3);
		КонецЕсли;
		Таб.Область(Таб.ВысотаТаблицы() - 3, 2, Таб.ВысотаТаблицы() - 3, Мин(13, Таб.ШиринаТаблицы())).ГоризонтальноеПоложение(1 + 4);
		Таб.Область(Таб.ВысотаТаблицы() - 3, 2, Таб.ВысотаТаблицы() - 3, Мин(13, Таб.ШиринаТаблицы())).Контроль(4);
		ТЗТМЦ.ВыбратьСтроки();
		Пока ТЗТМЦ.ПолучитьСтроку() = 1 Цикл
			Если ТЗТМЦ.ТМЦ.ЭтоГруппа() = 1 Тогда
				Таб.ВывестиСекцию("Группа|Основная");
			Иначе
				ПрисоединитьБлоки(ТЗТМЦ, "Строка");
			КонецЕсли;		
		КонецЦикла;		
	КонецЦикла;

	Если ЕстьПроблемы.РазмерСписка() > 0 Тогда
		Таб.Область(1, 3).Текст = "Есть проблемы";
		Таб.Область(1, 3).Расшифровка(ЕстьПроблемы, 0);
	Конецесли;
	
	Таб.ПараметрыСтраницы(2);
	Таб.ОбластьПечати(2, 1, Таб.ВысотаТаблицы(), Таб.ШиринаТаблицы());	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчет по ценам");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Элт.Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если (Элт.Вид() = "ТМЦ") Тогда
		ДобавитьвСписок(Элт, списТовар);
	ИначеЕсли (Элт.Вид() = "Контрагенты") Тогда
		ДобавитьвСписок(Элт, списКонтрагенты);		
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмяСписка = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	КФормы.ВыборГруппы(1);
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ПриОткрытии()
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		глФлагРасшифровки = 0;
		
		// восстанавливаем настройки из списка
		НачДата = глРасшифровка.Получить("НачДата");	
		КонДата = глРасшифровка.Получить("КонДата");	
		фТемпСтолбец1 = глРасшифровка.Получить("фТемпСтолбец1");	
		фСтолбец2 = глРасшифровка.Получить("фСтолбец2");	
		фСтолбец3 = глРасшифровка.Получить("фСтолбец3");	
		фСтолбец4 = глРасшифровка.Получить("фСтолбец4");	
		фСтолбец5 = глРасшифровка.Получить("фСтолбец5");
		фСтолбец6 = глРасшифровка.Получить("фСтолбец6");	
		УстановкаСкидокТМЦ = глРасшифровка.Получить("УстановкаСкидокТМЦ");
		глРасшифровка.Получить("списКонтрагенты").Выгрузить(списКонтрагенты);
		глРасшифровка.Получить("СписТовар").Выгрузить(списТовар);
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;		
	КонецЕсли;		
КонецПроцедуры // ПриОткрытии

//======================================================================
Процедура ИзСпецификации()
	ДокСпец = СоздатьОбъект("Документ.УМК_Спецификация");
	Если ДокСпец.Выбрать("Выберите спецификацию", "ФормаСписка") = 1 Тогда
		ДокСпец.ВыбратьСтроки();
		Пока ДокСпец.ПолучитьСтроку() = 1 Цикл
			ДобавитьВСписок(ДокСпец.ТМЦ, списТовар);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ИзСпецификации()

//======================================================================
Процедура ДобавитьУС()
	ДУС = СоздатьОбъект("Документ.УстановкаСкидокТМЦ");
	Если ДУС.Выбрать("Выберите установку скидок") = 1 Тогда
		ДУС.ВыбратьСтроки();
		Пока ДУС.ПолучитьСтроку() = 1 Цикл
			ДобавитьВСписок(ДУС.ТМЦ, списТовар);
		КонецЦикла;
		
		Док = СоздатьОбъект("Документ");
		Док.ВыбратьПодчиненныеДокументы(,,ДУС.ТекущийДокумент());
		Пока Док.ПолучитьДокумент() = 1 Цикл
			Если Док.Вид() = "ПолучателиСкидки" Тогда
				Док.ВыбратьСтроки();
				Пока Док.ПолучитьСтроку() = 1 Цикл
					ДобавитьВСписок(Док.Контрагент, списКонтрагенты);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		НачДата = ?(МинПериодСкидки = Дата(0), ДУС.ДатаДок, Мин(МинПериодСкидки, ДУС.ДатаДок));
		МинПериодСкидки = НачДата;
		
		КонДата = ?(МаксПериодСкидки = Дата(0), ДУС.ДатаПо, Макс(МаксПериодСкидки, ДУС.ДатаПо));
		МаксПериодСкидки = КонДата;
		
	КонецЕсли;
КонецПроцедуры // ДОбавитьУС

СписЗначений = СоздатьОбъект("СписокЗначений");
СписЗначений.ДобавитьЗначение("БазоваяКатегорияЦены", "Базовая КЦ");
СписЗначений.ДобавитьЗначение("Цена", "Цена базовая");
СписЗначений.ДобавитьЗначение("СхемаЦенообразования", "Схема");
СписЗначений.ДобавитьЗначение("Цена", "Цена");
СписЗначений.ДобавитьЗначение("Скидка", "Скидка");
СписЗначений.ДобавитьЗначение("ЦенаСоСкидкой", "Цена со ск.");
МаксСтолбцов = СписЗначений.РазмерСписка();
МинПериодСкидки = Дата(0);
МаксПериодСкидки = Дата(0);
ЕстьПроблемы = СоздатьОбъект("СписокЗначений");