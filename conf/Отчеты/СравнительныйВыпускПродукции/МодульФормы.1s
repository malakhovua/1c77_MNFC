//*******************************************
Процедура Сформировать()
	
	ТекстЗ = 
	"//{{ЗАПРОС(Сформировать)
	|Период с НачДата по КонДата;
	|Продукция = Регистр.ВыпускПродукции.Продукция;
	|Заказ = Регистр.ВыпускПродукции.Заказ;
	|Док = Регистр.ВыпускПродукции.ТекущийДокумент;
	|Количество = Регистр.ВыпускПродукции.Количество;
	|Сумма = Регистр.ВыпускПродукции.Сумма;
	|Функция Кво = Сумма(Количество);
	|Функция Себестоимость = Сумма(Сумма);
	|Группировка Продукция упорядочить по Продукция.Наименование" + ?(фГруппы = 1, "", " без групп") + ";
	|Группировка Заказ;
	|Группировка Док;
	|Условие(Продукция в списТовар);
	|Условие(ПустоеЗначение(Продукция) = 0);
	|Условие(ПустоеЗначение(Заказ) = 0);
	|"//}}ЗАПРОС
	;
	
	Запрос = СоздатьОбъект("Запрос");
	
	Если Запрос.Выполнить(ТекстЗ) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Документ");
	ТЗ.НоваяКолонка("Закладка", "Число", 15, 3);
	ТЗ.НоваяКолонка("НормаВыход", "Число", 15, 3);
	ТЗ.НоваяКолонка("ВыходПоФакту", "Число", 15, 3);
	ТЗ.НоваяКолонка("ВыходПоФактуСп", "Число", 15, 3);
	ТЗ.НоваяКолонка("ПлановаяСС", "Число", 15, 3);
	ТЗ.НоваяКолонка("ФактическаяСС", "Число", 15, 2);
	ТЗ.НоваяКолонка("ФактическаяЦена", "Число", 15, 2);
	ТЗ.НоваяКолонка("ЦенаП", "Число", 15, 2);
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0, 0, Таб.ВысотаТаблицы());
	ТЗИт = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.Выгрузить(ТЗИт);
	Расшифровка = СоздатьОбъект("СписокЗначений");
	КатЦенПрайс = Константа.ОсновнаяКатегорияЦен;
	
	Пока Запрос.Группировка(1) = 1 Цикл		
		Если Запрос.Продукция.ЭтоГруппа() = 1 Тогда
			Таб.ВывестиСекцию("Группа");
			Продолжить;
		КонецЕсли;
		ТЗ.УдалитьСтроки();
		ДокН = СоздатьОбъект("Документ");		
		ДокН.ОбратныйПорядок(1);
		ДокН.ВыбратьПоЗначению(, , ?(фОбычныеНормы = 1, "Продукция", "ПродукцияИ"), Запрос.Продукция);
		Пока ДокН.ПолучитьДокумент() = 1 Цикл
			Если (ДокН.Вид() = "НормыЗатрат") ИЛИ (ДокН.Вид() = "НормыЗатратИ") Тогда
				Если ДокН.фДляЗаказа = 1 Тогда
					СС = 0;
					ДокН.ВыбратьСтроки();
					Пока ДокН.ПолучитьСтроку() = 1 Цикл
						Если ДокН.Элемент.Вид() = "ТМЦ" Тогда
							НормаКво = Окр(ДокН.Кво, 4);
							СС = СС + ДокН.Цена * НормаКво;
						КонецЕсли;
					КонецЦикла;
					
					КвоПродукцииП = ДокН.КвоПродукции - ДокН.КвоПродукции * (ДокН.ПроцПотерь / 100);
					СС = СС / КвоПродукцииП;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;		
		
		Пока Запрос.Группировка(2) = 1 Цикл			
			ИтКвоКутеров = 0; ИтНормативныйВыход = 0;
			Зак = Запрос.Заказ;
			Зак.ВыбратьСтроки();
			ПроцПотерь = Запрос.Продукция.ПроцПотерь.Получить(Зак.ДатаДок) / 100;
			Пока Зак.ПолучитьСтроку() = 1 Цикл
				Если Зак.Продукция = Запрос.Продукция Тогда
					ИтКвоКутеров = ИтКвоКутеров + Зак.КвоКутеров * 100;
					Если ПустоеЗначение(Зак.НормыЗатрат) = 0 Тогда
						КвоПродНорма = Зак.НормыЗатрат.КвоПродукции; 
						ИтНормативныйВыход = ИтНормативныйВыход + (КвоПродНорма - КвоПродНорма * ПроцПотерь) * (Зак.КвоКутеров / 100);
					Иначе
						Сообщить("Не заданы нормы затрат в заказе: " + Строка(Зак) + " для товара: " + Строка(Запрос.Продукция));
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Пока Запрос.Группировка(3) = 1 Цикл
				//ОПЗ = Константа.ОПЗ.Получить(Запрос.Док.ДатаДок); // + umk уберем
				ТЗ.НоваяСтрока();
				ТЗ.Документ = Запрос.Док;				
				// тут нужно получить процент выпуска. Для этого придётся лезть в документ
				ИтПроцентВыпуска = 0;
				Выпущено = 0; ВыпущеноСп = 0;
				Док = Запрос.Док;
				Док.ВыбратьСтроки();
				Пока Док.ПолучитьСтроку() = 1 Цикл
					Если Запрос.Продукция = Док.Продукция Тогда
						ОПЗ = Док.Продукция.ОПР.Получить(Запрос.Док.ДатаДок); // + umk
						ИтПроцентВыпуска = ИтПроцентВыпуска + Док.ПроцентВыпуска;
						Выпущено = Выпущено + ?(Док.КвоГППриемки = 0, Док.Кво, Док.КвоГППриемки);
						ВыпущеноСп = ВыпущеноСп + Док.Кво;
					КонецЕсли;
				КонецЦикла;
				
				// + umk
				ЦенаУпаковки = -1;
				ТекРасшифровка = "";
				СтУпак = глПолучитьСтУпак(Запрос.Продукция, ТекРасшифровка, КонДата, ЦенаУпаковки);
				// - umk
				
				ТЗ.Закладка = ИтКвоКутеров * ИтПроцентВыпуска / 100;
				ТЗ.НормаВыход = ИтНормативныйВыход * ИтПроцентВыпуска;
				ТЗ.ВыходПоФакту = Выпущено;
				ТЗ.ВыходПоФактуСп = ВыпущеноСп;
				ТЗ.ПлановаяСС = СС + ОПЗ + СтУпак;
				ТЗ.ФактическаяСС = Запрос.Сумма;
				ТЗ.ФактическаяЦена = (Запрос.Сумма / ВыпущеноСп) + ОПЗ + СтУпак;
				ТЗ.ЦенаП = глВернутьЦену(Запрос.Продукция, КатЦенПрайс, Запрос.Док.ДатаДок, Гривня);
			КонецЦикла;			
		КонецЦикла;
		
		// + umk
		Продукция = Строка(Запрос.Продукция) + ?(Запрос.Продукция.НетБезУпаковки = 1," (" + Строка(Запрос.Продукция.ВидУпаковкиПоУмолчанию) + ")","");
		// - umk
	
		ОтношениеРазницы = (ТЗ.Итог("ПлановаяСС") / ТЗ.КоличествоСтрок() - ТЗ.Итог("ФактическаяЦена") / ТЗ.КоличествоСтрок()) / ?(ТЗ.Итог("ПлановаяСС") / ТЗ.КоличествоСтрок() = 0, 1, ТЗ.Итог("ПлановаяСС") / ТЗ.КоличествоСтрок()) * 100;
		Секц = Таб.ПолучитьСекцию("Строка");		
		Секц.Область().Полужирный(1-фБезДок);
		Таб.ВывестиСекцию(Секц);
		
		ТЗИт.НоваяСтрока();
		Для Инд = 1 По ТЗИт.КоличествоКолонок() Цикл
			ТЗИт.УстановитьЗначение(ТЗИт.НомерСтроки, Инд, ТЗ.Итог(Инд));
		КонецЦикла;
		Если фБезДок = 0 Тогда
			ТЗ.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСтроку() = 1 Цикл
				Расшифровка.Установить("Док", ТЗ.Документ);
				Расшифровка.Установить("Товар", Запрос.Продукция);
				Таб.ВывестиСекцию("Документ");
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ТЗИт.НоваяКолонка("Кол1");	
	ТЗИт.Свернуть("Кол1", "Закладка,НормаВыход,ВыходПоФакту,ВыходПоФактуСп,ПлановаяСС,ФактическаяСС,ФактическаяЦена");	
	Таб.ВывестиСекцию("Итого");
	
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчёт по выпуску");
КонецПроцедуры

Процедура ДобавитьВСписок(Элт, Наим, Спис)
	Если Спис.Принадлежит(Элт) = 0 Тогда
		Спис.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), списТовар);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, Один, ИмСп = "")
	ИмСпис = ИмСп;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, Один);
	Если Лев(НазваниеОбъекта, 1) = "С" Тогда
	    КФормы.ВыборГруппы(1);
	КонецЕсли;	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Спис)
	Если Спис.ТекущаяСтрока() <> 0 Тогда
		Спис.УдалитьЗначение(Спис.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

