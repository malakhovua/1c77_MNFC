Перем Расшифровка;
Перем Обновить;
Перем Таб;

Перем НомерСписка;
//Условие отборов
Перем Отбор_1;
Перем Отбор_2;

// Списки отборов
Перем СписокОтбор_1; 
Перем СписокОтбор_2;
Перем Заголовок_5;

Перем ЗаголовокГруппировок;

Процедура УстановитьЗаголовки() Далее
Процедура Сформировать() Далее

//***************Расшифровка************************************************

Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//***************Предопределенные процедуры*********************************

Процедура ПриОткрытии()
	
	Если ПоФорме = 0 Тогда
		ПоФорме = 1 
	КонецЕсли;
	Если ПустоеЗначение(ВыбДата) = 1 Тогда
		ВыбДата =ТекущаяДата();
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		глРасшифровка.Получить("Список_1").Выгрузить(Список_1);
		глРасшифровка.Получить("Список_2").Выгрузить(Список_2);
		ВыбДата = глРасшифровка.Получить("ВыбДата");
		ПоФорме = глРасшифровка.Получить("ПоФорме");
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
	
	Форма.КнопкаПоУмолчанию("Сформировать");
	
КонецПроцедуры

//***************Работа со списками отбора**********************************

Процедура ДобавитьВСписок(Элт, Наим, Список)
	Если Список.Принадлежит(Элт) = 0 Тогда
		Список.ДобавитьЗначение(Элт, Наим);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПодбора(Элт, конт)
	Если Элт.Вид() = "ТМЦ" Тогда
		ДобавитьвСписок(Элт, Строка(Элт), ?(НомерСписка=1,Список_1,Список_2));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЭлементВСписок(НазваниеОбъекта, МножВыбор, НомСпис)
	
	НомерСписка = НомСпис;
	КФормы = 0;
	ОткрытьПодбор(НазваниеОбъекта, "ДляВыбора", КФормы, МножВыбор);
	КФормы.ВыборГруппы(1);
	
КонецПроцедуры

Процедура УдалитьЗначениеСписка(Список)
	Если Список.ТекущаяСтрока() <> 0 Тогда
		Список.УдалитьЗначение(Список.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//**************Область компоновки отчета***********************************

Функция ТекстЗапроса1()
	Возврат 
	"SELECT 
	|     Подзапрос.значение_1 [значение_1 $Справочник.ТМЦ] 
	|   , Подзапрос.значение_2 [значение_2 $Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ]
	|	, Подзапрос.значение_3 [значение_3 $Справочник.ВидыУпаковки]
	|	, Подзапрос.значение_4 [значение_4 $Справочник.ФормыУпаковки]
	|	, Подзапрос.значение_5 [значение_5 $Справочник.ТМЦ]
	|	, Sum(Подзапрос.значение_6) значение_6
	|   , GROUPING(Подзапрос.значение_1) as Группа_1
	|   , GROUPING(Подзапрос.значение_2) as Группа_2
	|   , GROUPING(Подзапрос.значение_3) as Группа_3
	|   , GROUPING(Подзапрос.значение_4) as Группа_4 
	|   , GROUPING(Подзапрос.значение_5) as Группа_5
	|FROM (SELECT ТМЦ.ID значение_1
	|           ,УМК_РазрешенныеВидыУпаковкиТМЦ.ID значение_2
	|			, $УМК_РазрешенныеВидыУпаковкиТМЦ.ВидУпаковки значение_3
	|			, ФормыУпаковки.ID значение_4
	|			, $ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковокФорм.Материал(УМК_НормыСписанияМатериаловУпаковокФорм.ID, :ВыбДата) значение_5
	|			, $ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковокФорм.НормаСписания(УМК_НормыСписанияМатериаловУпаковокФорм.ID, :ВыбДата) значение_6
	|		FROM $Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ AS УМК_РазрешенныеВидыУпаковкиТМЦ
	|INNER JOIN $Справочник.ТМЦ AS ТМЦ ON ТМЦ.ID = УМК_РазрешенныеВидыУпаковкиТМЦ.PARENTEXT 
	|INNER JOIN $Справочник.ФормыУпаковки AS ФормыУпаковки ON $ПоследнееЗначение.УМК_РазрешенныеВидыУпаковкиТМЦ.ФормаУпаковки(УМК_РазрешенныеВидыУпаковкиТМЦ.ID, :ВыбДата) = ФормыУпаковки.ID
	|INNER JOIN $Справочник.УМК_НормыСписанияМатериаловУпаковокФорм AS УМК_НормыСписанияМатериаловУпаковокФорм ON ФормыУпаковки.ID = УМК_НормыСписанияМатериаловУпаковокФорм.PARENTEXT
	|		WHERE 1=1 " + 
	?(Отбор_2 = 0,"","AND ТМЦ.ID IN (SELECT Val FROM #СписокОтбор_2)") + " 
	|           AND (ТМЦ.ISMARK = 0)
	|			AND (ТМЦ.ISFOLDER = 2)
	|			AND (УМК_РазрешенныеВидыУпаковкиТМЦ.ISMARK = 0)
	|			AND (УМК_НормыСписанияМатериаловУпаковокФорм.ISMARK = 0)) AS Подзапрос
	|WHERE 1=1 " +
	?(Отбор_1 = 0,"","AND Подзапрос.значение_5 IN (SELECT Val FROM #СписокОтбор_1)") + "
	|GROUP BY 
	|     Подзапрос.значение_1
	|	, Подзапрос.значение_2
	|   , Подзапрос.значение_3
	|	, Подзапрос.значение_4
	|	, Подзапрос.значение_5 WITH ROLLUP
	|ORDER BY
	|     Подзапрос.значение_1
	|	, Подзапрос.значение_2
	|   , Подзапрос.значение_3
	|	, Подзапрос.значение_4
	|	, Подзапрос.значение_5 
	|";
		
КонецФункции

Функция ТекстЗапроса2()
	Возврат 
	"SELECT 
	|     Подзапрос.значение_1 [значение_1 $Справочник.ТМЦ]
	|	, Подзапрос.значение_2 [значение_2 $Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ]
	|	, Подзапрос.значение_3 [значение_3 $Справочник.ВидыУпаковки]
	|	, Подзапрос.значение_5 [значение_5 $Справочник.ТМЦ]
	|	, Sum(Подзапрос.значение_6) значение_6
	|   , GROUPING(Подзапрос.значение_1) as Группа_1
	|   , GROUPING(Подзапрос.значение_2) as Группа_2
	|   , GROUPING(Подзапрос.значение_3) as Группа_3 
	|   , GROUPING(Подзапрос.значение_5) as Группа_5
	|
	|FROM (SELECT ТМЦ.ID значение_1
	|			, УМК_РазрешенныеВидыУпаковкиТМЦ.ID значение_2
	|			, $УМК_РазрешенныеВидыУпаковкиТМЦ.ВидУпаковки значение_3
	|			, $ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковок.Материал(УМК_НормыСписанияМатериаловУпаковок.ID, :ВыбДата) значение_5
	|			, $ПоследнееЗначение.УМК_НормыСписанияМатериаловУпаковок.НормаСписания(УМК_НормыСписанияМатериаловУпаковок.ID, :ВыбДата) значение_6
	|		FROM $Справочник.ТМЦ AS ТМЦ
	|			INNER JOIN $Справочник.УМК_РазрешенныеВидыУпаковкиТМЦ AS УМК_РазрешенныеВидыУпаковкиТМЦ ON ТМЦ.ID = УМК_РазрешенныеВидыУпаковкиТМЦ.PARENTEXT
	|			INNER JOIN $Справочник.УМК_НормыСписанияМатериаловУпаковок AS УМК_НормыСписанияМатериаловУпаковок ON УМК_РазрешенныеВидыУпаковкиТМЦ.ID = УМК_НормыСписанияМатериаловУпаковок.PARENTEXT
	|		WHERE 1=1 " + 
	?(Отбор_2 = 0,"","AND ТМЦ.ID IN (SELECT Val FROM #СписокОтбор_2)") + "     
	|			AND (ТМЦ.ISMARK = 0)
	|			AND (ТМЦ.ISFOLDER = 2)
	|           AND (УМК_РазрешенныеВидыУпаковкиТМЦ.ISMARK = 0)
	|			AND (УМК_НормыСписанияМатериаловУпаковок.ISMARK = 0)) AS Подзапрос
	|WHERE 1=1 " +
	?(Отбор_1 = 0,"","AND Подзапрос.значение_5 IN (SELECT Val FROM #СписокОтбор_1)") + "
	|GROUP BY 
	|     Подзапрос.значение_1
	|	, Подзапрос.значение_2
	|   , Подзапрос.значение_3
	|	, Подзапрос.значение_5 WITH ROLLUP
	|ORDER BY
	|     Подзапрос.значение_1
	|	, Подзапрос.значение_2
	|   , Подзапрос.значение_3
	|	, Подзапрос.значение_5 
	|";
	
КонецФункции

Процедура УстановитьЗаголовки()
	
	Заголовок_1 = "Продукция";
	Заголовок_2 = "Упаковка";
	Заголовок_3 = "Форма";
	Заголовок_4 = "Материал";
	Заголовок_5 = "Количество";
	
	ЗаголовокГруппировок = ?(ПоФорме=1,
	Заголовок_1 + "/" +
	Заголовок_2 + "/" +
	Заголовок_3 + "/" +
	Заголовок_4,
	Заголовок_1 + "/" +
	Заголовок_2 + "/" +
	Заголовок_4)
	;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицу(Таб)
	Перем тз; //:ТаблицаЗначений
	
	рс = СоздатьОбъект("ODBCRecordset");
	ТекстЗапроса = ?(ПоФорме=1,ТекстЗапроса1(),ТекстЗапроса2());
	
	рс.УстановитьТекстовыйПараметр("ВыбДата", ВыбДата);
	
	рс.УложитьСписокОбъектов(СписокОтбор_1, "#СписокОтбор_1", "ТМЦ");
	рс.УложитьСписокОбъектов(СписокОтбор_2, "#СписокОтбор_2", "ТМЦ");
	
	тз = рс.ВыполнитьИнструкцию(ТекстЗапроса);
	
	тз.ВыбратьСтроки();
	
	Если тз.КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока тз.ПолучитьСтроку() = 1 Цикл
		// По материалам форм упаковки
		Если ПоФорме = 1 Тогда 
			Группировка = тз.Группа_1 + тз.Группа_2 + тз.Группа_3 +тз.Группа_4 + тз.Группа_5;
			Если Группировка = 4 Тогда
				Таб.ВывестиСекцию("Группировка_1");// Продукция
			КонецЕсли;
			Если Группировка = 2  Тогда
				Таб.ВывестиСекцию("Группировка_2");//Упаковка
			КонецЕсли;
			Если Группировка = 1 Тогда
				Таб.ВывестиСекцию("Группировка_3");//Форма упаковки
			КонецЕсли;
			Если Группировка = 0 Тогда
				Таб.ВывестиСекцию("Группировка_4");// Материал
			КонецЕсли;
			//По материалам упаковки
		ИначеЕсли ПоФорме = 2 Тогда 
			Группировка = тз.Группа_1 + тз.Группа_2 + тз.Группа_3 + тз.Группа_5;
			Если Группировка = 3 Тогда
				Таб.ВывестиСекцию("Группировка_1");// Продукция
			КонецЕсли;
			Если Группировка = 1  Тогда
				Таб.ВывестиСекцию("Группировка_2");//Упаковка
			КонецЕсли;
			Если Группировка = 0 Тогда
				Таб.ВывестиСекцию("Группировка_4");// Материал
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;   
	
КонецПроцедуры

Процедура Сформировать()
	Перем тз; 
	
	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Список_1", Список_1);
	Расшифровка.Установить("Список_2", Список_2);
	Расшифровка.Установить("ПоФорме", ПоФорме);
	Расшифровка.Установить("ВыбДата",  ВыбДата);
	Расшифровка.Установить("Отчет", "ПоМатериаламФормУпаковки");
	
	Расшифровка.Установить("Обновить");
	
	СписокОтбор_1 = СоздатьОбъект("СписокЗначений");
	СписокОтбор_2  = СоздатьОбъект("СписокЗначений");
	
	Отбор_1 = Список_2.РазмерСписка();
	Отбор_2  = Список_1.РазмерСписка();
	
	// Подготовка к заполнению выходных форм данными запроса
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить=0) Тогда
	  Таб = СоздатьОбъект("Таблица");
	Иначе
	  Таб.Очистить();
	КонецЕсли;
	
	Таб.ИсходнаяТаблица("ТабОтчета");
	ЗаголовокОтчета = ?(ПоФорме=1,"Материалы формы упаковки","Материалы упаковки");
	
	УстановитьЗаголовки();
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0,Таб.ВысотаТаблицы(),0);
	
	//Подготовим список материалов по условиям отбора
	Если Отбор_1 > 0 Тогда
		//Результат выборки материалов с учетом отбора
		рс = СоздатьОбъект("ODBCRecordset");
		ТекстЗапроса = 
		"SELECT ТМЦ.ID [Ссылка $Справочник.ТМЦ]
		|FROM $Справочник.ТМЦ AS ТМЦ 
		|WHERE ТМЦ.IsFolder = 2" + 
		?(Список_1.РазмерСписка() =0,"","AND ТМЦ.ID IN (SELECT Val FROM #СписокОтбор)") + "
		|ORDER BY ТМЦ.ID";
		рс.УложитьСписокОбъектов(Список_2, "#СписокОтбор", "ТМЦ");
		Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);
		Результат.Выгрузить(СписокОтбор_1,,,"Ссылка");
	КонецЕсли;
	
	//Подготовим список продукции с учетом отбора
	Если Отбор_2 > 0 Тогда
		рс = СоздатьОбъект("ODBCRecordset");
		ТекстЗапроса = 
		"SELECT ТМЦ.ID [Ссылка $Справочник.ТМЦ]
		|FROM $Справочник.ТМЦ AS ТМЦ
		|WHERE ТМЦ.IsFolder = 2" + 
		?(Список_1.РазмерСписка() =0,"","AND ТМЦ.ID IN (SELECT Val FROM #СписокОтбор)") + "
		|ORDER BY ТМЦ.ID";
		рс.УложитьСписокОбъектов(Список_1, "#СписокОтбор", "ТМЦ");
		Результат = рс.ВыполнитьИнструкцию(ТекстЗапроса);
		Результат.Выгрузить(СписокОтбор_2,,,"Ссылка");
	КонецЕсли;
		
	ЗаполнитьТаблицу(Таб);
		
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.Показать(ЗаголовокОтчета,"");

КонецПроцедуры
