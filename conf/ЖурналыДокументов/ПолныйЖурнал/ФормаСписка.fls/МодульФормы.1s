Перем ИмяФормы;

// ===============================
// Описание переменных 
Перем ЖурналПоДоговору;                  
Перем СписокВидовДокументов, ТипыУчета;
Перем КонтрагентДляОтбора,ФирмаДляОтбора,ВидДокументаДляОтбора,АвторДляОтбора,ТипДляОтбора;
Перем ЗначениеВВидеСтроки;

// ===============================
// функции и процедуры, вызываемые из формул элементов диалога

// ===============================
Функция Валюта()
    Если глЕстьРеквизитШапки("Валюта",ТекущийДокумент.Вид()) = Да Тогда
		Возврат ТекущийДокумент.Валюта.Кратко;
	Иначе
		Возврат "";
    КонецЕсли;
КонецФункции
                  
// ===============================
Процедура ОткрытьПодчиненные()
	Если ТекущийДокумент.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;  
	ОткрытьФорму("Журнал.Подчиненные.ФормаСписка",,ТекущийДокумент);
КонецПроцедуры

// ===============================
Процедура ПриВыбореКонтрагента()   
	Если КонтрагентДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Контрагент", КонтрагентДляОтбора);
		КонтрагентДляОтбора = КонтрагентДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
КонецПроцедуры 

// ===============================
Процедура ПриВыбореВидаДокумента()
	Если ПустоеЗначение(ВидДокументаДляОтбора) = 1 Тогда
		ВидДокументаДляОтбора=1;
	КонецЕсли;
	УстановитьОтбор(СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора), );
КонецПроцедуры 

// ===============================
Процедура ПриВыбореАвтора()  
	Если АвторДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Автор", АвторДляОтбора);
		АвторДляОтбора = АвторДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ПриВыбореТипа()    
	Если ПустоеЗначение(ТипДляОтбора)>0 Тогда
		ТипДляОтбора=1;
	КонецЕсли;	
	УстановитьОтбор("ТипУчета", ТипДляОтбора-1);
КонецПроцедуры

// ===============================
Процедура ПриВыбореФирмы()  
	Если ФирмаДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Фирма", ФирмаДляОтбора);
		ФирмаДляОтбора = ФирмаДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
КонецПроцедуры

// ===============================
//
Процедура ПриУстановкеБыстрогоОтбора()
	Перем ТекущДок;                           
	ТекущДок = ТекущийДокумент;
	Если ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "отсутствует" Тогда	// нет быстрого отбора
		ЗначениеВВидеСтроки = "";
		Форма.кнЗначение.Доступность(0);
		УстановитьОтбор("");
		ВидыОтбора("*");
	Иначе		// есть быстрый отбор
		Форма.кнЗначение.Доступность(1);
		Если ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по контрагенту" Тогда
			ПриВыбореКонтрагента();
			ЗначениеВВидеСтроки=Строка(КонтрагентДляОтбора);
		ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по виду документов" Тогда
			ПриВыбореВидаДокумента();                   
			СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора,ЗначениеВВидеСтроки);
		ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по автору" Тогда
			ПриВыбореАвтора();                          
			ЗначениеВВидеСтроки=Строка(АвторДляОтбора);
		ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по типу учета" Тогда
			ПриВыбореТипа();
			ТипыУчета.ПолучитьЗначение(ТипДляОтбора,ЗначениеВВидеСтроки);
		ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по фирме" Тогда
			ПриВыбореФирмы();
			ЗначениеВВидеСтроки=Строка(ФирмаДляОтбора);
		КонецЕсли;
		ВидыОтбора("");
	КонецЕсли;
	Попытка
		АктивизироватьОбъект(ТекущДок);
	Исключение	
	КонецПопытки;	
КонецПроцедуры // ПриУстановкеБыстрогоОтбора

// ===============================
//
Процедура ВыборЗначения()  
	Перем ЗначениеИзменилось,СтароеЗначение,ВыбЗначение;                     
	Если ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "отсутствует" Тогда	// нет быстрого отбора
		Возврат;
	КонецЕсли;	
	
	ЗначениеИзменилось=0;    

	Если ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по контрагенту" Тогда
		СтароеЗначение=КонтрагентДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Контрагенты");
		Если ВыбЗначение.Выбрать("Выбор контрагента","Форма списка")=1 Тогда
			КонтрагентДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по виду документов" Тогда
		СтароеЗначение=ВидДокументаДляОтбора;
		Если ПустоеЗначение(ВидДокументаДляОтбора)>0 Тогда
			ВыбЗначение = "";
		Иначе	
			ВыбЗначение = СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора);
		КонецЕсли;	
		СписокВидовДокументов.ВыбратьЗначение(ВыбЗначение,"Выбор вида документов",,,0);
		ВидДокументаДляОтбора = СписокВидовДокументов.НайтиЗначение(ВыбЗначение);
		Если ВидДокументаДляОтбора<>СтароеЗначение Тогда 
			ЗначениеИзменилось=1;    
		КонецЕсли;	              
		
	ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по автору" Тогда
		СтароеЗначение=АвторДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Пользователи");
		Если ВыбЗначение.Выбрать("Выбор автора","Форма списка")=1 Тогда
			АвторДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по типу учета" Тогда
		СтароеЗначение=ТипДляОтбора;                         
		Если ПустоеЗначение(ТипДляОтбора)>0 Тогда
			ВыбЗначение = "";
		Иначе	
			ВыбЗначение = ТипыУчета.ПолучитьЗначение(ТипДляОтбора);
		КонецЕсли;	
		ТипыУчета.ВыбратьЗначение(ВыбЗначение,"Выбор типа учета",,,0);
		ТипДляОтбора = ТипыУчета.НайтиЗначение(ВыбЗначение);
		Если ТипДляОтбора<>СтароеЗначение Тогда 
			ЗначениеИзменилось=1;    
		КонецЕсли;	
	
	ИначеЕсли ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока(),) = "по фирме" Тогда
		СтароеЗначение=ФирмаДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Фирмы");
		Если ВыбЗначение.Выбрать("Выбор фирмы","Форма списка")=1 Тогда
			ФирмаДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЕсли;
	        
	Если ЗначениеИзменилось=1 Тогда
		ПриУстановкеБыстрогоОтбора();
	КонецЕсли;	

КонецПроцедуры

// ===============================
Процедура ПриОткрытииЖурналаПоДоговору()
	
	УстановитьОтбор("Договор",Форма.Параметр.ТекущийДокумент());
	ВидыОтбора("");
	    
	Форма.текстБыстрыйОтбор.Видимость(0); 
	Форма.ВидОтбора.Видимость(0); 
	Форма.кнЗначение.Видимость(0); 
	ЗначениеВВидеСтроки = "";
	
	Форма.Заголовок("Журнал документов по договору """+СокрЛП(Строка(Форма.Параметр))+"""");
	
	глАктивныйДоговор = Форма.Параметр.ТекущийДокумент();
	
КонецПроцедуры

// ===============================
Процедура ПриОткрытииЖурналаСПозиционированиемНаДокументе(Док)
	
	АвторДляОтбора = Док.Автор;
	ВидДокументаДляОтбора = СписокВидовДокументов.НайтиЗначение(Док.Вид());
	КонтрагентД=глКонтрагентДок(Док);
	Если ПустоеЗначение(КонтрагентД)=0 Тогда
		КонтрагентДляОтбора = КонтрагентД;
	Иначе
		ВидОтбора.ТекущаяСтрока(2);
	КонецЕсли;
	
	ФирмаДляОтбора = Док.Фирма;
	
	Если (НачалоИнтервала()>Док.ДатаДок)ИЛИ(КонецИнтервала()<Док.ДатаДок) Тогда
		УстановитьИнтервал(НачКвартала(Док.ДатаДок),ПолучитьДатуТА());
	КонецЕсли;
	
КонецПроцедуры


// ===============================
Процедура ПриОбычномОткрытииЖурнала()
	
	// определение контрагента для отбора
	КонтрагентДляОтбора = глВосстановитьЗначение(ИмяФормы, "КонтрагентОтбораЖурнала", ПолучитьПустоеЗначение("Справочник.Контрагенты"));
	// определение фирмы для отбора
	ФирмаДляОтбора = глВосстановитьЗначение(ИмяФормы, "ФирмаОтбораЖурнала", ПолучитьПустоеЗначение("Справочник.Фирмы"));
	// определение вида документа для отбора
	ВидДокументаДляОтбора = глВосстановитьЗначение(ИмяФормы, "ВидДокументаДляОтбораЖурнала", 1); 
	// определение автора документа для отбора
	АвторДляОтбора = глВосстановитьЗначение(ИмяФормы, "АвторДляОтбораЖурнала", ПолучитьПустоеЗначение("Справочник.Пользователи"));
	// определение типа учета для отбора
	ТипДляОтбора = глВосстановитьЗначение(ИмяФормы, "ТипДляОтбораЖурнала", 1);
КонецПроцедуры


// ===============================
Процедура ПриОткрытииИЛИПереоткрытииЖурнала(Режим)
	ЖурналПоДоговору = 0;
	Если Режим="Открытие" Тогда
		// Позиционируемся на последнем документе
		ТекДок= глВосстановитьЗначение(ИмяФормы, "ТекДок", ПолучитьПустоеЗначение("Документ"));
		Форма.Заголовок("",1);
		ПриОбычномОткрытииЖурнала();
	ИначеЕсли Режим="Переоткрытие" Тогда	
		ТекДок = ТекущийДокумент;
	КонецЕсли;	
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если ВРег(ТипЗначенияСтр(Форма.Параметр))="ДОКУМЕНТ" Тогда
			// НАДО ПОЗИЦИОНИРОВАТЬСЯ НА ЭТОМ ДОКУМЕНТЕ
			Док = Форма.Параметр.ТекущийДокумент();
			Если Док.Выбран()>0 Тогда 
				ТекДок = Док;
				ПриОткрытииЖурналаСПозиционированиемНаДокументе(Док);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;      
	ПриУстановкеБыстрогоОтбора();
	Попытка
		АктивизироватьОбъект(ТекДок);
	Исключение	
	КонецПопытки;	
КонецПроцедуры

// ===============================
Процедура Параметры()
	Если ПустоеЗначение(ТекущийДокумент)=0 Тогда
		ОткрытьФормуМодально("Обработка.ЭП_ПараметрыОбъекта",ТекущийДокумент);
	КонецЕсли;
КонецПроцедуры 

// ===============================
Функция УстДоступность()
	Форма.кПравоваяПоддержка.Видимость(глВидимостьПравовойПоддержки);
	Возврат "";
КонецФункции

// ===============================
// Предопределенные процедуры

// ===============================
Процедура ПриПовторномОткрытии()
	ПриОткрытииИЛИПереоткрытииЖурнала("Переоткрытие");
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()                                        
	// Определение был ли быстрый отбор
	ТекСтр = глВосстановитьЗначение(ИмяФормы, "ВидОтбораЖурнала", 1);
	Если ТекСтр > ВидОтбора.РазмерСписка() Тогда
	    ТекСтр = ВидОтбора.РазмерСписка();
	КонецЕсли;
	ВидОтбора.ТекущаяСтрока(ТекСтр);
	
	ПриОткрытииИЛИПереоткрытииЖурнала("Открытие");
	
	// будем отслеживать только реальные изменения этих реквизитов
	Форма.ВидОтбора.ВыполнятьФормулуТолькоПриИзменении(1); 
	
КонецПроцедуры

// ===============================
Процедура ПриЗакрытии() // Предопределенная процедура
	Если ЖурналПоДоговору=0 Тогда
		глСохранитьЗначение(ИмяФормы,"ТекДок",ТекущийДокумент);
	    глСохранитьЗначение(ИмяФормы,"КонтрагентОтбораЖурнала",КонтрагентДляОтбора); 
		глСохранитьЗначение(ИмяФормы,"ФирмаОтбораЖурнала",ФирмаДляОтбора);
		глСохранитьЗначение(ИмяФормы,"ВидДокументаДляОтбораЖурнала", ВидДокументаДляОтбора);
	    глСохранитьЗначение(ИмяФормы,"АвторДляОтбораЖурнала",АвторДляОтбора);
	    глСохранитьЗначение(ИмяФормы,"ТипДляОтбораЖурнала",ТипДляОтбора);          
		глСохранитьЗначение(ИмяФормы,"ВидОтбораЖурнала",ВидОтбора.ТекущаяСтрока());
	Иначе
		глАктивныйДоговор = "";
	КонецЕсли;	                                              
КонецПроцедуры // ПриЗакрытии
// ===============================

ТипыУчета = СоздатьОбъект("СписокЗначений");
ТипыУчета.ДобавитьЗначение("Упр.","Упр.");
ТипыУчета.ДобавитьЗначение("Общ.","Общ.");
ТипыУчета.ДобавитьЗначение("Фин.","Фин.");

СписокВидовДокументов = СоздатьОбъект("СписокЗначений");
Для Счетчик = 1 по Метаданные.Документ() Цикл
	СписокВидовДокументов.ДобавитьЗначение(Метаданные.Документ(Счетчик).Идентификатор, Метаданные.Документ(Счетчик).Представление());
КонецЦикла;
СписокВидовДокументов.СортироватьПоПредставлению();
	                  
ВидОтбора.ДобавитьЗначение("по контрагенту");
ВидОтбора.ДобавитьЗначение("по виду документов");
ВидОтбора.ДобавитьЗначение("по автору");
ВидОтбора.ДобавитьЗначение("по фирме");
ВидОтбора.ДобавитьЗначение("отсутствует");

ЖурналПоДоговору = 0;

КонтрагентДляОтбора = СоздатьОбъект("Справочник.Контрагенты");
ФирмаДляОтбора = СоздатьОбъект("Справочник.Фирмы");
АвторДляОтбора = СоздатьОбъект("Справочник.Пользователи");
ТипДляОтбора = 1;
ВидДокументаДляОтбора = 1;

ЗначениеВВидеСтроки = "";

ИмяФормы = "Журнал.ПолныйЖурнал.Форма.ФормаСписка";